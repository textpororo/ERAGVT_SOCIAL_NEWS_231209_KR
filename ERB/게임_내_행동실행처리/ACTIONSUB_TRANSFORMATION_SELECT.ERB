;**********************************************************
;행동 개시 시 변신에 관한 처리
@ACTION_TRANSFORMATION_SELECT, ARG, ARGS
#DIMS ACTION_NAME,2
;컨피그의 변신 설정 주고받기용. 위에서부터 순서대로 통상 변신, 여체화 변신일 경우, 남성화 변신일 경우
;내용은 0=매번 선택、1=항상 변신한다、2=항상 변신하지 않는다
#DIM TF_CONFIG_NOMAL
#DIM TF_CONFIG_GIRL
#DIM TF_CONFIG_BOY

IF ARGS == "특별 활동"
	ACTION_NAME:0 = 변신한 상태로 특별 활동을 가시겠습니까?
	ACTION_NAME:1 = 변신하지 않은 상태 그대로 특별 활동을 갑니다.
	TF_CONFIG_NOMAL = GLOBAL:51
	TF_CONFIG_GIRL  = GLOBAL:52
	TF_CONFIG_BOY   = GLOBAL:53
ELSEIF ARGS == "정보 수집"
	ACTION_NAME:0 = 변신한 상태로 정보 수집을 진행하시겠습니까?
	ACTION_NAME:1 = 변신하지 않은 상태 그대로 정보 수집을 진행합니다.
	TF_CONFIG_NOMAL = GLOBAL:54
	TF_CONFIG_GIRL  = GLOBAL:55
	TF_CONFIG_BOY   = GLOBAL:56
;원문은 気晴らし 기분 전환
ELSEIF ARGS == "여가 활동"
	ACTION_NAME:0 = 변신한 상태로 여가 활동을 즐기러 가시겠습니까?
	ACTION_NAME:1 = 변신하지 않은 채로 여가 활동을 갑니다.
	TF_CONFIG_NOMAL = GLOBAL:57
	TF_CONFIG_GIRL  = GLOBAL:58
	TF_CONFIG_BOY   = GLOBAL:59
ENDIF

;변신 가능 캐릭터는 변신 상태가 될지 선택할 수 있다
;임신이 밝혀진 경우, 남체화 TS변신 캐릭터는 변신 불가능
IF TALENT:ARG:변신능력 > 0 && (ISFEMALE(ARG) && TALENT:ARG:변신시ＴＳ == 1 && CHECK_PREGNANT_F(ARG) > 0)
	PRINTFORMW %조사처리(CALLNAME:ARG,"는")% 임신 중이기 때문에, 변신하여 남성화할 수 없습니다. %ACTION_NAME:1%
ELSEIF TALENT:ARG:변신능력 > 0
	;변신 후 이름 체크
	CFLAG:ARG:1 = 1
	LOCALS'=(PRINT_TRANSCALLNAME(ARG)!="당신" && PRINT_TRANSCALLNAME(ARG)!=CALLNAME:ARG) ? PRINT_TRANSCALLNAME(ARG) # ""
	CFLAG:ARG:1 = 0

	;변신 전에 남자인 상태에서 변신 후에 여자가 되고, 컨피그에서 여체화를 항상 한다 or 하지 않는다
	IF CHARATALENT_F(ARG,0,"남자") > 0 && CHARATALENT_F(ARG,1,"남자") == 0 && TF_CONFIG_GIRL != 0
	;네or아니오 대체용. 여체화를 항상 한다면 계산 결과(１−１＝０＝네), 항상 하지 않는다면 계산 결과(２−１＝１＝아니오)
		RESULT = (TF_CONFIG_GIRL) - 1
	ELSEIF CHARATALENT_F(ARG,0,"남자") == 0 && CHARATALENT_F(ARG,1,"남자") > 0 && TF_CONFIG_BOY != 0
		RESULT = (TF_CONFIG_BOY) - 1
	ELSEIF TF_CONFIG_NOMAL != 0
		RESULT = (TF_CONFIG_NOMAL) - 1
	ELSE
		IF CHARATALENT_F(ARG,0,"남자") > 0 && CHARATALENT_F(ARG,1,"남자") == 0
			LOCAL=2
			PRINTFORM %조사처리(CALLNAME:ARG,"는")% %LOCALS!="" ? LOCALS+조사만처리(LOCALS,"로") # ""% 변신하여 여체화할 수 있습니다. %ACTION_NAME:0%
		ELSEIF CHARATALENT_F(ARG,0,"남자") == 0 && CHARATALENT_F(ARG,1,"남자") > 0
			LOCAL=3
			PRINTFORM %조사처리(CALLNAME:ARG,"는")% %LOCALS!="" ? LOCALS+조사만처리(LOCALS,"로") # ""% 변신하여 남성화할 수 있습니다. %ACTION_NAME:0%
		ELSE
			LOCAL=1
			PRINTFORM %조사처리(CALLNAME:ARG,"는")% %LOCALS!="" ? LOCALS+조사만처리(LOCALS,"로") # ""% 변신할 수 있습니다. %ACTION_NAME:0%
		ENDIF
		PRINTL 
		PRINTFORML 　[0] %"네",27,LEFT%	[1] 아니오
		PRINTL
		PRINTFORML 　[9] 네　 (다음부터 확인하지 않는다)	[10] 아니오(다음부터 확인하지 않는다)
		$INPUT_LOOP_0_0
		INPUT
	ENDIF

	IF RESULT == 9 || RESULT == 10
		IF ARGS == "특별 활동"
			GLOBAL:(50+LOCAL)=RESULT==9 ? 1 # 2
		ELSEIF ARGS == "정보 수집"
			GLOBAL:(53+LOCAL)=RESULT==9 ? 1 # 2
		ELSEIF ARGS == "여가 활동"
			GLOBAL:(56+LOCAL)=RESULT==9 ? 1 # 2
		ENDIF
		RESULT = RESULT==9 ? 0 # 1
	ENDIF

	IF RESULT == 0
		;임신했을 경우, 남체화 TS변신 캐릭터는 변신에 실패
		IF CHARATALENT_F(ARG,0,"남자") == 0 && CHARATALENT_F(ARG,1,"남자") > 0 && TALENT:ARG:임신 > 0
			PRINTFORML 그러나, %LOCALS!="" ? LOCALS+조사만처리(LOCALS,"로")+"의" # CALLNAME:ARG+"의"% 변신이 실패했다!
			PRINTFORML 평소처럼 변신하려고 다시 시도해 봤지만, 어째선지 아무 일도 일어나지 않는다……
			PRINTFORM 어쩔 수 없이, %조사처리(CALLNAME:ARG,"는")% %ACTION_NAME:1%
			IF TALENT:ARG:보추 > 0
				PRINT (보추)
			ELSEIF ISMALE(ARG)
				PRINT (♂)
			ELSEIF TALENT:ARG:후타나리
				PRINT (후타나리)
			ELSE
				PRINT (♀)
			ENDIF
			PRINTL
		ELSE
			PRINTFORM %조사처리(CALLNAME:ARG,"는")% %LOCALS!="" ? LOCALS+조사만처리(LOCALS,"로") # ""% 변신했다!
			IF CHARATALENT_F(ARG,0,"남자") != CHARATALENT_F(ARG,1,"남자")
				IF TALENT:ARG:보추 > 0
					PRINT (보추)→
				ELSEIF ISMALE(ARG)
					PRINT (♂)→
				ELSEIF TALENT:ARG:후타나리
					PRINT (후타나리)→
				ELSE
					PRINT (♀)→
				ENDIF
			ENDIF
			CALL TRANSFORM, 1
			IF TALENT:ARG:보추 > 0
				PRINT (보추)
			ELSEIF ISMALE(ARG)
				PRINT (♂)
			ELSEIF TALENT:ARG:후타나리
				PRINT (후타나리)
			ELSE
				PRINT (♀)
			ENDIF
		ENDIF
		PRINTL
	ELSEIF RESULT == 1
		PRINTFORM %ACTION_NAME:1%
		IF TALENT:ARG:보추 > 0
			PRINT (보추)
		ELSEIF ISMALE(ARG)
			PRINT (♂)
		ELSEIF TALENT:ARG:후타나리
			PRINT (후타나리)
		ELSE
			PRINT (♀)
		ENDIF
		PRINTL
	ELSE
		GOTO INPUT_LOOP_0_0
	ENDIF
	DRAWLINE
ENDIF
