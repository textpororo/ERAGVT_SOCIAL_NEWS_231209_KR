;**********************************************************
;의상 구입 처리
;**********************************************************
@SHOW_SHOP_CLOTH
#LOCALSIZE 1
#LOCALSSIZE 1
CALL SHOW_CLOTH("구입 화면")
RETURN 1

;----------------------------------------------------------
;의상 표시 UI
;----------------------------------------------------------
@SHOW_CLOTH(SHOW_TYPE)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS SHOW_TYPE

#DIM LCOUNT
#DIM KEEP_REDRAW
#DIM LIST_CLOTH, 100
#DIM LIST_CLOTH_LAST_NO
#DIM INPUT_NO
#DIMS CATEGORY = "겉옷", "커스터마이즈 파츠", "속옷", "기타"
#DIM SELECT_CATEGORY
#DIM 카탈로그
#DIM 간이정보
#DIM PAGE
#DIM MAXPAGE
#DIM LIST_CLOTH_SELECT_NO
#DIMS DRAW_TYPE
#DIM CONST FILTER_INPUT_OFFSET = 50

;1페이지에 표시할 의상의 수(리스트, 단일, 카탈로그, 간이정보)
#DIM SHOW_COUNT_LIST = 44, 1, 4, 22
;필터의 내용물 bit로 관리
#DIM FILTER
;필터 버튼 표시 문자
;油->방심, 撃->타격, 避->회피, ク->치명(크리티컬), 粘->점성, 邪->사악, 羞->치정, 癒->치유, 空->공중
{
#DIMS LABEL =
"공격", "방어", "민첩", "지성", "체력",
"기력", "성내성", "방심", "타격", "회피",
"치명", "점성", "사악", "치정", "B",
"C", "치유", "EX", "공중"
}
;필터 버튼의 효과 지정
{
#DIMS HOSEI_NAME =
"KOUGEKI", "BOUGYO", "BINSYOU", "CHISEI", "TAIRYOKU",
"KIRYOKU", "SEITAISEI", "YUDAN", "HIT", "AVOID",
"CRITICAL", "LIQUID", "WAVE", "SHYNESS", "ANTIBUP",
"ANTICUP", "HEALUP", "EXBOOST", "AIRPLUS"
}
;활성화할 필터의 번호
#DIM CLOTH_HOSEI_ACTIVE = 0, 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15, 16, 17, 18

;초기화
INPUT_NO = -1
SELECT_CATEGORY = 0
카탈로그 = 0
간이정보 = 0
PAGE = 0
MAXPAGE = 0
LIST_CLOTH_SELECT_NO = 0
DRAW_TYPE '= "리스트"

;현재 표시 방법을 저장하고 표시를 멈춘다
CURRENTREDRAW
KEEP_REDRAW = RESULT
REDRAW 0

$INPUT_LOOP

;戻る
IF INPUT_NO == 99
	IF DRAW_TYPE == "리스트" || DRAW_TYPE == "간이정보"
		IF SHOW_TYPE == "구입 화면"
			;실적
			LOCAL = 0
			FOR LCOUNT,100,400
				SIF LCOUNT == 100 || LCOUNT == 200 || LCOUNT == 300
					CONTINUE
				LOCAL += ITEM:LCOUNT
			NEXT
			IF LOCAL >= 10
				CALL UNLOCK_ACHIEVEMENT(269,"배틀 코스튬 마니아")
			ENDIF
			IF LOCAL >= 30
				CALL UNLOCK_ACHIEVEMENT(274,"패션 리더")
			ENDIF
		ENDIF
		REDRAW KEEP_REDRAW
		RETURN 1
	ELSE
		DRAW_TYPE '= 간이정보 ? "간이정보" # "리스트"
		PAGE = 0
		LIST_CLOTH_SELECT_NO = 0
	ENDIF
;앞 페이지로
ELSEIF INPUT_NO == 90
	PAGE = !PAGE ? MAXPAGE # PAGE - 1
	IF DRAW_TYPE == "단일"
		IF 0 < LIST_CLOTH_SELECT_NO
			LIST_CLOTH_SELECT_NO = LIST_CLOTH_SELECT_NO - 1
		ELSE
			LIST_CLOTH_SELECT_NO = LIST_CLOTH_LAST_NO - 1
		ENDIF
	ELSEIF DRAW_TYPE == "카탈로그"
		IF 0 < LIST_CLOTH_SELECT_NO - SHOW_COUNT_LIST:2
			LIST_CLOTH_SELECT_NO = LIST_CLOTH_SELECT_NO - SHOW_COUNT_LIST:2
		ELSE
			LIST_CLOTH_SELECT_NO = LIST_CLOTH_LAST_NO - 1
		ENDIF
	ENDIF
;뒷 페이지로
ELSEIF INPUT_NO == 91
	PAGE = 0 < MAXPAGE - PAGE ? PAGE + 1 # 0
	IF DRAW_TYPE == "단일"
		IF LIST_CLOTH_SELECT_NO < LIST_CLOTH_LAST_NO
			LIST_CLOTH_SELECT_NO ++
		ELSE
			LIST_CLOTH_SELECT_NO = 0
		ENDIF
	ELSEIF DRAW_TYPE == "카탈로그"
		IF LIST_CLOTH_SELECT_NO + SHOW_COUNT_LIST:2 < LIST_CLOTH_LAST_NO
			LIST_CLOTH_SELECT_NO = LIST_CLOTH_SELECT_NO + SHOW_COUNT_LIST:2
		ELSEIF LIST_CLOTH_SELECT_NO == LIST_CLOTH_LAST_NO - 1
			LIST_CLOTH_SELECT_NO = 0
		ELSE
			LIST_CLOTH_SELECT_NO = LIST_CLOTH_LAST_NO - 1
		ENDIF
	ENDIF
;카테고리
ELSEIF INRANGE(INPUT_NO, 0, VARSIZE("CATEGORY") - 1)
	IF 간이정보
		DRAW_TYPE '= "간이정보"
	ELSE
		DRAW_TYPE '= "리스트"
		간이정보 = 0
		카탈로그 = 0
	ENDIF
	SELECT_CATEGORY = INPUT_NO
	PAGE = 0
	LIST_CLOTH_SELECT_NO = 0
;카탈로그
ELSEIF INPUT_NO == 10
	IF 카탈로그
		카탈로그 = 0
		DRAW_TYPE '= "리스트"
	ELSE
		카탈로그 = 1
		간이정보 = 0
		DRAW_TYPE '= "카탈로그"
	ENDIF
	PAGE = GET_CLOTH_PAGE(LIST_CLOTH_SELECT_NO, DRAW_TYPE, LIST_CLOTH_LAST_NO, SHOW_COUNT_LIST)
;간이정보
ELSEIF INPUT_NO == 11
	IF 간이정보
		간이정보 = 0
		DRAW_TYPE '= "리스트"
	ELSE
		간이정보 = 1
		카탈로그 = 0
		DRAW_TYPE '= "간이정보"
	ENDIF
	PAGE = GET_CLOTH_PAGE(LIST_CLOTH_SELECT_NO, DRAW_TYPE, LIST_CLOTH_LAST_NO, SHOW_COUNT_LIST)
;필터
ELSEIF INRANGE(INPUT_NO, FILTER_INPUT_OFFSET, MIN(FILTER_INPUT_OFFSET + VARSIZE("LABEL") - 1, FILTER_INPUT_OFFSET + 19)) && (DRAW_TYPE == "리스트" || DRAW_TYPE == "간이정보")
	INVERTBIT FILTER, INPUT_NO - FILTER_INPUT_OFFSET
	PAGE = 0
	LIST_CLOTH_SELECT_NO = 0
;의상
ELSEIF INRANGE(INPUT_NO, 100, 1000) && ((ITEM:INPUT_NO == 0 && ITEMPRICE:INPUT_NO > 0) || (SHOW_TYPE == "표시 화면"))
	LIST_CLOTH_SELECT_NO = FINDELEMENT(LIST_CLOTH, INPUT_NO)
	IF LIST_CLOTH_SELECT_NO == -1
		INPUT_NO = -1
		GOTO INPUT_LOOP
	ENDIF
	IF DRAW_TYPE == "리스트" || DRAW_TYPE == "간이정보"
		DRAW_TYPE '= 카탈로그 ? "카탈로그" # "단일"
		간이정보 = 0
		PAGE = GET_CLOTH_PAGE(LIST_CLOTH_SELECT_NO, DRAW_TYPE, LIST_CLOTH_LAST_NO, SHOW_COUNT_LIST)
	ELSEIF SHOW_TYPE == "구입 화면"
		CALL BUY_CLOTH(LIST_CLOTH, INPUT_NO)
		IF RESULT == 1
			PRINTFORML %ITEMNAME:INPUT_NO%을(를) 구입했습니다.
			;구입한 의상이 맨 뒤의 것이라면 빈 페이지를 피하기 위해 한 개 앞의 물건을 건네준다
			IF LIST_CLOTH_SELECT_NO == LIST_CLOTH_LAST_NO - 1
				PAGE = GET_CLOTH_PAGE(LIST_CLOTH_SELECT_NO - 1, DRAW_TYPE, LIST_CLOTH_LAST_NO, SHOW_COUNT_LIST)
			ELSE
				PAGE = GET_CLOTH_PAGE(LIST_CLOTH_SELECT_NO, DRAW_TYPE, LIST_CLOTH_LAST_NO, SHOW_COUNT_LIST)
			ENDIF
		ELSEIF RESULT == -1
			PRINTFORML {RESULT}은(는) 유효하지 않은 값입니다.
		ELSEIF !RESULT
			PRINTFORML 돈이 부족합니다.
		ENDIF
		PRINTW
	ENDIF
ELSEIF INPUT_NO == 98
	FILTER = 0
ELSEIF INPUT_NO != -1
	PRINTFORMW {INPUT_NO}은(는) 유효하지 않은 값입니다.
	PRINTL
	INPUT_NO = -1
	GOTO INPUT_LOOP
ENDIF

;판매 중인 의상의 배열을 만든다
IF SHOW_TYPE == "구입 화면"
	CALL LIST_CLOTH_NOTHAVE(LIST_CLOTH, LIST_CLOTH_LAST_NO, CATEGORY:SELECT_CATEGORY)
;소지 의상의 배열을 만든다（변신 의상 제외）
ELSEIF SHOW_TYPE == "표시 화면"
	CALL LIST_CLOTH_HAVE(LIST_CLOTH, LIST_CLOTH_LAST_NO, CATEGORY:SELECT_CATEGORY)
ENDIF
IF !RESULT
	PRINTFORMW 존재하지 않는 카테고리가 지정되었습니다. (%CATEGORY:SELECT_CATEGORY%)
	SELECT_CATEGORY = 0
	GOTO INPUT_LOOP
ENDIF

;유효한 필터 버튼으로 필터
SIF FILTER
	CALL FILTER_CLOTH_HOSEI(LIST_CLOTH, LIST_CLOTH_LAST_NO, HOSEI_NAME, CLOTH_HOSEI_ACTIVE, SELECT_CATEGORY, FILTER)

;1페이지에 표시할 의상 수와 배열 내용물 수에서 최대 페이지를 가져온다
MAXPAGE = GET_CLOTH_MAXPAGE(DRAW_TYPE, LIST_CLOTH_LAST_NO, SHOW_COUNT_LIST)

;의상 표시 전 묘화
CALL SHOW_CLOTH_HEADER(SHOW_TYPE, LABEL, CLOTH_HOSEI_ACTIVE, DRAW_TYPE, SELECT_CATEGORY)

;의상 묘화
CALL SHOW_CLOTH_LIST(SHOW_TYPE, LIST_CLOTH, LIST_CLOTH_LAST_NO, DRAW_TYPE, SELECT_CATEGORY, PAGE, SHOW_COUNT_LIST, LABEL, HOSEI_NAME, CLOTH_HOSEI_ACTIVE)

;의상 표시 후 묘화
CALL SHOW_CLOTH_FOOTER(DRAW_TYPE, CATEGORY, SELECT_CATEGORY, 카탈로그, 간이정보, LABEL, CLOTH_HOSEI_ACTIVE, FILTER, FILTER_INPUT_OFFSET, MAXPAGE)

;표시를 재개하고 입력을 기다린다
REDRAW 2
INPUT
REDRAW 0

INPUT_NO = RESULT

GOTO INPUT_LOOP

;----------------------------------------------------------
;표시할 페이지 수를 반환한다
;----------------------------------------------------------
@GET_CLOTH_PAGE(CLOTH_NO, DRAW_TYPE, LIST_CLOTH_LAST_NO, SHOW_COUNT_LIST)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM CLOTH_NO
#DIMS DRAW_TYPE
#DIM LIST_CLOTH_LAST_NO
#DIM REF SHOW_COUNT_LIST

SIF CLOTH_NO < 1
	RETURNF 0
SIF LIST_CLOTH_LAST_NO < 1
	RETURNF 0
SELECTCASE DRAW_TYPE
	CASE "리스트"
		RETURNF CLOTH_NO / SHOW_COUNT_LIST
	CASE "단일"
		RETURNF CLOTH_NO / SHOW_COUNT_LIST:1
	CASE "카탈로그"
		RETURNF CLOTH_NO / SHOW_COUNT_LIST:2
	CASE "간이정보"
		RETURNF CLOTH_NO / SHOW_COUNT_LIST:3
ENDSELECT

;----------------------------------------------------------
;최대 페이지 수를 반환한다
;----------------------------------------------------------
@GET_CLOTH_MAXPAGE(DRAW_TYPE, LIST_CLOTH_LAST_NO, SHOW_COUNT_LIST)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS DRAW_TYPE
#DIM LIST_CLOTH_LAST_NO
#DIM REF SHOW_COUNT_LIST

SIF !LIST_CLOTH_LAST_NO
	RETURNF 0
SELECTCASE DRAW_TYPE
	CASE "리스트"
		RETURNF (LIST_CLOTH_LAST_NO - 1) / SHOW_COUNT_LIST
	CASE "단일"
		RETURNF (LIST_CLOTH_LAST_NO - 1) / SHOW_COUNT_LIST:1
	CASE "카탈로그"
		RETURNF (LIST_CLOTH_LAST_NO - 1) / SHOW_COUNT_LIST:2
	CASE "간이정보"
		RETURNF (LIST_CLOTH_LAST_NO - 1) / SHOW_COUNT_LIST:3
ENDSELECT

;----------------------------------------------------------
;머리말(헤더) 묘화
;----------------------------------------------------------
@SHOW_CLOTH_HEADER(SHOW_TYPE, LABEL, CLOTH_HOSEI_ACTIVE, DRAW_TYPE, SELECT_CATEGORY)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS SHOW_TYPE
#DIMS REF LABEL
#DIM REF CLOTH_HOSEI_ACTIVE
#DIMS DRAW_TYPE
#DIM SELECT_CATEGORY

#DIM WIDTH

CALL LB
DRAWLINE
IF SHOW_TYPE == "구입 화면"
	IF SELECT_CATEGORY == 0
		PRINTL 겉옷을 구입합니다.
	ELSEIF SELECT_CATEGORY == 1
		PRINTL 커스터마이즈 파츠를 구입합니다. (주의: 변신 코스튬 전용)
	ELSEIF SELECT_CATEGORY == 2
		PRINTL 속옷을 구입합니다.
	ELSEIF SELECT_CATEGORY == 3
		PRINTL 기타 장비를 구입합니다.
	ENDIF
	PRINTFORML 자금：{MONEY}＄
	DRAWLINE
ELSEIF SHOW_TYPE == "표시 화면"
	PRINTL 소지 의상 목록
	DRAWLINE
ENDIF
IF DRAW_TYPE == "간이정보"
	WIDTH = WIDTH_CLOTH + WIDTH_MONEY
	SIF SHOW_TYPE == "구입 화면"
		WIDTH += 11
	PRINTFORM %LOCALS, WIDTH%|
	FOR LOCAL, 0, VARSIZE("CLOTH_HOSEI_ACTIVE")
		PRINTFORM %LABEL:(CLOTH_HOSEI_ACTIVE:LOCAL)%|
	NEXT
	PRINTL
ENDIF

RETURN 1

;----------------------------------------------------------
;꼬리말(푸터) 묘사
;----------------------------------------------------------
@SHOW_CLOTH_FOOTER(DRAW_TYPE, CATEGORY, SELECT_CATEGORY,  카탈로그, 간이정보, LABEL, CLOTH_HOSEI_ACTIVE, FILTER, FILTER_INPUT_OFFSET, MAXPAGE)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS DRAW_TYPE
#DIMS REF CATEGORY
#DIM SELECT_CATEGORY
#DIM 카탈로그
#DIM 간이정보
#DIMS REF LABEL
#DIM REF CLOTH_HOSEI_ACTIVE
#DIM FILTER
#DIM FILTER_INPUT_OFFSET
#DIM MAXPAGE

#DIM CONST WIDTH = STRLENS("커스터마이즈 파츠") + 1
#DIM CATEGORY_NO

CALL CLOTH_BUTTON("앞 페이지　　", 90, !MAXPAGE ? 0 # 1)
CALL CLOTH_BUTTON("뒷 페이지", 91, !MAXPAGE ? 0 # 1)
PRINTL
IF DRAW_TYPE == "리스트" || DRAW_TYPE == "간이정보"
	DRAWLINE
	CALL SHOW_CLOTH_FILTERBUTTON(LABEL, CLOTH_HOSEI_ACTIVE, FILTER, FILTER_INPUT_OFFSET)
ENDIF
PRINTL
DRAWLINE

FOR CATEGORY_NO, 0, VARSIZE("CATEGORY")
	LOCALS '= CATEGORY:CATEGORY_NO
	LOCAL = CATEGORY:SELECT_CATEGORY != LOCALS ? 1 # 2
	LOCALS '= @"%LOCALS, WIDTH, LEFT%"
	CALL CLOTH_BUTTON(LOCALS, CATEGORY_NO, LOCAL, 0xFFFF00)
NEXT
PRINTL

LOCALS '= @"%"카탈로그 표시", WIDTH, LEFT%"
CALL CLOTH_TOGGLEBUTTON(LOCALS, 10, 카탈로그)

LOCALS '= @"%"리스트 표시　", WIDTH, LEFT%"
CALL CLOTH_TOGGLEBUTTON(LOCALS, 11, 간이정보)
PRINTL


PRINT  　　　　　　　　　　　　　　　　　　　　　　　　
PRINTBUTTON "[98] 필터를 초기화한다", 98
PRINTL

PRINTBUTTON "[99] 돌아간다", 99
PRINTL

RETURN 1

;----------------------------------------------------------
;필터 버튼 묘화
;----------------------------------------------------------
@SHOW_CLOTH_FILTERBUTTON(LABEL, CLOTH_HOSEI_ACTIVE, FILTER, FILTER_INPUT_OFFSET)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS REF LABEL
#DIM REF CLOTH_HOSEI_ACTIVE
#DIM FILTER
#DIM FILTER_INPUT_OFFSET

FOR LOCAL, 0, VARSIZE("CLOTH_HOSEI_ACTIVE")
	GETBIT FILTER, LOCAL
	CALL CLOTH_TOGGLEBUTTON(LABEL:(CLOTH_HOSEI_ACTIVE:LOCAL), FILTER_INPUT_OFFSET + LOCAL, RESULT)
	PRINT 　
NEXT

;----------------------------------------------------------
;의상 리스트 묘화
;----------------------------------------------------------
@SHOW_CLOTH_LIST(SHOW_TYPE, LIST_CLOTH, LIST_CLOTH_LAST_NO, DRAW_TYPE, SELECT_CATEGORY, PAGE, SHOW_COUNT_LIST, LABEL, HOSEI_NAME, CLOTH_HOSEI_ACTIVE)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS SHOW_TYPE
#DIM REF LIST_CLOTH
#DIM LIST_CLOTH_LAST_NO
#DIMS DRAW_TYPE
#DIM SELECT_CATEGORY
#DIM PAGE
#DIM REF SHOW_COUNT_LIST
#DIMS REF LABEL
#DIMS REF HOSEI_NAME
#DIM REF CLOTH_HOSEI_ACTIVE

#DIM NOTHAVE_COUNT
#DIM CLOTH_NO_FROM
#DIM CLOTH_NO_TO
#DIM SHOW_COUNT
#DIM BIT_POS
#DIM 역치
#DIM KEEP_CFLAG
#DIMS CALL_HOSEI_NAME
#DIM HOSEI_VAL

IF !LIST_CLOTH_LAST_NO
	IF SHOW_TYPE == "구입 화면"
		PRINTL     - SOLD OUT -
	ELSE
		PRINTL 미소지
	ENDIF
	RETURN 1
ENDIF

IF DRAW_TYPE == "리스트"
	SHOW_COUNT = SHOW_COUNT_LIST:0
ELSEIF DRAW_TYPE == "단일"
	SHOW_COUNT = SHOW_COUNT_LIST:1
ELSEIF DRAW_TYPE == "카탈로그"
	SHOW_COUNT = SHOW_COUNT_LIST:2
ELSEIF DRAW_TYPE == "간이정보"
	SHOW_COUNT = SHOW_COUNT_LIST:3
ENDIF

IF SELECT_CATEGORY == 0 || SELECT_CATEGORY == 2
	CALL_HOSEI_NAME '= "CLOTH_HOSEI"
ELSEIF SELECT_CATEGORY == 1
	CALL_HOSEI_NAME '= "CLOTH_CUSTOM_HOSEI"
ENDIF

CLOTH_NO_FROM = PAGE * SHOW_COUNT
CLOTH_NO_TO = MIN(PAGE * SHOW_COUNT + SHOW_COUNT, LIST_CLOTH_LAST_NO)
HOSEI_VAL = 0

IF DRAW_TYPE == "리스트"
	NOTHAVE_COUNT = 0
	FOR LOCAL, CLOTH_NO_FROM, CLOTH_NO_TO
		SIF (NOTHAVE_COUNT % 2) == 1
			PRINT 　
		PRINTFORM [%TOSTR(LIST_CLOTH:LOCAL)%] %ITEMNAME:(LIST_CLOTH:LOCAL), WIDTH_CLOTH, LEFT%
		IF SHOW_TYPE == "구입 화면"
			SIF MONEY < ITEMPRICE:(LIST_CLOTH:LOCAL)
				SETCOLOR 0x808080
			PRINTFORM ({ITEMPRICE:(LIST_CLOTH:LOCAL), WIDTH_MONEY, RIGHT}＄)
			RESETCOLOR
		ENDIF
		NOTHAVE_COUNT ++
		SIF (NOTHAVE_COUNT % 2) == 0
			PRINTL
	NEXT
	SIF (NOTHAVE_COUNT % 2) == 1
		PRINTL
ELSEIF DRAW_TYPE == "간이정보"
	KEEP_CFLAG = CFLAG:1
	FOR LOCAL, CLOTH_NO_FROM, CLOTH_NO_TO
		PRINTFORM [%TOSTR(LIST_CLOTH:LOCAL)%] %ITEMNAME:(LIST_CLOTH:LOCAL), WIDTH_CLOTH, LEFT%
		IF SHOW_TYPE == "구입 화면"
			LOCALS '= TOSTR(ITEMPRICE:(LIST_CLOTH:LOCAL))
			PRINTFORM (%LOCALS, WIDTH_MONEY, RIGHT%＄) 
		ENDIF
		PRINT |
		FOR BIT_POS, 0, VARSIZE("CLOTH_HOSEI_ACTIVE")
			IF SELECT_CATEGORY == 0
				SELECTCASE CLOTH_HOSEI_ACTIVE:BIT_POS
					CASE 0, 1, 2, 3, 7
						역치 = 100
					CASE 8, 9, 10, 14, 15, 16, 17, 18
						역치 = 0
					CASE 4, 5
						역치 = -95
					CASE 6, 11, 12, 13
						역치 = -100
				ENDSELECT
			ELSEIF SELECT_CATEGORY == 1
				SELECTCASE CLOTH_HOSEI_ACTIVE:BIT_POS
					CASE 4, 5, 11, 12
						역치 = -100
					CASEELSE
						역치 = 0
				ENDSELECT
			ELSEIF SELECT_CATEGORY == 2
				SELECTCASE CLOTH_HOSEI_ACTIVE:BIT_POS
					CASE 0, 1, 2, 3, 7
						역치 = 100
					CASE 8, 9, 10, 14, 15, 16, 17, 18
						역치 = 0
					CASE 6
						역치 = -95
					CASE 4, 5, 11, 12, 13
						역치 = -100
				ENDSELECT
			ELSE
				역치 = 0
			ENDIF

			;보정 유무 조사(겉옷～속옷은 참조 처리와 통합 완료)
			IF LIST_CLOTH:LOCAL<=499
				;변신 후 기준
				CFLAG:1 = 1

				CALL CLOTH_HOSEI(TARGET,LIST_CLOTH:LOCAL,HOSEI_NAME:BIT_POS)
				HOSEI_VAL=RESULT
			;커스터마이즈 파츠(처리가 구래(旧来)인 채로)
			ELSE
				TRYCCALLFORM %CALL_HOSEI_NAME%_%HOSEI_NAME:(CLOTH_HOSEI_ACTIVE:BIT_POS)%_{LIST_CLOTH:LOCAL}
					HOSEI_VAL = RESULT
				CATCH
					PRINT 　|
					CONTINUE
				ENDCATCH
				CFLAG:1 = 1
				TRYCCALLFORM %CALL_HOSEI_NAME%_%HOSEI_NAME:(CLOTH_HOSEI_ACTIVE:BIT_POS)%_{LIST_CLOTH:LOCAL}
					IF 역치 < 0
						HOSEI_VAL = MIN(RESULT, RESULT, HOSEI_VAL)
					ELSE
						HOSEI_VAL = MAX(RESULT, RESULT, HOSEI_VAL)
					ENDIF
				CATCH
				ENDCATCH
			ENDIF


			CALL SHOW_CLOTH_HOSEI_MARK(HOSEI_VAL, 역치)
			PRINT |
		NEXT
		PRINTL
	NEXT
	CFLAG:1 = KEEP_CFLAG
ELSE
	FOR LOCAL, CLOTH_NO_FROM, CLOTH_NO_TO
		PRINTFORM %ITEMNAME:(LIST_CLOTH:LOCAL), WIDTH_CLOTH, LEFT%
		IF SHOW_TYPE == "구입 화면"
			LOCALS '= TOSTR(ITEMPRICE:(LIST_CLOTH:LOCAL)) + "＄"
			PRINTFORM %LOCALS, WIDTH_MONEY + 2, RIGHT%
			PRINT 　
			CALL CLOTH_BUTTON("구입한다", LIST_CLOTH:LOCAL, ITEMPRICE:(LIST_CLOTH:LOCAL) <= MONEY ? 1 # 0)
		ENDIF
		PRINTL
		TRYCALLFORM CLOTH_DESCRIPTION_{LIST_CLOTH:LOCAL}
			CALL SHORTLINE
	NEXT
ENDIF

RETURN 1

;----------------------------------------------------------
;판매 중인 의상 중 소지하지 않은 의상을 배열에 넣는다
;----------------------------------------------------------
@LIST_CLOTH_NOTHAVE(LIST_CLOTH, LIST_CLOTH_LAST_NO, CATEGORY)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM REF LIST_CLOTH
#DIM REF LIST_CLOTH_LAST_NO
#DIMS CATEGORY

#DIM CLOTH_NO_FROM
#DIM CLOTH_NO_TO
#DIM NOTHAVE_COUNT

CALL GET_CLOTH_CATEGORY_FROM_TO(CATEGORY, CLOTH_NO_FROM, CLOTH_NO_TO)
SIF !RESULT
	RETURN 0

VARSET LIST_CLOTH
NOTHAVE_COUNT = 0

FOR LOCAL, CLOTH_NO_FROM, CLOTH_NO_TO
	IF ITEMPRICE:LOCAL > 0 && ISCHECK_CLOTH(LOCAL)
		LIST_CLOTH:NOTHAVE_COUNT = LOCAL
		NOTHAVE_COUNT ++
	ENDIF
NEXT

LIST_CLOTH_LAST_NO = FINDELEMENT(LIST_CLOTH, 0)

RETURN 1

;----------------------------------------------------------
;소지하고 있는 의상을 배열에 넣는다
;----------------------------------------------------------
@LIST_CLOTH_HAVE(LIST_CLOTH, LIST_CLOTH_LAST_NO, CATEGORY)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM REF LIST_CLOTH
#DIM REF LIST_CLOTH_LAST_NO
#DIMS CATEGORY

#DIM CLOTH_NO_FROM
#DIM CLOTH_NO_TO
#DIM HAVE_COUNT

CALL GET_CLOTH_CATEGORY_FROM_TO(CATEGORY, CLOTH_NO_FROM, CLOTH_NO_TO)
SIF !RESULT
	RETURN 0

VARSET LIST_CLOTH
HAVE_COUNT = 0

FOR LOCAL, CLOTH_NO_FROM, CLOTH_NO_TO
	IF ITEM:LOCAL
		LIST_CLOTH:HAVE_COUNT = LOCAL
		HAVE_COUNT ++
	ENDIF
NEXT

LIST_CLOTH_LAST_NO = FINDELEMENT(LIST_CLOTH, 0)

RETURN 1

;----------------------------------------------------------
;카테고리 별로 의상 범위를 반환한다
;----------------------------------------------------------
@GET_CLOTH_CATEGORY_FROM_TO(CATEGORY, CLOTH_NO_FROM, CLOTH_NO_TO)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS CATEGORY
#DIM REF CLOTH_NO_FROM
#DIM REF CLOTH_NO_TO
#DIMS SHOW_TYPE

SELECTCASE CATEGORY
	CASE "겉옷"
		CLOTH_NO_FROM = 100
		CLOTH_NO_TO = 300
	CASE "커스터마이즈 파츠"
		CLOTH_NO_FROM = 600
		CLOTH_NO_TO = 700
	CASE "속옷"
		CLOTH_NO_FROM = 300
		CLOTH_NO_TO = 400
	CASE "기타"
		CLOTH_NO_FROM = 500
		CLOTH_NO_TO = 600
	CASEELSE
		RETURN 0
ENDSELECT

RETURN 1

;----------------------------------------------------------
;필터에 대응한 능력치 보정을 수행하는 함수를 찾아 필터링
;----------------------------------------------------------
@FILTER_CLOTH_HOSEI(LIST_CLOTH, LIST_CLOTH_LAST_NO, HOSEI_NAME, CLOTH_HOSEI_ACTIVE, SELECT_CATEGORY, FILTER)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM REF LIST_CLOTH
#DIM REF LIST_CLOTH_LAST_NO
#DIMS REF HOSEI_NAME
#DIM REF CLOTH_HOSEI_ACTIVE
#DIM SELECT_CATEGORY
#DIM FILTER

#DIM BIT_POS
#DIM NEW_LIST_CLOTH, 100
#DIM NEW_LIST_CLOTH_NO
#DIM CLOTH_NO
#DIM FILTER_FLAG
#DIMS CALL_HOSEI_NAME

SIF !LIST_CLOTH_LAST_NO
	RETURN 0

VARSET NEW_LIST_CLOTH
NEW_LIST_CLOTH_NO = 0
FILTER_FLAG = 1

;IF SELECT_CATEGORY == 0 || SELECT_CATEGORY == 2
;	CALL_HOSEI_NAME '= "CLOTH_HOSEI"
;ELSEIF SELECT_CATEGORY == 1
;	CALL_HOSEI_NAME '= "CLOTH_CUSTOM_HOSEI"
;ENDIF

;활성화된 모든 필터의 효과에 보정을 거는 함수가 있는 것을 취득
FOR CLOTH_NO, 0, LIST_CLOTH_LAST_NO
	FOR BIT_POS, 0, VARSIZE("CLOTH_HOSEI_ACTIVE")
		GETBIT FILTER, BIT_POS
		SIF !RESULT
			CONTINUE
		IF SELECT_CATEGORY == 0 || SELECT_CATEGORY == 2
			CALL CLOTH_HOSEI,0,LIST_CLOTH:CLOTH_NO,HOSEI_NAME:(CLOTH_HOSEI_ACTIVE:BIT_POS),1
			IF RESULT==-99999
				FILTER_FLAG = 0
				BREAK
			ENDIF
			;마이너스 보정이라면 친다(弾く)
			IF HOSEI_NAME:(CLOTH_HOSEI_ACTIVE:BIT_POS) == "TAIRYOKU" || HOSEI_NAME:(CLOTH_HOSEI_ACTIVE:BIT_POS) == "KIRYOKU" || HOSEI_NAME:(CLOTH_HOSEI_ACTIVE:BIT_POS) == "SEITAISEI"
				IF RESULT >= 100
					FILTER_FLAG = 0
					BREAK
				ENDIF
			ELSE
				IF RESULT <= 0
					FILTER_FLAG = 0
					BREAK
				ENDIF
			ENDIF
		ELSEIF SELECT_CATEGORY == 1
			TRYCCALLFORM CLOTH_CUSTOM_HOSEI_%HOSEI_NAME:(CLOTH_HOSEI_ACTIVE:BIT_POS)%_{LIST_CLOTH:CLOTH_NO}
			CATCH
				FILTER_FLAG = 0
				BREAK
			ENDCATCH
		ENDIF
	NEXT
	IF FILTER_FLAG
		NEW_LIST_CLOTH:NEW_LIST_CLOTH_NO = LIST_CLOTH:CLOTH_NO
		NEW_LIST_CLOTH_NO ++
	ELSE
		FILTER_FLAG = 1
	ENDIF
NEXT

VARSET LIST_CLOTH

FOR LOCAL, 0, NEW_LIST_CLOTH_NO
	LIST_CLOTH:LOCAL = NEW_LIST_CLOTH:LOCAL
NEXT

LIST_CLOTH_LAST_NO = FINDELEMENT(LIST_CLOTH, 0)

;----------------------------------------------------------
@ISCHECK_CLOTH, ARG
#FUNCTION
SIF ARG > 999
	RETURNF 0
SIF ITEMNAME:ARG != "" && ITEM:ARG == 0
	RETURNF 1
RETURNF 0

;----------------------------------------------------------
;구입 처리
;----------------------------------------------------------
@BUY_CLOTH(LIST_CLOTH, INPUT_NO)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM REF LIST_CLOTH
#DIM INPUT_NO

SIF FINDELEMENT(LIST_CLOTH, INPUT_NO) == -1
	RETURN -1
SIF MONEY < ITEMPRICE:INPUT_NO
	RETURN 0
MONEY -= ITEMPRICE:INPUT_NO
ITEM:INPUT_NO = 1

RETURN 1

;----------------------------------------------------------
;리스트 표시에 대한 보정 마크를 묘화
;----------------------------------------------------------
@SHOW_CLOTH_HOSEI_MARK(VAL, 역치)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM VAL
#DIM 역치

IF 역치 < 0
	역치 *= -1
	IF 역치 < VAL
		SETCOLOR 255, 96, 96
		PRINT ▼
		RESETCOLOR
	ELSEIF VAL < 역치
		SETCOLOR 96, 96, 255
		PRINT ▲
		RESETCOLOR
	ELSE
		PRINT 　
	ENDIF
ELSE
	IF VAL < 역치
		SETCOLOR 255, 96, 96
		PRINT ▼
		RESETCOLOR
	ELSEIF 역치 < VAL
		SETCOLOR 96, 96, 255
		PRINT ▲
		RESETCOLOR
	ELSE
		PRINT 　
	ENDIF
ENDIF

RETURN

;----------------------------------------------------------
;유효 무효 버튼
;----------------------------------------------------------
@CLOTH_BUTTON(LABEL, INPUT_VAL, BUTTON = 1, COLOR = 0x808080, 괄호 = 1)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS LABEL
#DIM INPUT_VAL
#DIM BUTTON
#DIM COLOR
#DIM 괄호

IF 괄호
	LOCALS '= @"%TOSTR(INPUT_VAL), 2%"
	LOCALS '= "[" + LOCALS + "]" + LABEL
ELSE
	LOCALS '= LABEL
ENDIF

IF BUTTON == 1
	PRINTBUTTON @"%LOCALS%", INPUT_VAL
ELSEIF BUTTON == 2
	GETCOLOR
	SETCOLOR COLOR
	PRINTBUTTON @"%LOCALS%", INPUT_VAL
	SETCOLOR RESULT
ELSE
	GETCOLOR
	SETCOLOR COLOR
	PRINTPLAINFORM %LOCALS%
	SETCOLOR RESULT
ENDIF

RETURN 1

;----------------------------------------------------------
;ON/OFF 버튼
;----------------------------------------------------------
@CLOTH_TOGGLEBUTTON(LABEL, INPUT_VAL, SELECT = 0, COLOR = 0x00FF00, 괄호 = 1)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS LABEL
#DIM INPUT_VAL
#DIM SELECT
#DIM COLOR
#DIM 괄호

IF 괄호
	LOCALS '= @"%TOSTR(INPUT_VAL), 2%"
	LOCALS '= "[" + LOCALS + "]" + LABEL
ELSE
	LOCALS '= LABEL
ENDIF
IF SELECT
	GETCOLOR
	SETCOLOR COLOR
	PRINTBUTTON @"%LOCALS%", INPUT_VAL
	SETCOLOR RESULT
ELSE
	PRINTBUTTON @"%LOCALS%", INPUT_VAL
ENDIF
