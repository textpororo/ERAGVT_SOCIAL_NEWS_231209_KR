;2018/6/16 작성 (제７지부의>>252)
;-------------------------------------------------
;CFLAG:100 (SHOP화면에서의 스케쥴)을 바탕으로
;특정 스케줄의 캐릭터를 TARGET까지 포함하여 랜덤으로 한 명을 구한다
;제1인수를 CFLAG:100으로 하면 TARGET과 동일 스케줄의 캐릭터만 뽑힌다
;제2인수를 TARGET 등으로 하면 그 캐릭터만 제외된다
;CFLAG:271 = CHOOSE_ACTION_TOGETHER_F(CFLAG:100, TARGET) 이렇게 하는 게 기본?
;없으면 -1을, 있으면 캐릭터의 번호(NO)를 반환한다
; (작례)
;전투 종료 시에「%출격하는 캐릭터%는 괜찮을까, 촉수한테 잡히지 않았겠지……?」라고 말하게 한다
;휴식 시에「그럼 %휴식하는 캐릭터%도 데려오자!」라고 말하게 한다
;흑화 캐릭터에게「%출격하는 캐릭터%가 어떻게 됐는지, 알고 싶어?」라고 부추긴다
@CHOOSE_ACTION_TOGETHER_F, ARG:0, ARG:1 = -1
;ARG:0　CFLAG:100 (스케쥴)의 값
;ARG:1　조작되는 캐릭터(弾かれるキャラ)
#FUNCTION
#DIM LCOUNT, 2
LCOUNT:0 = -1
;LCOUNT:0　NO탐사
LCOUNT:1 = 0
;LCOUNT:1　검출 횟수
REPEAT CHARANUM
	LOCAL:COUNT = 0
REND
;LOCAL:0〜　NO격납용
SIF GAME_OPTION_CHECK_F(OPTION_solo)
	RETURNF -1
WHILE 1
	LCOUNT:0 = FINDCHARA(CFLAG:100, ARG:0, LCOUNT:0 + 1)
	SIF LCOUNT:0 < 0
		BREAK
	IF LCOUNT:0 != ARG:1
		LOCAL:(LCOUNT:1) = LCOUNT:0
		LCOUNT:1 += 1
	ENDIF
	SIF LCOUNT:0 + 1 > CHARANUM - 1
		BREAK
WEND
SIF LCOUNT:1 == 0
	RETURNF -1
RETURNF LOCAL:(RAND:(LCOUNT:1))



;FLAG:53 (여가 시설)의 소지하고 있는 시설 중 랜덤으로 하나 고른다
;선택되지 않으면 0을, 선택되면 그 값을 반환한다
@CHOOSE_RELAXATION_FACILITY, ARG:0 = 0b111111111111
;ARG:0　택지　생략되면 전부를 선택지로　FLAG:53을 참고로
#DIM LCOUNT
;LCOUNT:0　검출 횟수
#DIM KEY
;KEY:0　선택 가능한 선택지　선택지와 소지 시설을 논리곱한 것
LCOUNT = 0
REPEAT 15
	LOCAL:COUNT = 0
REND
;LOCAL:0〜　선택된 여가 시설
KEY:0 = ARG:0 & FLAG:53
IF (KEY:0 & 관엽식물)
	LOCAL:(LCOUNT:0) = 관엽식물
	LCOUNT:0 += 1
ENDIF
IF (KEY:0 & 마음이편안해지는풍경화)
	LOCAL:(LCOUNT:0) = 마음이편안해지는풍경화
	LCOUNT:0 += 1
ENDIF
IF (KEY:0 & 세련된오브제)
	LOCAL:(LCOUNT:0) = 세련된오브제
	LCOUNT:0 += 1
ENDIF
IF (KEY:0 & 호화로운분수)
	LOCAL:(LCOUNT:0) = 호화로운분수
	LCOUNT:0 += 1
ENDIF
IF (KEY:0 & 질좋은침대) || (KEY:0 & 최고급침대)
	IF (KEY:0 & 최고급침대)
		LOCAL:(LCOUNT:0) = 최고급침대
	ELSE
		LOCAL:(LCOUNT:0) = 질좋은침대
	ENDIF
	LCOUNT:0 += 1
ENDIF
IF (KEY:0 & 안마의자)
	LOCAL:(LCOUNT:0) = 안마의자
	LCOUNT:0 += 1
ENDIF
IF (KEY:0 & 목욕설비)
	LOCAL:(LCOUNT:0) = 목욕설비
	LCOUNT:0 += 1
ENDIF
IF (KEY:0 & 게임코너)
	LOCAL:(LCOUNT:0) = 게임코너
	LCOUNT:0 += 1
ENDIF
IF (KEY:0 & 대욕탕) || (KEY:0 & 미용온천)
	IF (KEY:0 & 대욕탕)
		LOCAL:(LCOUNT:0) = 미용온천
	ELSE
		LOCAL:(LCOUNT:0) = 대욕탕
	ENDIF
	LCOUNT:0 += 1
ENDIF
IF (KEY:0 & 뷰티살롱)
	LOCAL:(LCOUNT:0) = 뷰티살롱
	LCOUNT:0 += 1
ENDIF
SIF LCOUNT:0 == 0
	RETURN 0
RETURN LOCAL:(RAND:(LCOUNT:0))



