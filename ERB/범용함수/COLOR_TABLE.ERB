
@COLOR_TABLE
#DIM LCOUNT, 2
#DIM 축
#DIM 축색

#DIM 빨강
#DIM 초록
#DIM 파랑

#DIM 명도

#DIM SELECT_R
#DIM SELECT_G
#DIM SELECT_B

#DIM MODE

축색 = 8

PRINTFORML 　%UNICODE(0x250C)%%UNICODE(0x2500) * 32%%UNICODE(0x2510)%
DO
	FOR LCOUNT:0, 0, 32
		PRINTFORM 　%UNICODE(0x2502)%
		FOR LCOUNT:1, 0, 32
			빨강 = LIMIT((축 == 0) * 축색 + (축 == 1) * (256 - (LCOUNT:0 + 1) * 8) + (축 == 2) * (LCOUNT:1 + 1) * 8, 0, 255)
			초록 = LIMIT((축 == 1) * 축색 + (축 == 2) * (256 - (LCOUNT:0 + 1) * 8) + (축 == 0) * (LCOUNT:1 + 1) * 8, 0, 255)
			파랑 = LIMIT((축 == 2) * 축색 + (축 == 0) * (256 - (LCOUNT:0 + 1) * 8) + (축 == 1) * (LCOUNT:1 + 1) * 8, 0, 255)
			SETCOLOR 빨강, 초록, 파랑
			PRINTBUTTON "■", LCOUNT:1 * 100 + LCOUNT:0 + 10000
			RESETCOLOR
		NEXT
		PRINTFORM %UNICODE(0x2502)%　
		빨강 = (축 == 0) * LIMIT(256 - LCOUNT:0 * 8, 0, 255)
		초록 = (축 == 1) * LIMIT(256 - LCOUNT:0 * 8, 0, 255)
		파랑 = (축 == 2) * LIMIT(256 - LCOUNT:0 * 8, 0, 255)
		PRINT [
		SETCOLOR 빨강, 초록, 파랑
		PRINTBUTTON "■", LCOUNT:0 + 20000
		RESETCOLOR
		PRINT ]
		IF LCOUNT:0 == 32 - 축색 / 8
			PRINTFORM %UNICODE(0x25C4)%
		ELSE
			PRINTPLAIN  
		ENDIF
		PRINTPLAIN 　
		IF MODE == 1
			빨강 = SELECT_R * (32 - LCOUNT:0) / 32
			초록 = SELECT_G * (32 - LCOUNT:0) / 32
			파랑 = SELECT_B * (32 - LCOUNT:0) / 32
			PRINT [
			SETCOLOR 빨강, 초록, 파랑
			PRINTBUTTON "■", LCOUNT:0 + 30000
			RESETCOLOR
			PRINT ]
			PRINTFORM 명도 {100 * (32 - LCOUNT:0) / 32}％
		ENDIF
		PRINTL 
	NEXT
	PRINTFORML 　%UNICODE(0x2514)%%UNICODE(0x2500) * 32%%UNICODE(0x2518)%

	IF MODE == 0
		PRINTL 
		PRINTL 　　축의 색을 변경합니다.
		PRINTL 　　　　[0]빨강　[1]초록　[2]파랑　
		PRINTL 
	ELSEIF MODE == 1
		RESULT:1 = LIMIT(SELECT_R * 명도 / 32, 0,255)
		RESULT:2 = LIMIT(SELECT_G * 명도 / 32, 0,255)
		RESULT:3 = LIMIT(SELECT_B * 명도 / 32, 0,255)
		PRINTL 
		PRINT 　　[
		SETCOLOR RESULT:1, RESULT:2, RESULT:3
		PRINT ■
		RESETCOLOR
		PRINT ]
		PRINTFORML 　　이 색으로 하시겠습니까?（{RESULT:1}, {RESULT:2}, {RESULT:3}, 명도 선택：{100 * 명도 / 32}％）
		PRINTL 　　　　　　[0] 네　[1] 아니오
		IF RESULT == 0
			BREAK
		ENDIF
		PRINTL 
	ENDIF

	DRAWLINE
	PRINTL [99] 돌아간다

	INPUT

	IF RESULT >= 0 && RESULT < 3
		IF MODE == 0
			축 = RESULT
		ELSEIF MODE == 1
			IF RESULT == 0
				;색을 결정한 경우
				RESULT = 1
				BREAK
			ELSE
				MODE = 0
			ENDIF
		ENDIF
	ELSEIF RESULT == 99
		RESULT = -1
		RESULT:1 = -1
		RESULT:2 = -1
		RESULT:3 = -1
		BREAK
	ELSEIF RESULT >= 10000 && RESULT < 13232
		IF 축 == 0
			빨강 = LIMIT(축색, 0, 255)
			초록 = LIMIT((RESULT / 100 % 100 + 1) * 8, 0, 255)
			파랑 = LIMIT(256 - RESULT % 100 * 8, 0, 255)
		ELSEIF 축 == 1
			빨강 = LIMIT(256 - RESULT % 100 * 8, 0, 255)
			초록 = LIMIT(축색, 0, 255)
			파랑 = LIMIT((RESULT / 100 + 1) % 100 * 8, 0, 255)
		ELSE
			빨강 = LIMIT((RESULT / 100 % 100 + 1) * 8, 0, 255)
			초록 = LIMIT(256 - RESULT % 100 * 8, 0, 255)
			파랑 = LIMIT(축색, 0, 255)
		ENDIF
		MODE = 1
		;색을 변경하면 명도는 100%로 되돌린다
		명도 = 32
		SELECT_R = 빨강
		SELECT_G = 초록
		SELECT_B = 파랑
	ELSEIF RESULT >= 20000 && RESULT < 20032
		축색 = (32 - RESULT % 100) * 8
	ELSEIF RESULT >= 30000 && RESULT < 30032
		명도 = 30032 - RESULT
		RESULT:1 = 빨강 * 명도 / 32
		RESULT:2 = 초록 * 명도 / 32
		RESULT:3 = 파랑 * 명도 / 32
	ELSE
		BREAK
	ENDIF
	CLEARLINE 40
LOOP 1
RETURN RESULT, RESULT:1, RESULT:2, RESULT:3
