;*******************************************************************************************************
;髪色を返す式中間数
;*******************************************************************************************************
@PRINT_HAIRCOLORNAME, ARG
#FUNCTIONS
;変身中または、悪落ち中かつ변신능력がある場合は変身後の髪色を返す
IF CFLAG:ARG:1 == 1 || (TALENT:ARG:변신능력 == 1 && CFLAG:ARG:0 == 3)
	RETURNF GETCOLORNAME(CSTR:ARG:31)
ELSE
	RETURNF GETCOLORNAME(CSTR:ARG:30)
ENDIF
;*******************************************************************************************************

;*******************************************************************************************************
;髪型を返す式中間数
;*******************************************************************************************************
@PRINT_HAIRNAME, ARG
#FUNCTIONS
;変身中または、悪落ち中かつ변신능력がある場合は変身後の髪型を返す
IF CFLAG:ARG:1 == 1 || (TALENT:ARG:변신능력 == 1 && CFLAG:ARG:0 == 3)
	RETURNF CSTR:ARG:14
ELSE
	RETURNF CSTR:ARG:13
ENDIF

;*******************************************************************************************************
;目の色を返す式中間数
;第2引数で左右を指定
;*******************************************************************************************************
@PRINT_EYECOLORNAME, ARG, ARGS = "오른쪽"
#FUNCTIONS
;変身中または、悪落ち中かつ변신능력がある場合は変身後の目色を返す
IF CFLAG:ARG:1 == 1 || (TALENT:ARG:변신능력 == 1 && CFLAG:ARG:0 == 3)
	IF ARGS == "오른쪽" || ARGS == "RIGHT"
		RETURNF GETCOLORNAME(CSTR:ARG:34)
	ELSE
		RETURNF GETCOLORNAME(CSTR:ARG:35)
	ENDIF
ELSE
	IF ARGS == "오른쪽" || ARGS == "RIGHT"
		RETURNF GETCOLORNAME(CSTR:ARG:32)
	ELSE
		RETURNF GETCOLORNAME(CSTR:ARG:33)
	ENDIF
ENDIF

;*******************************************************************************************************

;*******************************************************************************************************
;肌色を返す式中間数
;*******************************************************************************************************
@PRINT_SKINCOLORNAME, ARG
#FUNCTIONS
;変身中または、悪落ち中かつ변신능력がある場合は変身後の肌色を返す
IF CFLAG:ARG:1 == 1 || (TALENT:ARG:변신능력 == 1 && CFLAG:ARG:0 == 3)
	RETURNF GETCOLORNAME(CSTR:ARG:37)
ELSE
	RETURNF GETCOLORNAME(CSTR:ARG:36)
ENDIF
;*******************************************************************************************************

;*******************************************************************************************************
;色名を返す
;色情報は色名の場合とRGB値の場合があるので、
;RGB値の場合はSTRから色名を探す
;*******************************************************************************************************
@GETCOLORNAME, ARGS
#FUNCTIONS

;ガバガバ判定
SIF STRCOUNT(ARGS, "/") ==4
	RETURNF GETCOLORNAMESTR(ARGS)

IF ARGS == "빨강" || ARGS == "레드"
	RETURNF "빨간색"
ELSEIF ARGS == "초록" || ARGS == "그린"
	RETURNF "초록색"
ELSEIF ARGS == "파랑" || ARGS == "블루"
	RETURNF "파란색"
ELSEIF ARGS == "노랑" || ARGS == "옐로우"
	RETURNF "노란색"
ELSEIF ARGS == "금색" || ARGS == "블론드" || ARGS == "골드"
	RETURNF "금색"
ELSEIF ARGS == "보라" || ARGS == "퍼플"
	RETURNF "보라색"
ELSEIF ARGS == "주황" || ARGS == "오렌지"
	RETURNF "주황색"
ELSEIF ARGS == "분홍" || ARGS == "핑크"
	RETURNF "분홍색"
ELSEIF ARGS == "은색" || ARGS == "실버"
	RETURNF "은색"
ELSEIF ARGS == "다색" || ARGS == "브라운" || ARGS == "브루넷"
	RETURNF "다색"
ELSEIF ARGS == "검정" || ARGS == "블랙"
	RETURNF "검은색"
ELSEIF ARGS == "살구색"
	RETURNF "살구색"
ELSEIF ARGS == "갈색"
	RETURNF "갈색"
ELSEIF ARGS == "백색"
	RETURNF "백색"
ELSE
	RETURNF ARGS
ENDIF

;*******************************************************************************************************
;引数で指定されたRGB値に最も近い色名をSTRから探して返す
;引数：R//G//B形式のRGB値
;返り値：色名
;*******************************************************************************************************
@GETCOLORNAMESTR, ARGS
#FUNCTIONS

#DIM RGBStart
#DIM NumberOfColor
#DIM SimilarColorNo
#DIM ColorDiff
#DIM NewColorDiff
#DIM ColorNameOffset

RGBStart = 20000
NumberOfColor = 1000
SimilarColorNo = RGBStart
ColorDiff = 999
ColorNameOffset = 1000

SIF ARGS == ""
	RETURNF "미정의"

;ガバガバエラー処理
SIF STRCOUNT(ARGS, "/") !=4
	RETURNF "인수가 R//G//B 형식이 아닙니다."

;RGB値が一致すれば色名を返す
FOR COUNT, RGBStart, RGBStart + NumberOfColor
	SIF STR:COUNT == ""
		CONTINUE
	SIF ARGS == STR:COUNT
		RETURNF STR:(COUNT + ColorNameOffset)
NEXT

;RGB値が一致しなければ、近い色名を探す
FOR COUNT, RGBStart, RGBStart + NumberOfColor
	SIF STR:COUNT == ""
		CONTINUE
	;RGB値の差の合計を出す
	NewColorDiff = COMPARE_COLOR(ARGS, STR:(COUNT))
	IF NewColorDiff < ColorDiff
		ColorDiff = NewColorDiff
		SimilarColorNo = COUNT
	ENDIF
NEXT

;最もRGB値が近かった色名を返す
RETURNF STR:(SimilarColorNo + ColorNameOffset)
;*******************************************************************************************************

;*******************************************************************************************************
;2つのRGB値の差の合計を返す
;*******************************************************************************************************
@COMPARE_COLOR, ARGS:0, ARGS:1
#FUNCTION

#DIMS SeparatorMajor
#DIMS RGB_A, 3
#DIMS RGB_B, 3

SeparatorMajor = //

SPLIT ARGS:0, SeparatorMajor, RGB_A
SPLIT ARGS:1, SeparatorMajor, RGB_B

RETURNF ABS(TOINT(RGB_A:0) - TOINT(RGB_B:0)) + ABS(TOINT(RGB_A:1) - TOINT(RGB_B:1)) + ABS(TOINT(RGB_A:2) - TOINT(RGB_B:2))

