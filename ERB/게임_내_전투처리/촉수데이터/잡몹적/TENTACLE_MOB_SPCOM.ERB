;--------------------------------------------------------------------------------------------
;잡몹적専用コマンド共通ヘッダー処理
;ARG:0 = コマンドのID
;ARG:1  = 直接受け渡し（추가 고문の部位）
;ARG:2  = メイン部位		なし0 Ｃ1 Ｖ2 Ａ4 Ｂ8
;ARG:3  = 방심도 상승량
;ARG:4  = 사정フラグ		なし0 질2 애널4 가슴8 입16 手32 균열64 오발128 （ビット演算）
;ARG:5  = 사정 게이지 상승량
;ARG:6  = 의상에의 피해
;ARG:7  = コマンドの強度	弱0 強1 SP2
;ARG:8  = 처녀 상실の有無	なし0 あり1 あり、지문なし2
;ARG:9  = 特殊効果		ビット演算
;	1	Ｖ삽입방어貫通
;	2	拡張責め
;	4	
;	8	
;	16	
;	32	
;	64	
;	128	
;	256	
;ARG:10 = 쾌Ｃ 강도
;ARG:11 = 쾌Ｖ 강도
;ARG:12 = 쾌Ａ 강도
;ARG:13 = 쾌Ｂ 강도
;ARG:14 = マゾによる굴복、공포影響度
;ARG:15 = 삽입에 따른 공순 영향도
;ARG:16 = 습득 영향도
;ARG:17 = 노출벽による욕정、치정、공포影響度
;ARG:18 = 強制奉仕による굴복影響度


@MOB_CREATE_COM,ARG = 0,ARG:1 = 0,ARG:2,ARG:3,ARG:4,ARG:5,ARG:6,ARG:7,ARG:8,ARG:9,ARG:10,ARG:11,ARG:12,ARG:13,ARG:14,ARG:15,ARG:16,ARG:17,ARG:18
;EX_COM	추가 고문 실행 부위（비트）
;	1：Ｃ　2：Ｖ　4：Ａ　8：Ｂ
;추가 고문
CALL SEX_COMEX_RANDOM, ARG:2, ARG:1
EX_COM = RESULT:0
SH_COM = RESULT:1

;청순파라면 한 번만 자동으로 Ｖ방어
SIF TALENT:청순파 > 0 && TCVARn:2 != 100 && ARG:8 > 0
	CALL AUTO_V_DEFENCE, TARGET

;사정フラグ
SIF ARG:4 > 0
	TFLAG:4 = ARG:4

;취득 파라미터 계산
;BASE값의 명시적 초기화
REPEAT 12
	LOCAL:COUNT = 0
REND

;방심도 상승
SIF ARG:3 > 0
	TFLAG:3 += ARG:3

;사정치
SIF ARG:5 > 0
	CALL TENTACLE_SYASEI_UP, ARG:5

;촉수 크기 설정
CALL SET_TENTACLE_SIZE, FLAG:10, FLAG:11, ENEMY_TYPE_CHECK_F("AKUOTI"), FLAG:111, ARG:0

;지문호출
TRYCALLFORM MESSAGE_MOB_{FLAG:11}_COM{ARG:0}, EX_COM, SH_COM

;의상에의 피해
SIF ARG:6 > 0
	CALL CLOTH_BATTLE_DAMAGE, ARG:6

;BASEの獲得
;ここ以降のLOCALはPALAMの상승량として扱う
;------------------------------------------------------------------------------------------------------------------
VARSET LOCAL

;처녀 상실あり
IF ARG:8 == 2
	;고통이 대량으로 들어간다
	IF TALENT:처녀 > 0 && BASE:Ｖ결계내구력 <= 0 && (TCVARn:2 != 자세：Ｖ방어 || (ARG:9 & 1) > 0)
		LOCAL:10 = 10000
		TALENT:처녀 = -1
		CFLAG:206 = 2
		TCVARn:1 = 1
	ELSE
		LOCAL:10 = 0
	ENDIF
ELSEIF ARG:8 == 1
	;고통이 대량으로 들어간다
	IF TALENT:처녀 > 0 && BASE:Ｖ결계내구력 <= 0 && (TCVARn:2 != 자세：Ｖ방어 || (ARG:9 & 1) > 0)
		LOCAL:10 = 10000
		CALL LOSTVIRGIN
	ELSE
		LOCAL:10 = 0
	ENDIF
ENDIF

;쾌Ｃ
IF ARG:10 > 0
	IF ARG:7 == 0
		LOCAL:0 += MIN(ABL:Ｃ감각 * ABL:Ｃ감각, 25) * ARG:10 * 60 + ARG:10 * 100
	ELSEIF ARG:7 == 1
		LOCAL:0 += MIN(ABL:Ｃ감각 * ABL:Ｃ감각, 25) * ARG:10 * 150 + ARG:10 * 80
	ELSE
		LOCAL:0 += MIN(ABL:Ｃ감각 * ABL:Ｃ감각, 25) * ARG:10 * 300 + ARG:10 * 200
	ENDIF
	;페니스가 있다면 효과가 크다
	SIF ISMANLY() || TALENT:후타나리 > 0
		LOCAL:0 += 500 * (ARG:7 + 1)
	;Ｃ감각が低いと공포と고통が少し入る
	IF ABL:Ｃ감각 < 3
		LOCAL:10 += ARG:7 * ARG:7 * ARG:10 * 40 / (ABL:Ｃ감각 + 1) * (1 - (BASE:Ｃ결계내구력 > 0))
		LOCAL:11 += ARG:7 * ARG:7 * ARG:10 * 40 / (ABL:Ｃ감각 + 1)
	ENDIF
ENDIF
;쾌Ｖ
IF ARG:11 > 0
	;Ｖ경험
	EXP:Ｖ경험 += 1
	IF ARG:7 == 0
		LOCAL:1 += MIN(ABL:Ｖ감각 * ABL:Ｖ감각, 25) * ARG:11 * 60 + ARG:11 * 100
	ELSEIF ARG:7 == 1
		LOCAL:1 += MIN(ABL:Ｖ감각 * ABL:Ｖ감각, 25) * ARG:11 * 150 + ARG:11 * 80
	ELSE
		LOCAL:1 += MIN(ABL:Ｖ감각 * ABL:Ｖ감각, 25) * ARG:11 * 300 + ARG:11 * 200
	ENDIF
	;Ｖ감각が低いと공포と고통が少し入る
	IF ABL:Ｖ감각 < 3
		LOCAL:10 += ARG:7 * ARG:7 * ARG:11 * 20 / (ABL:Ｃ감각 + 1) * (1 - (BASE:Ｖ결계내구력 > 0))
		LOCAL:11 += ARG:7 * ARG:7 * ARG:11 * 20 / (ABL:Ｃ감각 + 1) / (1 + (BASE:Ｖ결계내구력 > 0))
	ENDIF
	;윤활이 낮으면 공포と고통が少し入る
	IF PALAM:윤활 < 2000
		LOCAL:10 += ARG:7 * ARG:7 * ARG:11 * 20 / (ABL:Ｃ감각 + 1) * (1 - (BASE:Ｖ결계내구력 > 0))
		LOCAL:11 += ARG:7 * ARG:7 * ARG:11 * 20 / (ABL:Ｃ감각 + 1) / (1 + (BASE:Ｖ결계내구력 > 0))
	ENDIF
	;Ｖ방어中かつ貫通なしなら쾌Ｖが減少
	SIF TCVARn:2 == 자세：Ｖ방어 && (ARG:9 & 1) == 0
		LOCAL:1 /= 4
	;Ｖ挿入系コマンドの場合
	TRYCCALLFORM SEX_TYPE_MOB_{FLAG:11}_COM{ARG:0}
		IF (RESULT & 삽입) > 0
			;윤활이 낮으면 고통이 들어간다
			IF PALAM:윤활 < 1000
				LOCAL:10 += 5000 * (1 - (BASE:Ｖ결계내구력 > 0))
			ELSEIF PALAM:윤활 < 2000
				LOCAL:10 += 1000 * (1 - (BASE:Ｖ결계내구력 > 0))
			ENDIF
			;挿入できていれば追加のＶ경험
			IF BASE:Ｖ결계내구력 <= 0 && (TCVARn:2 != 자세：Ｖ방어 || (ARG:9 & 1) > 0)
				EXP:Ｖ경험 += 1
				;Ｖ拡張の場合
				IF (ARG:9 & 2) > 0
					EXP:Ｖ확장경험 += 1
					EXP:Ｖ경험 += 3
				ENDIF
			ENDIF
		ENDIF
	CATCH
	ENDCATCH
	;Ｖ拡張の場合快楽、굴복、치정、고통、공포が入る
	IF (ARG:9 & 2) > 0
		IF EXP:Ｖ확장경험 < 3
			TIMES LOCAL:1, 0.10
			LOCAL:8 += ARG:11 * 400 / (1 + (BASE:Ｖ결계내구력 > 0))
			LOCAL:9 += ARG:11 * 40 / (1 + (BASE:Ｖ결계내구력 > 0))
			LOCAL:10 += ARG:11 * 800 * (1 - (BASE:Ｖ결계내구력 > 0))
			LOCAL:11 += ARG:11 * 1600 / (1 + (BASE:Ｖ결계내구력 > 0))
		ELSEIF EXP:Ｖ확장경험 < 5
			TIMES LOCAL:1, 0.50
			LOCAL:8 += ARG:11 * 800 / (1 + (BASE:Ｖ결계내구력 > 0))
			LOCAL:9 += ARG:11 * 80 / (1 + (BASE:Ｖ결계내구력 > 0))
			LOCAL:10 += ARG:11 * 640 * (1 - (BASE:Ｖ결계내구력 > 0))
			LOCAL:11 += ARG:11 * 1280 / (1 + (BASE:Ｖ결계내구력 > 0))
		ELSEIF EXP:Ｖ확장경험 < 8
			TIMES LOCAL:1, 0.80
			LOCAL:8 += ARG:11 * 1600 / (1 + (BASE:Ｖ결계내구력 > 0))
			LOCAL:9 += ARG:11 * 160 / (1 + (BASE:Ｖ결계내구력 > 0))
			LOCAL:10 += ARG:11 * 320 * (1 - (BASE:Ｖ결계내구력 > 0))
			LOCAL:11 += ARG:11 * 640 / (1 + (BASE:Ｖ결계내구력 > 0))
		ELSE
			LOCAL:8 += ARG:11 * 4000 / (1 + (BASE:Ｖ결계내구력 > 0))
			LOCAL:9 += ARG:11 * 400 / (1 + (BASE:Ｖ결계내구력 > 0))
			LOCAL:10 += ARG:11 * 80 * (1 - (BASE:Ｖ결계내구력 > 0))
			LOCAL:11 += ARG:11 * 160 / (1 + (BASE:Ｖ결계내구력 > 0))
		ENDIF
	ENDIF
ENDIF
;쾌Ａ
IF ARG:12 > 0
	;Ａ경험
	EXP:Ａ경험 += 1
	IF ARG:7 == 0
		LOCAL:2 += MIN(ABL:Ａ감각 * ABL:Ａ감각, 25) * ARG:12 * 60 + ARG:12 * 100
	ELSEIF ARG:7 == 1
		LOCAL:2 += MIN(ABL:Ａ감각 * ABL:Ａ감각, 25) * ARG:12 * 150 + ARG:12 * 80
	ELSE
		LOCAL:2 += MIN(ABL:Ａ감각 * ABL:Ａ감각, 25) * ARG:12 * 300 + ARG:12 * 200
	ENDIF
	;Ａ감각が低いと공포と고통が少し入る
	IF ABL:Ａ감각 < 3
		LOCAL:10 += ARG:7 * ARG:7 * ARG:11 * 50 / (ABL:Ａ감각 + 1) * (1 - (BASE:Ａ결계내구력 > 0))
		LOCAL:11 += ARG:7 * ARG:7 * ARG:11 * 50 / (ABL:Ａ감각 + 1) / (1 + (BASE:Ｖ결계내구력 > 0))
	ENDIF
	;Ａ감각に굴복が入る
	IF ARG:7 == 0
		LOCAL:8 += MIN(ABL:Ａ감각 * ABL:Ａ감각, 25) * 120 + 100
		SIF ABL:Ａ감각 >= 5
			LOCAL:8 += ABL:Ａ감각 * 100 + 1400
	ELSE
		LOCAL:8 += (MIN(ABL:Ａ감각 * ABL:Ａ감각, 25) * 120 + 100) * 2
		SIF ABL:Ａ감각 >= 5
			LOCAL:8 += (ABL:Ａ감각 * 100 + 1400) * 2
	ENDIF
	;Ａ挿入系の場合
	TRYCCALLFORM SEX_TYPE_MOB_{FLAG:11}_COM{ARG:0}
		IF (RESULT & 삽입) > 0
			;挿入できていれば追加のＡ경험
			IF BASE:Ａ결계내구력 <= 0
				EXP:Ａ경험 += 1
				;Ａ拡張の場合
				IF (ARG:9 & 2) > 0
					EXP:Ａ확장경험 += 1
					EXP:Ａ경험 += 3
				ENDIF
			ENDIF
		ENDIF
	CATCH
	ENDCATCH
	;Ａ拡張の場合快楽、굴복、치정、고통、공포が入る
	IF (ARG:9 & 2) > 0
		IF EXP:Ａ확장경험 < 3
			TIMES LOCAL:2, 0.10
			LOCAL:8 += ARG:12 * 400 / (1 + (BASE:Ａ결계내구력 > 0))
			LOCAL:9 += ARG:12 * 40 / (1 + (BASE:Ａ결계내구력 > 0))
			LOCAL:10 += ARG:12 * 800 * (1 - (BASE:Ａ결계내구력 > 0))
			LOCAL:11 += ARG:12 * 1600 / (1 + (BASE:Ａ결계내구력 > 0))
		ELSEIF EXP:Ａ확장경험 < 5
			TIMES LOCAL:2, 0.50
			LOCAL:8 += ARG:12 * 800 / (1 + (BASE:Ａ결계내구력 > 0))
			LOCAL:9 += ARG:12 * 80 / (1 + (BASE:Ａ결계내구력 > 0))
			LOCAL:10 += ARG:12 * 640 * (1 - (BASE:Ａ결계내구력 > 0))
			LOCAL:11 += ARG:12 * 1280 / (1 + (BASE:Ａ결계내구력 > 0))
		ELSEIF EXP:Ａ확장경험 < 8
			TIMES LOCAL:2, 0.80
			LOCAL:8 += ARG:12 * 1600 / (1 + (BASE:Ａ결계내구력 > 0))
			LOCAL:9 += ARG:12 * 160 / (1 + (BASE:Ａ결계내구력 > 0))
			LOCAL:10 += ARG:12 * 320 * (1 - (BASE:Ａ결계내구력 > 0))
			LOCAL:11 += ARG:12 * 640 / (1 + (BASE:Ａ결계내구력 > 0))
		ELSE
			LOCAL:8 += ARG:12 * 4000 / (1 + (BASE:Ａ결계내구력 > 0))
			LOCAL:9 += ARG:12 * 400 / (1 + (BASE:Ａ결계내구력 > 0))
			LOCAL:10 += ARG:12 * 80 * (1 - (BASE:Ａ결계내구력 > 0))
			LOCAL:11 += ARG:12 * 160 / (1 + (BASE:Ａ결계내구력 > 0))
		ENDIF
	ENDIF
ENDIF
;쾌Ｂ
IF ARG:13 > 0
	IF ARG:7 == 0
		LOCAL:3 += MIN(ABL:Ｂ감각 * ABL:Ｂ감각, 25) * ARG:13 * 60 + ARG:13 * 100
	ELSEIF ARG:7 == 1
		LOCAL:3 += MIN(ABL:Ｂ감각 * ABL:Ｂ감각, 25) * ARG:13 * 150 + ARG:13 * 80
	ELSE
		LOCAL:3 += MIN(ABL:Ｂ감각 * ABL:Ｂ감각, 25) * ARG:13 * 300 + ARG:13 * 200
	ENDIF
ENDIF
;マゾによる굴복、공포影響度
IF ARG:14 > 0
	LOCAL:8 += MIN(ABL:마조끼 * ABL:마조끼, 25) * ARG:14 * 150 + ARG:14 * 100
	IF ABL:순종 < 4
		IF ABL:마조끼 < 4
			LOCAL:11 += ARG:14 * 2000
		ELSE
			LOCAL:11 += ARG:14 * 200
		ENDIF
	ELSE
		IF ABL:마조끼 < 4
			LOCAL:11 += ARG:14 * 200
		ELSE
			LOCAL:11 += ARG:14 * 20
		ENDIF
	ENDIF
	;고통쾌락경험
	EXP:고통쾌락경험 += 1
ENDIF
;삽입에 따른 공순 영향도
IF ARG:15 > 0
	LOCAL:5 += MIN(ABL:순종 * ABL:순종, 25) * ARG:15 * 15 + ARG:15 * 40
ENDIF
;습득 영향도
IF ARG:16 > 0
	LOCAL:6 += MIN(ABL:봉사정신 * ABL:봉사정신, 25) * ARG:16 * 15 / 2 + ARG:16 * 4
ENDIF
;노출벽による욕정、치정、공포影響度
IF ARG:17 > 0
	LOCAL:7 += MIN(ABL:봉사정신 * ABL:봉사정신, 25) * ARG:17 * 15 + ARG:17 * 10
	LOCAL:9 += MIN(ABL:노출벽, 5) * ARG:17 * 100 + ARG:17 * 400
	LOCAL:11 += MIN((5 - ABL:봉사정신) * (5 - ABL:봉사정신), 25) * ARG:17 * 15 + ARG:17 * 10
	;노출쾌락경험
	EXP:노출쾌락경험 += 1
ENDIF
;強制奉仕による굴복影響度
IF ARG:18 > 0
	LOCAL:8 += MIN(ABL:순종 * ABL:순종, 25) * ARG:18 * 40 + ARG:18 * 20
	;봉사정신에 따라 보너스
	IF ABL:봉사정신 < 3
	ELSEIF ABL:봉사정신 < 4
		LOCAL:5 += 500
		LOCAL:6 += 200
	ELSEIF ABL:봉사정신 < 5
		LOCAL:5 += 1000
		LOCAL:6 += 500
	ELSE
		LOCAL:5 += 2000
		LOCAL:6 += 1000
	ENDIF
	;봉사쾌락경험
	EXP:봉사쾌락경험 += 1
ENDIF

;추가 고문 실행
IF EX_COM
	CALL SEX_COMEX, ARG:2, ARG:7, EX_COM | SH_COM
	REPEAT 12
		LOCAL:COUNT += RESULT:COUNT
	REND
ENDIF

;結果を返す
RETURN LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12
