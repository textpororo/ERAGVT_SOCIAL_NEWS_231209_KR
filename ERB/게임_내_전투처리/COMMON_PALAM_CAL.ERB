;--------------------------------------------------
;パラメータの計算に関する共通関数
;ARG:0 쾌C　ARG:1 쾌V　ARG:2 쾌A　ARG:3 쾌B 
;ARG:4 윤활  ARG:5 공순  ARG:6 습득  ARG:7 욕정
;ARG:8 굴복  ARG:9 치정  ARG:10고통　ARG:11공포
;ARG:12 체력消費指定
@PALAM_CAL, ARG:0, ARG:1, ARG:2, ARG:3, ARG:4, ARG:5, ARG:6, ARG:7, ARG:8, ARG:9, ARG:10, ARG:11 , ARG:12 = 0
#DIM ECS_FLAG
#DIM ECS_NUM
#DIM LCOUNT,3

VARSET LOCAL

;現在のパラメータを表示用に逃がす
FOR LOCAL:98, 0, PALAM수
	LOCAL:99 = LOCAL:98
	SIF LOCAL:98 >= 감각수
		LOCAL:99 += PALAM시점 - 감각수
	LOCAL:(LOCAL:99+100) = 0
	LOCAL:(LOCAL:99+100) = PALAM:(LOCAL:99)
NEXT

;감각LV6以上補正
FOR LOCAL:0, 0, 감각수
	CALL PALAM_OVERFEEL, TARGET, LOCAL:0
	ARG:(LOCAL:0) = ARG:(LOCAL:0) * RESULT / 100
NEXT

VARSET ECS_FLAG
FOR LOCAL:0, 0, 감각수
	IF ARG:(LOCAL:0) > 0
		;体勢に関する補正
		CALL PALAM_HOSEI_POSE, ARG:(LOCAL:0)
		ARG:(LOCAL:0) = RESULT

		;성내성に関する補正
		CALL PALAM_HOSEI_SEITAISEI, ARG:(LOCAL:0)
		ARG:(LOCAL:0) = RESULT

		;素質に関する補正
		CALL PALAM_HOSEI_TALENT, LOCAL:0, ARG:(LOCAL:0)
		ARG:(LOCAL:0) = RESULT

		;성격に関する補正
		CALL PALAM_HOSEI_SEIKAKU, LOCAL:0, ARG:(LOCAL:0)
		ARG:(LOCAL:0) = RESULT

		;촉수に関する補正
		CALL PALAM_HOSEI_TENTACLE, LOCAL:0, ARG:(LOCAL:0)
		ARG:(LOCAL:0) = RESULT

		;ランダム性付与
		CALL PALAM_HOSEI_RANDOM, ARG:(LOCAL:0)
		ARG:(LOCAL:0) = RESULT
		
		;下限値の설정
		SIF ARG:(LOCAL:0) <= 0
			ARG:(LOCAL:0) = 1
	ENDIF
	;PALAM補正をARGに加算する
	ARG:(LOCAL:0) += COMMON_PALAM:(LOCAL:0)

	;絶頂抑制チェックのための計算
	SIF PALAM:(LOCAL:0) + ARG:(LOCAL:0) > 10000
		LOCAL:1 += PALAM:(LOCAL:0) + ARG:(LOCAL:0) * (ABL:(LOCAL:0) + 10) / 15 * (TALENT:(LOCAL:0 + 153) + 5) / 5

NEXT

;絶頂の可否の判定
IF LOCAL:1 > 0
	ECS_FLAG = LOCAL:1 * (ABL:욕망 + 5) / 10
	ECS_FLAG = ECS_FLAG * (ABL:촉수중독 + 5) / 10
	SIF FLAG:700
		ECS_FLAG = ECS_FLAG * (ABL:정액중독 * (FLAG:15 / FLAG:14) + 10) / 15
	ECS_FLAG = ECS_FLAG * (PALAMLV_F(PALAM:공순) + 25) / 30
	ECS_FLAG = ECS_FLAG * (PALAMLV_F(PALAM:욕정) + 20) / 25
	ECS_FLAG = ECS_FLAG * (PALAMLV_F(PALAM:굴복) + 25) / 30
	ECS_FLAG /= SQRT(LIMIT(BASE:성내성 * 4, 1, 250) * LIMIT(BASE:기력 / 4, 1, 2500)) / 2 + 10
	ECS_FLAG = LIMIT(ECS_FLAG, 32, 240)
	;견딘다選択中なら絶頂しにくくなる
	SIF TCVARn:2 == 자세：견딘다
		TIMES ECS_FLAG, 0.65
	;가만히있는다選択中なら絶頂しやすくなる
	SIF TCVARn:2 == 자세：가만히있는다
		TIMES ECS_FLAG, 1.25
	;사정 게이지が限界なら絶頂しやすくなる
	IF BASE:사정 >= MAXBASE:사정 * 2
		TIMES ECS_FLAG, 1.5
	ELSEIF BASE:사정 >= MAXBASE:사정
		TIMES ECS_FLAG, 1.25
	ENDIF
	;받아들인다、または기절中なら抑制できない
	SIF TCVARn:2 == 자세：받아들인다 || (TCVARn:12 & 기절)
		ECS_FLAG = 256

	SETCOLOR 96, 96, 96
	SIF FLAG:999 == 1
		PRINTFORML /* debug */ 絶頂確率{ECS_FLAG, 3} / 256
	RESETCOLOR

	LOCAL:2 = RAND:256
	;絶頂禁止中
	IF TCVARn:40 > 0
		ARG:(LOCAL:0 == 욕정 - PALAM시점 + 감각수) = ARG:(LOCAL:0 == 욕정 - PALAM시점 + 감각수) * 3 / 2
		ECS_FLAG += 1000
	ELSE
		;絶頂可能
		IF ECS_FLAG > LOCAL:2
			ECS_FLAG = 0
		;絶頂抑制
		ELSE
			ARG:(LOCAL:0 == 욕정 - PALAM시점 + 감각수) = ARG:(LOCAL:0 == 욕정 - PALAM시점 + 감각수) * 6 / 5
		ENDIF
	ENDIF
ELSE
	ECS_FLAG = -1
ENDIF

;絶頂懇願
IF ECS_FLAG > 1000
	ARG:(LOCAL:0 == 공순 - PALAM시점 + 감각수) *= 2
	ARG:(LOCAL:0 == 굴복 - PALAM시점 + 감각수) *= 2
	LOCAL:2 = LOCAL:1 / 800
	LOCAL:2 = LOCAL:2 * (ABL:순종 * 3 + ABL:욕망 * 4 + ABL:촉수중독 * 3 + 10) / 10
	LOCAL:2 = LOCAL:2 * (PALAMLV_F(PALAM:공순) + 25) / 30
	LOCAL:2 = LOCAL:2 * (PALAMLV_F(PALAM:욕정) + 20) / 25
	LOCAL:2 = LOCAL:2 * (PALAMLV_F(PALAM:굴복) + 25) / 30
	LOCAL:2 = LOCAL:2 * (TALENT:촉수의포로 + 1)
	LOCAL:2 = LIMIT(LOCAL:2, 32, 240)

	LOCAL:1 = RAND:256
	SETCOLOR 96, 96, 96
	SIF FLAG:999 == 1
		PRINTFORML /* debug */ 懇願確率{LOCAL:2, 3} / 256
	RESETCOLOR

	SIF LOCAL:2 > LOCAL:1
		ECS_FLAG += 1000
ENDIF

;以降、
;ECS_FLAG > 2000で絶頂懇願
;ECS_FLAG > 1000で絶頂禁止
;ECS_FLAG > 0で絶頂抑制
;ECS_FLAG <= 0で絶頂可能

;戦闘中でなければ堪えない
SIF ECS_FLAG > 0 && ECS_FLAG < 1000 && FLAG:700 == 0
	ECS_FLAG = 0

;PALAM補正をARGに加算する
;FOR LOCAL, 0, 13
;	ARG:LOCAL += COMMON_PALAM:LOCAL
;	COMMON_PALAM:LOCAL = 0
;NEXT

ECS_NUM = 0
;快部位の絶頂チェック
FOR LOCAL:0, 0, 감각수
	IF ARG:(LOCAL:0) > 0
		LOCAL:3 = ARG:(LOCAL:0)
		;部位結界チェック
		;戦闘中かつ拘束時でなければ結界は動作しない
		IF BASE:(LOCAL:0 + 30) > 0 && FLAG:700 && TCVARn:0 == 0 && TFLAG:10 != 1006

			;デバッグ表示
			STRLENFORM {MAXBASE:(LOCAL:0 + 30)}
			SETCOLOR 96, 96, 96
			SIF FLAG:999
				PRINTFORML /* debug */ %BASENAME:(LOCAL:0 + 30)%：{BASE:(LOCAL:0 + 30), RESULT} - {LOCAL:3, RESULT}　= {BASE:(LOCAL:0 + 30) - LOCAL:3, RESULT}
			RESETCOLOR
			BASE:(LOCAL:0 + 30) -= LOCAL:3
			LOCAL:3 = 0
			;戦闘中でなければ表示しない
			SIF FLAG:700
				CALL MESSAGE_SHIELD_STATE, LOCAL:0
		ENDIF

		CALL PALAM_ECSTASY, LOCAL:0, LOCAL:3, LOCAL:0, ECS_FLAG
	ENDIF

	;絶頂による굴복の増加
	ECS_NUM += NOWEX:(LOCAL:0)
NEXT

;촉수サイズ表示、拡張度チェック
IF TFLAG:1 == 0
	SIF FLAG:700 > 0 && CONFIG_CHECK_MANIAC_F(16) == 1 && CFLAG:34 > 0
		CALL PRINT_TENTACLE_SIZE, ARG:0, ARG:1, ARG:2, ARG:3
	;拡張判定
	;改行判定用フラグ
	LOCAL:1 = 0
	SIF TENTACLE_SIZE:1:쾌V > 0 && (INSERT & Ｖ)
		CALL GET_V_GAPING_EXP(GAPING_SIZE_TO_POINT("Ｖ"))
	SIF TENTACLE_SIZE:1:쾌A > 0 && (INSERT & Ａ)
		CALL GET_A_GAPING_EXP(GAPING_SIZE_TO_POINT("Ａ"))
	IF TENTACLE_SIZE:1:쾌V > 0 && (INSERT & Ｖ)
		CALL V_GAPING(GAPING_SIZE_TO_POINT("Ｖ"))
		IF RESULT > 0 && CONFIG_CHECK_MANIAC_F(16) == 1
			PRINTFORML 　膣径：＋{RESULT / 10}.{RESULT % 10} cm
			LOCAL:1 = 1
		ENDIF
	ENDIF
	IF TENTACLE_SIZE:1:쾌A > 0 && (INSERT & Ａ)
		CALL A_GAPING(GAPING_SIZE_TO_POINT("Ａ"))
		IF RESULT > 0 && CONFIG_CHECK_MANIAC_F(16) == 1
			SIF LOCAL:1 == 0
				PRINTL
			PRINTFORML 　肛径：＋{RESULT / 10}.{RESULT % 10} cm
			LOCAL:1 = 1
		ENDIF
	ENDIF
ENDIF
FOR LOCAL:0, 0, 감각수
	COMMON_PALAM:(LOCAL:0) = 0
NEXT

SIF LOCAL:1 == 1
	PRINTL

;動画撮影フラグの処理
IF FLAG:71 > 0 || GETBATTLESITUATION("상시 촬영")
	;질내사정されたところを見られた
	SIF (STAIN:3 & 4)
		SETBIT TFLAG:21,4
	;애널에 사정されたところを見られた
	SIF (STAIN:4 & 4)
		SETBIT TFLAG:21,5
	;精液ぶっかけされたところを見られた
	SIF (STAIN:0 & 4) || (STAIN:1 & 4) || (STAIN:2 & 4)
		SETBIT TFLAG:21,6
	;絶頂したところを見られた
	SIF NOWEX:0 + NOWEX:1 + NOWEX:2 + NOWEX:3
		SETBIT TFLAG:21,8
ENDIF

;굴복の増加
SIF ECS_NUM > 0
	ARG:(굴복 - PALAM시점 + 감각수) += MIN(ECS_NUM * 5000, 20000)
;공순の増加
SIF ABL:촉수중독 > 0
	ARG:(공순 - PALAM시점 + 감각수) += MIN(ECS_NUM * 1500 * ABL:촉수중독, 30000)
SIF TALENT:촉수의포로 > 0
	ARG:(공순 - PALAM시점 + 감각수) += MIN(ECS_NUM * 1500, 6000)

FOR LOCAL:0, 감각수, PALAM수
	LOCAL:1 = LOCAL:0 + PALAM시점 - 감각수
	;윤활,욕정,치정は後で個別に計算
	SIF LOCAL:0 == 윤활 - PALAM시점 + 감각수 || LOCAL:0 == 욕정 - PALAM시점 + 감각수 || LOCAL:0 == 치정 - PALAM시점 + 감각수
		CONTINUE

	IF ARG:(LOCAL:0) > 0
		;体勢に関する補正
		CALL PALAM_HOSEI_POSE, ARG:(LOCAL:0)
		ARG:(LOCAL:0) = RESULT

		;성내성に関する補正
		CALL PALAM_HOSEI_SEITAISEI, ARG:(LOCAL:0)
		ARG:(LOCAL:0) = RESULT

		;素質に関する補正
		CALL PALAM_HOSEI_TALENT, LOCAL:1, ARG:(LOCAL:0)
		ARG:(LOCAL:0) = RESULT

		;성격に関する補正
		CALL PALAM_HOSEI_SEIKAKU, LOCAL:1, ARG:(LOCAL:0)
		ARG:(LOCAL:0) = RESULT

		;촉수に関する補正
		CALL PALAM_HOSEI_TENTACLE, LOCAL:1, ARG:(LOCAL:0)
		ARG:(LOCAL:0) = RESULT

		;촉수중독に関する補正
		IF LOCAL:1 == 공순 || LOCAL:1 == 욕정 || LOCAL:1 == 굴복
			CALL PALAM_HOSEI_POISONING_TENTACLE, ARG:(LOCAL:0)
			ARG:(LOCAL:0) = RESULT
		ENDIF

		;ランダム性付与
		CALL PALAM_HOSEI_RANDOM, ARG:(LOCAL:0)
		ARG:(LOCAL:0) = RESULT
		
		;下限値の설정
		SIF ARG:(LOCAL:0) <= 0
			ARG:(LOCAL:0) = 1
		
		;取得パラメータ 상승チェック
		;CALL PALAM_LIMIT, LOCAL:1, ARG:(LOCAL:0)
	ENDIF
NEXT

FOR LOCAL:0, 감각수, PALAM수
	;PALAM補正をARGに加算する
	ARG:(LOCAL:0) += COMMON_PALAM:(LOCAL:0)
	COMMON_PALAM:(LOCAL:0) = 0
NEXT

;初めてのV拡張の場合、이상경험이 入る
IF CFLAG:202 == 0 && TFLAG:20 == 1009
	CFLAG:202 = 1
	EXP:이상경험 += 1
	PRINTFORML %EXPNAME:50%＋1
ENDIF
;初めてのA拡張の場合、이상경험이 入る
IF CFLAG:203 == 0 && TFLAG:20 == 1010
	CFLAG:203 = 1
	EXP:이상경험 += 1
	PRINTFORML %EXPNAME:50%＋1
ENDIF

;촉수の사정チェック
IF FLAG:700 == 1
	CALL TENTACLE_SYASEI_CHECK
	SIF (TCVARn:12 & 배란) && (TFLAG:4 & 질)
		RESULT:1 *= 4
	ARG:4 += RESULT:0
	ARG:8 += RESULT:1
	ARG:5 += RESULT:2
	ARG:7 += RESULT:3
ENDIF

;----------------------------------------------------------
;キャラの사정チェック
IF (TALENT:후타나리 > 0 || (ISMALE() && TALENT:미성숙 != 1))
	IF ARG:0 && CONFIG_CHECK_SCREEN_F(1) > 0
		FONTBOLD
		PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%의 사정値＋{ARG:0 + 500 * (NOWEX:C절정 + 1)}
		FONTREGULAR
	ENDIF
	SIF ARG:0 > 0
		BASE:사정 += ARG:0 + 500 * (NOWEX:C절정 + 1)
	IF BASE:사정 >= MAXBASE:사정 * 2 && NOWEX:C절정 > 0
		BASE:사정 = 0
		ARG:7 += 10000
		ARG:8 += 8000
		ARG:9 += 8000
		ARG:12 += 600
		;지문；キャラが대량사정した
		CALL MESSAGE_SEX_CHARA_SYASEI_HI
		;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 대량사정を見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_CHARA_SYASEI_HI
		;現在の性別か元の性別のどちらかが남자でなく、その状態での사정が初であれば이상경험獲得
		IF CFLAG:200 == 0 && (ISFEMALE() || CHARATALENT_F(TARGET,0,"남자") == 0)
			CFLAG:200 = 1
			EXP:이상경험 += 1
			PRINTFORML %EXPNAME:50%＋1
		ENDIF
		EXP:사정경험 += 2
		PRINTFORML %EXPNAME:53%＋2
		PRINTL 
		NOWEX:C절정 += 2
	ELSE
		IF BASE:사정 >= MAXBASE:사정 && NOWEX:C절정 > 0
			BASE:사정 = MAX(BASE:사정 - MAXBASE:사정, 0)
			ARG:7 += 5000
			ARG:8 += 4000
			ARG:9 += 4000
			ARG:12 += 300
			;지문: キャラが사정した
			CALL MESSAGE_SEX_CHARA_SYASEI
			;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 사정を見た
			SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
				CALL MESSAGE_OTHER_SEX_CHARA_SYASEI
			;現在の性別か元の性別のどちらかが남자でなく、その状態での사정が初であれば이상경험獲得
			IF CFLAG:200 == 0 && (ISFEMALE() || CHARATALENT_F(TARGET,0,"남자") == 0)
				CFLAG:200 = 1
				EXP:이상경험 += 1
				PRINTFORML %EXPNAME:50%＋1
			ENDIF
			EXP:사정경험 += 1
			PRINTFORML %EXPNAME:53%＋1
			PRINTL 
			NOWEX:C절정 += 1
		ELSEIF ARG:0 && CONFIG_CHECK_SCREEN_F(1) > 0
			PRINTL 
		ENDIF
	ENDIF
ENDIF

;----------------------------------------------------------
;キャラの모유チェック
IF TALENT:모유체질 == 1
	IF ARG:3 && CONFIG_CHECK_SCREEN_F(1) > 0
		FONTBOLD
		PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%의 모유値＋{ARG:3 / 4 + 500 * (NOWEX:B절정 + 1)}
		FONTREGULAR
	ENDIF
	SIF ARG:3 > 0
		BASE:모유 += (ARG:3 / 4 + 500 * (NOWEX:B절정 + 1))
	IF BASE:모유 >= MAXBASE:모유 * 2 && NOWEX:B절정 > 0
		BASE:모유 = MAXBASE:모유 - 1
		ARG:5 += 2000
		ARG:7 += 2000
		ARG:9 += 2000
		;지문；キャラが大量모유した
		CALL MESSAGE_SEX_CHARA_HUNNYU_HI
		;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 大量모유を見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_CHARA_HUNNYU_HI
		IF CFLAG:201 == 0
			CFLAG:201 = 1
			EXP:이상경험 += 1
			PRINTFORML %EXPNAME:50%＋1
		ENDIF
		EXP:모유경험 += 2
		PRINTFORML %EXPNAME:54%＋2
		PRINTL 
	ELSE
		IF BASE:모유 >= MAXBASE:모유 && NOWEX:B절정 > 0
			BASE:모유 = BASE:모유 - MAXBASE:모유
			ARG:5 += 1000
			ARG:7 += 1000
			ARG:9 += 1000
			;지문: キャラが모유した
			CALL MESSAGE_SEX_CHARA_HUNNYU
			;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 모유を見た
			SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
				CALL MESSAGE_OTHER_SEX_CHARA_HUNNYU
			IF CFLAG:201 == 0
				CFLAG:201 = 1
				EXP:이상경험 += 1
				PRINTFORML %EXPNAME:50%＋1
			ENDIF
			EXP:모유경험 += 1
			PRINTFORML %EXPNAME:54%＋1
			PRINTL 
		ELSEIF ARG:3 && CONFIG_CHECK_SCREEN_F(1) > 0
			PRINTL 
		ENDIF
	ENDIF
ENDIF
PRINTL 


;----------------------------------------------------------
;습득の取得
SIF TCVARn:2 == 자세：봉사한다
	ARG:6 += 150 + 15 * (ABL:순종 + ABL:봉사정신 * 2)

;----------------------------------------------------------
;치정の取得

;衣服の損傷LVによる치정の獲得
CALL PALAM_TIJOU_CLOTH_DAMAGE
ARG:9 += RESULT

;快C,快V,快A,快B 상승値,絶頂の状態で計算する
CALL PALAM_TIJOU, ARG:0, ARG:1, ARG:2, ARG:3
ARG:9 += RESULT
IF ARG:9 > 0
	;体勢に関する補正
	CALL PALAM_HOSEI_POSE, ARG:9
	ARG:9 = RESULT

	;観衆の有無による補正
	IF FLAG:71 || GETBATTLESITUATION("상시 촬영")
		ARG:9 = ARG:9 * (360 + FLAG:71 * 40 + FLAG:70 * 20) / 100
	ELSEIF FLAG:70
		ARG:9 = ARG:9 * (180 + FLAG:70 * 20) / 100
	ENDIF

	;성내성に関する補正
	CALL PALAM_HOSEI_SEITAISEI, ARG:9
	ARG:9 = RESULT

	;素質に関する補正
	CALL PALAM_HOSEI_TALENT, 15, ARG:9
	ARG:9 = RESULT

	;성격に関する補正
	CALL PALAM_HOSEI_SEIKAKU, 15, ARG:9
	ARG:9 = RESULT

	;촉수に関する補正
	CALL PALAM_HOSEI_TENTACLE, 15, ARG:9
	ARG:9 = RESULT
	
	;ランダム性付与
	CALL PALAM_HOSEI_RANDOM, ARG:9
	ARG:9 = RESULT
	
	;下限値の설정
	SIF ARG:9 < 1
		ARG:9 = 1
	
	;取得パラメータの上限チェック
	;CALL PALAM_LIMIT, 15, ARG:9
ENDIF

;----------------------------------------------------------
;욕정の主計算
;快C,快V,快A,快B,치정 상승値,고통 상승値(마조끼あり),絶頂の状態で計算する
CALL PALAM_YOKUJOU, ARG:0, ARG:1, ARG:2, ARG:3, ARG:9, ARG:10
ARG:7 += RESULT
IF ARG:7 > 0
	;体勢に関する補正
	CALL PALAM_HOSEI_POSE, ARG:7
	ARG:7 = RESULT

	;성내성に関する補正
	CALL PALAM_HOSEI_SEITAISEI, ARG:7
	ARG:7 = RESULT

	;素質に関する補正
	CALL PALAM_HOSEI_TALENT, 13, ARG:7
	ARG:7 = RESULT

	;성격に関する補正
	CALL PALAM_HOSEI_SEIKAKU, 13, ARG:7
	ARG:7 = RESULT

	;촉수に関する補正
	CALL PALAM_HOSEI_TENTACLE, 13, ARG:7
	ARG:7 = RESULT
	
	;ランダム性付与
	CALL PALAM_HOSEI_RANDOM, ARG:7
	ARG:7 = RESULT
	
	;下限値の설정
	SIF ARG:7 < 1
		ARG:7 = 1
	
	;取得パラメータの上限チェック
	;CALL PALAM_LIMIT, 13, ARG:7
ENDIF

;----------------------------------------------------------
;윤활の主計算
;욕정 상승値で計算する
CALL PALAM_JUNKATU, ARG:7
ARG:4 += RESULT
IF ARG:4 > 0
	;体勢に関する補正
	CALL PALAM_HOSEI_POSE, ARG:4
	ARG:4 = RESULT

	;素質に関する補正
	CALL PALAM_HOSEI_TALENT, 10, ARG:4
	ARG:4 = RESULT

	;성격に関する補正
	CALL PALAM_HOSEI_SEIKAKU, 10, ARG:4
	ARG:4 = RESULT

	;촉수に関する補正
	CALL PALAM_HOSEI_TENTACLE, 10, ARG:4
	ARG:4 = RESULT
	
	;ランダム性付与
	CALL PALAM_HOSEI_RANDOM, ARG:4
	ARG:4 = RESULT
	
	;下限値の설정
	SIF ARG:4 < 1
		ARG:4 = 1
	
	;取得パラメータの上限チェック
	;CALL PALAM_LIMIT, 10, ARG:4
ENDIF

;----------------------------------------------------------
;공순の主計算
;快C,快V,快A,快B 상승値,絶頂の状態で計算する
CALL PALAM_KYOUJUN, ARG:0, ARG:1, ARG:2, ARG:3
ARG:5 += RESULT
IF ARG:5 > 0
	;体勢に関する補正
	CALL PALAM_HOSEI_POSE, ARG:5
	ARG:5 = RESULT

	;성내성に関する補正
	CALL PALAM_HOSEI_SEITAISEI, ARG:5
	ARG:5 = RESULT

	;素質に関する補正
	CALL PALAM_HOSEI_TALENT, 11, ARG:5
	ARG:5 = RESULT

	;성격に関する補正
	CALL PALAM_HOSEI_SEIKAKU, 11, ARG:5
	ARG:5 = RESULT

	;촉수に関する補正
	CALL PALAM_HOSEI_TENTACLE, 11, ARG:5
	ARG:5 = RESULT
	
	;ランダム性付与
	CALL PALAM_HOSEI_RANDOM, ARG:5
	ARG:5 = RESULT
	
	;下限値の설정
	SIF ARG:5 < 1
		ARG:5 = 1
	
	;取得パラメータの上限チェック
	;CALL PALAM_LIMIT, 11, ARG:5
ENDIF

;----------------------------------------------------------
;기절中は굴복、치정、고통、공포 상승が抑えられる
IF (TCVARn:12 & 기절)
	TIMES ARG:8, 0.25
	TIMES ARG:9, 0.25
	TIMES ARG:10, 0.25
	TIMES ARG:11, 0.25
;황홀の場合は공순、습득、욕정、굴복が上昇しやすい
ELSEIF (TCVARn:12 & 황홀)
	TIMES ARG:5, 1.25
	TIMES ARG:6, 1.25
	TIMES ARG:7, 1.25
	TIMES ARG:8, 1.25
ENDIF

;絶頂回数と珠の取得
LOCAL = 0
REPEAT 감각수
	SIF NOWEX:COUNT > 0
		LOCAL += 1
REND
EXP:절정경험 += NOWEX:C절정 * LOCAL
JUEL:쾌C += 1000 * NOWEX:C절정 * LOCAL
EXP:절정경험 += NOWEX:V절정 * LOCAL
JUEL:쾌V += 1000 * NOWEX:V절정 * LOCAL
EXP:절정경험 += NOWEX:A절정 * LOCAL
JUEL:쾌A += 1000 * NOWEX:A절정 * LOCAL
EXP:절정경험 += NOWEX:B절정 * LOCAL
JUEL:쾌B += 1000 * NOWEX:B절정 * LOCAL

;絶頂回数に応じて방심도を上昇
TFLAG:3 += 50 * LOCAL

;経験の加算
;노출쾌락경험
SIF ARG:9 > 2000 && ARG:7 > 2000
	EXP:노출쾌락경험 += 1

;봉사쾌락경험
SIF ARG:7 > 2000 && (TCVARn:2 == 자세：봉사한다 || SELECTCOM >= 100 && SELECTCOM < 200)
	EXP:봉사쾌락경험 += 1

;고통쾌락경험
SIF ARG:10 > 2000 && ARG:7 > 2000
	EXP:고통쾌락경험 += 1

;絶頂の表示
LOCAL = 0

;多重絶頂＆単絶頂
IF NOWEX:C절정 > 0
	IF NOWEX:V절정 > 0 && NOWEX:A절정 > 0 && NOWEX:B절정 > 0
		;지문: 四重絶頂
		CALL MESSAGE_SEX_MULTIECSTASY_CVAB
		;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 四重絶頂を見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MULTIECSTASY_CVAB
	ELSEIF NOWEX:V절정 > 0 && NOWEX:A절정 > 0
		;지문: CVA三重絶頂
		CALL MESSAGE_SEX_MULTIECSTASY_CVA
		;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 CVA三重絶頂を見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MULTIECSTASY_CVA
	ELSEIF NOWEX:V절정 > 0 && NOWEX:B절정 > 0
		;지문: CVB三重絶頂
		CALL MESSAGE_SEX_MULTIECSTASY_CVB
		;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 CVB三重絶頂を見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MULTIECSTASY_CVB
	ELSEIF NOWEX:A절정 > 0 && NOWEX:B절정 > 0
		;지문: CAB三重絶頂
		CALL MESSAGE_SEX_MULTIECSTASY_CAB
		;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 CAB三重絶頂を見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MULTIECSTASY_CAB
	ELSEIF NOWEX:V절정 > 0
		;지문: CV二重絶頂
		CALL MESSAGE_SEX_MULTIECSTASY_CV
		;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 CV二重絶頂を見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MULTIECSTASY_CV
	ELSEIF NOWEX:A절정 > 0
		;지문: CA二重絶頂
		CALL MESSAGE_SEX_MULTIECSTASY_CA
		;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 CA二重絶頂を見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MULTIECSTASY_CA
	ELSEIF NOWEX:B절정 > 0
		;지문: CB二重絶頂
		CALL MESSAGE_SEX_MULTIECSTASY_CB
		;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 CB二重絶頂を見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MULTIECSTASY_CB
	ELSE
		IF NOWEX:C절정 == 1
			;지문: C絶頂
			CALL MESSAGE_SEX_ECSTASY_C
			;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 C絶頂を見た
			SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
				CALL MESSAGE_OTHER_SEX_ECSTASY_C
		ELSEIF NOWEX:C절정 > 1
			;지문: C強絶頂
			CALL MESSAGE_SEX_ECSTASY_C_HI, NOWEX:C절정
			;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 C強絶頂を見た
			SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
				CALL MESSAGE_OTHER_SEX_ECSTASY_C_HI
		ENDIF
	ENDIF
ELSEIF NOWEX:V절정 > 0
	IF NOWEX:A절정 > 0 && NOWEX:B절정 > 0
		;지문: VAB三重絶頂
		CALL MESSAGE_SEX_MULTIECSTASY_VAB
		;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 VAB三重絶頂を見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MULTIECSTASY_VAB
	ELSEIF NOWEX:A절정 > 0
		;지문: VA二重絶頂
		CALL MESSAGE_SEX_MULTIECSTASY_VA
		;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 VA二重絶頂を見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MULTIECSTASY_VA
	ELSEIF NOWEX:B절정 > 0
		;지문: VB二重絶頂
		CALL MESSAGE_SEX_MULTIECSTASY_VB
		;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 VB二重絶頂を見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MULTIECSTASY_VB
	ELSE
		IF NOWEX:V절정 == 1
			;지문: V絶頂
			CALL MESSAGE_SEX_ECSTASY_V
			;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 V絶頂を見た
			SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
				CALL MESSAGE_OTHER_SEX_ECSTASY_V
		ELSEIF NOWEX:V절정 > 1
			;지문: V強絶頂
			CALL MESSAGE_SEX_ECSTASY_V_HI, NOWEX:V절정
			;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 V強絶頂を見た
			SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
				CALL MESSAGE_OTHER_SEX_ECSTASY_V_HI
		ENDIF
	ENDIF
ELSEIF NOWEX:A절정 > 0
	IF NOWEX:B절정 > 0
		;지문: AB二重絶頂
		CALL MESSAGE_SEX_MULTIECSTASY_AB
		;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 AB二重絶頂を見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MULTIECSTASY_AB
	ELSE
		IF NOWEX:A절정 == 1
			;지문: A絶頂
			CALL MESSAGE_SEX_ECSTASY_A
			;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 A絶頂を見た
			SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
				CALL MESSAGE_OTHER_SEX_ECSTASY_A
		ELSEIF NOWEX:A절정 > 1
			;지문: A強絶頂
			CALL MESSAGE_SEX_ECSTASY_A_HI, NOWEX:A절정
			;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 A強絶頂を見た
			SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
				CALL MESSAGE_OTHER_SEX_ECSTASY_A_HI
		ENDIF
	ENDIF
ELSEIF NOWEX:B절정 > 0
	IF NOWEX:B절정 == 1
		;지문: B絶頂
		CALL MESSAGE_SEX_ECSTASY_B
		;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 B絶頂を見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_ECSTASY_B
	ELSEIF NOWEX:B절정 > 1
		;지문: B強絶頂
		CALL MESSAGE_SEX_ECSTASY_B_HI, NOWEX:B절정
		;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 B強絶頂を見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_ECSTASY_B_HI
	ENDIF
ENDIF

;絶頂禁止
IF ECS_FLAG > 1000
	CALL MESSAGE_SEX_ECSTASY_PROHIBITION

	;絶頂懇願
	SIF ECS_FLAG > 2000
		CALL MESSAGE_SEX_ECSTASY_ENTREATY

;絶頂抑制
ELSEIF ECS_FLAG > 0 && FLAG:700
	CALL MESSAGE_SEX_ECSTASY_INHIBITION, ECS_FLAG
ENDIF

;絶頂による状態異常
SIF ECS_NUM && FLAG:700 > 0
	CALL STATE_CHANGE_EX, ECS_NUM

;電流による状態異常
SIF TFLAG:20 == 1004
	CALL STATE_CHANGE_DENGEKI

;媚薬効果のある行動を受けた場合
SIF TFLAG:20 == 6 || TFLAG:20 == 1000 || TFLAG:20 == 1001
	CALL STATE_CHANGE_HATUJOU, 100

;強制기절の行動
IF (TFLAG:20 == 1012 || TFLAG:20 == 1015) && (TCVARn:12 & 기절) == 0
	CALL STATE_CHANGE_KIZETU, 100
;特定コマンドを受けると目が覚める
ELSEIF (TFLAG:20 == 8 || TFLAG:20 == 9 || TFLAG:20 == 12 || TFLAG:20 == 1009 || TFLAG:20 == 1010 || TFLAG:20 == 1013) && (TCVARn:12 & 기절) && (BASE:체력 > 0 || BASE:기력 > 0 || BASE:성내성 > 0)
	TCVARn:101 = -999
ENDIF

;성격 보정
IF TALENT:소심함 > 0
	TIMES ARG:9, 0.90
	TIMES ARG:10, 1.10
ENDIF
IF TALENT:자존심높음 > 0 && MARK:굴복각인 >= 3
	TIMES ARG:8, 1.20
ELSEIF TALENT:자존심높음 > 0
	TIMES ARG:8, 0.90
ENDIF
SIF TALENT:보수적 > 0
	TIMES ARG:6, 0.90
SIF TALENT:호기심 > 0
	TIMES ARG:6, 1.10
IF TALENT:쾌감에솔직 > 0
	TIMES ARG:5, 1.10
	TIMES ARG:10, 0.90
ENDIF
IF TALENT:쾌감을부정 > 0
	TIMES ARG:7, 0.90
	TIMES ARG:8, 1.10
ENDIF
IF TALENT:전투거부 > 0
	TIMES ARG:8, 0.90
	TIMES ARG:11, 1.10
ENDIF
IF TALENT:싸움꾼 > 0
	TIMES ARG:7, 1.10
	TIMES ARG:11, 0.90
ENDIF
IF TALENT:수줍음 > 0
	TIMES ARG:5, 0.90
	TIMES ARG:9, 1.10
ENDIF
IF TALENT:배짱 > 0
	TIMES ARG:7, 1.10
	TIMES ARG:9, 0.90
ENDIF
SIF TALENT:모성적 > 0
	TIMES ARG:5, 1.10
SIF TALENT:소악마 > 0
	TIMES ARG:8, 1.10
SIF TALENT:눈에띄고싶어함 > 0
	TIMES ARG:10, 1.10
SIF TALENT:고상함 > 0
	TIMES ARG:9, 1.10
SIF TALENT:울보 > 0
	TIMES ARG:11, 1.10

FOR LOCAL:0, 감각수, PALAM수
	;取得パラメータ 상승チェック
	SIF ARG:(LOCAL:0) > 0
		CALL PALAM_LIMIT, LOCAL:0 + PALAM시점 - 감각수, ARG:(LOCAL:0)
NEXT

;체력、기력、성내성の減算
;체력は고통から、기력は굴복と공포と치정から、성내성は공순と욕정からそれぞれ減算値を求める
IF FLAG:700 == 1 && (TCVARn:0 == 0 || (TCVARn:41 != 0 && CFLAG:(CFLAG:1==0 ? 40 # 41) == 199))
	CALL PALAM_TAIRYOKUDOWN, ARG:10, ARG:12
	CALL PALAM_KIRYOKUDOWN, ARG:8, ARG:11, ARG:9
	CALL PALAM_SEITAISEIDOWN, ARG:5, ARG:7
	PRINTL 
	IF ARG:10 + ARG:12 + ARG:8 + ARG:11 + ARG:7
		;被弾するたびに、방어基礎値に応じてＥＸゲージが上昇する
		SIF CFLAG:1 != 2
			TCVARn:6 += LIMIT(SQRT(MAX(BASE:방어 - 100,0)), 0, 20) + 5
	ENDIF
ENDIF

IF CONFIG_CHECK_SCREEN_F(1) > 0
	SIF ARG:0 + ARG:1 + ARG:2 + ARG:3 + ARG:4 + ARG:5 + ARG:6 + ARG:7 + ARG:8 + ARG:9 + ARG:10 + ARG:11
		WAIT
	;PALAM変化桁合わせ用変数
	LOCAL:998 = 4
	LOCAL:997 = 4
	FOR LCOUNT:1, 0, PALAM수
		LCOUNT:2 = LCOUNT:1
		SIF LCOUNT:1 >= 감각수
			LCOUNT:2 += PALAM시점 - 감각수
		STRLENFORM {ARG:(LCOUNT:1)}
			SIF RESULT > LOCAL:998
				LOCAL:998 = RESULT
		STRLENFORM {LOCAL:(100 + LCOUNT:2) + ARG:(LCOUNT:1)}
			SIF RESULT > LOCAL:997
				LOCAL:997 = RESULT
	NEXT
	REPEAT PALAM수
		LOCAL = COUNT
		SIF COUNT >= 감각수
			LOCAL += PALAM시점 - 감각수

		IF ARG:COUNT > 0
			IF LOCAL:999 == 0
				LOCAL:999 = 1
				CALL SHORTLINE
			ENDIF
			IF LOCAL < 감각수 && LOCAL:(100+LOCAL) + ARG:COUNT > 19999 && ECS_FLAG < 1
				PRINTFORML %PALAMNAME:LOCAL%：{LOCAL:(100+LOCAL), LOCAL:997} + {ARG:COUNT, LOCAL:998} - {LOCAL:(100+LOCAL) + ARG:COUNT - 9999, LOCAL:997} = {PALAM:LOCAL, LOCAL:997} %ECSTASY_RANK(NOWEX:LOCAL)%絶頂
			ELSEIF LOCAL < 감각수 && LOCAL:(100+LOCAL) + ARG:COUNT > 9999 && ECS_FLAG < 1
				PRINTFORML %PALAMNAME:LOCAL%：{LOCAL:(100+LOCAL), LOCAL:997} + {ARG:COUNT, LOCAL:998} - 10000 = {PALAM:LOCAL, LOCAL:997}　絶頂
			ELSEIF LOCAL >= 감각수 && LOCAL:(100+LOCAL) + ARG:COUNT > 999999
				PRINTFORML %PALAMNAME:LOCAL%：{LOCAL:(100+LOCAL), LOCAL:997} + {ARG:COUNT, LOCAL:998} - {LOCAL:(100+LOCAL) + ARG:COUNT - 999999, LOCAL:997} = {PALAM:LOCAL, LOCAL:997}　限界
			ELSE
				PRINTFORML %PALAMNAME:LOCAL%：{LOCAL:(100+LOCAL), LOCAL:997} + {ARG:COUNT, LOCAL:998} = {PALAM:LOCAL, LOCAL:997}\@BASE:(LOCAL + 30) > 0 && LOCAL < 감각수 && FLAG:700 ? 　（%TALENTNAME:(LOCAL + 190)%） # \@
			ENDIF
		ENDIF
	REND
	IF LOCAL:999 == 1
		LOCAL:999 = 0
		CALL SHORTLINE
		PRINTL 
	ENDIF
ENDIF

;刻印の取得(※악질 시민戦での取得は判定を厳しく見る)
;쾌락각인　一定量の욕정
LOCAL = 0
SIF ARG:7 >= (12000 + MARK:쾌락각인방지 * 300 + (FLAG:73 > 0 ? 5000 # 0))
	LOCAL = 1
SIF ARG:7 >= (24000 + MARK:쾌락각인방지 * 600 + (FLAG:73 > 0 ? 10000 # 0)) && MARK:쾌락각인 == 1
	LOCAL = 2
SIF ARG:7 >= (48000 + MARK:쾌락각인방지 * 1200 + (FLAG:73 > 0 ? 20000 # 0)) && MARK:쾌락각인 == 2
	LOCAL = 3
SIF ARG:7 >= (96000 + MARK:쾌락각인방지 * 2400 + (FLAG:73 > 0 ? 40000 # 0)) && MARK:쾌락각인 == 3
	LOCAL = 4
SIF ARG:7 >= (192000 + MARK:쾌락각인방지 * 4800 + (FLAG:73 > 0 ? 80000 # 0)) && MARK:쾌락각인 == 4
	LOCAL = 5
IF LOCAL > MARK:쾌락각인 && (FLAG:700 == 1 || CFLAG:0 == 1) && (TCVARn:12 & 기절) == 0
	MARK:쾌락각인 = LOCAL
	IF LOCAL == 1
		;지문: 쾌락각인１
		CALL MESSAGE_SEX_MARK_KAIRAKU_1
		;지문: 세뇌/흑화 캐릭터가 操作キャラに쾌락각인１が付くのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MARK_KAIRAKU_1
	ELSEIF LOCAL == 2
		;지문: 쾌락각인２
		CALL MESSAGE_SEX_MARK_KAIRAKU_2
		;지문: 세뇌/흑화 캐릭터가 操作キャラに쾌락각인２が付くのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MARK_KAIRAKU_2
	ELSEIF LOCAL == 3
		;지문: 쾌락각인３
		CALL MESSAGE_SEX_MARK_KAIRAKU_3
		;지문: 세뇌/흑화 캐릭터가 操作キャラに쾌락각인３が付くのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MARK_KAIRAKU_3
	ELSEIF LOCAL == 4
		;지문: 쾌락각인４
		CALL MESSAGE_SEX_MARK_KAIRAKU_4
		;지문: 세뇌/흑화 캐릭터가 操作キャラに쾌락각인４が付くのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MARK_KAIRAKU_4
	ELSEIF LOCAL == 5
		;지문: 쾌락각인５
		CALL MESSAGE_SEX_MARK_KAIRAKU_5
		;지문: 세뇌/흑화 캐릭터가 操作キャラに쾌락각인５が付くのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MARK_KAIRAKU_5
	ENDIF
	PRINTFORM %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")%
	FONTBOLD
	PRINTFORM %MARKNAME:0%{LOCAL}
	FONTREGULAR
	PRINTFORML を取得した
	PRINTL 
ENDIF

;고통각인　一定量の고통
LOCAL = 0
SIF ARG:10 >= (10000 + MARK:고통각인방지 * 250 + (FLAG:73 > 0 ? 4000 # 0))
	LOCAL = 1
SIF ARG:10 >= (20000 + MARK:고통각인방지 * 500 + (FLAG:73 > 0 ? 8000 # 0)) && MARK:고통각인 == 1
	LOCAL = 2
SIF ARG:10 >= (40000 + MARK:고통각인방지 * 1000 + (FLAG:73 > 0 ? 16000 # 0)) && MARK:고통각인 == 2
	LOCAL = 3
SIF ARG:10 >= (80000 + MARK:고통각인방지 * 2000 + (FLAG:73 > 0 ? 32000 # 0)) && MARK:고통각인 == 3
	LOCAL = 4
SIF ARG:10 >= (160000 + MARK:고통각인방지 * 4000 + (FLAG:73 > 0 ? 64000 # 0)) && MARK:고통각인 == 4
	LOCAL = 5
IF LOCAL > MARK:고통각인 && (FLAG:700 == 1 || CFLAG:0 == 1) && (TCVARn:12 & 기절) == 0
	MARK:고통각인 = LOCAL
	IF LOCAL == 1
		;지문: 고통각인１
		CALL MESSAGE_SEX_MARK_KUTUU_1
		;지문: 세뇌/흑화 캐릭터가 操作キャラに고통각인１が付くのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MARK_KUTUU_1
	ELSEIF LOCAL == 2
		;지문: 고통각인２
		CALL MESSAGE_SEX_MARK_KUTUU_2
		;지문: 세뇌/흑화 캐릭터가 操作キャラに고통각인２が付くのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MARK_KUTUU_2
	ELSEIF LOCAL == 3
		;지문: 고통각인３
		CALL MESSAGE_SEX_MARK_KUTUU_3
		;지문: 세뇌/흑화 캐릭터가 操作キャラに고통각인３が付くのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MARK_KUTUU_3
	ELSEIF LOCAL == 4
		;지문: 고통각인４
		CALL MESSAGE_SEX_MARK_KUTUU_4
		;지문: 세뇌/흑화 캐릭터가 操作キャラに고통각인４が付くのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MARK_KUTUU_4
	ELSEIF LOCAL == 5
		;지문: 고통각인５
		CALL MESSAGE_SEX_MARK_KUTUU_5
		;지문: 세뇌/흑화 캐릭터가 操作キャラに고통각인５が付くのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MARK_KUTUU_5
	ENDIF
	PRINTFORM %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")%
	FONTBOLD
	PRINTFORM %MARKNAME:1%{LOCAL}
	FONTREGULAR
	PRINTFORML を取得した
	PRINTL 
ENDIF

;굴복각인　一定量の굴복
LOCAL = 0
SIF ARG:8 >= (8000 + MARK:굴복각인방지 * 200 + (FLAG:73 > 0 ? 3000 # 0))
	LOCAL = 1
SIF ARG:8 >= (16000 + MARK:굴복각인방지 * 400 + (FLAG:73 > 0 ? 6000 # 0)) && MARK:굴복각인 == 1
	LOCAL = 2
SIF ARG:8 >= (32000 + MARK:굴복각인방지 * 800 + (FLAG:73 > 0 ? 12000 # 0)) && MARK:굴복각인 == 2
	LOCAL = 3
SIF ARG:8 >= (64000 + MARK:굴복각인방지 * 1600 + (FLAG:73 > 0 ? 24000 # 0)) && MARK:굴복각인 == 3
	LOCAL = 4
SIF ARG:8 >= (128000 + MARK:굴복각인방지 * 3200 + (FLAG:73 > 0 ? 48000 # 0)) && MARK:굴복각인 == 4
	LOCAL = 5
IF LOCAL > MARK:굴복각인 && (FLAG:700 == 1 || CFLAG:0 == 1) && (TCVARn:12 & 기절) == 0
	MARK:굴복각인 = LOCAL
	IF LOCAL == 1
		;지문: 굴복각인１
		CALL MESSAGE_SEX_MARK_KUPPUKU_1
		;지문: 세뇌/흑화 캐릭터가 操作キャラに굴복각인１が付くのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MARK_KUPPUKU_1
	ELSEIF LOCAL == 2
		;지문: 굴복각인２
		CALL MESSAGE_SEX_MARK_KUPPUKU_2
		;지문: 세뇌/흑화 캐릭터가 操作キャラに굴복각인２が付くのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MARK_KUPPUKU_2
	ELSEIF LOCAL == 3
		;지문: 굴복각인３
		CALL MESSAGE_SEX_MARK_KUPPUKU_3
		;지문: 세뇌/흑화 캐릭터가 操作キャラに굴복각인３が付くのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MARK_KUPPUKU_3
	ELSEIF LOCAL == 4
		;지문: 굴복각인４
		CALL MESSAGE_SEX_MARK_KUPPUKU_4
		;지문: 세뇌/흑화 캐릭터가 操作キャラに굴복각인４が付くのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MARK_KUPPUKU_4
	ELSEIF LOCAL == 5
		;지문: 굴복각인５
		CALL MESSAGE_SEX_MARK_KUPPUKU_5
		;지문: 세뇌/흑화 캐릭터가 操作キャラに굴복각인５が付くのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MARK_KUPPUKU_5
	ENDIF
	PRINTFORM %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")%
	FONTBOLD
	PRINTFORM %MARKNAME:2%{LOCAL}
	FONTREGULAR
	PRINTFORML を取得した
	PRINTL 
ENDIF

;공포각인　一定量の공포
LOCAL = 0
SIF ARG:11 >= (8000 + MARK:공포각인방지 * 200 + (FLAG:73 > 0 ? 1500 # 0))
	LOCAL = 1
SIF ARG:11 >= (16000 + MARK:공포각인방지 * 400 + (FLAG:73 > 0 ? 3000 # 0)) && MARK:공포각인 == 1
	LOCAL = 2
SIF ARG:11 >= (32000 + MARK:공포각인방지 * 800 + (FLAG:73 > 0 ? 6000 # 0)) && MARK:공포각인 == 2
	LOCAL = 3
SIF ARG:11 >= (64000 + MARK:공포각인방지 * 1600 + (FLAG:73 > 0 ? 12000 # 0)) && MARK:공포각인 == 3
	LOCAL = 4
SIF ARG:11 >= (128000 + MARK:공포각인방지 * 3200 + (FLAG:73 > 0 ? 24000 # 0)) && MARK:공포각인 == 4
	LOCAL = 5
IF LOCAL > MARK:공포각인 && (FLAG:700 == 1 || CFLAG:0 == 1) && (TCVARn:12 & 기절) == 0
	MARK:공포각인 = LOCAL
	IF LOCAL == 1
		;지문: 공포각인１
		CALL MESSAGE_SEX_MARK_KYOUHU_1
		;지문: 세뇌/흑화 캐릭터가 操作キャラに공포각인１が付くのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MARK_KYOUHU_1
	ELSEIF LOCAL == 2
		;지문: 공포각인２
		CALL MESSAGE_SEX_MARK_KYOUHU_2
		;지문: 세뇌/흑화 캐릭터가 操作キャラに공포각인２が付くのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MARK_KYOUHU_2
	ELSEIF LOCAL == 3
		;지문: 공포각인３
		CALL MESSAGE_SEX_MARK_KYOUHU_3
		;지문: 세뇌/흑화 캐릭터가 操作キャラに공포각인３が付くのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MARK_KYOUHU_3
	ELSEIF LOCAL == 4
		;지문: 공포각인４
		CALL MESSAGE_SEX_MARK_KYOUHU_4
		;지문: 세뇌/흑화 캐릭터가 操作キャラに공포각인４が付くのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MARK_KYOUHU_4
	ELSEIF LOCAL == 5
		;지문: 공포각인５
		CALL MESSAGE_SEX_MARK_KYOUHU_5
		;지문: 세뇌/흑화 캐릭터가 操作キャラに공포각인５が付くのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MARK_KYOUHU_5
	ENDIF
	PRINTFORM %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")%
	FONTBOLD
	PRINTFORM %MARKNAME:3%{LOCAL}
	FONTREGULAR
	PRINTFORML を取得した
	PRINTL 
ENDIF

;치욕각인　一定量の치정
LOCAL = 0
SIF ARG:9 >= (8000 + MARK:치욕각인방지 * 200 + (FLAG:73 > 0 ? 1000 # 0))
	LOCAL = 1
SIF ARG:9 >= (16000 + MARK:치욕각인방지 * 400 + (FLAG:73 > 0 ? 2000 # 0)) && MARK:치욕각인 == 1
	LOCAL = 2
SIF ARG:9 >= (32000 + MARK:치욕각인방지 * 800 + (FLAG:73 > 0 ? 4000 # 0)) && MARK:치욕각인 == 2
	LOCAL = 3
SIF ARG:9 >= (64000 + MARK:치욕각인방지 * 1600 + (FLAG:73 > 0 ? 8000 # 0)) && MARK:치욕각인 == 3
	LOCAL = 4
SIF ARG:9 >= (128000 + MARK:치욕각인방지 * 3200 + (FLAG:73 > 0 ? 16000 # 0)) && MARK:치욕각인 == 4
	LOCAL = 5
IF LOCAL > MARK:치욕각인 && (FLAG:700 == 1 || CFLAG:0 == 1) && (TCVARn:12 & 기절) == 0
	MARK:치욕각인 = LOCAL
	IF LOCAL == 1
		;지문: 치욕각인１
		CALL MESSAGE_SEX_MARK_TIJYOKU_1
		;지문: 세뇌/흑화 캐릭터가 操作キャラに치욕각인１が付くのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MARK_TIJYOKU_1
	ELSEIF LOCAL == 2
		;지문: 치욕각인２
		CALL MESSAGE_SEX_MARK_TIJYOKU_2
		;지문: 세뇌/흑화 캐릭터가 操作キャラに치욕각인２が付くのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MARK_TIJYOKU_2
	ELSEIF LOCAL == 3
		;지문: 치욕각인３
		CALL MESSAGE_SEX_MARK_TIJYOKU_3
		;지문: 세뇌/흑화 캐릭터가 操作キャラに치욕각인３が付くのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MARK_TIJYOKU_3
	ELSEIF LOCAL == 4
		;지문: 치욕각인４
		CALL MESSAGE_SEX_MARK_TIJYOKU_4
		;지문: 세뇌/흑화 캐릭터가 操作キャラに치욕각인４が付くのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MARK_TIJYOKU_4
	ELSEIF LOCAL == 5
		;지문: 치욕각인５
		CALL MESSAGE_SEX_MARK_TIJYOKU_5
		;지문: 세뇌/흑화 캐릭터가 操作キャラに치욕각인５が付くのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_SEX_MARK_TIJYOKU_5
	ENDIF
	PRINTFORM %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")%
	FONTBOLD
	PRINTFORM %MARKNAME:4%{LOCAL}
	FONTREGULAR
	PRINTFORML を取得した
	PRINTL 
ENDIF

;理性強度低下による구상分岐
CALL MESSAGE_BRANCH_MESSAGE_FAITH_DOWN

;실적
CALL GET_STATE_EXPUP,TARGET

;搾精用ＨＰチェック
SIF FLAG:13 <= 0 && FLAG:700 == 1
	JUMP SOURCE_CHECK

;즉사공격ヒット直後とトドメの一撃は촉수・観衆関係のテキストを出さない
IF TFLAG:23 != -1 && (BASE:TARGET:체력>0 ||BASE:TARGET:기력>0 ||BASE:TARGET:성내성>0)
	;촉수の油断の確認
	CALL CLOTH_BATTLE_HOSEI, "YUDAN"
	LOCAL = RESULT
	CALL TENTACLE_LEVEL
	LOCAL:1 = RESULT
	SIF LOCAL:1 > 60
		LOCAL:1 = 60
	;방심도関係のFEAT効果
	SIF TALENT:평범 > 0
		LOCAL += 25
	SIF TALENT:공세구축 > 0
		LOCAL -= 25
	IF FLAG:700 == 1
		TFLAG:3 *= LOCAL / 100
		FLAG:17 = LIMIT(FLAG:17 + TFLAG:3, 0 , FLAG:16 * 2)
		;拘束中の場合、油断は毎ターン６％だけ増加する
		IF TCVARn:0 == 0
			FLAG:17 += FLAG:16 * 6 * LOCAL / 10000
		;油断している場合、방심도が毎ターン１２％だけ減少する
		ELSEIF TFLAG:2 > 0
			FLAG:17 -= FLAG:16 * 12 / 100
		;それ以外の場合、방심도が毎ターン１％だけ減少する
		ELSE
			FLAG:17 -= FLAG:16 * 1 / 100
		ENDIF
		SIF FLAG:17 < 0
			FLAG:17 = 0

		;デバッグ用
		IF FLAG:999
			SETCOLOR 105,105,105
			PRINTFORM 방심도　補正＝{LOCAL}％ 방심도＝{FLAG:17}/{FLAG:16}
			SIF TCVARn:0 == 0
				PRINTFORM  ＋ ターン経過{FLAG:16 * 12 * LOCAL / 10000}
			SIF TFLAG:3
				PRINTFORM  ＋ 直接上昇{TFLAG:3}
			PRINTL 
			PRINTL 
			RESETCOLOR
		ENDIF

		;방심도상승량をリセット
		TFLAG:3 = 0

		;２ターン目までなら確実に油断が持続
		IF TFLAG:2 > 0 && TFLAG:2 <= 2 && FLAG:17 < FLAG:16
			FLAG:17 = FLAG:16
		;最大でも１２ターンまでしか持続しない、레벨に応じて短くなる
		ELSEIF TFLAG:2 >= 12 - LOCAL:1 / 10
			FLAG:17 = 0
		ENDIF
		;역치を越えていれば油断플래그를加算
		IF FLAG:17 >= FLAG:16
			TFLAG:2 += 1
		;ターン経過で油断が解ける場合
		ELSEIF TFLAG:2 > 0
			TFLAG:2 = 0
			FLAG:17 = 0
			PRINTL 
			IF (FLAG:110 == 0)
				CALL TENTACLE_ACCESS, "NAME"
				PRINTL は油断から立ち直った
			ELSEIF (FLAG:110 == 1)
				PRINTFORML %PRINT_TRANSCALLNAME(FLAG:111)%は油断から立ち直った
			ENDIF
		ENDIF
	ENDIF

	;凌辱時の応援
	SIF TCVARn:0 == 0 && FLAG:700 == 1
		CALL PERFORM_CHEERS_TENTACLE_SEX_HANTEI

	IF TFLAG:2 > 0 && FLAG:700 == 1
		PRINTL 
		IF (FLAG:110 == 0)
			CALL TENTACLE_ACCESS, "NAME"
			PRINTL は油断しているようだ
		ELSEIF (FLAG:110 == 1)
			PRINTFORML %PRINT_TRANSCALLNAME(FLAG:111)%は油断しているようだ
		ENDIF
	ENDIF
ENDIF

;変身中かつ처녀でないなら、변신시비처녀を付与
SIF CFLAG:1 > 0 && TALENT:처녀 == -1 && ISFEMALE() && TALENT:변신시비처녀 == 0
	TALENT:변신시비처녀 = 1

;--------------------------------------------------
;성내성に関する補正
;성내성の残りが少なくなってくると取得値が大きくなってくる
@PALAM_HOSEI_SEITAISEI, ARG
LOCAL = 0
IF BASE:성내성 > 0
	CALL PERCENT_CAL, BASE:성내성, MAXBASE:성내성
	LOCAL = RESULT
ENDIF

IF LOCAL > 80
ELSEIF LOCAL > 60
	TIMES ARG, 1.10
ELSEIF LOCAL > 40
	TIMES ARG, 1.20
ELSEIF LOCAL > 20
	TIMES ARG, 1.35
ELSEIF	LOCAL > 10
	TIMES ARG, 1.50
ELSE
	TIMES ARG, 1.70
ENDIF

RETURN ARG

;--------------------------------------------------
;素質に関する補正
@PALAM_HOSEI_TALENT, ARG:0, ARG:1
SELECTCASE ARG:0
	;敏感、鈍感など
	CASE 0
	;快C
		IF TALENT:C민감 > 0
			TIMES ARG:1, 1.25
		ELSEIF TALENT:C둔감 > 0
			TIMES ARG:1, 0.75
		ENDIF
		SIF TALENT:음핵 > 0
			TIMES ARG:1, 1.50
	CASE 1
	;快V
		IF TALENT:V민감 > 0
			TIMES ARG:1, 1.25
		ELSEIF TALENT:V둔감 > 0
			TIMES ARG:1, 0.75
		ENDIF
		SIF TALENT:음호 > 0
			TIMES ARG:1, 1.50
	CASE 2
	;快A
		IF TALENT:A민감 > 0
			TIMES ARG:1, 1.25
		ELSEIF TALENT:A둔감 > 0
			TIMES ARG:1, 0.75
		ENDIF
		SIF TALENT:음고 > 0
			TIMES ARG:1, 1.50
	CASE 3
	;快B
		IF TALENT:B민감 > 0
			TIMES ARG:1, 1.25
		ELSEIF TALENT:B둔감 > 0
			TIMES ARG:1, 0.75
		ENDIF
		IF TALENT:빈유 == 2
			TIMES ARG:1, 0.75
		ELSEIF TALENT:빈유 == 1
			TIMES ARG:1, 0.80
		ELSEIF TALENT:거유 == 5
			TIMES ARG:1, 1.40
		ELSEIF TALENT:거유 == 4
			TIMES ARG:1, 1.35
		ELSEIF TALENT:거유 == 3
			TIMES ARG:1, 1.30
		ELSEIF TALENT:거유 == 2
			TIMES ARG:1, 1.25
		ELSEIF TALENT:거유 == 1
			TIMES ARG:1, 1.20
		ENDIF
		SIF TALENT:음유 > 0
			TIMES ARG:1, 1.50
	CASE 10
	;윤활
		IF TALENT:젖기쉬움 > 0
			TIMES ARG:1, 2.00
		ELSEIF TALENT:젖기어려움 > 0
			TIMES ARG:1, 0.50
		ENDIF
		SIF TALENT:음란 > 0
			TIMES ARG:1, 2.00
	CASE 11
	;공순
		SIF TALENT:촉수의포로 > 0
			TIMES ARG:1, 2.00
	CASE 12
	;습득
	CASE 13
	;욕정
		SIF TALENT:촉수의포로 > 0
			TIMES ARG:1, 2.00
		SIF TALENT:음란 > 0
			TIMES ARG:1, 2.00
	CASE 14
	;굴복
		SIF TALENT:촉수의포로 > 0
			TIMES ARG:1, 2.00
	CASE 15
	;치정
		SIF TALENT:백보지 > 0
			TIMES ARG:1, 1.50
		SIF TALENT:음란 > 0
			TIMES ARG:1, 2.00
	CASE 16
	;고통
	CASE 17
	;공포
ENDSELECT
RETURN ARG:1

;--------------------------------------------------
;성격に関する補正
@PALAM_HOSEI_SEIKAKU, ARG:0, ARG:1
LOCAL = SEIKAKU_HOSEI_PALAM_F(SEIKAKU_CHECK_F(TARGET), ARG:0, ARG:1)
RETURN LOCAL

;--------------------------------------------------
;촉수に関する補正
@PALAM_HOSEI_TENTACLE, ARG:0, ARG:1
SIF FLAG:700 == 0 || ENEMY_TYPE_CHECK_F("AKUOTI")
	RETURN ARG:1

CALL TENTACLE_ACCESS, "PALAM_HOSEI"
;RESULT:0=快C　以降順番に RESULT:11=공포

SELECTCASE ARG:0
	CASE 0
	;快C
		ARG:1 = ARG:1 * RESULT:0 / 100
	CASE 1
	;快V
		ARG:1 = ARG:1 * RESULT:1 / 100
	CASE 2
	;快A
		ARG:1 = ARG:1 * RESULT:2 / 100
	CASE 3
	;快B
		ARG:1 = ARG:1 * RESULT:3 / 100
	CASE 10
	;윤활
		ARG:1 = ARG:1 * RESULT:4 / 100
	CASE 11
	;공순
		ARG:1 = ARG:1 * RESULT:5 / 100
	CASE 12
	;습득
		ARG:1 = ARG:1 * RESULT:6 / 100
	CASE 13
	;욕정
		ARG:1 = ARG:1 * RESULT:7 / 100
	CASE 14
	;굴복
		ARG:1 = ARG:1 * RESULT:8 / 100
	CASE 15
	;치정
		ARG:1 = ARG:1 * RESULT:9 / 100
	CASE 16
	;고통
		ARG:1 = ARG:1 * RESULT:10 / 100
	CASE 17
	;공포
		ARG:1 = ARG:1 * RESULT:11 / 100
ENDSELECT
RETURN ARG:1

;--------------------------------------------------
;촉수중독に関する補正
@PALAM_HOSEI_POISONING_TENTACLE, ARG
IF ABL:촉수중독 == 1
	TIMES ARG, 1.20
ELSEIF ABL:촉수중독 == 2
	TIMES ARG, 1.50
ELSEIF ABL:촉수중독 == 3
	TIMES ARG, 2.00
ELSEIF ABL:촉수중독 == 4
	TIMES ARG, 2.50
ELSEIF ABL:촉수중독 == 5
	TIMES ARG, 4.00
ENDIF
RETURN ARG

;--------------------------------------------------
;体勢に関する補正
@PALAM_HOSEI_POSE, ARG
;견딘다選択中ならパラメータ上昇しにくくなる
SIF TCVARn:2 == 자세：견딘다
	TIMES ARG, 0.65
;받아들인다選択中ならパラメータ上昇しやすくなる
SIF TCVARn:2 == 자세：받아들인다
	TIMES ARG, 1.25
;쥐어짠다選択中ならパラメータ上昇しやすくなる
SIF TCVARn:2 == 자세：쥐어짠다
	TIMES ARG, 1.50
RETURN ARG

;--------------------------------------------------
;ランダム性付与
@PALAM_HOSEI_RANDOM, ARG
LOCAL= RAND:100
IF LOCAL < 20
	TIMES ARG, 1.10
ELSEIF LOCAL < 40
	TIMES ARG, 1.05
ELSEIF LOCAL < 60
	TIMES ARG, 1.00
ELSEIF LOCAL < 80
	TIMES ARG, 0.95
ELSE
	TIMES ARG, 0.90
ENDIF
RETURN ARG

;--------------------------------------------------
;快部位の絶頂チェック
@PALAM_ECSTASY, ARG:0, ARG:1, ARG:2, ARG:3

IF ARG:1 + PALAM:(ARG:0) < 9999 || ARG:3 > 0
	ARG:1 += PALAM:(ARG:0)
ELSEIF ARG:1 + PALAM:(ARG:0) >=1000000
	;最狂絶頂
	ARG:1 = 9999
	NOWEX:(ARG:2) += 10
ELSEIF ARG:1 + PALAM:(ARG:0) >=600000
	;狂絶頂
	ARG:1 = 9999
	NOWEX:(ARG:2) += 9
ELSEIF ARG:1 + PALAM:(ARG:0) >=400000
	;獄絶頂
	ARG:1 = 9999
	NOWEX:(ARG:2) += 8
ELSEIF ARG:1 + PALAM:(ARG:0) >=250000
	;極絶頂
	ARG:1 = 9999
	NOWEX:(ARG:2) += 7
ELSEIF ARG:1 + PALAM:(ARG:0) >=150000
	;凄絶頂
	ARG:1 = 9999
	NOWEX:(ARG:2) += 6
ELSEIF ARG:1 + PALAM:(ARG:0) >=100000
	;最強絶頂
	ARG:1 = 9999
	NOWEX:(ARG:2) += 5
ELSEIF ARG:1 + PALAM:(ARG:0) >=75000
	;超強絶頂
	ARG:1 = 9999
	NOWEX:(ARG:2) += 4
ELSEIF ARG:1 + PALAM:(ARG:0) >=50000
	;超絶頂
	ARG:1 = 9999
	NOWEX:(ARG:2) += 3
ELSEIF ARG:1 + PALAM:(ARG:0) >=20000
	;強絶頂
	ARG:1 = 9999
	NOWEX:(ARG:2) += 2
ELSEIF ARG:1 + PALAM:(ARG:0) >=10000
	;絶頂
	ARG:1 = PALAM:(ARG:0) - 10000 + ARG:1
	NOWEX:(ARG:2) += 1
ENDIF

;減衰計算用変数をリセット
SIF NOWEX:(ARG:2) > 0
	MAX_PALAM:(ARG:0) = 0

PALAM:(ARG:0) = ARG:1


;--------------------------------------------------
;パラメータ上昇チェック
@PALAM_LIMIT, ARG:0, ARG:1
IF ARG:1 > 999999
	ARG:1 = 999999
ELSE
	ARG:1 += PALAM:(ARG:0)
ENDIF
PALAM:(ARG:0) = ARG:1


;--------------------------------------------------
;衣服の損傷LVによる치정の獲得
@PALAM_TIJOU_CLOTH_DAMAGE
;壊れない衣装用
IF (CFLAG:1 == 0 && TCVARn:20 == -1) || (CFLAG:1 == 1 && TCVARn:22 == -1)
	IF CFLAG:(CFLAG:1==0 ? 40 # 41) == 199 && FIGURE_SPLIT(EQUIP:(CFLAG:1==0 ? 99 # 199), 1) != 0 && TCVARn:41 == 0
		RETURN
	ELSEIF CFLAG:(CFLAG:1==0 ? 40 # 41) == 299 || CFLAG:(CFLAG:1==0 ? 40 # 41) == 401
		RETURN 350
	ENDIF
ENDIF

LOCAL = 0
IF TCVARn:(CFLAG:1==0 ? 21 # 23) == 0 && (TCVARn:25 == 0 || CLOTH_NO_INNER)
	LOCAL += 500
ELSEIF CLOTH_NO_INNER
	IF CLOTH_OUTER_PER < 25
		LOCAL += 350
	ELSEIF CLOTH_OUTER_PER < 50
		LOCAL += 150
	ELSEIF CLOTH_OUTER_PER < 75
		LOCAL += 50
	ENDIF
ELSE
	IF CLOTH_OUTER_PER < 25
		LOCAL += 100
	ELSEIF CLOTH_OUTER_PER < 50
		LOCAL += 50
	ENDIF
	
	IF CLOTH_INNER_PER < 25
		LOCAL += 250
	ELSEIF CLOTH_INNER_PER < 50
		LOCAL += 100
	ELSEIF CLOTH_INNER_PER < 100
		LOCAL += 50
	ENDIF
ENDIF
;戦闘中なら의상에 따른 보정がある
IF FLAG:700
	;衣装
	CALL CLOTH_BATTLE_HOSEI, "SHYNESS"
	LOCAL = LOCAL * RESULT / 100
ENDIF
RETURN LOCAL

;--------------------------------------------------
;치정の取得
@PALAM_TIJOU, ARG:0, ARG:1, ARG:2, ARG:3
LOCAL = 0
REPEAT 4
	IF ARG:COUNT == 0
	ELSEIF ARG:COUNT < 100
		LOCAL += 50
	ELSEIF ARG:COUNT < 1000
		LOCAL += 100
	ELSEIF ARG:COUNT < 2000
		LOCAL += 200
	ELSEIF ARG:COUNT < 5000
		LOCAL += 500
	ELSE
		LOCAL += 1000
	ENDIF
	
	IF NOWEX:COUNT == 1
		LOCAL += 500
	ELSEIF NOWEX:COUNT == 2
		LOCAL += 1000
	ELSEIF NOWEX:COUNT > 2
		LOCAL += 2000
	ENDIF
REND
RETURN LOCAL

;--------------------------------------------------
;욕정の取得
@PALAM_YOKUJOU, ARG:0, ARG:1, ARG:2, ARG:3, ARG:4, ARG:5
LOCAL = 0
REPEAT 4
	IF ARG:COUNT == 0
	ELSEIF ARG:COUNT < 100
		LOCAL += 500
	ELSEIF ARG:COUNT < 500
		LOCAL += 900
	ELSEIF ARG:COUNT < 1000
		LOCAL += 1200
	ELSEIF ARG:COUNT < 2000
		LOCAL += 1500
	ELSEIF ARG:COUNT < 5000
		LOCAL += 2000
	ELSEIF ARG:COUNT < 7500
		LOCAL += 3500
	ELSE
		LOCAL += 5000
	ENDIF
	
	IF NOWEX:COUNT == 1
		LOCAL += 1000
	ELSEIF NOWEX:COUNT == 2
		LOCAL += 2000
	ELSEIF NOWEX:COUNT > 2
		LOCAL += 5000
	ENDIF
REND

;노출벽があると치정に応じて욕정が入る
IF ABL:노출벽 == 1
	IF ARG:4 == 0
	ELSEIF ARG:4 < 100
		LOCAL += 10
	ELSEIF ARG:4 < 1000
		LOCAL += 20
	ELSEIF ARG:4 < 2000
		LOCAL += 50
	ELSEIF ARG:4 < 5000
		LOCAL += 100
	ELSE
		LOCAL += 200
	ENDIF
ELSEIF ABL:노출벽 == 2
	IF ARG:4 == 0
	ELSEIF ARG:4 < 100
		LOCAL += 20
	ELSEIF ARG:4 < 1000
		LOCAL += 50
	ELSEIF ARG:4 < 2000
		LOCAL += 100
	ELSEIF ARG:4 < 5000
		LOCAL += 200
	ELSE
		LOCAL += 500
	ENDIF
ELSEIF ABL:노출벽 == 3
	IF ARG:4 == 0
	ELSEIF ARG:4 < 100
		LOCAL += 50
	ELSEIF ARG:4 < 1000
		LOCAL += 100
	ELSEIF ARG:4 < 2000
		LOCAL += 200
	ELSEIF ARG:4 < 5000
		LOCAL += 500
	ELSE
		LOCAL += 1000
	ENDIF
ELSEIF ABL:노출벽 == 4
	IF ARG:4 == 0
	ELSEIF ARG:4 < 100
		LOCAL += 100
	ELSEIF ARG:4 < 1000
		LOCAL += 200
	ELSEIF ARG:4 < 2000
		LOCAL += 500
	ELSEIF ARG:4 < 5000
		LOCAL += 1000
	ELSE
		LOCAL += 2000
	ENDIF
ELSEIF ABL:노출벽 == 5
	IF ARG:4 == 0
	ELSEIF ARG:4 < 100
		LOCAL += 200
	ELSEIF ARG:4 < 1000
		LOCAL += 500
	ELSEIF ARG:4 < 2000
		LOCAL += 1000
	ELSEIF ARG:4 < 5000
		LOCAL += 2000
	ELSE
		LOCAL += 5000
	ENDIF
ENDIF

;마조끼があると고통に応じて욕정が入る
IF ABL:마조끼 == 1
	IF ARG:5 == 0
	ELSEIF ARG:5 < 100
		LOCAL += 10
	ELSEIF ARG:5 < 1000
		LOCAL += 20
	ELSEIF ARG:5 < 2000
		LOCAL += 50
	ELSEIF ARG:5 < 5000
		LOCAL += 100
	ELSE
		LOCAL += 200
	ENDIF
ELSEIF ABL:마조끼 == 2
	IF ARG:5 == 0
	ELSEIF ARG:5 < 100
		LOCAL += 20
	ELSEIF ARG:5 < 1000
		LOCAL += 50
	ELSEIF ARG:5 < 2000
		LOCAL += 100
	ELSEIF ARG:5 < 5000
		LOCAL += 200
	ELSE
		LOCAL += 500
	ENDIF
ELSEIF ABL:마조끼 == 3
	IF ARG:5 == 0
	ELSEIF ARG:5 < 100
		LOCAL += 50
	ELSEIF ARG:5 < 1000
		LOCAL += 100
	ELSEIF ARG:5 < 2000
		LOCAL += 200
	ELSEIF ARG:5 < 5000
		LOCAL += 500
	ELSE
		LOCAL += 1000
	ENDIF
ELSEIF ABL:마조끼 == 4
	IF ARG:5 == 0
	ELSEIF ARG:5 < 100
		LOCAL += 100
	ELSEIF ARG:5 < 1000
		LOCAL += 200
	ELSEIF ARG:5 < 2000
		LOCAL += 500
	ELSEIF ARG:5 < 5000
		LOCAL += 1000
	ELSE
		LOCAL += 2000
	ENDIF
ELSEIF ABL:마조끼 == 5
	IF ARG:5 == 0
	ELSEIF ARG:5 < 100
		LOCAL += 200
	ELSEIF ARG:5 < 1000
		LOCAL += 500
	ELSEIF ARG:5 < 2000
		LOCAL += 1000
	ELSEIF ARG:5 < 5000
		LOCAL += 2000
	ELSE
		LOCAL += 5000
	ENDIF
ENDIF
RETURN LOCAL

;--------------------------------------------------
;윤활の取得
@PALAM_JUNKATU, ARG
LOCAL = 0
IF ARG == 0
ELSEIF ARG < 100
	LOCAL += 50
ELSEIF ARG < 1000
	LOCAL += 100
ELSEIF ARG < 2000
	LOCAL += 200
ELSEIF ARG < 5000
	LOCAL += 500
ELSEIF ARG < 10000
	LOCAL += 1000
ELSE
	LOCAL += 2000
ENDIF
RETURN LOCAL

;--------------------------------------------------
;공순の取得
@PALAM_KYOUJUN, ARG:0, ARG:1, ARG:2, ARG:3
LOCAL = 0
REPEAT 4
	IF ARG:COUNT == 0
	ELSEIF ARG:COUNT < 4000
		LOCAL += 50
	ELSEIF ARG:COUNT < 8000
		LOCAL += 100
	ELSEIF ARG:COUNT < 12000
		LOCAL += 200
	ELSEIF ARG:COUNT < 20000
		LOCAL += 400
	ELSE
		LOCAL += 800
	ENDIF
	
	IF NOWEX:COUNT == 1
		LOCAL += 500
	ELSEIF NOWEX:COUNT == 2
		LOCAL += 1000
	ELSEIF NOWEX:COUNT > 2
		LOCAL += 2000
	ENDIF
REND
RETURN LOCAL

;--------------------------------------------------
;체력の減算
@PALAM_TAIRYOKUDOWN, ARG:0, ARG:1
LOCAL = MAXBASE:체력 * 50 / 1000
IF ARG == 0
ELSEIF ARG < 100
	LOCAL += 50 + RAND:10
ELSEIF ARG < 1000
	LOCAL += 100 + RAND:20
ELSEIF ARG < 5000
	LOCAL += 150 + RAND:40
ELSEIF ARG < 10000
	LOCAL += 250 + RAND:80
ELSEIF ARG < 20000
	LOCAL += 350 + RAND:160
ELSE
	LOCAL += 500 + RAND:320
ENDIF

CALL PERCENT_CAL, BASE:기력, MAXBASE:기력
IF RESULT <= 0
	TIMES LOCAL,1.25
	LOCAL += 100
ENDIF
LOCAL += ARG:1

CALL PERCENT_CAL, BASE:성내성, MAXBASE:성내성
IF RESULT > 75
	TIMES LOCAL,0.05
ELSEIF RESULT > 50
	TIMES LOCAL,0.10
ELSEIF RESULT > 25
	TIMES LOCAL,0.25
ELSEIF RESULT > 5
	TIMES LOCAL,0.50
ELSEIF RESULT > 0
	TIMES LOCAL,0.75
ENDIF

;의상에 따른 보정
CALL CLOTH_BATTLE_HOSEI, "TAIRYOKU"
LOCAL = LOCAL * RESULT / 100
;敵の레벨による補正
CALL TENTACLE_LEVEL
LOCAL = LOCAL * (100 + RESULT) / 100

SIF TALENT:보수적 > 0
	TIMES LOCAL, 0.90
SIF TALENT:호기심 > 0
	TIMES LOCAL, 1.10

SIF BASE:체력 - LOCAL < 0
	LOCAL = BASE:체력

IF LOCAL > 0
	FONTBOLD
	PRINTFORML %BASENAME:0%が{LOCAL}減った！
	FONTREGULAR
ENDIF
BASE:체력 -= LOCAL

;--------------------------------------------------
;기력の減算
@PALAM_KIRYOKUDOWN, ARG:0, ARG:1, ARG:2
LOCAL = MAXBASE:기력 * 75 / 1000
IF ARG:0 == 0
ELSEIF ARG:0 < 100
	LOCAL += 50 + RAND:10
ELSEIF ARG:0 < 1000
	LOCAL += 100 + RAND:20
ELSEIF ARG:0 < 5000
	LOCAL += 150 + RAND:30
ELSEIF ARG:0 < 10000
	LOCAL += 200 + RAND:40
ELSEIF ARG:0 < 20000
	LOCAL += 250 + RAND:60
ELSE
	LOCAL += 300 + RAND:80
ENDIF
IF ARG:1 == 0
ELSEIF ARG:1 < 100
	LOCAL += 25 + RAND:5
ELSEIF ARG:1 < 1000
	LOCAL += 50 + RAND:10
ELSEIF ARG:1 < 5000
	LOCAL += 75 + RAND:15
ELSEIF ARG:1 < 10000
	LOCAL += 100 + RAND:20
ELSEIF ARG:1 < 20000
	LOCAL += 125 + RAND:30
ELSE
	LOCAL += 150 + RAND:40
ENDIF
IF ARG:2 == 0
ELSEIF ARG:1 < 100
	LOCAL += 25 + RAND:5
ELSEIF ARG:1 < 1000
	LOCAL += 50 + RAND:10
ELSEIF ARG:1 < 5000
	LOCAL += 75 + RAND:15
ELSEIF ARG:1 < 10000
	LOCAL += 100 + RAND:20
ELSEIF ARG:1 < 20000
	LOCAL += 125 + RAND:30
ELSE
	LOCAL += 150 + RAND:40
ENDIF

CALL PERCENT_CAL, BASE:체력, MAXBASE:체력
IF RESULT <= 0
	TIMES LOCAL,1.25
	LOCAL += 100
ENDIF

CALL PERCENT_CAL, BASE:성내성, MAXBASE:성내성
IF RESULT > 75
	TIMES LOCAL,0.25
ELSEIF RESULT > 50
	TIMES LOCAL,0.50
ELSEIF RESULT > 25
	TIMES LOCAL,0.75
ELSEIF RESULT > 0
	TIMES LOCAL,0.90
ENDIF

;의상에 따른 보정
CALL CLOTH_BATTLE_HOSEI, "KIRYOKU"
LOCAL = LOCAL * RESULT / 100
;敵の레벨による補正
CALL TENTACLE_LEVEL
LOCAL = LOCAL * (100 + RESULT) / 100

SIF TALENT:보수적 > 0
	TIMES LOCAL, 0.90
SIF TALENT:호기심 > 0
	TIMES LOCAL, 1.10

SIF LOCAL == 0
	RETURN

SIF BASE:기력 - LOCAL < 0
	LOCAL = BASE:기력

IF LOCAL > 0
	FONTBOLD
	IF GAME_OPTION_CHECK_F(OPTION_스탯저하있음)
	;インスタントモードなら기초기력も減らす
		PRINTFORML %BASENAME:1%が{LOCAL} %BASENAME:1%基礎が{LOCAL / 2}減った！
	ELSE
		PRINTFORML %BASENAME:1%が{LOCAL}減った！
	ENDIF
	FONTREGULAR
ENDIF
SIF PALAM_GUTS_F("KIRYOKU", LOCAL) > 0
	LOCAL -= 1
BASE:기력 -= LOCAL
IF GAME_OPTION_CHECK_F(OPTION_스탯저하있음)
	;インスタントモードなら기초기력も減らす
	BASE:TARGET:기초기력 -= LOCAL / 2
ENDIF

;--------------------------------------------------
;성내성の減算
@PALAM_SEITAISEIDOWN, ARG:0, ARG:1
LOCAL = 0
IF ARG:0 < 500
ELSEIF ARG:0 < 1000
	LOCAL += 1
ELSEIF ARG:0 < 2000
	LOCAL += 2
ELSEIF ARG:0 < 4000
	LOCAL += 3
ELSEIF ARG:0 < 8000
	LOCAL += 4
ELSEIF ARG:0 < 12000
	LOCAL += 5
ELSEIF ARG:0 < 16000
	LOCAL += 6
ELSEIF ARG:0 < 20000
	LOCAL += 10
ELSE
	LOCAL += 12
ENDIF
IF ARG:1 == 0
ELSEIF ARG:1 < 200
	LOCAL += 2
ELSEIF ARG:1 < 500
	LOCAL += 4 + RAND:2
ELSEIF ARG:1 < 1000
	LOCAL += 6 + RAND:2
ELSEIF ARG:1 < 2000
	LOCAL += 8 + RAND:3
ELSEIF ARG:1 < 3500
	LOCAL += 10 + RAND:4
ELSEIF ARG:1 < 5000
	LOCAL += 12 + RAND:5
ELSEIF ARG:1 < 7500
	LOCAL += 15 + RAND:7
ELSEIF ARG:1 < 10000
	LOCAL += 20 + RAND:9
ELSEIF ARG:1 < 20000
	LOCAL += 28 + RAND:12
ELSEIF ARG:1 < 50000
	LOCAL += 36 + RAND:15
ELSE
	LOCAL += 48 + RAND:15
ENDIF

CALL PERCENT_CAL, BASE:체력, MAXBASE:체력
SIF RESULT <= 0
	TIMES LOCAL,1.25
CALL PERCENT_CAL, BASE:기력, MAXBASE:기력
SIF RESULT <= 0
	TIMES LOCAL,1.25
;기교が高いと消耗を軽減できる
IF ABL:기교 == 2
	TIMES LOCAL,0.95
ELSEIF ABL:기교 == 3
	TIMES LOCAL,0.90
ELSEIF ABL:기교 == 4
	TIMES LOCAL,0.85
ELSEIF ABL:기교 >= 5
	TIMES LOCAL,0.80
ENDIF

;연속系コマンドは効果を小さく
SIF TFLAG:20 == 15
	TIMES LOCAL,0.35

;의상에 따른 보정
CALL CLOTH_BATTLE_HOSEI, "SEITAISEI"
LOCAL = LOCAL * RESULT / 100
;방어による補正
LOCAL = LOCAL * 100 / LIMIT(100 + (BASE:방어 - 100) / 5, 80, 160)
SIF BASE:방어 - 100 < 0
	LOCAL = LOCAL * (100 + (100 - BASE:방어) / 2) / 100
SIF BASE:방어 - 100 > 0
	LOCAL = LOCAL * 100 / LIMIT(100 + SQRT(BASE:방어 - 100), 100, 200)

;敵の레벨による補正
CALL TENTACLE_LEVEL
LOCAL = LOCAL * (200 + RESULT) / 200

SIF BASE:성내성 - LOCAL < 0
	LOCAL = BASE:성내성

IF LOCAL > 0
	FONTBOLD
	IF GAME_OPTION_CHECK_F(OPTION_스탯저하있음)
		;インスタントモードなら기초성내성も減らす
		PRINTFORML %BASENAME:2%が{LOCAL} %BASENAME:2%基礎が{LOCAL / 2}減った！
	ELSE
		PRINTFORML %BASENAME:2%が{LOCAL}減った！
	ENDIF
	FONTREGULAR
ENDIF
;피임결계の消滅
IF TALENT:피임결계 > 0 && BASE:성내성 > 0 && BASE:성내성 - LOCAL < 1
	PRINTW 
	CALL MESSAGE_PREGNANCY_SHIELD_COLLAPSED
	PRINTW 
ENDIF
BASE:성내성 -= LOCAL
IF GAME_OPTION_CHECK_F(OPTION_스탯저하있음)
	;インスタントモードなら기초성내성も減らす
	BASE:TARGET:기초성내성 -= LOCAL / 2
ENDIF

;--------------------------------------------------
;食いしばり効果
@PALAM_GUTS_F(ARGS, ARG)
#FUNCTION
LOCAL = 0
SIF ARG < 2
	RETURNF 0
IF SELECTCOM >= 100
	IF ARGS == "TAIRYOKU" && BASE:체력 >= MAXBASE:체력 / 20 && BASE:체력 <= ARG
		LOCAL = 1
	ELSEIF ARGS == "KIRYOKU" && BASE:기력 >= MAXBASE:기력 / 20 && BASE:기력 <= ARG
		LOCAL = 1
	ELSEIF ARGS == "SEITAISEI" && BASE:성내성 >= MAXBASE:성내성 / 20 && BASE:성내성 <= ARG
		LOCAL = 1
	ENDIF
ENDIF
RETURNF LOCAL

;--------------------------------------------------
;감각LV6以上補正
;ARG:0	対象キャラ
;ARG:1	部位
@PALAM_OVERFEEL, ARG:0, ARG:1
#DIM LCOUNT
#DIM CAL_VAR
#DIM LV_VAR

CAL_VAR = 100
LV_VAR = ABL:(ARG:0):(ARG:1) - 5
IF LV_VAR > 0
	CAL_VAR = CAL_VAR * 6 / 5
	FOR LCOUNT, 0, LV_VAR - 1
		CAL_VAR = CAL_VAR * (1087 - LCOUNT) / 1000
	NEXT
ENDIF
RETURN CAL_VAR



;(性コマンド実行時に呼び出す)
;「今回の공격でメイン部位のPALAMはどの程度になりそうか？」を予測する
;10000超えるなら絶頂相当の快楽になる
@PALAM_VABCestimate(部位,ARG:0,ARG:1,ARG:2,ARG:3,ARG:4)
#DIM REF 部位,0;LOCALの参照渡し
#DIM 現在部位,1
#DIM 現在値,1

FOR LOCAL,0,감각수
		SIF ARG:LOCAL==-1
			BREAK

		現在部位=ARG:LOCAL
		現在値=部位:現在部位

		;감각LV6以上補正
		CALL PALAM_OVERFEEL, TARGET, 現在部位
		現在値 = 現在値 * RESULT / 100

		;体勢に関する補正
		CALL PALAM_HOSEI_POSE, 現在値
		現在値 = RESULT

		;성내성に関する補正
		CALL PALAM_HOSEI_SEITAISEI, 現在値
		現在値 = RESULT

		;素質に関する補正
		CALL PALAM_HOSEI_TALENT, 現在部位, 現在値
		現在値 = RESULT

		;성격に関する補正
		CALL PALAM_HOSEI_SEIKAKU, 現在部位, 現在値
		現在値 = RESULT

		;촉수に関する補正
		CALL PALAM_HOSEI_TENTACLE, 現在部位, 現在値
		現在値 = RESULT

		;予測結果
		TFLAG:100+=現在値/10000
NEXT

RETURN
