;추가 고문 관련
;LOCAL:0 쾌Ｃ
;LOCAL:1 쾌Ｖ
;LOCAL:2 쾌Ａ
;LOCAL:3 쾌Ｂ
;LOCAL:4 윤활
;LOCAL:5 공순
;LOCAL:6 습득
;LOCAL:7 욕정
;LOCAL:8 굴복
;LOCAL:9 치정
;LOCAL:10 고통
;LOCAL:11 공포

;추가 부위 결정
;**********************************************************
@SEX_COMEX_RANDOM, ARG, ARG:1 = 0
;AT_RAND
;AT_FLAG
;SHIELDED
;**********************************************************
#DIM LCOUNT
#DIM AT_RAND
#DIM AT_FLAG
#DIM SHIELDED, 감각수 + 1

AT_FLAG = 0
;고문할 부위 결정
FOR LCOUNT, 0, 감각수
	AT_RAND = 30

	;레벨 보정
	CALL TENTACLE_LEVEL
	SIF RESULT > 30
		RESULT = 30
	AT_RAND += RESULT

	;각 촉수는 그 이름에 해당하는 부위를 고문할 확률이 높다
	SIF LCOUNT + 1 == FLAG:11
		AT_RAND += 25
	;최종 보스 촉수는 모든 부위를 고문할 확률이 높다
	SIF FLAG:10
		AT_RAND += 15
	;판정
	SIF RAND:100 < AT_RAND
		SETBIT AT_FLAG, LCOUNT
NEXT
;고유의 고문 패턴이 있는 경우
IF ARG:1 > 0
	AT_FLAG = ARG:1
ELSEIF ARG:1 < 0
	AT_FLAG = 0
ENDIF

;조교 대상이 남자일 때 Ｖ공격이 있을 경우, Ｃ공격이나 Ａ공격으로 변경
IF (AT_FLAG & 2) && ISMALE()
	AT_FLAG -= 2
	IF RAND:2 == 0
		SETBIT AT_FLAG, 0
	ELSE
		SETBIT AT_FLAG, 2
	ENDIF
ENDIF

;상대가 세뇌/흑화인 동시에 기생되어 있지 않은 경우, 추가 고문 플래그를 리셋
IF ENEMY_TYPE_CHECK_F("AKUOTI") && ISTENTACLER(FLAG:111) == 0
	AT_FLAG = 0
	;Ｃ공격 계통에서 남자가 아니라면 50% 확률로 Ｖ공격 플래그를 세운다
	SIF ARG == Ｃ && ISFEMALE() && RAND:2 == 0
		SETBIT AT_FLAG, (Ｖ - 1)
	;Ｖ공격（ＳＰ）이라면 50% 확률로 Ａ공격 플래그를 세운다
	SIF TFLAG:10 == 1001 && RAND:2 == 0
		SETBIT AT_FLAG, (Ａ - 1)
ENDIF

;원래의 성 커맨드도 플래그를 세운다
SIF (ARG & Ｃ)
	AT_FLAG |= Ｃ
SIF (ARG & Ｖ) && ISFEMALE()
	AT_FLAG |= Ｖ
SIF (ARG & Ａ)
	AT_FLAG |= Ａ
SIF (ARG & Ｂ)
	AT_FLAG |= Ｂ

;부위 결계의 유무 확인
SHIELDED:감각수 = 0
FOR LCOUNT, 0, 감각수
	IF BASE:(LCOUNT + 30) > 0
		SETBIT SHIELDED:감각수, LCOUNT
		SHIELDED:LCOUNT = 1
	ELSE
		SHIELDED:LCOUNT = 0
	ENDIF
NEXT

;고문 부위가 전부 부위 결계에 막혔다
IF (AT_FLAG & ~SHIELDED:감각수) == 0 && ARG:1 == 0
	;고문하지 않은 부위가 있으면 한 군데 추가
	REPEAT 감각수
		LOCAL:COUNT = 0
	REND
	LCOUNT = 0
	REPEAT 감각수
		GETBIT AT_FLAG, COUNT
		IF RESULT == 0 && (TALENT:남자 < 1 || COUNT != 1)
			LOCAL:LCOUNT = COUNT
			LCOUNT += 1
		ENDIF
	REND
	SIF LCOUNT
		SETBIT AT_FLAG, LOCAL:(RAND:LCOUNT)
;구버전까지 사용된 코드
;	;DO
;	AT_RAND = RAND:감각수
;		;조교 대상이 남자
;		SIF (ISMALE() && AT_RAND == 1) || SHIELDED:AT_RAND
;			AT_RAND = -1
;	;LOOP AT_RAND == -1
;	SETBIT AT_FLAG, AT_RAND
ENDIF

RETURN AT_FLAG & ~SHIELDED:감각수, AT_FLAG & SHIELDED:감각수



;추가 부위 고문 실행
;**********************************************************
@SEX_COMEX, ARG:0, ARG:1, ARG:2
;ARG:0	원래의 성 커맨드가 어떤 부위를 고문할 것인지（비트）
;	0：없음　1：Ｃ　2：Ｖ　4：Ａ　8：Ｂ
;ARG:1	커맨드의 강도（변수）
;	0：약　1：강　2：SP
;ARG:2	추가 고문 실행 부위（비트）
;	1：Ｃ　2：Ｖ　4：Ａ　8：Ｂ
;AT_PART	추가 고문 실행 부위
;		0：Ｃ　1：Ｖ　2：Ａ　3：Ｂ　각각 1이면 실행, 2는 본래의 커맨드로 고문
;AT_MAG		
;SHIEDED	
;L_VAR		
;**********************************************************
#DIM LCOUNT
#DIM CAL_VAR, 2
#DIM AT_PART, 감각수
#DIM AT_MAG, 감각수
#DIM SHIEDED, 감각수
#DIM L_VAR, 15

;지역 변수 초기화
VARSET L_VAR:LCOUNT

;고문 상황을 지역 변수로 취득
CAL_VAR:0 = 1
FOR LCOUNT, 0, 감각수
	IF ARG:2 & CAL_VAR:0
		AT_PART:LCOUNT = 1
		AT_MAG:LCOUNT = 100
	ELSE
		AT_PART:LCOUNT = 0
	ENDIF
	IF BASE:(LCOUNT + 30) > 0
		SHIEDED:LCOUNT = 1
	ELSE
		SHIEDED:LCOUNT = 0
	ENDIF
	CAL_VAR:0 *= 2
NEXT

;본래의 성 커맨드 부위
FOR LCOUNT, 0, 감각수
	IF ARG:0 & POWER(2, LCOUNT)
		AT_PART:LCOUNT = 2
		AT_MAG:LCOUNT = 200
	ENDIF
NEXT

;부위 결계・장소수(箇所数)에 따른 배율의 변화
;CAL_VAR:0	막힌 배율의 총량
;CAL_VAR:1	통한 고문의 수
CAL_VAR:0 = 0
CAL_VAR:1 = 0
FOR LCOUNT, 0, 감각수
	;장소수(箇所数)에 따라 경감
	SIF AT_PART:0 + AT_PART:1 + AT_PART:2 + AT_PART:3 > 2
		AT_MAG:LCOUNT = AT_MAG:LCOUNT * (130 - (AT_PART:0 + AT_PART:1 + AT_PART:2 + AT_PART:3) * 15) / 100

	IF AT_PART:LCOUNT
		IF SHIEDED:LCOUNT
			CAL_VAR:0 += AT_MAG:LCOUNT
		ELSE
			CAL_VAR:1 += 1
		ENDIF
	ENDIF
NEXT

FOR LCOUNT, 0, 감각수
	IF AT_PART:LCOUNT > 0
		;각 쾌~ 추가 BASE
		IF LCOUNT != 2
			IF ABL:LCOUNT == 0
				L_VAR:LCOUNT = 400
			ELSEIF ABL:LCOUNT == 1
				L_VAR:LCOUNT = 1000
			ELSEIF ABL:LCOUNT == 2
				L_VAR:LCOUNT = 2000
			ELSEIF ABL:LCOUNT == 3
				L_VAR:LCOUNT = 4000
			ELSEIF ABL:LCOUNT == 4
				L_VAR:LCOUNT = 6000
			ELSEIF ABL:LCOUNT >= 5
				L_VAR:LCOUNT = 8000
			ENDIF
		ELSE
			;쾌Ａ　감각이 낮을 때는 적다
			IF CONFIG_CHECK_BALANCE_F(7) == 0
				IF ABL:LCOUNT == 0
					L_VAR:LCOUNT = 40
				ELSEIF ABL:LCOUNT == 1
					L_VAR:LCOUNT = 400
				ELSEIF ABL:LCOUNT == 2
					L_VAR:LCOUNT = 1000
				ELSEIF ABL:LCOUNT == 3
					L_VAR:LCOUNT = 2000
				ELSEIF ABL:LCOUNT == 4
					L_VAR:LCOUNT = 4000
				ELSEIF ABL:LCOUNT >= 5
					L_VAR:LCOUNT = 10000
				ENDIF
			;컨피그 ON일 시 쾌Ａ 증가
			ELSEIF CONFIG_CHECK_BALANCE_F(7) > 0
				IF ABL:LCOUNT == 0
					L_VAR:LCOUNT = 400
				ELSEIF ABL:LCOUNT == 1
					L_VAR:LCOUNT = 1000
				ELSEIF ABL:LCOUNT == 2
					L_VAR:LCOUNT = 2000
				ELSEIF ABL:LCOUNT == 3
					L_VAR:LCOUNT = 4000
				ELSEIF ABL:LCOUNT == 4
					L_VAR:LCOUNT = 6000
				ELSEIF ABL:LCOUNT >= 5
					L_VAR:LCOUNT = 8000
				ENDIF
			ENDIF
		ENDIF

	;막힌 만큼의 배율을 통과한 장소에 가산
	AT_MAG:LCOUNT += CAL_VAR:0 / CAL_VAR:1
	;부위 배율 적용
	L_VAR:LCOUNT = L_VAR:LCOUNT * AT_MAG:LCOUNT / 100
	ENDIF
NEXT

;부위별로 처리
;Ｃ
IF AT_PART:0 == 1
	;페니스가 있다면 효과가 크다
	SIF ISMANLY() || TALENT:후타나리 > 0
		L_VAR:0 += 100
ENDIF

;Ｖ
IF AT_PART:1 == 1
;Ｖ방어 효과
	SIF TCVARn:2 == 자세：Ｖ방어
		L_VAR:1 /= 4

;Ｖ경험+1
	SIF SHIEDED:1 == 0
		EXP:Ｖ경험 += 1

;처녀라면 공포가 조금 들어간다
	CAL_VAR:0 = 0
	IF TALENT:처녀 > 0
		IF ABL:Ｖ감각 == 0
			CAL_VAR:0 = 200
		ELSEIF ABL:Ｖ감각 == 1
			CAL_VAR:0 = 100
		ELSEIF ABL:Ｖ감각 == 2
			CAL_VAR:0 = 20
		ENDIF
	ENDIF
	SIF SHIEDED:1
		CAL_VAR:0 /= 2
	L_VAR:11 += CAL_VAR:0

ENDIF

;Ａ
IF AT_PART:2 == 1
;Ａ경험+1
	SIF SHIEDED:2 == 0
		EXP:Ａ경험 += 1

;감각과 윤활이 낮으면 고통이 들어간다
	CAL_VAR:0 = 0
	IF ABL:Ａ감각 < 3
		IF PALAM:윤활 < 100
			CAL_VAR:0 += 1000
		ELSEIF PALAM:윤활 < 10000
			CAL_VAR:0 += 100
		ELSE
			CAL_VAR:0 += 10
		ENDIF
	ELSE
		IF PALAM:윤활 < 100
			CAL_VAR:0 += 100
		ELSEIF PALAM:윤활 < 10000
			CAL_VAR:0 += 10
		ELSE
		ENDIF
	ENDIF
	SIF SHIEDED:2 == 0
		L_VAR:10 += CAL_VAR:0

;감각이 낮으면 공포가 들어간다
	CAL_VAR:0 = 0
	IF ABL:Ａ감각 == 0
		CAL_VAR:0 += 1000
	ELSEIF ABL:Ａ감각 == 1
		CAL_VAR:0 += 500
	ELSEIF ABL:Ａ감각 == 2
		CAL_VAR:0 += 200
	ELSE
	ENDIF
	SIF SHIEDED:2
		CAL_VAR:0 /= 2
	L_VAR:11 += CAL_VAR:0

ENDIF

;Ｂ
IF AT_PART:3 == 1
;해당 처리 없음

ENDIF

RETURN L_VAR:0, L_VAR:1, L_VAR:2, L_VAR:3, L_VAR:4, L_VAR:5, L_VAR:6, L_VAR:7, L_VAR:8, L_VAR:9, L_VAR:10, L_VAR:11
