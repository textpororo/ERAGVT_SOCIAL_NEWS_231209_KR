;V攻め強

;性コマンド分類
@SEX_TYPE_COM3
RETURN 삽입

@SEX_COM3,ARG = 0,ARG:1 = 0
;EX_COM	追加責め実行部位（ビット）
;	1：Ｃ　2：Ｖ　4：Ａ　8：Ｂ
;追加責め
CALL SEX_COMEX_RANDOM, Ｖ, ARG:1
EX_COM = RESULT:0
SH_COM = RESULT:1

;청순파なら１度だけ自動でＶ방어
SIF TALENT:청순파 > 0 && TCVARn:2 != 100
	CALL AUTO_V_DEFENCE, TARGET

;사정部位フラグ:膣を立てる、Ｖ방어しているなら口を立てる
IF TCVARn:2 == 자세：Ｖ방어 || BASE:V결계내구력 > 0
	TFLAG:4 = TFLAG:4 | 입
	;結界がなければワレメ사정플래그를 세운다
	SIF BASE:V결계내구력 <= 0
		TFLAG:4 = TFLAG:4 | 균열
ELSE
	TFLAG:4 = TFLAG:4 | 질
	INSERT = INSERT | 질
ENDIF
;Ａ追加攻めがあるなら애널사정플래그를 세운다
IF (EX_COM & Ａ)
	TFLAG:4 = TFLAG:4 | 애널
	INSERT = INSERT | 애널
ENDIF

;흑화 캐릭터のペニス位置をセット
SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1 && (ISPENIS(FLAG:111))
	TFLAG:18 = 질

;取得パラメータ計算
;ベース値の明示的初期化
REPEAT 12
	LOCAL:COUNT = 0
REND

;油断度 상승
TFLAG:3 += 50

;사정値 250
CALL TENTACLE_SYASEI_UP, 250

;촉수サイズの설정
CALL SET_TENTACLE_SIZE, FLAG:10, FLAG:11, ENEMY_TYPE_CHECK_F("AKUOTI"), FLAG:111, 3

;快V
IF ABL:V감각 == 0
	LOCAL:1 = 400
ELSEIF ABL:V감각 == 1
	LOCAL:1 = 1000
ELSEIF ABL:V감각 == 2
	LOCAL:1 = 2000
ELSEIF ABL:V감각 == 3
	LOCAL:1 = 4000
ELSEIF ABL:V감각 == 4
	LOCAL:1 = 10000
ELSEIF ABL:V감각 >= 5
	LOCAL:1 = 20000
ENDIF
;Ｖ방어効果
SIF TCVARn:2 == 자세：Ｖ방어
	LOCAL:1 /= 4
;メイン部位絶頂見込みの予測
CALL PALAM_VABCestimate(LOCAL,1,-1)


IF ENEMY_TYPE_CHECK_F("MOB") == 1
	TRYCALLFORM MESSAGE_MOB_{ARG}_COM3, EX_COM, SH_COM
	;처녀なら고통が大量に入る
	IF TALENT:처녀 > 0 && TCVARn:2 != 자세：Ｖ방어 && BASE:V결계내구력 <= 0
		LOCAL:10 = 10000
		TALENT:처녀 = -1
		CFLAG:206 = 2
		TCVARn:1 = 1
	ELSE
		LOCAL:10 = 0
	ENDIF
ELSE
	;처녀なら고통が大量に入る
	IF TALENT:처녀 > 0 && TCVARn:2 != 자세：Ｖ방어 && BASE:V결계내구력 <= 0
		LOCAL:10 = 10000
		CALL LOSTVIRGIN
	ELSE
		LOCAL:10 = 0
	ENDIF
	;지문: 세뇌/흑화 캐릭터가 操作キャラにV攻め強をした
	SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
		CALL MESSAGE_OTHER_SEX_COM3
	;지문: COM3
	CALL MESSAGE_SEX_COM3, EX_COM, SH_COM
ENDIF

;結界による촉수本数の変動
CALL SET_TENTACLE_POOL
;服へのダメージ
CALL CLOTH_BATTLE_DAMAGE, 8

;ベース
IF TCVARn:2 == 자세：Ｖ방어
	EXP:V경험 += 1
	SIF FLAG:15 >= FLAG:14
		EXP:펠라경험 += 1
ELSEIF EX_COM & Ｖ
	EXP:V경험 += 2
	SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 0 && INCEST_F(TARGET, FLAG:10 * 100 + FLAG:11, 1) > 0
		EXP:근친교배경험 += 1
	SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1 && INCEST_F(TARGET, FLAG:111) > 0
		EXP:근친교배경험 += 1
ENDIF



;공순
IF ABL:순종 <= 2
	LOCAL:5 = 200
ELSEIF ABL:순종 == 3
	LOCAL:5 = 500
ELSEIF ABL:순종 == 4
	LOCAL:5 = 1000
ELSEIF ABL:순종 >= 5
	LOCAL:5 = 2000
ENDIF

;습득
IF ABL:봉사정신 == 0
	LOCAL:6 = 20
ELSEIF ABL:봉사정신 == 1
	LOCAL:6 = 50
ELSEIF ABL:봉사정신 == 2
	LOCAL:6 = 100
ELSEIF ABL:봉사정신 == 3
	LOCAL:6 = 200
ELSEIF ABL:봉사정신 == 4
	LOCAL:6 = 500
ELSEIF ABL:봉사정신 >= 5
	LOCAL:6 = 1000
ENDIF

;윤활が低いと고통が入る
IF PALAM:윤활 < 1000
	LOCAL:10 += 5000
ELSEIF PALAM:윤활 < 2000
	LOCAL:10 += 1000
ENDIF
SIF BASE:V결계내구력 > 0
	LOCAL:10 = 0

;V감각が低いと공포が入る
IF ABL:V감각 == 0
	LOCAL:11 = 4000
ELSEIF ABL:V감각 == 1
	LOCAL:11 = 1000
ELSEIF ABL:V감각 == 2
	LOCAL:11 = 400
ELSEIF ABL:V감각 == 3
	LOCAL:11 = 200
ELSE
ENDIF
SIF BASE:V결계내구력 > 0
	LOCAL:11 /= 2

;追加責め実行
IF EX_COM
	CALL SEX_COMEX, Ｖ, 1, EX_COM | SH_COM
	REPEAT 12
		LOCAL:COUNT += RESULT:COUNT
	REND
ENDIF

;성내성が残り50%以上なら派生しにくい
IF PERCENT_CAL_F(BASE:성내성, MAXBASE:성내성) >= 50 && RAND:2 == 0
;ＶＡ挿入成功＆사정 게이지不足なら両穴ピストン＋강구속状態
ELSEIF (EX_COM & Ｖ) && (EX_COM & Ａ) && FLAG:15 < FLAG:14 && BASE:V결계내구력 == 0 && BASE:A결계내구력 == 0 && TCVARn:2 != 자세：Ｖ방어 && (ENEMY_TYPE_CHECK_F("MOB") == 0 && ENEMY_TYPE_CHECK_F("CITIZEN") == 0) && ISFEMALE()
	TFLAG:17 = 19
	;状態異常：강구속
	CALL STATE_CHANGE_PKOUSOKU, 100
;Ｖ挿入成功＆사정 게이지不足なら種付けピストン＋강구속状態
ELSEIF FLAG:15 < FLAG:14 && BASE:V결계내구력 == 0 && TCVARn:2 != 자세：Ｖ방어 && (ENEMY_TYPE_CHECK_F("MOB") == 0 && ENEMY_TYPE_CHECK_F("CITIZEN") == 0) && ISFEMALE()
	TFLAG:17 = 15
	;状態異常：강구속
	CALL STATE_CHANGE_PKOUSOKU, 100
ELSE
	;行動予約なし
	TFLAG:17 = -1
ENDIF
;촉수の行動を記録
TFLAG:20 = 3

;습득基本値
LOCAL:6 += 1500

;욕정基本値
LOCAL:7 += 0

;굴복基本値
LOCAL:8 += 2500

;치정基本値
LOCAL:9 += 200

;고통基本値
LOCAL:10 += 100

;공포基本値
LOCAL:11 += 1000

;체력消費基本値
LOCAL:12 += 200

;計算へ
CALL PALAM_CAL, LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12
