;노려본다
@COM44

;거리 표시
CALL PRINT_DISTANCE
PRINTL 

;방심도 상승
TFLAG:3 += 25

;방어 플래그：노려본다로 한다
TCVARn:2 = 자세：노려본다

;게이지가 증가
IF CFLAG:99 < 5
	TCVARn:6 += 20 + RAND:16
ELSEIF CFLAG:99 < 15
	TCVARn:6 += 15 + RAND:11
ELSE
	TCVARn:6 += 10 + RAND:6
ENDIF

;기력이 5% 회복　피로의 영향이 없다
;회복빠름이라면 +1%
;회복느림이라면 -1%
;최대값과 의상 및 방어력에 따른 보정에서 회복량 계산
CALL CLOTH_BATTLE_HOSEI, "BOUGYO"
LOCAL:1 = MAXBASE:기력 + BASE:방어 * RESULT * 4 / 200
;FEAT에 의한 회복량 보너스
SIF TALENT:넘치는생명력 > 0
	TIMES LOCAL:1, 1.25
IF TALENT:회복빠름 == 1
	TIMES LOCAL:1, 0.06
ELSEIF TALENT:회복느림 == 1
	TIMES LOCAL:1, 0.04
ELSE
	TIMES LOCAL:1, 0.05
ENDIF
SIF BASE:기력 + LOCAL:1 >= MAXBASE:기력
	LOCAL:1 = MAXBASE:기력 - BASE:기력
;기력이 다한 경우, 기력이 회복되지 않는다
SIF BASE:기력 == 0
	LOCAL:1 = 0
BASE:기력 += LOCAL:1

;지문: 노려본다
CALL MESSAGE_BATTLE_CHARA_NIRAMI
;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의「노려본다」를 봤다
SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
	CALL MESSAGE_OTHER_BATTLE_CHARA_NIRAMI

;행동 포인트를 +1
EX:행동포인트 += 1

IF BASE:기력
	PRINTFORML %BASENAME:1%이 {LOCAL:1} 회복됐다.
ELSE
	PRINTFORML 이미 %BASENAME:1%을 다하여 회복되지 않았다……
ENDIF
PRINTFORML ＥＸ 게이지가 조금 쌓였다！
PRINTL 

RETURN 1
