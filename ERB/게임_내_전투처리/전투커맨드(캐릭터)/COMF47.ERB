;설득해 본다
@COM47

;거리 표시
CALL PRINT_DISTANCE
PRINTL 

;방심도 상승
TFLAG:3 += 5

;방어 플래그：설득해본다로 한다
TCVARn:2 = 자세：설득해본다

;지문: 설득해 본다
CALL MESSAGE_BATTLE_CHARA_PERSUADE
;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의「설득해본다」를 봤다
SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
	CALL MESSAGE_OTHER_BATTLE_CHARA_PERSUADE

;성패 여부
LOCAL:0 = MAXBASE:지성

;의상에 따른 보정
CALL CLOTH_BATTLE_HOSEI, "CHISEI"
LOCAL:0 = LOCAL:0 * RESULT / 100

;심경에 의한 보정
CALL SHINKYOU_CHECK, "CHISEI", LOCAL:0
LOCAL:0 = RESULT

;보스 촉수/적 캐릭터의 지성
IF ENEMY_TYPE_CHECK_F("AKUOTI")
	LOCAL:1 = MAXBASE:(FLAG:111):지성
ELSE
	CALL TENTACLE_ACCESS, "CHISEI"
	LOCAL:1 = RESULT
ENDIF

CALL PERCENT_CAL, LOCAL:0, LOCAL:1
IF RAND:360 + 90 < RESULT
	LOCAL = 650 + RAND:RESULT / 2
	LOCALS = {LOCAL}
	TOFULL LOCALS
	TFLAG:3 += LOCAL
	PRINTFORML %PRINT_CALLNAME(TARGET)%의 박진하고 절실한 말에 상대는 동요하고 있다！
	PRINTFORML 방심도 ＋（%RESULTS%）
ELSEIF RAND:240 + 60 < RESULT
	LOCAL = 400 + RAND:RESULT / 2
	LOCALS = {LOCAL}
	TOFULL LOCALS
	TFLAG:3 += LOCAL
	PRINTFORML 상대방을 조금은 동요시킬 수 있었던 것 같다・・・
	PRINTFORML 방심도 ＋（%RESULTS%）
ELSEIF RAND:180 + 30 < RESULT
	LOCAL = 250 + RAND:RESULT / 4
	LOCALS = {LOCAL}
	TOFULL LOCALS
	TFLAG:3 += LOCAL
	PRINTFORML 상대방의 눈동자 속에 희미한 변화가 있었던 것처럼 보인다・・・
	PRINTFORML 방심도 ＋（%RESULTS%）
ELSE
	PRINTFORML 상대는 전혀 귀를 기울이지 않는 눈치다・・・
ENDIF
PRINTL 

;행동 포인트를 +1
EX:행동포인트 += 1

IF TCVARn:1 > 0 && RAND:100 < (TCVARn:11 * 10)
	TCVARn:1 = 6
	PRINTFORM %PRINT_CALLNAME(TARGET)%의 심경이 
	CALL SHINKYOU_CHECK, "PRINT", 0
	PRINTL 으로 변화했다.
ELSE
	TCVARn:11 ++
ENDIF
PRINTL 
RETURN 1
