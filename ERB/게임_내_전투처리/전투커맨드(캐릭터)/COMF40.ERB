;떼어낸다
@COM40

;각인에 의한 행동 제한 판정　１이면 행동할 수 없다
CALL ACT_LIMIT
SIF RESULT == 1
	RETURN 1

;거리 표시
CALL PRINT_DISTANCE
PRINTL 

;지문: 떼어낸다
CALL MESSAGE_BATTLE_CHARA_HIKIHAGASU

;성공 확률 계산
CALL COM40_HANTEI
LOCAL = RESULT

;성공 판정
;Ａ촉수는 강제 실패
IF ENEMY_TYPE_CHECK_F("BOSS") == 1 && FLAG:11 == 3
;성공
ELSEIF RAND:100 < LOCAL
	;보스의 자세가 흐트러진다
	TFLAG:1 = 1
	SIF (TCVARn:12 & 강구속)
		TCVARn:12 -= 강구속
ENDIF

IF TFLAG:1 == 1
	;지문: 떼어낸다 성공
	CALL MESSAGE_BATTLE_CHARA_HIKIHAGASU_SUCCESS
	;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의「떼어낸다」를 막지 못했다
	SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
		CALL MESSAGE_OTHER_BATTLE_CHARA_HIKIHAGASU_SUCCESS
ELSE
	;지문: 떼어낸다 실패
	CALL MESSAGE_BATTLE_CHARA_HIKIHAGASU_FALSE
	;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의「떼어낸다」를 막았다
	SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
		CALL MESSAGE_OTHER_BATTLE_CHARA_HIKIHAGASU_FALSE
ENDIF
;행동 포인트를 +1
EX:행동포인트 += 1

RETURN 1


;성공 확률 산출
@COM40_HANTEI
VARSET LOCAL

;캐릭터의 체력＋기력의 잔량을 구한다
LOCAL:0 = PERCENT_CAL_F(BASE:체력 * 2 + BASE:기력, MAXBASE:체력 * 2 + MAXBASE:기력)
;소모도 최저치는 방어력 의존
SIF LOCAL:0 < MIN(SQRT(MAX(BASE:방어 - 100, 0)) * 8, 75)
	LOCAL:0 = MIN(SQRT(MAX(BASE:방어 - 100, 0)) * 8, 75)
;마비로 잔량 반감
SIF (TCVARn:12 & 마비)
	LOCAL:0 /= 2
;SP변신 중에는 체력 및 기력의 감소로 인한 영향을 받지 않다
SIF CFLAG:1 == 2
	LOCAL:0 = 100
;효과는 1/4
LOCAL:0 /= 4

;적의 체력 잔량에 따른 보정
CALL PERCENT_CAL, FLAG:13, FLAG:12
IF RESULT > 50
	LOCAL:1 = 75
ELSEIF RESULT > 25
	LOCAL:1 = 100
ELSE
	LOCAL:1 = 125
ENDIF

;기본 성공 확률
LOCAL:2 = (LOCAL:0 + 40) * LOCAL:1 / 100

;「날뛴다」가 성공한 다음 턴은 풀어내기가 쉬워진다
SIF TCVARn:2 == 자세：날뛴다방어
	LOCAL:2 += 25
;「견딘다」를 택한 다음의 턴은 풀어내기가 약간 쉬워진다
SIF TCVARn:2 == 자세：견딘다
	LOCAL:2 += 10
;「노려본다」를 택한 다음의 턴은 풀어내기가 쉬워진다
SIF TCVARn:2 == 자세：노려본다
	LOCAL:2 += 20
;「매도/반항한다」를 택한 다음의 턴은 풀어내기가 조금 쉬워진다
SIF TCVARn:2 == 자세：매도한다
	LOCAL:2 += 15
SIF TCVARn:2 == 자세：반항한다
	LOCAL:2 += 15
;Ａ촉수를 풀어내는 것은 매우 어렵다
SIF ENEMY_TYPE_CHECK_F("BOSS") == 1 && FLAG:11 == 3
	LOCAL:2 = 1
;EX:방을 사용하고 있는 경우 확률 대상승
SIF GETBIT(TCVARn:3, 1)
	LOCAL:2 += 50

RETURN LOCAL:2

