;SP변신
@COM73

;거리 표시
CALL PRINT_DISTANCE
PRINTL 

;지문: SP변신
CALL MESSAGE_BATTLE_CHARA_SP_TRANSFORMCALL

;SP모드
CFLAG:1 = 2
TCVARn:5 = TCVARn:4
TCVARn:4 = 0

;상태 이상이 회복된다
IF TCVARn:12 > 0
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%의 상태 이상이 회복됐다！
	PRINTL 
	TCVARn:12 = 0
ENDIF

;심경의 변화
;성격의 성장 보정과 심경의 스테이터스 보정이 가까운 것
CALL SEIKAKU_CHECK, "GET_TALENT_VALUE", TARGET

;SP변신 구상부터 계속해서 심경 구상으로 이어지면 부자연스러워서, 지문을 불러내지 않고 여기서 직접 쓴다
PRINTFORM %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 

;「분노」로 변하는 성격：게으름, 고풍적, 사나움
IF RESULT == 15 || RESULT == 24 || RESULT == 27
	TCVARn:1 = 2
	PRINT 분노
;「냉정」으로 변하는 성격：겁쟁이, 부끄럼쟁이, 대범함, 낙천적, 비관적, 과묵함
ELSEIF RESULT == 10 || RESULT == 11 || RESULT == 14 || RESULT == 17 || RESULT == 18 || RESULT == 22
	TCVARn:1 = 4
	PRINT 냉정
;나머지 성격들은「고양」으로 변한다
ELSE
	TCVARn:1 = 6
	PRINT 고양
ENDIF

PRINTL  상태가 됐다.
;심경의 지속 턴을 리셋
TCVARn:11 = 0

;겉옷(변신 후)의 피해량이 모두 회복된다
SIF CFLAG:41 != 0
	TCVARn:23 = TCVARn:22

;상대를 기죽인다
TFLAG:1 = 1

;반동으로 피로 축적
TFLAG:99 += 3

;자동으로 전투 메뉴를 선택한다
TCVARn:8 = 1

RETURN 1
