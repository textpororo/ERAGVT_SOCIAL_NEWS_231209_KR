
;공격コマンドの共通処理
@COM_ATTACK_COMMON
#DIM ATTACK_NUM
#DIM HIT_FLAG
VARSET LOCAL
ATTACK_NUM = 0
HIT_FLAG = 0


;전투스타일チェンジスキーム
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_BATTLE_STYLE_CHANGE")

;[連続]スタイルによる공격回数アップ
SIF FSTYLE_NAME_F(TARGET, TCVARn:0) == "連続"
	ATTACK_NUM += 1
;[連続]バーストによる공격回数アップ
SIF GETBIT(TCVARn:217, 0) && FSTYLE_NAME_F(TARGET, TCVARn:0) == "連続"
	ATTACK_NUM += 4
;[使役]スタイルによる공격回数アップ
SIF GETBIT(TCVARn:217, 0) == 0 && FSTYLE_NAME_F(TARGET, TCVARn:0) == "使役"
	ATTACK_NUM += 1

;距離表示
CALL PRINT_DISTANCE
PRINT 　
;武器詳細表示
FONTBOLD
SIF CSTR:(TCVARn:0 + 4) != ""
	PRINTFORM 『%CSTR:(TCVARn:0 + 4)%』
PRINTFORML <<%FSTYLE_NAME_F(TARGET,TCVARn:0)%>>
FONTREGULAR
;コンボ表示
CALL PRINT_COMBO
PRINTL 

;지문
IF TCVARn:0 == 1
	CALL MESSAGE_BATTLE_CHARA_ATTACK_RANGE_SHORT
ELSEIF TCVARn:0 == 2
	CALL MESSAGE_BATTLE_CHARA_ATTACK_RANGE_MIDDLE
ELSEIF TCVARn:0 == 3
	CALL MESSAGE_BATTLE_CHARA_ATTACK_RANGE_LONG
ENDIF

$ATTACK_AGAIN
;命中判定
IF TCVARn:0 == 1
	CALL ACT_HANTEI_CHARA_TO_TENTACLE, "ATTACK_RANGE_SHORT"
ELSEIF TCVARn:0 == 2
	CALL ACT_HANTEI_CHARA_TO_TENTACLE, "ATTACK_RANGE_MIDDLE"
ELSEIF TCVARn:0 == 3
	CALL ACT_HANTEI_CHARA_TO_TENTACLE, "ATTACK_RANGE_LONG"
ENDIF
IF ENEMY_TYPE_CHECK_F("LASTBOSS") >= 1 && FLAG:11 == 2 && FLAG:21 == 1 && (TFLAG:13 & TCVARn:0) == 0
	PRINTFORML 待ち構えていた촉수に本体への공격を阻まれた……
	PRINTL 
	RESULT = 0
ENDIF
IF RESULT == 0
	;カス当たりの判定
	IF TCVARn:0 == 1
		CALL ACT_HANTEI_CHARA_TO_TENTACLE_GUARD, "ATTACK_RANGE_SHORT"
	ELSEIF TCVARn:0 == 2
		CALL ACT_HANTEI_CHARA_TO_TENTACLE_GUARD, "ATTACK_RANGE_MIDDLE"
	ELSEIF TCVARn:0 == 3
		CALL ACT_HANTEI_CHARA_TO_TENTACLE_GUARD, "ATTACK_RANGE_LONG"
	ENDIF

	IF RESULT
		;カス当たりフラグ
		HIT_FLAG = -1
		;チェイン加算
		TFLAG:31 += 1
		;ダメージ計算
		IF TCVARn:0 == 1
			CALL DAMAGE, "ATTACK_RANGE_SHORT"
			;[広範]スタイルはカス当たりの威力が高い
			IF FSTYLE_NAME_F(TARGET,TCVARn:0) == "広範"
				LOCAL:1 = MIN(RESULT * 100 * MIN(BASE:체력 * 100 / MAXBASE:체력 + 20, 40) / 10000 + 100, RESULT) * (RAND:26 + 75) / 100
			ELSE
				LOCAL:1 = MIN(RESULT * 25 * MIN(BASE:체력 * 100 / MAXBASE:체력 + 15, 30) / 10000 + 50, RESULT)
			ENDIF
		ELSEIF TCVARn:0 == 2
			CALL DAMAGE, "ATTACK_RANGE_MIDDLE"
			;[広範]スタイルはカス当たりの威力が高い
			IF FSTYLE_NAME_F(TARGET,TCVARn:0) == "広範"
				LOCAL:1 = MIN(RESULT * 200 * MIN(BASE:체력 * 100 / MAXBASE:체력 + 15, 30) / 10000 + 100, RESULT) * (RAND:51 + 50) / 100
			ELSE
				LOCAL:1 = MIN(RESULT * 100 * MIN(BASE:체력 * 100 / MAXBASE:체력 + 10, 20) / 10000 + 50, RESULT)
			ENDIF
		ELSEIF TCVARn:0 == 3
			CALL DAMAGE, "ATTACK_RANGE_LONG"
			;[広範]スタイルはカス当たりの威力が高い
			IF FSTYLE_NAME_F(TARGET,TCVARn:0) == "広範"
				LOCAL:1 = MIN(RESULT * 300 * MIN(BASE:체력 * 100 / MAXBASE:체력 + 10, 20) / 10000 + 100, RESULT) * (RAND:76 + 25) / 100
			ELSE
				LOCAL:1 = MIN(RESULT * 300 * MIN(BASE:체력 * 100 / MAXBASE:체력 + 5, 10) / 10000 + 50, RESULT)
			ENDIF
		ENDIF
		FLAG:13 -= LOCAL:1
		;지문: カス当たり
		CALL MESSAGE_BATTLE_CHARA_ATTACK_GUARD, ATTACK_NUM
		;지문: 세뇌/흑화 캐릭터가 操作キャラにカス当たりされた
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_BATTLE_CHARA_ATTACK_GUARD, ATTACK_NUM
		FONTBOLD
		PRINTFORML {LOCAL:1}のダメージを与えた！
		FONTREGULAR
		PRINTW 
		IF LOCAL:1 >= 5000
			CALL UNLOCK_ACHIEVEMENT(263,"파괴신의 일격")
		ENDIF
		LOCAL:2 = 1
	ELSE
		;공격ミスフラグ
		HIT_FLAG = 0
		;チェインリセット
		TFLAG:31 = 0
		;지문: 공격失敗
		CALL MESSAGE_BATTLE_CHARA_ATTACK_FALSE, ATTACK_NUM
		;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 공격を回避した
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_BATTLE_CHARA_ATTACK_FALSE, ATTACK_NUM
	ENDIF

	;心境変化：高揚か消沈
	SIF TCVARn:1 == 0 && ATTACK_NUM == 0
		CALL SHINKYOU_CHANGE, "KOUYOU_SYOUTIN"

	;방심도上昇の追加効果
	LOCAL:3 = 0
	;[設置]スタイル
	IF FSTYLE_NAME_F(TARGET,TCVARn:0) == "設置" && LOCAL:2
		LOCAL:3 += 125
		;[設置]バーストの効果
		SIF GETBIT(TCVARn:217, 0)
			LOCAL:3 += 125
	ENDIF
	IF LOCAL:3 > 0
		IF (FLAG:110 == 0)
			CALL TENTACLE_ACCESS, "NAME"
		ELSEIF (FLAG:110 == 1)
			PRINTFORM %PRINT_TRANSCALLNAME(FLAG:111)%
		ENDIF
		LOCALS = {LOCAL:3}
		TOFULL LOCALS
		PRINTFORML の방심도が少し上昇した……（＋%RESULTS%）
		PRINTW 
		FLAG:17 += LOCAL:3
	ENDIF
	;[撹乱]バーストの怯み効果10％
	IF GETBIT(TCVARn:217, 0) && FSTYLE_NAME_F(TARGET,TCVARn:0) == "撹乱" && RAND:10 == 0 && HIT_FLAG == -1
		TFLAG:1 = 1
		PRINTFORML うまく相手の体勢を崩した！
	ENDIF
ELSE
	;直撃時の処理

	;チェイン加算
	TFLAG:31 += 1
	;의상에 따른 보정
	RESULT = 0
	CALL CLOTH_BATTLE_HOSEI, "CRITICAL"
	;FEATによる치명타 보정
	SIF TALENT:야수의후손 > 0
		RESULT += 6
	SIF TALENT:숨겨진힘 > 0 && PERCENT_CAL_F(BASE:체력 + BASE:기력, MAXBASE:체력 + MAXBASE:기력) <= 25
		RESULT += 12
	SIF TALENT:심안 > 0
		RESULT += 3
	SIF TALENT:공생 > 0
		RESULT += 2
	;[撹乱]スタイルの치명타 보정
	SIF FSTYLE_NAME_F(TARGET,TCVARn:0) == "撹乱"
		RESULT += 5
	;[知略]バーストの치명타율アップ
	SIF GETBIT(TCVARn:217, 0) && FSTYLE_NAME_F(TARGET,TCVARn:0) == "知略"
		RESULT += 5
	;EVADERの치명타 보정
	SIF TFLAG:33 > 1
		RESULT += GET_EVADER_CRT(TFLAG:33 - 1)
	;방어力による치명타 보정（隠し効果）
	LOCAL:0 = MAX(BASE:방어 - 100, 0)
	IF LOCAL:0 < 0
		LOCAL:0 = ABS(LOCAL:0)
		RESULT -= MIN(SQRT(LOCAL:0) * 2 / 3 , 12)
	ELSE
		RESULT += MIN(SQRT(LOCAL:0) * 2 / 3 , 12)
	ENDIF
	;근거리はクリティカルしやすい。さらにBRAVE HIT中は追加の補正が掛かる
	IF TCVARn:0 == 1
		RESULT += 3+(TFLAG:30)*2
	ENDIF
	;중거리は少しだけクリティカルしやすい
	SIF TCVARn:0 == 2
		RESULT += 1
	;クリティカル判定
	IF RAND:100 < 5 + RESULT
		LOCAL:0 = 15
		;バースト공격時はクリティカル倍率が上昇
		SIF GETBIT(TCVARn:217, 0)
			LOCAL:0 += 5
		;숨겨진힘の隠し補正
		SIF TALENT:숨겨진힘 > 0 && PERCENT_CAL_F(BASE:체력 + BASE:기력, MAXBASE:체력 + MAXBASE:기력) <= 25
			RESULT += 5
		SIF TALENT:숨겨진힘 > 0 && PERCENT_CAL_F(BASE:체력 + BASE:기력, MAXBASE:체력 + MAXBASE:기력) <= 10
			RESULT += 5
	ELSE
		LOCAL:0 = 10
	ENDIF

	;ダメージ計算
	IF TCVARn:0 == 1
		CALL DAMAGE, "ATTACK_RANGE_SHORT"
	ELSEIF TCVARn:0 == 2
		CALL DAMAGE, "ATTACK_RANGE_MIDDLE"
	ELSEIF TCVARn:0 == 3
		CALL DAMAGE, "ATTACK_RANGE_LONG"
	ENDIF

	LOCAL:1 = RESULT * LOCAL:0 / 10
	FLAG:13 -= LOCAL:1
	LOCAL:2 = LOCAL:1
	RESULT = 0
	IF LOCAL:0 > 10
		;クリティカルフラグ
		HIT_FLAG = 2
		;지문: 공격がクリティカルヒット
		CALL MESSAGE_BATTLE_CHARA_ATTACK_CRITICAL_HIT, ATTACK_NUM
		;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 공격をクリティカルヒットされた
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_BATTLE_CHARA_ATTACK_CRITICAL_HIT, ATTACK_NUM
	ELSE
		;直撃フラグ
		HIT_FLAG = 1
		;지문: 공격が通常ヒット
		CALL MESSAGE_BATTLE_CHARA_ATTACK_HIT, ATTACK_NUM
		;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 공격を通常ヒットされた
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_BATTLE_CHARA_ATTACK_HIT, ATTACK_NUM
	ENDIF
	;とどめ演出用
	SIF FLAG:13 <= 0 && RESULT == 999
		LOCAL:1 = LOCAL:1 * 16
	FONTBOLD
	PRINTFORML {LOCAL:1}のダメージを与えた！
	FONTREGULAR
	PRINTW 
	IF LOCAL:2 >= 5000
		CALL UNLOCK_ACHIEVEMENT(263,"파괴신의 일격")
	ENDIF

	;방심도上昇の追加効果
	LOCAL:3 = 0
	;[設置]スタイル
	IF FSTYLE_NAME_F(TARGET,TCVARn:0) == "設置"
		LOCAL:3 += 300
		;[設置]バーストの効果
		SIF GETBIT(TCVARn:217, 0)
			LOCAL:3 += 300
	ENDIF
	IF LOCAL:3 > 0
		IF (FLAG:110 == 0)
			CALL TENTACLE_ACCESS, "NAME"
		ELSEIF (FLAG:110 == 1)
			PRINTFORM %PRINT_TRANSCALLNAME(FLAG:111)%
		ENDIF
		LOCALS = {LOCAL:3}
		TOFULL LOCALS
		PRINTFORML の방심도が少し上昇した……（＋%RESULTS%）
		PRINTW 
		FLAG:17 += LOCAL:3
	ENDIF
	;[撹乱]バーストの怯み効果50％、クリティカルで100％
	IF GETBIT(TCVARn:217, 0) && FSTYLE_NAME_F(TARGET,TCVARn:0) == "撹乱"
		IF HIT_FLAG == 2 || RAND:2 == 0
			TFLAG:1 = 1
			PRINTFORML うまく相手の体勢を崩した！
		ENDIF
	ENDIF
ENDIF

;追撃の発動
IF ATTACK_NUM
	ATTACK_NUM --
	GOTO ATTACK_AGAIN
ENDIF

;[全力]スタイルの反動
SIF FSTYLE_NAME_F(TARGET,TCVARn:0) == "全力"
	TFLAG:99 += 2

;バースト공격の反動
IF GETBIT(TCVARn:217, 0)
	SELECTCASE FSTYLE_NAME_F(TARGET, TCVARn:0)
		CASE "連続"
			TFLAG:99 += 2
		CASE "装甲"
			TFLAG:99 += 2
		CASE "撹乱"
			TFLAG:99 += 2
		CASE "重撃"
			TFLAG:99 += 3
		CASE "広範"
			TFLAG:99 += 3
		CASE "全力"
			TFLAG:99 += 4
		CASE "知略"
			TFLAG:99 += 1
		CASE "設置"
			TFLAG:99 += 1
		CASE "使役"
			TFLAG:99 += 3
		CASE "通常"
			TFLAG:99 += 1
	ENDSELECT
	;FEAT 효과에 따른反動増加
	SIF TALENT:핫스타트 > 0
		TFLAG:99 += 1
	SIF TALENT:풀버스트 > 0
		TFLAG:99 += 1

	;[装甲]バーストのオートガード効果
	IF FSTYLE_NAME_F(TARGET,TCVARn:0) == "装甲"
		TCVARn:2 = 자세：방어
		PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")%방어態勢になった！
	ENDIF
	;[知略]バーストの관찰한다効果
	IF FSTYLE_NAME_F(TARGET,TCVARn:0) == "知略"
		TFLAG:25 = 1
		PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")%관찰한다状態になった！
	ENDIF
ENDIF

;サディストの効果
IF TALENT:새드기질 > 0 && HIT_FLAG > 0
	;기력が微量に回復
	LOCAL:1 = RAND:MAX((MAXBASE:기력 * HIT_FLAG * 8 / 100), 1) + 10
	SIF BASE:기력 + LOCAL:1 >= MAXBASE:기력
		LOCAL:1 = MAXBASE:기력 - BASE:기력
	BASE:기력 += LOCAL:1
	;욕정が微量に増加
	COMMON_PALAM:7 += LOCAL:1 * 10
	SIF LOCAL:1 > 0
		PRINTFORML サディスティックな快楽で%BASENAME:1%が{LOCAL:1}回復した
ENDIF

RETURN 1



;空中공격フラグ管理
@ATTACK_AIR_FLAG
IF GETBIT(TCVARn:216, 1) && GETBIT(TCVARn:216, 0) == 0
	TCVARn:216 = 4
	;FEAT 효과에 따른着地時の反動
	SIF TALENT:날개 > 0
		TFLAG:99 += 1
ELSEIF GETBIT(TCVARn:216, 0) && BASE:공중대시
	SETBIT TCVARn:216, 1
	BASE:공중대시 -= 1
	;空中공격の反動
	TFLAG:99 += 1
	;FEAT 효과에 따른反動の軽減
	SIF TALENT:공중부유 > 0
		TFLAG:99 -= 1
	;FEAT 효과에 따른反動の増加
	SIF TALENT:에어마스터 > 0
		TFLAG:99 += 1
ENDIF


