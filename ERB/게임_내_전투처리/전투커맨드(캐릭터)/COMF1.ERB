;근거리 공격
@COM1
VARSET LOCAL

;각인에 의한 행동 제한 판정　１이면 행동할 수 없다
CALL ACT_LIMIT
SIF RESULT == 1
	RETURN 1
;공중 공격 플래그를 진행한다
CALL ATTACK_AIR_FLAG
;다른 거리로부터의 경우, 끈적끈적 상태라면 실패할 수가 있다
IF (TCVARn:12 & 끈적끈적) && (RAND:100 < 10 && TCVARn:0 != 1) && GETBIT(TCVARn:216, 1) == 0
	CALL PRINT_DISTANCE
	PRINTL 
	PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%의 발이 미끄러지고 말았다！
	PRINTFORML 이동을 수반하는 행동이 실패했다！
	PRINTW 
	RETURN 1
ENDIF

;동일 거리 연속 실행
IF TCVARn:0 == 1 && TFLAG:30
	TFLAG:30 += 1
ELSE
	TFLAG:30 = 1
ENDIF
;거리를 근거리로 한다
TCVARn:0 = 1

;통상 공격 공통 처리
CALL COM_ATTACK_COMMON

;근거리전투경험
IF TALENT:변신능력 != -1
	EXP:근거리전투경험 += RAND:5 + 1
	CALL SENGIUP, TARGET, 0
ELSE
	EXP:기초전투경험 += RAND:5 + 1
	CALL SENGIUP, TARGET, 3
ENDIF

;행동 포인트를 +2
EX:행동포인트 += 2

RETURN 1
