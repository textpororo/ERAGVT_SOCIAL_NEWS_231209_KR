;변신한다
@COM0
#DIM MOVESELECT
;각인에 의한 행동 제한 판정　１이면 행동할 수 없다
CALL ACT_LIMIT
SIF RESULT == 1
	RETURN 1


CALL TRANSFORM_MOVESELECT
MOVESELECT = RESULT


;공중 공격 플래그를 진행한다
CALL ATTACK_AIR_FLAG
;거리 표시
CALL PRINT_DISTANCE
PRINTL 

;지문: 변신　변신 시 구호가 있는 경우는 이 안에서 처리
CALL MESSAGE_BATTLE_CHARA_TRANSFORMCALL

;이 전투에서 두 번째로 변신한 경우, 자칭 구상을 내보내지 않는다
IF TCVARn:10 == 1
	;
ELSE
	CALL MESSAGE_BATTLE_CHARA_NANORI
	TCVARn:10 = 1
	CALL MESSAGE_BATTLE_CHARA_NANORI_BYOUSHA
	CALL PERFORM_CHEERS_HATE
ENDIF
PRINTW 

;ＴＳ남성화한 캐릭터가 임신 중인 동시에 발각되지 않은 경우
IF CHARATALENT_F(TARGET,0,"남자") == 0 && CHARATALENT_F(TARGET,1,"남자") > 0 && TALENT:임신 > 0
	PRINTL
	PRINTFORML 하지만, 어째서인지 남성화할 수 없었다！
	PRINTFORMW %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 무방비한 상태인 채로 변신에 실패하고 말았다！
	TCVARn:2 = 자세：아무것도안한다
	RETURN 1
ENDIF

IF GETBATTLESITUATION("기습") && TALENT:변신시ＴＳ && TFLAG:0 < 3
	PRINTFORML 그러나, 갑작스러운 습격으로 몸이 변신 준비에 들어서지 못했다！（변신 충전 완료까지：{3-TFLAG:0}턴）
	PRINTFORMW %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 무방비한 상태인 채로 변신에 실패하고 말았다！
	TCVARn:2 = 자세：아무것도안한다
	RETURN 1
ENDIF

;변신
CALL TRANSFORM, 1

;겉옷(변신 후)의 피해량이 조금 회복된다
SIF CFLAG:41 != 0 && TCVARn:23 == 0
	TCVARn:23 = 10

;게이지가 증가
IF MOVESELECT == 1
	IF CFLAG:99 < 5
		TCVARn:6 += 30 + RAND:16
	ELSEIF CFLAG:99 < 15
		TCVARn:6 += 20 + RAND:11
	ELSE
		TCVARn:6 += 10 + RAND:6
	ENDIF
ENDIF

;「관찰한다」플래그를 세운다
SIF MOVESELECT == 2
	TFLAG:25 = 1

;체력 20% 회복
;회복빠름이라면 +2%
;회복느림이라면 -2%
;[공생] 이 있다면 더욱이 +2%
LOCAL:0 = MAXBASE:체력
IF TALENT:회복빠름 == 1
	TIMES LOCAL:0, 0.22
ELSEIF TALENT:회복느림 == 1
	TIMES LOCAL:0, 0.18
ELSE
	TIMES LOCAL:0, 0.20
ENDIF
SIF TALENT:(FLAG:799):공생 == 1
	TIMES LOCAL:0, 1.02
;거리를 벌린 경우
SIF MOVESELECT == 3
	TIMES LOCAL:0, 1.50
SIF BASE:체력 + LOCAL:0 >= MAXBASE:체력
	LOCAL:0 = MAXBASE:체력 - BASE:체력
;[스태미나] 가 없으며 체력이 다한 경우, 체력이 회복되지 않는다
SIF BASE:체력 == 0 && TALENT:스태미나 == 0
	LOCAL:0 = 0
BASE:체력 += LOCAL:0

;기력 20% 회복
;회복빠름이라면 +2%
;회복느림이라면 -2%
LOCAL:1 = MAXBASE:기력
IF TALENT:회복빠름 == 1
	TIMES LOCAL:1, 0.22
ELSEIF TALENT:회복느림 == 1
	TIMES LOCAL:1, 0.18
ELSE
	TIMES LOCAL:1, 0.20
ENDIF
;거리를 벌린 경우
SIF MOVESELECT == 3
	TIMES LOCAL:1, 1.50
SIF BASE:기력 + LOCAL:1 >= MAXBASE:기력
	LOCAL:1 = MAXBASE:기력 - BASE:기력
BASE:기력 += LOCAL:1

RETURN 1


;거리 지정 변신의 間借り(뭔말인지 모르겠다)
@COM201
	CALL COM0
	RETURN RESULT
@COM202
	CALL COM0
	RETURN RESULT
@COM203
	CALL COM0
	RETURN RESULT
