;날뛴다
@COM9
#DIM HIT_FLAG
HIT_FLAG = 1

;刻印による行動制限判定　１で行動できない
CALL ACT_LIMIT
SIF RESULT == 1
	RETURN 1

;距離表示
CALL PRINT_DISTANCE
PRINTL 

;지문: 날뛴다
CALL MESSAGE_BATTLE_CHARA_ABARERU
;지문: 세뇌/흑화 캐릭터가 操作キャラの날뛴다を見た
SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
	CALL MESSAGE_OTHER_BATTLE_CHARA_ABARERU

;날뛴다には命中判定なし　100%成功

;방어フラグ判定：確率で相手を怯ませる
IF FLAG:111 == 0
	;촉수の공격力を見る
	CALL TENTACLE_ACCESS, "KOUGEKI"
	LOCAL:0 = RESULT
ELSE
	;敵キャラの공격力
	LOCAL:0 = MAXBASE:(FLAG:111):공격
ENDIF
;相手の油断による補正
SIF TFLAG:2 >= 1
	LOCAL:0 = 1
;キャラの방어を見る
CALL SHINKYOU_CHECK, "BOUGYO", CORRECTION_TRANS(MAXBASE:방어)
LOCAL:1 = RESULT
CALL CLOTH_BATTLE_HOSEI, "BOUGYO"
LOCAL:1 = RESULT * LOCAL:1 / 100
;「EX:防」中は방어値上昇
SIF GETBIT(TCVARn:3, 1)
	LOCAL:1 *= 2

;行動成功率確率算出
LOCAL:0 = LIMIT(20 * LOCAL:1 / LOCAL:0 + 5, 5, 70)
SIF ENEMY_TYPE_CHECK_F("MOB") == 1
	LOCAL:0 /= 2
IF RAND:100 < LOCAL:0
	TCVARn:2 = 자세：날뛴다방어
ELSE
	TCVARn:2 = 자세：날뛴다실패
ENDIF

;クリティカル判定
;衣装による補正
RESULT = 0
CALL CLOTH_BATTLE_HOSEI, "CRITICAL"
IF RAND:100 < 5 + RESULT
	LOCAL:0 = 15
	;直撃フラグ
	HIT_FLAG = 2
	;지문: 공격がクリティカルヒット
	CALL MESSAGE_BATTLE_CHARA_ATTACK_CRITICAL_HIT
	;지문: 세뇌/흑화 캐릭터가 操作キャラの공격をクリティカルヒットされた
	SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
		CALL MESSAGE_OTHER_BATTLE_CHARA_ATTACK_CRITICAL_HIT
	;クリティカルした場合、相手を怯ませる
	TCVARn:2 = 자세：날뛴다치명타
ELSE
	LOCAL:0 = 10
	;지문: 공격が通常ヒット
	CALL MESSAGE_BATTLE_CHARA_ATTACK_HIT
	;지문: 세뇌/흑화 캐릭터가 操作キャラの공격を通常ヒットされた
	SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
		CALL MESSAGE_OTHER_BATTLE_CHARA_ATTACK_HIT
	
ENDIF

;ダメージ計算
CALL DAMAGE, "ABARERU"

FLAG:13 -= RESULT * LOCAL:0 / 10
FONTBOLD
PRINTFORML {RESULT * LOCAL:0 / 10}のダメージを与えた！
FONTREGULAR
PRINTW 

;服へのダメージ
CALL CLOTH_BATTLE_DAMAGE, 4

;행동포인트を+3
EX:행동포인트 += 3

;サディストの効果
IF TALENT:새드기질 > 0 && HIT_FLAG > 0
	;기력が微量に回復
	LOCAL:1 = RAND:MAX((MAXBASE:기력 * HIT_FLAG * 8 / 100), 1) + 10
	SIF BASE:기력 + LOCAL:1 >= MAXBASE:기력
		LOCAL:1 = MAXBASE:기력 - BASE:기력
	BASE:기력 += LOCAL:1
	;욕정が微量に増加
	COMMON_PALAM:7 += LOCAL:1 * 10
	SIF LOCAL:1 > 0
		PRINTFORML サディスティックな快楽で%BASENAME:1%が{LOCAL:1}回復した
ENDIF

RETURN 1
