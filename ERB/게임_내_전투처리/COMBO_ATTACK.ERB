;--------------------------------------------------
;공격時コンボ系の処理


;--------------------------------------------------

;空中공격の計算
@GET_AIR_STRIKE(ARG)
#FUNCTION
;회피율上昇
IF ARG == 1
	LOCAL = MAX(50 - CFLAG:99 / 5 * 5, 25)
	;FEATによる공중 성능 강화
	SIF TALENT:에어마스터 > 0
		LOCAL += 25
;공중전특기による直撃率ボーナス
ELSEIF ARG == 2
	LOCAL = 25
;공중전서툼による直撃率ペナルティ
ELSEIF ARG == 3
	LOCAL = 10
ENDIF
RETURNF LOCAL

;同一距離연속実行による直撃ボーナス/回避ペナルティの計算
;　ARG…BraveHit発生回数　ARGS…"명중"または"회피"
@GET_BRAVE_HIT(ARG,ARGS)
#FUNCTION
IF ARG < 4
	LOCAL = 3 + ARG * 3
ELSEIF ARG < 7
	LOCAL = 6 + ARG * 2
ELSE
	LOCAL = 12 + ARG
ENDIF
SIF LOCAL > 25
	LOCAL = 25
IF ARGS=="명중" && TCVARn:TARGET:0==1
	;근거리は연속実行ボーナスを極端に
	IF TALENT:근거리특기
		TIMES LOCAL,3
	ELSEIF TALENT:근거리서툼
		TIMES LOCAL,1.1
	ELSE
		TIMES LOCAL,1.7
	ENDIF
ELSEIF ARGS=="회피" && TCVARn:TARGET:0==2
	;중거리は回避ペナルティを軽減
	IF TALENT:중거리특기
		TIMES LOCAL,0.8
	ELSEIF TALENT:중거리서툼
		TIMES LOCAL,0.9
	ELSE
		TIMES LOCAL,0.85
	ENDIF
ELSEIF ARGS=="회피" && TCVARn:TARGET:0==3
	;원거리は回避ペナルティを軽減
	IF TALENT:원거리특기
		TIMES LOCAL,0.7
	ELSEIF TALENT:원거리서툼
		TIMES LOCAL,0.8
	ELSE
		TIMES LOCAL,0.75
	ENDIF
ENDIF
RETURNF LOCAL

;연속ヒットコンボの計算
@GET_CHAIN_HIT(ARG)
#FUNCTION
IF ARG < 6
	LOCAL = 100 + ARG * 3
ELSEIF LOCAL < 11
	LOCAL = 105 + ARG * 2
ELSE
	LOCAL = 115 + ARG
ENDIF
SIF LOCAL > 200
	LOCAL = 200
RETURNF LOCAL

;방어공격コンボの計算
@GET_COUNTER_ATTACK(ARG)
#FUNCTION
IF ARG == 1
	LOCAL = 6 + MIN(SQRT(MAX(BASE:방어 - 80, 0)) / 2, 15)
ELSEIF ARG == 2
	LOCAL = 35 + MIN(SQRT(MAX(BASE:방어 - 100, 0)), 25)
ENDIF
RETURNF LOCAL

;연속回避コンボの計算
@GET_EVADER_ATK(ARG)
#FUNCTION
IF ARG < 6
	LOCAL = 100 + ARG * 3
ELSEIF LOCAL < 11
	LOCAL = 105 + ARG * 2
ELSE
	LOCAL = 115 + ARG
ENDIF
SIF LOCAL > 200
	LOCAL = 200
RETURNF LOCAL
@GET_EVADER_CRT(ARG)
#FUNCTION
IF ARG < 6
	LOCAL = ARG
ELSE
	LOCAL = -3 + ARG * 2
ENDIF
SIF LOCAL > 15
	LOCAL = 15
RETURNF LOCAL


;--------------------------------------------------
;コンボ系表示
@PRINT_COMBO
;文字間隔
#DIM LENS

LENS = 26

IF GETBIT(TCVARn:216, 1)
	;エアストライク
	LOCALS:0 = 　＋AIR STRIKE
	LOCALS:1 = 空中공격
	CALL CLOTH_BATTLE_HOSEI, "AIRPLUS", TARGET
	LOCAL = MIN(TALENT:공중전특기 > 0, 1) * GET_AIR_STRIKE(2) + RESULT * 25 - MIN(TALENT:공중전서툼 > 0, 1) * GET_AIR_STRIKE(3)
	IF LOCAL > 0
		LOCALS:2 = 直撃率＋{LOCAL}％　회피율＋{GET_AIR_STRIKE(1)}％
	ELSEIF LOCAL < 0
		;負の値を反転して表示
		LOCAL *= -1
		LOCALS:2 = 直撃率−{LOCAL}％　회피율＋{GET_AIR_STRIKE(1)}％
	ELSE
		LOCALS:2 = 회피율＋{GET_AIR_STRIKE(1)}％
	ENDIF
	IF TALENT:공중전특기 > 0
		LOCALS:2 += @"（공중전특기）"
	ELSEIF TALENT:공중전서툼 > 0
		LOCALS:2 += @"（공중전서툼）"
	ENDIF
	SIF TALENT:에어마스터 > 0
		LOCALS:2 += @"（에어마스터）"
	PRINTFORM %LOCALS:0,LENS - 4,LEFT%
	PRINTFORM %LOCALS:1,LENS,LEFT%
	PRINTFORM %LOCALS:2%
	PRINTL 
ENDIF
IF GETBIT(TCVARn:217, 0)
	;バースト공격
	LOCALS:0 = 　＋BURST STRIKE
	SELECTCASE FSTYLE_NAME_F(TARGET, TCVARn:0)
		CASE "연속"
			LOCALS:1 = バースト공격：연속
			LOCALS:2 = 6回연속공격
		CASE "장갑"
			LOCALS:1 = バースト공격：장갑
			LOCALS:2 = 直撃率アップ＋공격後にオートガード
		CASE "교란"
			LOCALS:1 = バースト공격：교란
			LOCALS:2 = 与ダメージアップ＋怯み効果
		CASE "중격"
			LOCALS:1 = バースト공격：중격
			LOCALS:2 = 直撃しにくい大ダメージ공격
		CASE "광역"
			LOCALS:1 = バースト공격：광역
			LOCALS:2 = 必中공격＋反動で回避不能
		CASE "전력"
			LOCALS:1 = バースト공격：전력
			LOCALS:2 = 捨て身の大ダメージ공격
		CASE "지략"
			LOCALS:1 = バースト공격：지략
			LOCALS:2 = クリティカル率アップ＋관찰한다効果
		CASE "설치"
			LOCALS:1 = バースト공격：설치
			LOCALS:2 = 油断効果2倍
		CASE "사역"
			LOCALS:1 = バースト공격：사역
			LOCALS:2 = 直撃しにくい大ダメージ공격
		CASE "통상"
			LOCALS:1 = バースト공격：통상
			LOCALS:2 = 与ダメージアップ＋直撃率アップ
	ENDSELECT
	PRINTFORM %LOCALS:0,LENS - 4,LEFT%
	PRINTFORM %LOCALS:1,LENS,LEFT%
	PRINTFORM %LOCALS:2%
	PRINTL 
ENDIF
;※악질 시민戦のブレイブヒット、チェインヒットは別の判定に使われている
IF TFLAG:30 > 1 && FLAG:73 == 0
	;ブレイブヒット
	;同じ距離で戦い続けると加算されていく
	;拘束された場合、または移動するか距離を取るとリセットされる
	LOCALS:0 = 　＋BRAVE HIT
	LOCALS:1 = 同一距離연속実行({TFLAG:30 - 1})
	LOCALS:2 = 直撃率＋{GET_BRAVE_HIT(TFLAG:30 - 1,"명중") - 3}％　회피율−{GET_BRAVE_HIT(TFLAG:30 - 1,"회피")}％
	PRINTFORM %LOCALS:0,LENS - 4,LEFT%
	PRINTFORM %LOCALS:1,LENS,LEFT%
	PRINTFORM %LOCALS:2%
	PRINTL 
ENDIF
IF TFLAG:31 > 0 && FLAG:73 == 0
	;チェインヒット
	;연속で공격を命中させると加算されていく
	;カス当たりでも加算されるが、拘束されるか距離を取るとリセットされる
	LOCALS:0 = 　＋CHAIN HIT
	LOCALS:1 = 연속で공격ヒット({TFLAG:31})
	LOCALS:2 = 与ダメージ＋{GET_CHAIN_HIT(TFLAG:31) - 100}％
	PRINTFORM %LOCALS:0,LENS - 4,LEFT%
	PRINTFORM %LOCALS:1,LENS,LEFT%
	PRINTFORM %LOCALS:2%
	PRINTL 
ENDIF
IF TFLAG:32 > 0
	;カウンターアタック
	;방어を使った後に공격すると発動する、累積はしない
	;キャラの방어力に応じて性能が強化されていく
	LOCALS:0 = 　＋COUNTER ATTACK
	LOCALS:1 = 방어しながら공격({TFLAG:32})
	LOCALS:2 = 直撃率＋{GET_COUNTER_ATTACK(1)}％　被ダメージ−{GET_COUNTER_ATTACK(2)}％
	PRINTFORM %LOCALS:0,LENS - 4,LEFT%
	PRINTFORM %LOCALS:1,LENS,LEFT%
	PRINTFORM %LOCALS:2%
	PRINTL 
ENDIF
IF TFLAG:33 > 1
	;イベーダー
	;연속で回避を成功させると加算されていく。敵が距離を取った場合は加算されない
	;始動が遅いが、距離を取ってもリセットされない。被弾するとリセットされる
	LOCALS:0 = 　＋EVADER
	LOCALS:1 = 연속で回避成功({TFLAG:33 - 1})
	LOCALS:2 = 与ダメージ＋{(GET_EVADER_ATK(TFLAG:33 - 1) - 100) / 2}％　クリティカル率＋{GET_EVADER_CRT(TFLAG:33 - 1)}％
	PRINTFORM %LOCALS:0,LENS - 4,LEFT%
	PRINTFORM %LOCALS:1,LENS,LEFT%
	PRINTFORM %LOCALS:2%
	PRINTL 
ENDIF


