
;戦闘開始時処理
;　この後の流れはTRAIN⇒COM～COMAFTER⇒AFTERTRAIN
@EVENTTRAIN
#DIM CHISEI_CAL
#DIM CHISEI_SUM
#DIM CHISEI_ENEMY
#DIM INITIATIVE
#DIM LCOUNT
#DIM CCOUNT
REDRAW 1



;通常戦闘(イベント戦闘以外)ならTCVARを初期化しておく
IF FLAG:45==0
	CALL INITBATTLESITUATION()
	VARSET TCVARn
	;雑魚戦15ターン、보스전50ターンが基本
	턴상한=(FLAG:10 == 2 ? 15 # 50)
ENDIF
;戦闘開始時に戦闘距離を원거리に설정する
IF FLAG:73 > 0
	TCVARn:0 = 0
ELSE
	TCVARn:0 = 3
ENDIF
FOR LCOUNT, 0, TCRLENGTH
	TCREPORT:LCOUNT = 0
NEXT
;避妊効果中に受けた사정量をリセット
TFLAG:6 = 0
;行動予約플래그를折る
TFLAG:16 = -1
TFLAG:17 = -1
;追加責め関係の플래그를折る
VARSET EX_COM
VARSET SH_COM
VARSET INSERT
;拡張度関係の플래그를折る
VARSET TENTACLE_SIZE
VARSET TENTACLE_NUM

;最初から타락
SIF (MESSAGE_BRANCH_F(TARGET) & 타락)
	TFLAG:97 |= 타락
;最初から쾌락굴복
SIF (MESSAGE_BRANCH_F(TARGET) & 쾌락굴복)
	TFLAG:97 |= 쾌락굴복
;最初から절망
SIF (MESSAGE_BRANCH_F(TARGET) & 절망)
	TFLAG:97 |= 절망
;最初から성저항
SIF (MESSAGE_BRANCH_F(TARGET) & 성저항)
	TFLAG:97 |= 성저항
;最初から고전
SIF (MESSAGE_BRANCH_F(TARGET) & 고전)
	TFLAG:97 |= 고전

;PALAM最大値をリセット
VARSET MAX_PALAM

;사정 게이지、모유 게이지のリセット
BASE:사정 = 0
BASE:모유 = 0

;공중대시回数をリセット
;衣装による空中性能強化
CALL CLOTH_BATTLE_HOSEI, "AIRPLUS",TARGET
SIF RESULT > 0
	MAXBASE:공중대시 += 1
;입체기동장치の効果###
SIF CFLAG:43 == 510
	MAXBASE:공중대시 += 1
;FEATによる공중대시増加
SIF TALENT:날개 > 0
	MAXBASE:공중대시 += 1
BASE:공중대시 = MAXBASE:공중대시
SIF BASE:공중대시 >= 8
	BASE:공중대시 = 8

;FEAT 효과에 따른EX 게이지増加
IF TALENT:마력저장 > 0
	SETCOLOR 255,128,0
	PRINTFORMW [마력저장]の効果で%CALLNAME%のEX 게이지が2つチャージされた！
	RESETCOLOR
	TCVARn:4 += 200
ENDIF

;イベント戦以外なら衣装の初期化をここで行う
IF FLAG:45==0
	;衣装の耐久度を설정
	CALL CLOTH_BATTLE_SETHP
	;衣装の情報を更新
	CALL REFRESH_CLOTH_DATA
ENDIF

;＠중국판より
;　アイデアだけ書いてたsetbattlesituationに対応してくれた分
IF GETBATTLESITUATION("강제 발정") == 1
	TCVARn:12 |= 발정
	PRINTFORMW %CALLNAME%은(는) 抑えきれないほど발정している……
ENDIF
IF GETBATTLESITUATION("강제 마비") == 1
	TCVARn:12 |= 마비
	PRINTFORMW %CALLNAME%の全身が痺れ、自由に動けない……
ENDIF
IF GETBATTLESITUATION("強制全裸") == 1
	CFLAG:TARGET:40 = 0
	SIF CFLAG:TARGET:41 != 402
		CFLAG:TARGET:41 = 0
	SIF CFLAG:TARGET:42 != 400
		CFLAG:TARGET:42 = 0
	CFLAG:TARGET:43 = 0
	PRINTFORMW %CALLNAME%은(는) 裸で敵と戦うしかない…
ENDIF
IF GETBATTLESITUATION("強制拘束") == 1
	TCVARn:0 = 0
	PRINTFORMW %CALLNAME%은(는) 力ずくで戦闘に引きずり込まれた……
ENDIF
IF GETBATTLESITUATION("強制挿入") == 1
	TCVARn:0 = 0
	TCVARn:12 |= 강구속
	PRINTFORMW %CALLNAME%がっちりと巻き上げられ、強引に戦闘に引きずり込まれていく…。
ENDIF


;보스 촉수の行動選択
IF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
	CALL SELECT_ENEMY_ACTION
ELSE
	CALL SELECT_TENTACLE_ACTION
ENDIF

;戦闘中플래그를 세운다
FLAG:700 = 1

;解析度を読み込む
IF ENEMY_TYPE_CHECK_F("BOSS") == 1
	FLAG:20 = FLAG:(500 + FLAG:11)
ELSEIF ENEMY_TYPE_CHECK_F("LASTBOSS") >= 1
	FLAG:20 = FLAG:(600 + FLAG:11)
ELSEIF ENEMY_TYPE_CHECK_F("MOB") == 1
	FLAG:20 = 100
ENDIF

;실적
CHISEI_SUM = 0
CHISEI_CAL = 0
;전투 지원による補正
CALL CALC_CHISEI_SHIEN(0)
CHISEI_SUM = RESULT
IF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
	CHISEI_ENEMY = MAXBASE:(FLAG:111):지성
	SIF CHISEI_ENEMY < 100
		CHISEI_ENEMY = 100
ELSE
	CALL TENTACLE_ACCESS, "CHISEI"
	CHISEI_ENEMY = RESULT
ENDIF
IF PERCENT_CAL_F(CHISEI_SUM, CHISEI_ENEMY) >= 200
	CALL UNLOCK_ACHIEVEMENT(268,"Tactical Order")
ENDIF

;先制공격の判定
INITIATIVE = MIN(SQRT(EXP:전투경험), 20) + MIN(SQRT(PERCENT_CAL_F(CHISEI_SUM, CHISEI_ENEMY)), 25) + 5
;生き残っている보스の数を見る
CALL TENTACLE_SURVIVE, "NUM"
;１体目の보스撃破前は先制率アップ
SIF FLAG:3 == RESULT
	INITIATIVE += 15
;２体目の보스撃破前は先制率アップ
SIF FLAG:3 - 1 == RESULT
	INITIATIVE += 15
;FEAT 효과에 따른先制率アップ
SIF TALENT:정령교신 > 0
	INITIATIVE += 15
SIF TALENT:사냥꾼의직감 > 0
	INITIATIVE += 15
;잡몹적が相手の場合は先制率２倍
SIF ENEMY_TYPE_CHECK_F("MOB") == 1 && FLAG:73 == 0
	INITIATIVE *= 2

;市民確定なら先制공격は発生しない
SIF GETBATTLESITUATION("市民確定") || GETBATTLESITUATION("先制無し")
	INITIATIVE = 0

;判定に成功したら確率を半分にして再度判定を行う
$INITIATIVE_LOOP
SIF FLAG:73 > 0
	INITIATIVE = 0
IF FLAG:999 > 0 && FLAG:73 == 0
	SETCOLOR 105,105,105
	PRINTL 
	PRINTFORML 　先制率　{INITIATIVE}％
	PRINTL 
	RESETCOLOR
ENDIF
IF RAND:100 < INITIATIVE
	TFLAG:24 += 1
	INITIATIVE = INITIATIVE * 3 / 4
	SIF INITIATIVE > 0
		GOTO INITIATIVE_LOOP
ENDIF
IF TFLAG:24 > 0
	IF TALENT:정령교신 > 0
		SETCOLOR 255,128,0
		PRINTFORMW [정령교신]の効果で先制率が上昇！
		RESETCOLOR
	ENDIF
	IF TALENT:사냥꾼의직감 > 0
		SETCOLOR 255,128,0
		PRINTFORMW [사냥꾼의직감]の効果で先制率が上昇！
		RESETCOLOR
	ENDIF

	PRINTL 
	FONTBOLD
	PRINT 　先制공격
	SIF TFLAG:24 > 1
		PRINTFORM (+{TFLAG:24 - 1})
	PRINT ！ 
	FONTREGULAR
	IF TFLAG:24 == 1
		PRINTW 敵はこのターン動けない！
	ELSE
		PRINTFORMW 敵は%TOSTR(TFLAG:24)%ターンの間動けない！
	ENDIF
	PRINTL 
;応援判定　先制공격中は発生しない
ELSEIF CONFIG_CHECK_EVENT_F(3) > 0 && FLAG:73 == 0
	CALL PERFORM_CHEERS_FIRST_HANTEI
ENDIF

;カテゴリ分けする場合、変身素質がないなら공격コマンドをデフォに
SIF TALENT:변신능력 != 1 && CONFIG_CHECK_SCREEN_F(2) > 0
	TCVARn:8 = 1