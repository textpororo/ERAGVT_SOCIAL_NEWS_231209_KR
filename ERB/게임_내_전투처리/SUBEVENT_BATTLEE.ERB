;**********************************************************
;전투의 서브 이벤트류
;AFTERTRAIN.ERB에서 호출되는 것은 SUBEVENT_BATTLEEND으로
;AFTERCOM.ERB에서 호출되는 것은 각자 개별 함수로

@SUBEVENT_BATTLEEND
;전투 후 자위
;절정 금지 해방
SIF TCVARn:40 > 0
	CALL SUBEVENT_RELEASE_ECSTASY
;제한 시간 경과 혹은 승리 시(철수 포함)에 조건을 충족하면 자위를 한다
SIF TFLAG:98 == 0 || TFLAG:98 == 1
	CALL SELF_BATTLEEND, TARGET

;이 이후로는 AFTERCOM.ERB에서 호출되는 개별 이벤트

;----------------------------------------------------------
;서브 이벤트 계산 전의 초기화
@SUBEVENT_BATTLE_PRECALCRESET
VARSET NOWEX, 0

;----------------------------------------------------------
;흑화 캐릭터에게 패배하여 범해진다
@SUBEVENT_BATTLE_RAPED_ENEMY

;파라미터 취득은 잠정치(暫定値) (β３)
;BASE값의 명시적 초기화
REPEAT 12
	LOCAL:COUNT = 0
REND
;굴복
LOCAL:8 = 2000
;치정
LOCAL:9 = 2000

;지문: 강간하는 쪽
CALL MESSAGE_OTHER_SUBEVENT_BATTLE_RAPE_ENEMY

;지문: 흑화 캐릭터에게 패배하여 범해진다
CALL MESSAGE_SUBEVENT_BATTLE_RAPED_ENEMY


;피간경험을 가산
EXP:피간경험 += 1

;계산으로

;서브 이벤트 계산 전의 초기화
CALL SUBEVENT_BATTLE_PRECALCRESET

CALL PALAM_CAL, LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11

;----------------------------------------------------------
;악질 시민에게 패배하여 범해진다
@SUBEVENT_BATTLE_RAPED_CITIZEN

;지문: 악질 시민에게 패배하여 범해진다
CALL MESSAGE_SUBEVENT_BATTLE_RAPED_CITIZEN

;피간경험을 가산
EXP:피간경험 += 1

;계산으로
;서브 이벤트 계산 전의 초기화
CALL SUBEVENT_BATTLE_PRECALCRESET
CALL PALAM_CAL, LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11


;----------------------------------------------------------
;촉수 구속구 강제 장착
@SUBEVENT_BATTLE_SETTENTACLECLOTH
SIF CFLAG:42 == 400
	RETURN 0

LOCAL = 0
IF CFLAG:1 == 0
	SIF (PERCENT_CAL_F(TCVARn:21,TCVARn:20) < CLOTH_OUTER_DEF || TCVARn:21 == 0) && (PERCENT_CAL_F(TCVARn:25,TCVARn:24) < CLOTH_INNER_DEF || TCVARn:25 == 0)
		LOCAL = 1
ELSE
	SIF (PERCENT_CAL_F(TCVARn:23,TCVARn:22) < CLOTH_OUTER_DEF || TCVARn:23 == 0) && (PERCENT_CAL_F(TCVARn:25,TCVARn:24) < CLOTH_INNER_DEF || TCVARn:25 == 0)
		LOCAL = 1
ENDIF

LOCAL:1 = 35 - MIN((FLAG:45>0 ? 1 # 0) * 12, 12)
IF LOCAL == 1 && RAND(100) < LOCAL:1
	CFLAG:42 = 400
	;지문: 촉수 구속구를 씌워졌다
	CALL MESSAGE_SUBEVENT_BATTLE_SETTENTACLECLOTH
ENDIF

;----------------------------------------------------------
;촉수 구속구에 의한 피간
@SUBEVENT_BATTLE_ACTTENTACLECLOTH
#DIM LCOUNT
SIF CFLAG:42 != 400
	RETURN 0

;취득 파라미터 계산
;β２시점에서는 미미한 상승으로
CALL SEX_COMEX, 0, 0, 15
REPEAT 12
	LOCAL:COUNT = RESULT:COUNT / 2
REND
SIF ISMALE()
	LOCAL:1 = 0

;지문: 촉수 구속구에 의한 피간
CALL MESSAGE_SUBEVENT_BATTLE_ACTTENTACLECLOTH

;피간경험을 가산
EXP:피간경험 += 1

;서브 이벤트 계산 전의 초기화
CALL SUBEVENT_BATTLE_PRECALCRESET

;계산으로
;CALL PALAM_CAL, LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11
;LOCAL을 PALAM 보정에 가산한다
FOR LCOUNT, 0, 13
	IF LOCAL:LCOUNT > 0
		;자세에 관한 보정
		CALL PALAM_HOSEI_POSE, LOCAL:LCOUNT
		LOCAL:LCOUNT = RESULT

		;성내성에 관한 보정
		CALL PALAM_HOSEI_SEITAISEI, LOCAL:LCOUNT
		LOCAL:LCOUNT = RESULT

		;소질에 관한 보정
		CALL PALAM_HOSEI_TALENT, LCOUNT, LOCAL:LCOUNT
		LOCAL:LCOUNT = RESULT

		;성격에 관한 보정
		CALL PALAM_HOSEI_SEIKAKU, LCOUNT, LOCAL:LCOUNT
		LOCAL:LCOUNT = RESULT

		;촉수에 관한 보정
		;CALL PALAM_HOSEI_TENTACLE, LCOUNT, LOCAL:LCOUNT
		;LOCAL:LCOUNT = RESULT

		;촉수중독에 관한 보정
		IF LCOUNT + PALAM시점 - 감각수 == 공순 || LCOUNT + PALAM시점 - 감각수 == 욕정 || LCOUNT + PALAM시점 - 감각수 == 굴복
			CALL PALAM_HOSEI_POISONING_TENTACLE, LOCAL:LCOUNT
			LOCAL:LCOUNT = RESULT
		ENDIF

		;랜덤성 부여
		CALL PALAM_HOSEI_RANDOM, LOCAL:LCOUNT
		LOCAL:LCOUNT = RESULT
		
		;하한치 설정
		SIF LOCAL:LCOUNT <= 0
			LOCAL:LCOUNT = 1
		;상한치 설정
		SIF LOCAL:LCOUNT > 999999 && LCOUNT >= 감각수
			LOCAL:LCOUNT = 999999
	ENDIF
	COMMON_PALAM:LCOUNT += LOCAL:LCOUNT
NEXT

;----------------------------------------------------------
;절정 금지 해방
@SUBEVENT_RELEASE_ECSTASY
#DIM LCOUNT
SIF TCVARn:40 <= 0
	RETURN 0

VARSET NOWEX
FOR LCOUNT, 0, 감각수
	CALL PALAM_ECSTASY, LCOUNT, 0, LCOUNT, 0
NEXT

;절정 횟수와 구슬의 취득
LOCAL = 0
REPEAT 감각수
	SIF NOWEX:COUNT > 0
		LOCAL += 1
REND
EXP:절정경험 += NOWEX:Ｃ절정 * LOCAL
JUEL:쾌Ｃ += 1000 * NOWEX:Ｃ절정 * LOCAL
EXP:절정경험 += NOWEX:Ｖ절정 * LOCAL
JUEL:쾌Ｖ += 1000 * NOWEX:Ｖ절정 * LOCAL
EXP:절정경험 += NOWEX:Ａ절정 * LOCAL
JUEL:쾌Ａ += 1000 * NOWEX:Ａ절정 * LOCAL
EXP:절정경험 += NOWEX:Ｂ절정 * LOCAL
JUEL:쾌Ｂ += 1000 * NOWEX:Ｂ절정 * LOCAL

EXP:노출쾌락경험 += LOCAL

IF NOWEX:0 + NOWEX:1 + NOWEX:2 + NOWEX:3
	PRINTL 
	;지문: 절정 금지 해방
	SIF TFLAG:98 != 2
		CALL MESSAGE_SUBEVENT_BATTLE_RELEASE_ECSTASY
	;절정 표시
	IF NOWEX:Ｃ절정 == 1
		;지문: C절정
		CALL MESSAGE_SEX_ECSTASY_C
	ELSEIF NOWEX:Ｃ절정 > 1
		;지문: C강절정
		CALL MESSAGE_SEX_ECSTASY_C_HI, NOWEX:Ｃ절정
	ENDIF
	IF NOWEX:Ｖ절정 == 1
		;지문: V절정
		CALL MESSAGE_SEX_ECSTASY_V
	ELSEIF NOWEX:Ｖ절정 > 1
		;지문: V강절정
		CALL MESSAGE_SEX_ECSTASY_V_HI, NOWEX:Ｖ절정
	ENDIF
	IF NOWEX:Ａ절정 == 1
		;지문: A절정
		CALL MESSAGE_SEX_ECSTASY_A
	ELSEIF NOWEX:Ａ절정 > 1
		;지문: A강절정
		CALL MESSAGE_SEX_ECSTASY_A_HI, NOWEX:Ａ절정
	ENDIF
	IF NOWEX:Ｂ절정 == 1
		;지문: B절정
		CALL MESSAGE_SEX_ECSTASY_B
	ELSEIF NOWEX:Ｂ절정 > 1
		;지문: B강절정
		CALL MESSAGE_SEX_ECSTASY_B_HI, NOWEX:Ｂ절정
	ENDIF
ENDIF
TCVARn:40 = 0


;---------------------------------------------------------------------------
;자동 풀어내기 처리
@AUTO_UNTANGLE
;기절 중에는 불가능
IF (TCVARn:12 & 기절)
	TFLAG:22 = 1
	RETURN
ENDIF
;황홀 중에는 불가능
IF (TCVARn:12 & 황홀)
	TFLAG:22 = 1
	RETURN
ENDIF
;일부러 잡힌 경우에는 불가능
IF SELECTCOM == 69
	TFLAG:22 = 1
	RETURN
ENDIF
;악질 시민전은 템포가 안 좋으므로 불가능
IF FLAG:73 > 0
	TFLAG:22 = 1
	RETURN
ENDIF

IF TCVARn:0 == 0
	IF TFLAG:22 > 0 || (TCVARn:12 & 강구속)
		TFLAG:22 = 1
		RETURN
	ENDIF
	;풀어낸다 성공 판정
	CALL ACT_HANTEI_CHARA_TO_TENTACLE, "HURIHODOKU"

	;확률 판정
	IF RESULT == 1
		PRINTL 
		FONTBOLD
		PRINTL 자동 풀어내기
		FONTREGULAR
		;지문: 풀어낸다
		CALL MESSAGE_BATTLE_CHARA_HURIHODOKU

		TCVARn:0 = 2
		TFLAG:4 = 0

		;지문: 풀어낸다 성공
		CALL MESSAGE_BATTLE_CHARA_HURIHODOKU_SUCCESS
		;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의「풀어낸다」를 막지 못했다
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_BATTLE_CHARA_HURIHODOKU_SUCCESS

		;자동으로 전투 메뉴를 선택한다
		TCVARn:8 = 1

		;플래그류를 초기화
		TFLAG:1 = 0
		TFLAG:16 = -1
		TFLAG:17 = -1
		TFLAG:20 = -1
		IF ENEMY_TYPE_CHECK_F("AKUOTI") == 0
			;CALL TENTACLE_ACCESS, "NAME"
			CALL TENTACLE_ACCESS, "GETNAME"
			PRINTFORM %조사처리(RESULTS,"는")% 
		ELSEIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			PRINTFORM %조사처리(PRINT_TRANSCALLNAME(FLAG:111),"는")% 
		ENDIF
		PRINTL 자세를 가다듬고 있다・・・
		PRINTL 
	ELSE
		PRINTL 
		PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 단단히 구속되어 꼼짝도 못 했다・・・
		PRINTL 
		TFLAG:22 = 1
	ENDIF
ELSE
	TFLAG:22 = 0
ENDIF




;---------------------------------------------------------------------------
;촉수옷 처리
@SUBEVENT_BATTLE_ACTTENTACLESUIT
#DIM LCOUNT
SIF CFLAG:1 == 0 && CFLAG:40 != 199
	RETURN 0
SIF CFLAG:1 > 0 && CFLAG:41 != 199
	RETURN 0
VARSET LOCAL
;촉수 슈트의 이름을 LOCALS에 대입
IF CFLAG:1 == 0 && CSTR:8 != ""
	LOCALS = %CSTR:8%
ELSEIF CSTR:9 != ""
	LOCALS = %CSTR:9%
ELSE
	LOCALS = %ITEMNAME:199%
ENDIF

IF BASE:성내성 > 0 && FIGURE_SPLIT(EQUIP:(CFLAG:1==0 ? 99 # 199), 4) != 4 && (FIGURE_SPLIT(EQUIP:(CFLAG:1==0 ? 99 # 199), 4) != 3 || RAND:100 >= 5)
	LOCAL = 7 + RAND:5 + FIGURE_SPLIT(EQUIP:(CFLAG:1==0 ? 99 # 199), 2) * 7
	SIF FIGURE_SPLIT(EQUIP:(CFLAG:1==0 ? 99 # 199), 3) > 0
		LOCAL += 2
	SELECTCASE FIGURE_SPLIT(EQUIP:(CFLAG:1==0 ? 99 # 199), 4)
		CASE 1
			TIMES LOCAL, 0.9
		CASE 2
			TIMES LOCAL, 0.8
		CASE 3
			TIMES LOCAL, 0.5
	ENDSELECT
	BASE:성내성 = MAX(BASE:성내성 - LOCAL, 0)
	PRINTL 
	PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 성내성을 {LOCAL} 소비했다！
	SIF BASE:성내성 == 0
		PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%의 성내성이 다 해 버렸다！
	PRINTW 
ELSE
	PRINTL 
	;취득 파라미터 계산
	CALL SEX_COMEX, 0, 0, 15
	REPEAT 12
		LOCAL:COUNT = RESULT:COUNT * (100 + FIGURE_SPLIT(EQUIP:(CFLAG:1==0 ? 99 # 199), 2) * 20) / 100
		SELECTCASE FIGURE_SPLIT(EQUIP:(CFLAG:1==0 ? 99 # 199), 4)
			CASE 1
				TIMES LOCAL:COUNT, 1.1
			CASE 2
				TIMES LOCAL:COUNT, 1.2
		ENDSELECT
	REND

	;지문: 촉수옷에 의한 피간
	CALL MESSAGE_SUBEVENT_BATTLE_ACTTENTACLESUIT
	IF TCVARn:0 == 0
		SELECTCASE TCVARn:2
			CASE 자세：아무것도안한다,자세：통상,자세：가만히있는다,자세：받아들인다
			CASEELSE
				TCVARn:2 = 자세：통상
		ENDSELECT
	ELSEIF ISMALE()
		LOCAL:1 = 0
	ELSEIF TCVARn:41 == 1
		IF PALAM:윤활 >= 500
			;쾌V
			TIMES LOCAL:1, 1.3
			;쾌A
			TIMES LOCAL:2, 1.3
			;윤활
			;LOCAL:4 = 2000

			;처녀라면 고통이 대량으로 들어간다
			IF TALENT:처녀 == 1 && CHECK_HOLYVIRGIN_F()==0
				LOCAL:10 += 10000
				TALENT:처녀 = -1
				CFLAG:206 = 4
				TCVARn:1 = 1
			ENDIF
		ELSE
			TCVARn:41 = 0
		ENDIF
	ELSEIF TCVARn:41 == 2
		;쾌V
		SIF TALENT:처녀 < 1
			TIMES LOCAL:1, 1.5
		;쾌A
		TIMES LOCAL:2, 1.5

	ELSEIF TCVARn:41 == 3
		IF TALENT:처녀 < 1 && ISFEMALE()
			FONTBOLD
			PRINTL 질내 대량 사정
			FONTREGULAR
			PRINTFORML %LOCALS%의 촉수와 %PRINT_TRANSCALLNAME(TARGET)%의 질 구멍 사이로부터, 미처 다 가라앉지 않은 정액이 흘러나왔다.
			TRYCALLFORM KOJO_ROOT(CFLAG:6, "SEX_TENTACLE_SYASEI_VAGINA_HI")
			PRINTL 
		ENDIF
		FONTBOLD
		PRINTL 애널 대량 사정
		FONTREGULAR
		PRINTFORML 직장에 대량으로 부어 넣어진 정액으로 인해,
		PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%의 배는 %조사처리(LOCALS,"를")% 밀어 올려 불룩하게 부풀어 있다……
		TRYCALLFORM KOJO_ROOT(CFLAG:6, "SEX_TENTACLE_SYASEI_ANAL_HI")
		PRINTL 
		;쾌V
		SIF TALENT:처녀 < 1
			LOCAL:1 *= 2
		;쾌A
		LOCAL:2 *= 2

		;불결
		SIF ISFEMALE()
			STAIN:3 |= 4
		STAIN:4 |= 4
		;임신 확률　출산 후의 질내사정 횟수와 지금까지의 출산경험에 따라 가능성이 늘어난다
		SIF TALENT:처녀 < 1
			CALL NINSIN_HANTEI,2,3,200

		EXP:정액경험 += 2
	ELSEIF TCVARn:41 >= 4
		IF TALENT:처녀 < 1 && ISFEMALE()
			FONTBOLD
			PRINTL 질내사정
			FONTREGULAR
			PRINTFORML %조사처리(LOCALS,"는")% %PRINT_TRANSCALLNAME(TARGET)%의 질내에 정액을 부어 넣었다.
			TRYCALLFORM KOJO_ROOT(CFLAG:6, "SEX_TENTACLE_SYASEI_VAGINA")
			PRINTL 
		ENDIF
		FONTBOLD
		PRINTL 애널사정
		FONTREGULAR
		PRINTFORML %조사처리(LOCALS,"는")% %PRINT_TRANSCALLNAME(TARGET)%의 직장에 정액을 부어 넣었다.
		TRYCALLFORM KOJO_ROOT(CFLAG:6, "SEX_TENTACLE_SYASEI_ANAL")
		PRINTL 
		;쾌V
		TIMES LOCAL:1, 1.7
		;쾌A
		TIMES LOCAL:2, 1.7

		;임신 확률　출산 후의 질내사정 횟수와 지금까지의 출산경험에 따라 가능성이 늘어난다
		SIF TALENT:처녀 < 1
			CALL NINSIN_HANTEI,1,3,200

		EXP:정액경험 += 1
	ENDIF

	;피간경험을 가산
	EXP:피간경험 += 1

	IF TCVARn:0 == 0
		TCVARn:41 = -1
	ELSEIF TCVARn:41 == -1
		TCVARn:41 = 1
	ELSE
		TCVARn:41 ++
	ENDIF
	;서브 이벤트 계산 전의 초기화
	CALL SUBEVENT_BATTLE_PRECALCRESET

	;계산으로
	;LOCAL을 PALAM 보정에 가산한다
	FOR LCOUNT, 0, 13
		IF LOCAL:LCOUNT > 0
			;자세에 관한 보정
			CALL PALAM_HOSEI_POSE, LOCAL:LCOUNT
			LOCAL:LCOUNT = RESULT

			;성내성에 관한 보정
			CALL PALAM_HOSEI_SEITAISEI, LOCAL:LCOUNT
			LOCAL:LCOUNT = RESULT

			;소질에 관한 보정
			CALL PALAM_HOSEI_TALENT, LCOUNT, LOCAL:LCOUNT
			LOCAL:LCOUNT = RESULT

			;성격에 관한 보정
			CALL PALAM_HOSEI_SEIKAKU, LCOUNT, LOCAL:LCOUNT
			LOCAL:LCOUNT = RESULT

			;촉수에 관한 보정
			;CALL PALAM_HOSEI_TENTACLE, LCOUNT, LOCAL:LCOUNT
			;LOCAL:LCOUNT = RESULT

			;촉수중독에 관한 보정
			IF LCOUNT + PALAM시점 - 감각수 == 공순 || LCOUNT + PALAM시점 - 감각수 == 욕정 || LCOUNT + PALAM시점 - 감각수 == 굴복
				CALL PALAM_HOSEI_POISONING_TENTACLE, LOCAL:LCOUNT
				LOCAL:LCOUNT = RESULT
			ENDIF

			;랜덤성 부여
			CALL PALAM_HOSEI_RANDOM, LOCAL:LCOUNT
			LOCAL:LCOUNT = RESULT
			
			;하한치 설정
			SIF LOCAL:LCOUNT <= 0
				LOCAL:LCOUNT = 1
			;상한치 설정
			SIF LOCAL:LCOUNT > 999999 && LCOUNT >= 감각수
				LOCAL:LCOUNT = 999999
		ENDIF
		COMMON_PALAM:LCOUNT += LOCAL:LCOUNT
	NEXT
ENDIF



;---------------------------------------------------------------------------
;발정에 의해 배란하고 만다 (현재로서는 종족：짐승족만)
@HATUJOU_TO_HAIRAN
SIF TALENT:짐승족 == 0
	RETURN 0
SIF ISMALE()
	RETURN 0
SIF TALENT:임신 > 0
	RETURN 0
SIF (TCVARn:12 & 배란) > 0
	RETURN 0
SIF (TCVARn:12 & 발정) == 0
	RETURN 0
;매 턴 발생률 10%
SIF RAND:100 >= 10
	RETURN 0

IF TALENT:주관시점 > 0
	PRINTFORML 가슴의 격렬한 고동이 느껴진다……
ELSE
	PRINTFORML 느닷없이, %PRINT_CALLNAME(TARGET)%의 가슴이 격렬하게 고동치고 있었다……
ENDIF
PRINTFORML 미약의 효과에 의해 강제로 발정기를 맞이하게 되어,
IF TFLAG:7 > 0
	SIF TFLAG:7 >= 2
		PRINTFORM 듬뿍 
	PRINTFORM 정자를 받은 
ELSEIF EXP:출산경험 >= 20
	PRINTFORM 지금껏 여러 번이고 잉태되어 온 
ENDIF
PRINTFORM %PRINT_CALLNAME(TARGET)%의 
IF TALENT:주관시점 > 0 && TALENT:청순파 > 0
	PRINTFORM 아랫배 안쪽
ELSE
	PRINTFORM %ESTRUS_TEXT_F(TARGET,"의")% 자궁
ENDIF
PRINTFORM 이 
SIF !(MESSAGE_BRANCH_F(TARGET) & 타락) && !(MESSAGE_BRANCH_F(TARGET) & 쾌락굴복)
	PRINTFORM 의사에 반하여 
SIF TFLAG:7 == 0 && EXP:출산경험 >= 20
	PRINTFORM 다시금 
IF TFLAG:7 == 0
	PRINTFORM 꾸욱꾸욱하고 애달프게 쑤셔
	IF TALENT:주관시점 > 0
		PRINTFORML 왔다.
	ELSE
		PRINTFORML 와,
		IF FLAG:96 == 0
			PRINTFORM 아이 만들기 교미의 
		ELSE
			PRINTFORM 자손을 밸 
		ENDIF
		PRINTFORML 준비를 시작하고 말았다.
	ENDIF
ELSE
	PRINTFORM 조금씩 뜨겁게 달아
	IF TALENT:주관시점 > 0
		PRINTFORML 올라 있었다.
	ELSE
		PRINTFORML 올라, 자손을 배려 하고 있었다.
	ENDIF
ENDIF
PRINTW 
PRINTFORM %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 발정에 의해 
FONTBOLD
SETCOLOR 150,0,250
PRINT [배란] 
RESETCOLOR
FONTREGULAR
PRINTL 상태가 되고 말았다！
PRINTW 
TCVARn:12 |= 배란

RETURN 1


