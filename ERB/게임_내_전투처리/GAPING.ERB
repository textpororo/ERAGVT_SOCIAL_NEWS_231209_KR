
;CON	캐릭터의 변신 상황	0：변신 전　1：변신 후
;TAR	캐릭터 ID		0：현재 TARGET


;RANK 0　E 섬모
;	중앙값：0
;	범위　：-5 〜 5

;RANK 1　E+
;	중앙값：10
;	범위　：5 〜 15

;RANK 2　D 손가락
;	중앙값：20
;	범위　：15 〜 25

;RANK 3　D+
;	중앙값：32
;	범위　：25 〜 40

;RANK 4　C 남성기
;	중앙값：50
;	범위　：40 〜 60

;RANK 5　C+
;	중앙값：72
;	범위　：60 〜 85

;RANK 6　B 팔다리
;	중앙값：100
;	범위　：85 〜 115

;RANK 7　B+
;	중앙값：135
;	범위　：115 〜 155

;RANK 8　A 머리
;	중앙값：180
;	범위　：155 〜 205

;RANK 9　A+
;	중앙값：235
;	범위　：205 〜 265

;RANK10　S 허리
;	중앙값：300
;	범위　：265 〜 335

;RANK11　S+
;	중앙값：400
;	범위　：335 〜 465

;＠중국판 역수출
;정말이지 중국판만 나오면 뭐든지 극으로 치닫는 느낌이 든다
;RANK12　처 머리 (凄 头)
;	중앙값：550
;	범위　：465 〜 635
;RANK13　극(極) 
;	중앙값：700
;	범위　：635 〜 765
;RANK14　절 허리 (絶 腰)
;	중앙값：850
;	범위　：765 〜 935
;RANK15　옥 (獄)
;	중앙값：1000
;	범위　：935 〜 1065
;RANK16　광 망치 (狂 槌)
;	중앙값：1135
;	범위　：1065 〜 1205
;RANK17　최광 선수 (最狂 艏)
;	중앙값：1280
;	범위　：1205 〜 1355

;--------------------------------------------------
;FUNCTION함수	구슬로 변수 산출
;--------------------------------------------------

;--------------------------------------------------
;확장도로부터 현재의 확장 등급을 반환한다
;ARG	확장도
@GAPING_RANK(ARG)
#FUNCTION

;E	섬모
IF ARG < 5
	LOCAL = 0
;E+
ELSEIF ARG < 15
	LOCAL = 1
;D	손가락
ELSEIF ARG < 25
	LOCAL = 2
;D+
ELSEIF ARG < 40
	LOCAL = 3
;C	남성기
ELSEIF ARG < 60
	LOCAL = 4
;C+
ELSEIF ARG < 85
	LOCAL = 5
;B	팔다리
ELSEIF ARG < 115
	LOCAL = 6
;B+
ELSEIF ARG < 155
	LOCAL = 7
;A	머리
ELSEIF ARG < 205
	LOCAL = 8
;A+
ELSEIF ARG < 265
	LOCAL = 9
;S	허리
ELSEIF ARG < 335
	LOCAL = 10
;S+
ELSEIF ARG < 465
	LOCAL = 11
;처(凄)
ELSEIF ARG < 635
	LOCAL = 12
;극(極)
ELSEIF ARG < 765
	LOCAL = 13
;절(絶)
ELSEIF ARG < 935
	LOCAL = 14
;옥(獄)
ELSEIF ARG < 1065
	LOCAL = 15
;광(狂)
ELSEIF ARG < 1205
	LOCAL = 16
;최광(最狂)
ELSE
	LOCAL = 17
ENDIF

RETURNF LOCAL



;--------------------------------------------------
;확장 등급으로부터, Ｅ〜Ｓ+ 혹은 설명 텍스트를 반환한다
;ARG	확장 등급
;ARGS	NAME	등급의 알파벳 표기
;	TEXT	등급별 설명문
;	T_TEXT	촉수 측 설명문
@GAPING_RANK_STR(ARG, ARGS)
#FUNCTIONS
SELECTCASE ARGS
	CASE "NAME"
		IF ARG == -2
			LOCALS = %UNICODE(0x0020)%―%UNICODE(0x0020)%
		ELSEIF ARG == -1
			LOCALS = 처녀
		ELSEIF ARG == 0
			LOCALS = %UNICODE(0x0020)%Ｅ%UNICODE(0x0020)%
		ELSEIF ARG == 1
			LOCALS = %UNICODE(0x0020)%Ｅ+
		ELSEIF ARG == 2
			LOCALS = %UNICODE(0x0020)%Ｄ%UNICODE(0x0020)%
		ELSEIF ARG == 3
			LOCALS = %UNICODE(0x0020)%Ｄ+
		ELSEIF ARG == 4
			LOCALS = %UNICODE(0x0020)%Ｃ%UNICODE(0x0020)%
		ELSEIF ARG == 5
			LOCALS = %UNICODE(0x0020)%Ｃ+
		ELSEIF ARG == 6
			LOCALS = %UNICODE(0x0020)%Ｂ%UNICODE(0x0020)%
		ELSEIF ARG == 7
			LOCALS = %UNICODE(0x0020)%Ｂ+
		ELSEIF ARG == 8
			LOCALS = %UNICODE(0x0020)%Ａ%UNICODE(0x0020)%
		ELSEIF ARG == 9
			LOCALS = %UNICODE(0x0020)%Ａ+
		ELSEIF ARG == 10
			LOCALS = %UNICODE(0x0020)%Ｓ%UNICODE(0x0020)%
		ELSEIF ARG == 11
			LOCALS = %UNICODE(0x0020)%Ｓ+
		ELSEIF ARG == 12
			LOCALS = %UNICODE(0x0020)%처%UNICODE(0x0020)%
		ELSEIF ARG == 13
			LOCALS = %UNICODE(0x0020)%극%UNICODE(0x0020)%
		ELSEIF ARG == 14
			LOCALS = %UNICODE(0x0020)%절%UNICODE(0x0020)%
		ELSEIF ARG == 15
			LOCALS = %UNICODE(0x0020)%옥%UNICODE(0x0020)%
		ELSEIF ARG == 16
			LOCALS = %UNICODE(0x0020)%광%UNICODE(0x0020)%
		ELSE
			LOCALS = %UNICODE(0x0020)%최광%UNICODE(0x0020)%
		ENDIF
	CASE "TEXT"
		IF ARG == 0
			LOCALS = 섬모와 같은 정도의
		ELSEIF ARG == 1
			LOCALS = 섬모보다도 굵은
		ELSEIF ARG == 2
			LOCALS = 자신의 손가락과 같은 정도의
		ELSEIF ARG == 3
			LOCALS = 자신의 손가락보다도 굵은
		ELSEIF ARG == 4
			LOCALS = 일반적인 남성기와 같은 정도의
		ELSEIF ARG == 5
			LOCALS = 일반적인 남성기보다도 굵은
		ELSEIF ARG == 6
			LOCALS = 자신의 팔과 같은 정도의
		ELSEIF ARG == 7
			LOCALS = 자신의 팔다리보다도 굵은
		ELSEIF ARG == 8
			LOCALS = 자신의 정강이와 같은 정도의
		ELSEIF ARG == 9
			LOCALS = 자신의 정강이보다도 굵은
		ELSEIF ARG == 10
			LOCALS = 자신의 허벅지와 같은 정도의
		ELSEIF ARG == 11
			LOCALS = 자신의 허벅지보다도 굵은
		ELSEIF ARG == 12
			LOCALS = 자신의 머리와 같은 정도의
		ELSEIF ARG == 13
			LOCALS = 자신의 머리보다 굵은
		ELSEIF ARG == 14
			LOCALS = 자신의 허리보다 굵은
		ELSEIF ARG == 15
			LOCALS = 자신의 허리보다도 현격히 굵은
		ELSEIF ARG == 16
			;＠중국판스러움이 느껴지는 비유
			;원문은 破城槌のように太い
			LOCALS = 공성추만큼이나 굵은
		ELSE
			LOCALS = 뱃머리만 한 크기의
		ENDIF
		LOCALS = %LOCALS% 것을 받아들일 수 있다
		SIF ARG == -2
			LOCALS = 
		SIF ARG == -1
			LOCALS = 여태까지 순결을 유지하고 있다
	CASE "T_TEXT"
		IF ARG == 0
			LOCALS = 섬모 크기
		ELSEIF ARG == 1
			LOCALS = 섬모보다 굵은 정도
		ELSEIF ARG == 2
			LOCALS = %PRINT_CALLNAME(TARGET)%의 손가락과 같은 정도
		ELSEIF ARG == 3
			LOCALS = %PRINT_CALLNAME(TARGET)%의 손가락 보다도 굵다
		ELSEIF ARG == 4
			LOCALS = 일반적인 남성기와 같은 정도
		ELSEIF ARG == 5
			LOCALS = 일반적인 남성기보다도 굵다
		ELSEIF ARG == 6
			LOCALS = %PRINT_CALLNAME(TARGET)%의 팔과 같은 정도
		ELSEIF ARG == 7
			LOCALS = %PRINT_CALLNAME(TARGET)%의 팔보다도 굵다
		ELSEIF ARG == 8
			LOCALS = %PRINT_CALLNAME(TARGET)%의 정강이와 같은 정도
		ELSEIF ARG == 9
			LOCALS = %PRINT_CALLNAME(TARGET)%의 정강이보다도 굵다
		ELSEIF ARG == 10
			LOCALS = %PRINT_CALLNAME(TARGET)%의 허벅지와 같은 정도
		ELSEIF ARG == 11
			LOCALS = %PRINT_CALLNAME(TARGET)%의 허벅지보다도 굵다
		ELSEIF ARG == 12
			LOCALS = %PRINT_CALLNAME(TARGET)%의 머리와 같은 정도
		ELSEIF ARG == 13
			LOCALS = %PRINT_CALLNAME(TARGET)%의 머리보다도 굵다
		ELSEIF ARG == 14
			LOCALS = %PRINT_CALLNAME(TARGET)%의 허리보다도 굵다
		ELSEIF ARG == 15
			LOCALS = %PRINT_CALLNAME(TARGET)%의 허리보다도 현격히 굵다
		ELSEIF ARG == 16
			LOCALS = %조사처리(PRINT_CALLNAME(TARGET),"를")% 찢을 수 있을 만큼 굵다
		ELSE
			LOCALS = %조사처리(PRINT_CALLNAME(TARGET),"를")% 으깨 버릴 만큼 굵다
		ENDIF
ENDSELECT

RETURNF LOCALS



;--------------------------------------------------
;그 확장 등급까지 필요한 확장도를 반환한다
;ARG	확장 등급
@GAPING_RANK_TO_POINT(ARG)
#FUNCTION

VARSET LOCAL
IF ARG == 0
	LOCAL = 0
ELSEIF ARG == 1
	LOCAL = 5
ELSEIF ARG == 2
	LOCAL = 15
ELSEIF ARG == 3
	LOCAL = 25
ELSEIF ARG == 4
	LOCAL = 40
ELSEIF ARG == 5
	LOCAL = 60
ELSEIF ARG == 6
	LOCAL = 85
ELSEIF ARG == 7
	LOCAL = 115
ELSEIF ARG == 8
	LOCAL = 155
ELSEIF ARG == 9
	LOCAL = 205
ELSEIF ARG == 10
	LOCAL = 265
ELSEIF ARG == 11
	LOCAL = 335
;계산 사정상, 등급의 1단계 바깥쪽도 설정
ELSEIF ARG == -1
	LOCAL = -5
ELSEIF ARG == 12
	LOCAL = 465
ELSEIF ARG == 13
	LOCAL = 635
ELSEIF ARG == 14
	LOCAL = 765
ELSEIF ARG == 15
	LOCAL = 935
ELSEIF ARG == 16
	LOCAL = 1065
ELSEIF ARG == 17
	LOCAL = 1205

ENDIF

RETURNF LOCAL



;--------------------------------------------------
;확장도로부터 TARGET에 따른 실제 크기(mm)를 반환한다
;ARG	확장도
;D_RANK	TARGET의 현재 확장 등급
;D_SIZE	그 확장 등급의 상한・하한
;MED_VAR	각 등급의 중앙값
@GAPING_SIZE(ARG, CON = -1, TAR = -1)
#FUNCTION
#DIM CON
#DIM TAR
#DIM D_RANK
#DIM D_SIZE, 2
#DIM MED_VAR, 2

SIF TAR == -1
	TAR = TARGET
SIF CON == -1
	CON = CFLAG:TAR:1

D_RANK = GAPING_RANK(ARG)
;그 등급의 중앙값을 구한다
MED_VAR:0 = (GAPING_RANK_TO_POINT(D_RANK) + GAPING_RANK_TO_POINT(D_RANK + 1)) / 2

;현재의 확장도가 중앙값보다 높은지 낮은지
IF ARG > MED_VAR:0
	MED_VAR:1 = (GAPING_RANK_TO_POINT(D_RANK + 1) + GAPING_RANK_TO_POINT(D_RANK + 2)) / 2
ELSE
	MED_VAR:1 = MED_VAR:0
	MED_VAR:0 = (GAPING_RANK_TO_POINT(D_RANK) + GAPING_RANK_TO_POINT(D_RANK - 1)) / 2
	D_RANK --
ENDIF

IF D_RANK == 0
	D_SIZE:0 = 0
	D_SIZE:1 = GAPING_SIZE_01(CON, TAR)
ELSEIF D_RANK == 1
	D_SIZE:0 = GAPING_SIZE_01(CON, TAR)
	D_SIZE:1 = GAPING_SIZE_02(CON, TAR)
ELSEIF D_RANK == 2
	D_SIZE:0 = GAPING_SIZE_02(CON, TAR)
	D_SIZE:1 = GAPING_SIZE_03(CON, TAR)
ELSEIF D_RANK == 3
	D_SIZE:0 = GAPING_SIZE_03(CON, TAR)
	D_SIZE:1 = GAPING_SIZE_04(CON, TAR)
ELSEIF D_RANK == 4
	D_SIZE:0 = GAPING_SIZE_04(CON, TAR)
	D_SIZE:1 = GAPING_SIZE_05(CON, TAR)
ELSEIF D_RANK == 5
	D_SIZE:0 = GAPING_SIZE_05(CON, TAR)
	D_SIZE:1 = GAPING_SIZE_06(CON, TAR)
ELSEIF D_RANK == 6
	D_SIZE:0 = GAPING_SIZE_06(CON, TAR)
	D_SIZE:1 = GAPING_SIZE_07(CON, TAR)
ELSEIF D_RANK == 7
	D_SIZE:0 = GAPING_SIZE_07(CON, TAR)
	D_SIZE:1 = GAPING_SIZE_08(CON, TAR)
ELSEIF D_RANK == 8
	D_SIZE:0 = GAPING_SIZE_08(CON, TAR)
	D_SIZE:1 = GAPING_SIZE_09(CON, TAR)
ELSEIF D_RANK == 9
	D_SIZE:0 = GAPING_SIZE_09(CON, TAR)
	D_SIZE:1 = GAPING_SIZE_10(CON, TAR)
ELSEIF D_RANK == 10
	D_SIZE:0 = GAPING_SIZE_10(CON, TAR)
	D_SIZE:1 = GAPING_SIZE_11(CON, TAR)
ELSEIF D_RANK == 11
	D_SIZE:0 = GAPING_SIZE_11(CON, TAR)
	D_SIZE:1 = GAPING_SIZE_12(CON, TAR)
ELSEIF D_RANK == 12
	D_SIZE:0 = GAPING_SIZE_12(CON, TAR)
	D_SIZE:1 = GAPING_SIZE_13(CON, TAR)
ELSEIF D_RANK == 13
	D_SIZE:0 = GAPING_SIZE_13(CON, TAR)
	D_SIZE:1 = GAPING_SIZE_14(CON, TAR)
ELSEIF D_RANK == 14
	D_SIZE:0 = GAPING_SIZE_14(CON, TAR)
	D_SIZE:1 = GAPING_SIZE_15(CON, TAR)
ELSEIF D_RANK == 15
	D_SIZE:0 = GAPING_SIZE_15(CON, TAR)
	D_SIZE:1 = GAPING_SIZE_16(CON, TAR)
ELSEIF D_RANK == 16
	D_SIZE:0 = GAPING_SIZE_16(CON, TAR)
	D_SIZE:1 = GAPING_SIZE_17(CON, TAR)

ENDIF

IF D_RANK > 16
	RETURNF (ARG - 125) * GET_HEIGHT(CON, TAR) / 1581 + GAPING_SIZE_11(CON, TAR)
ENDIF

;RETURNF (D_SIZE:1 - D_SIZE:0) * (ARG - GAPING_RANK_TO_POINT(D_RANK)) / (GAPING_RANK_TO_POINT(D_RANK + 1) - (GAPING_RANK_TO_POINT(D_RANK))) + D_SIZE:0
RETURNF (D_SIZE:1 - D_SIZE:0) * (ARG - MED_VAR:0) / (MED_VAR:1 - MED_VAR:0) + D_SIZE:0


;E(~E+?)　섬모
@GAPING_SIZE_01(CON, TAR)
#FUNCTION
#DIM CON
#DIM TAR

RETURNF GAPING_SIZE_02(CON, TAR) * 10 / 20

;D	손가락
@GAPING_SIZE_02(CON, TAR)
#FUNCTION
#DIM CON
#DIM TAR

RETURNF GET_HEIGHT(CON, TAR) * 10 / 1054

;D+	손가락
@GAPING_SIZE_03(CON, TAR)
#FUNCTION
#DIM CON
#DIM TAR

LOCAL = GAPING_SIZE_02(CON, TAR)

RETURNF (GAPING_SIZE_04(CON, TAR) - LOCAL) * 10 / 25 + LOCAL

;C	남성기
@GAPING_SIZE_04(CON, TAR)
#FUNCTION
#DIM CON
#DIM TAR

RETURNF GET_HEIGHT(CON, TAR) * 100 / 3952

;C+	남성기
@GAPING_SIZE_05(CON, TAR)
#FUNCTION
#DIM CON
#DIM TAR

LOCAL = GAPING_SIZE_04(CON, TAR)

RETURNF (GAPING_SIZE_06(CON, TAR) - LOCAL) * 15 / 35 + LOCAL

;B	팔다리
@GAPING_SIZE_06(CON, TAR)
#FUNCTION
#DIM CON
#DIM TAR

RETURNF GET_HEIGHT(CON, TAR) * 100 / 2108

;B+	팔다리
@GAPING_SIZE_07(CON, TAR)
#FUNCTION
#DIM CON
#DIM TAR

LOCAL = GAPING_SIZE_06(CON, TAR)

RETURNF (GAPING_SIZE_08(CON, TAR) - LOCAL) * 40 / 85 + LOCAL

;A	머리
@GAPING_SIZE_08(CON, TAR)
#FUNCTION
#DIM CON
#DIM TAR

RETURNF GET_HEIGHT(CON, TAR) * 10 / 131 + 40

;A+	머리
@GAPING_SIZE_09(CON, TAR)
#FUNCTION
#DIM CON
#DIM TAR

LOCAL = GAPING_SIZE_08(CON, TAR)

RETURNF (GAPING_SIZE_10(CON, TAR) - LOCAL) * 40 / 83 + LOCAL

;S	허벅지
@GAPING_SIZE_10(CON, TAR)
#FUNCTION
#DIM CON
#DIM TAR

RETURNF GET_HEIGHT(CON, TAR) * 10 / 160 + 20
;S+	허벅지
@GAPING_SIZE_11(CON, TAR)
#FUNCTION
#DIM CON
#DIM TAR

LOCAL = GAPING_SIZE_10(CON, TAR)

RETURNF (GAPING_SIZE_12(CON, TAR) - LOCAL) * 40 / 81 + LOCAL

;처	머리
@GAPING_SIZE_12(CON, TAR)
#FUNCTION
#DIM CON
#DIM TAR

RETURNF GET_HEIGHT(CON, TAR) * 10 / 131 + 40

;극	머리
@GAPING_SIZE_13(CON, TAR)
#FUNCTION
#DIM CON
#DIM TAR

LOCAL = GAPING_SIZE_12(CON, TAR)

RETURNF (GAPING_SIZE_14(CON, TAR) - LOCAL) * 45 / 90 + LOCAL

;절	허리
@GAPING_SIZE_14(CON, TAR)
#FUNCTION
#DIM CON
#DIM TAR

RETURNF GET_HIP(CON, TAR) * 100 / 314

;옥	허리
@GAPING_SIZE_15(CON, TAR)
#FUNCTION
#DIM CON
#DIM TAR

RETURNF GET_HIP(CON, TAR) * 13849 / 31415

;RANK16　광 망치
@GAPING_SIZE_16(CON, TAR)
#FUNCTION
#DIM CON
#DIM TAR

RETURNF GET_HIP(CON, TAR) * 13660 / 31415

;RANK17　최광 선수(艏)
@GAPING_SIZE_17(CON, TAR)
#FUNCTION
#DIM CON
#DIM TAR

RETURNF GET_HIP(CON, TAR) * 13740 / 31415





;--------------------------------------------------
;변신 상황에 따른 신장을 반환한다
;TARGET이 -1일 경우,「일반적인」크기
@GET_HEIGHT(CON, TAR)
#FUNCTION
#DIM CON
#DIM TAR
#DIM 신장치
#DIM KEEPTARGET

KEEPTARGET = TARGET

IF TAR < 0
	신장치 = 1581
ELSE
	SIF TAR > 0
		TARGET = TAR

	IF CON == 0 || TALENT:변신능력 != 1
		신장치 = BASE:신장
	ELSE
		신장치 = MAXBASE:신장
	ENDIF
ENDIF

TARGET = KEEPTARGET

RETURNF 신장치



;--------------------------------------------------
;변신 상황에 따른 골반 둘레를 반환한다
;TARGET이 -1일 경우,「일반적인」크기
@GET_HIP(CON, TAR)
#FUNCTION
#DIM CON
#DIM TAR
#DIM 골반둘레치
#DIM KEEPTARGET

KEEPTARGET = TARGET

IF TAR < 0
	골반둘레치 = 816
ELSE
	SIF TAR > 0
		TARGET = TAR

	IF CON == 1 || TALENT:변신능력 != 1 || (CON == 0 && CFLAG:1 == 0)
		골반둘레치 = BASE:골반둘레
	ELSE
		골반둘레치 = MAXBASE:골반둘레
	ENDIF
ENDIF

TARGET = KEEPTARGET

RETURNF 골반둘레치



;--------------------------------------------------
;실제 크기(mm)로부터 TARGET에 따른 확장 등급을 반환한다
;ARG
@GAPING_SIZE_TO_RANK(ARG, CON, TAR)
#FUNCTION
#DIM CON
#DIM TAR

IF ARG < GAPING_SIZE_01(CON, TAR)
	LOCAL = 0
ELSEIF ARG < GAPING_SIZE_02(CON, TAR)
	LOCAL = 1
ELSEIF ARG < GAPING_SIZE_03(CON, TAR)
	LOCAL = 2
ELSEIF ARG < GAPING_SIZE_04(CON, TAR)
	LOCAL = 3
ELSEIF ARG < GAPING_SIZE_05(CON, TAR)
	LOCAL = 4
ELSEIF ARG < GAPING_SIZE_06(CON, TAR)
	LOCAL = 5
ELSEIF ARG < GAPING_SIZE_07(CON, TAR)
	LOCAL = 6
ELSEIF ARG < GAPING_SIZE_08(CON, TAR)
	LOCAL = 7
ELSEIF ARG < GAPING_SIZE_09(CON, TAR)
	LOCAL = 8
ELSEIF ARG < GAPING_SIZE_10(CON, TAR)
	LOCAL = 9
ELSEIF ARG < GAPING_SIZE_11(CON, TAR)
	LOCAL = 10
ELSEIF ARG < GAPING_SIZE_12(CON, TAR)
	LOCAL = 11
ELSEIF ARG < GAPING_SIZE_13(CON, TAR)
	LOCAL = 12
ELSEIF ARG < GAPING_SIZE_14(CON, TAR)
	LOCAL = 13
ELSEIF ARG < GAPING_SIZE_15(CON, TAR)
	LOCAL = 14
ELSEIF ARG < GAPING_SIZE_16(CON, TAR)
	LOCAL = 15
ELSEIF ARG < GAPING_SIZE_17(CON, TAR)
	LOCAL = 16
ELSE
	LOCAL = 17
ENDIF

RETURNF LOCAL



;--------------------------------------------------
;크기차에 따른 확장도의 상승치를 결정
;ARGS	부위 지정　Ｖ, Ａ

;P_SIZE	플레이어 크기
;P_RANK	플레이어 등급
;E_SIZE	촉수 크기
@GAPING_SIZE_TO_POINT(ARGS, TAR)
#FUNCTION
#DIM TAR
#DIM P_SIZE
#DIM P_RANK
#DIM E_SIZE
#DIM KEEPTARGET

SIF ARGS != "Ｖ" && ARGS != "Ａ"
	RETURNF 0

KEEPTARGET = TARGET
SIF TAR > 0
	TARGET = TAR

;대상이 남자임과 동시에 Ｖ라면 종료
IF ISMALE() && ARGS == "Ｖ"
	TARGET = KEEPTARGET
	RETURNF 0
ENDIF

IF ARGS == "Ｖ"
	P_SIZE = GAPING_SIZE(CFLAG:35, 0)
	P_RANK = GAPING_RANK(CFLAG:35)
	E_SIZE = TENTACLE_SIZE:1:쾌Ｖ
	IF INSERT & 질 || CFLAG:0 == 1
		LOCAL = (E_SIZE > P_SIZE) * 3
	ELSE
		LOCAL = 0
	ENDIF
ELSEIF ARGS == "Ａ"
	P_SIZE = GAPING_SIZE(CFLAG:36, 0)
	P_RANK = GAPING_RANK(CFLAG:36)
	E_SIZE = TENTACLE_SIZE:1:쾌Ａ
	IF INSERT & 애널 || CFLAG:0 == 1
		LOCAL = (E_SIZE > P_SIZE) * 3
	ELSE
		LOCAL = 0
	ENDIF
ENDIF

LOCAL = LOCAL + LIMIT(GAPING_SIZE_TO_RANK(E_SIZE) - P_RANK, 0, 99)

TARGET = KEEPTARGET

RETURNF LOCAL


;-----------------------------------------------------------------------------------------------------------------------------------------------------------
;통상 함수	처리
;-----------------------------------------------------------------------------------------------------------------------------------------------------------

;--------------------------------------------------
;각 부위에 사용되는 촉수 크기 표시
@PRINT_TENTACLE_SIZE, LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3
;T_RANK	촉수 크기 등급
#DIM T_RANK, 2
REPEAT 감각수
	IF TENTACLE_SIZE:0:COUNT > 0 || TENTACLE_SIZE:1:COUNT > 0
		T_RANK:0 = GAPING_SIZE_TO_RANK(TENTACLE_SIZE:0:COUNT)
		T_RANK:1 = GAPING_SIZE_TO_RANK(TENTACLE_SIZE:1:COUNT)
		;부위명 표시
		SUBSTRINGU ABLNAME:COUNT, 0, 1
		LOCALS = 　%RESULTS%용 촉수　
		;촉수가 아니기 때문에, TENTACLE_SIZE:0:COUNT는 0인 채로
		IF TENTACLE_SIZE:0:COUNT == 0
			LOCALS = %LOCALS%[%GAPING_RANK_STR(T_RANK:1, "NAME")%] %GAPING_RANK_STR(T_RANK:1, "T_TEXT")%
		;그 외
		ELSE
			LOCALS = %LOCALS%[%GAPING_RANK_STR(T_RANK:1, "NAME")%] %GAPING_RANK_STR(T_RANK:0, "T_TEXT")% {TENTACLE_NUM:0:COUNT}마리 
			SIF FLAG:700 == 1 && ((COUNT == 1 && INSERT & 질) || (COUNT == 2 && INSERT & 애널))
				LOCALS += "（삽입중）"
			SIF (SHIELD:COUNT > 0 && FLAG:700 == 0) || (TENTACLE_NUM:1:COUNT == 0 && FLAG:700 == 1)
				LOCALS = %LOCALS, 50, LEFT% → [ × ] 결계에 막혀 있다
		ENDIF
		;debug표시
		SIF FLAG:999 > 0
			LOCALS = %LOCALS% 　<font color='#606060' >/* debug */ {TENTACLE_SIZE:0:COUNT / 10, 2}.{TENTACLE_SIZE:0:COUNT % 10} cm × {TENTACLE_NUM:0:COUNT} → {TENTACLE_SIZE:1:COUNT / 10, 2}.{TENTACLE_SIZE:1:COUNT % 10} cm</font>
		SIF LOCAL:COUNT - COMMON_PALAM:COUNT > 0 && (FLAG:700 == 0 || TFLAG:10 != 1006 || CFLAG:0 == 1)
			HTML_PRINT LOCALS
	ENDIF
REND
PRINTL 




;현재의 확장도를 표시
;--------------------------------------------------
@PRINTFORM_GAPING_NOW, TAR = -1, CON = -1
#DIM CON
#DIM TAR
SIF TAR == -1
	TAR = TARGET
SIF CON == -1
	CON = CFLAG:TAR:1


;확장도 컨피그 플래그 체크
IF CONFIG_CHECK_MANIAC_F(16) == 1 && CFLAG:TAR:34>0
	;확장도 초기값 설정
	IF CFLAG:TAR:35 == 0
		CFLAG:TAR:35 = LIMIT((BASE:TAR:나이 - 10), 0, 15) / 3 - (TALENT:TAR:미성숙 > 0) * 3 + (TALENT:TAR:Ｖ민감 > 0) * 3 - (TALENT:TAR:Ｖ둔감 > 0) * 3 + 20
		CALL V_GAPING, EXP:TAR:Ｖ확장경험 * 10, TAR
	ENDIF
	IF CFLAG:TAR:36 == 0
		CFLAG:TAR:36 = LIMIT((20 - BASE:TAR:나이), 0, 15) / 3 + (TALENT:TAR:Ａ민감 > 0) * 3 - (TALENT:TAR:Ａ둔감 > 0) * 3 + 20
		CALL A_GAPING, EXP:TAR:Ａ확장경험 * 10, TAR
	ENDIF

	PRINTL 　　확장도

	FOR LOCAL:1, 0, 2
		IF LOCAL:1 == 0
			IF ISMALE(TAR)
				LOCALS = %UNICODE(0x0020)%―%UNICODE(0x0020)%
			ELSE
				LOCALS = 　질 지름
			ENDIF
		ELSE
			LOCALS = 애널 지름
		ENDIF

		LOCAL:2 = GAPING_RANK(CFLAG:TAR:(35 + LOCAL:1))
		LOCAL:3 = GAPING_SIZE(CFLAG:TAR:(35 + LOCAL:1), CON, TAR)

		LOCALS:1 = %LOCALS:1, 3, LEFT%

		IF LOCAL:1 == 0 && (TALENT:TAR:처녀 == 1 || ISMALE(TAR))
			LOCALS = 　　　　%LOCALS%：　 ―%UNICODE(0x0020)%
		ELSE
			LOCALS = 　　　　%LOCALS%：{LOCAL:3 / 10, 4}.{LOCAL:3 % 10}
		ENDIF

		SIF LOCAL:1 == 0 && TALENT:TAR:처녀 == 1
			LOCAL:2 = -1
		SIF LOCAL:1 == 0 && ISMALE(TAR)
			LOCAL:2 = -2

		LOCALS = %LOCALS% cm　/　<nonbutton title='%GAPING_RANK_STR(LOCAL:2, "TEXT")%'>[%GAPING_RANK_STR(LOCAL:2, "NAME")%]</nonbutton>
		SIF FLAG:999 && FLAG:700 == 0
			LOCALS = %LOCALS%　　<font color='#606060' >/* debug */ (확장도：{CFLAG:TAR:(35 + LOCAL:1), 4})　　　<button value='{30 + LOCAL:1}'>[{30 + LOCAL:1}] 초기화</button></font>
		HTML_PRINT LOCALS
	NEXT
ENDIF

;--------------------------------------------------
;Ｖ확장도 상승
;CFLAG:35	Ｖ확장도
;ARG		상승치
;P_SIZE		확장 후와의 비교를 위해 크기를 일시 기억
@V_GAPING(ARG, TAR)
#DIM TAR
#DIM KEEPTARGET
#DIM V_RANK
#DIM UPRATE
#DIM P_SIZE
#DIM LCOUNT

KEEPTARGET = TARGET
SIF TAR > 0
	TARGET = TAR
SIF ISMALE()
	RETURN 0
SIF CFLAG:34 == 0
	RETURN 0

;상승값이 0이라면 종료
SIF ARG == 0
	RETURN 0

P_SIZE = GAPING_SIZE(CFLAG:35)

FOR LCOUNT, 0, ARG * ARG

	V_RANK = GAPING_RANK_TO_POINT(CFLAG:35)
	IF CFLAG:1 > 0
		UPRATE = LIMIT((20 - MAXBASE:나이), 0, 15) * 3 + 30
	ELSE
		UPRATE = LIMIT((20 - BASE:나이), 0, 15) * 3 + 30
	ENDIF
	UPRATE = 2500 - (POWER(CFLAG:35, 2) / 150 + GAPING_RANK(CFLAG:35) * 100) * UPRATE / 20 + EXP:Ｖ확장경험 * 10 + EXP:출산경험 * 20

	SIF TALENT:미성숙 > 0
		UPRATE -= 200
	SIF TALENT:Ｖ민감 > 0
		UPRATE += 200
	SIF TALENT:Ｖ둔감 > 0
		UPRATE -= 300
	SIF TALENT:음호 > 0
		UPRATE += 300

	IF ARG >= 8
		UPRATE += 4000
	ELSEIF ARG >= 7
		UPRATE += 2000
	ELSEIF ARG >= 6
		UPRATE += 1000
	ELSEIF ARG >= 5
		UPRATE += 500
	ENDIF

	UPRATE = LIMIT(UPRATE, 50, 9999)

	;극단적인 확장이 억제되어 있는 경우
	IF CONFIG_CHECK_MANIAC_F(17) == 0
		SIF CFLAG:35 >= GAPING_RANK_TO_POINT(8) - 1
			RETURN 0
		UPRATE /= 3
		
		SIF CFLAG:35 >= 110
			CFLAG:35 = 110
	ENDIF

	SIF UPRATE > RAND:10000
		CFLAG:35 ++
NEXT

IF TALENT:처녀 > 0
	LOCAL = 0
ELSE
	LOCAL = GAPING_SIZE(CFLAG:35) - P_SIZE
ENDIF

TARGET = KEEPTARGET
RETURN LOCAL

;--------------------------------------------------
;Ａ 확장도 상승
;CFLAG:36	Ａ확장도
;ARG		상승치
;P_SIZE		확장 후와의 비교를 위해 크기를 일시 기억
@A_GAPING(ARG, TAR)
#DIM TAR
#DIM KEEPTARGET
#DIM UPRATE
#DIM P_SIZE
#DIM LCOUNT

KEEPTARGET = TARGET
SIF TAR > 0
	TARGET = TAR
SIF CFLAG:34 == 0
	RETURN 0

;상승값이 0이라면 종료
SIF ARG == 0
	RETURN 0

P_SIZE = GAPING_SIZE(CFLAG:36)

FOR LCOUNT, 0, ARG * ARG

	IF CFLAG:1 > 0
		UPRATE = LIMIT(MAXBASE:나이 - 10, 0, 15) * 3 + 30
	ELSE
		UPRATE = LIMIT(BASE:나이 - 10, 0, 15) * 3 + 30

	ENDIF
	UPRATE = 2500 - (POWER(CFLAG:36, 2) / 150 + GAPING_RANK(CFLAG:36) * 100) * UPRATE / 20 + EXP:Ａ확장경험 * 10

	SIF TALENT:Ａ민감 > 0
		UPRATE += UPRATE / 8 + 200
	SIF TALENT:Ａ둔감 > 0
		UPRATE -= UPRATE / 8 + 200
	SIF TALENT:음고 > 0
		UPRATE += UPRATE / 8 + 100

	IF ARG >= 8
		UPRATE += 4000
	ELSEIF ARG >= 7
		UPRATE += 2000
	ELSEIF ARG >= 6
		UPRATE += 1000
	ELSEIF ARG >= 5
		UPRATE += 500
	ENDIF

	UPRATE = LIMIT(UPRATE, 500, 9999)

	;극단적인 확장이 억제되어 있는 경우
	IF CONFIG_CHECK_MANIAC_F(17) == 0
		SIF CFLAG:36 >= GAPING_RANK_TO_POINT(8) - 1
			RETURN 0
		UPRATE /= 3
		
		SIF CFLAG:36 >= 110
			CFLAG:36 = 110
	ENDIF

	SIF UPRATE > RAND:10000
		CFLAG:36 ++
NEXT

LOCAL = GAPING_SIZE(CFLAG:36) - P_SIZE

TARGET = KEEPTARGET

RETURN LOCAL

;--------------------------------------------------
;Ｖ확장경험 획득
;CFLAG:35	Ｖ확장도
;ARG		크기차
@GET_V_GAPING_EXP(ARG, TAR, OPTION = 0)
#DIM OPTION
#DIM TAR
#DIM KEEPTARGET
#DIM LCOUNT

KEEPTARGET = TARGET
SIF TAR > 0
	TARGET = TAR
SIF ISMALE()
	RETURN 0
SIF CFLAG:34 == 0
	RETURN 0

IF ARG >= 6
	LOCAL = 2
ELSEIF ARG >= 3
	LOCAL = 1
ENDIF
IF LOCAL > 0 && CFLAG:0 != 1 && OPTION == 0
	EXP:Ｖ확장경험 += LOCAL
	PRINTFORML Ｖ확장경험 + {LOCAL}
ENDIF
TARGET = KEEPTARGET

RETURN LOCAL

;--------------------------------------------------
;크기차에 따른 Ａ확장 판정
;CFLAG:36	Ａ확장도
;ARG		크기차
@GET_A_GAPING_EXP(ARG, TAR, OPTION = 0)
#DIM OPTION
#DIM TAR
#DIM KEEPTARGET
#DIM LCOUNT

KEEPTARGET = TARGET
SIF TAR > 0
	TARGET = TAR
SIF CFLAG:34 == 0
	RETURN 0

IF ARG >= 6
	LOCAL = 2
ELSEIF ARG >= 3
	LOCAL = 1
ENDIF
IF LOCAL > 0 && CFLAG:0 != 1 && OPTION == 0
	EXP:Ａ확장경험 += LOCAL
	PRINTFORML Ａ확장경험 + {LOCAL}
ENDIF

TARGET = KEEPTARGET

RETURN LOCAL


;--------------------------------------------------
;보스 촉수・최종 보스의 각 부위용 촉수 크기
;ARG:0	ARG:1의 번호가 보스 촉수의 것인지, 최종 보스 촉수의 것인지를 판단한다 (0=보스 촉수, 1=최종 보스 촉수, 2=잡몹 전투)
;ARG:1	상대의 촉수를 나타낸다. 촉수의 번호로 보며, -1이라면 보스 촉수가 출현하지 않는다
;ARG:2	세뇌/흑화 캐릭터가 상대일 경우, 1
;ARG:3	세뇌/흑화인 캐릭터 번호
;ARG:4　적이 선택한 커맨드 번호
@SET_TENTACLE_SIZE, ARG:0, ARG:1, ARG:2, ARG:3, ARG:4
#DIM LCOUNT
IF ARG:2 > 0
	RESULT = ABL:(ARG:3):레벨
ELSE
	CALL TENTACLE_LEVEL
ENDIF
;극단적인 사이즈 ON일 경우
SIF CONFIG_CHECK_MANIAC_F(20) == 1
	RESULT = RESULT*10

;촉수 크기 기본값
;강제 자위
IF ARG:4 == 1006 && FLAG:700 == 1
	;LOCAL = GAPING_SIZE(10, 0, TARGET)
	FOR LCOUNT, 0, 감각수
		;TENTACLE_SIZE:0:LCOUNT = LOCAL
		TENTACLE_SIZE:0:LCOUNT = GAPING_SIZE_02(CFLAG:1, TARGET)
		TENTACLE_NUM:0:LCOUNT = 2 + RAND:4
	NEXT
	RETURN 0
;세뇌/흑화 캐릭터가 상대
ELSEIF ARG:2 > 0
	;기생에 의한 촉수가 있다
	IF TALENT:(ARG:3):기생 > 0
		LOCAL = SQRT((RESULT * 4 + 15 + ABL:(ARG:3):욕망) * (20 + TALENT:(ARG:3):촉수의포로 * 15))
		;극단적인 사이즈 Off 시에는 억제
		SIF CONFIG_CHECK_MANIAC_F(20) == 0
			LOCAL = SQRT(LOCAL * 55)
		FOR LCOUNT, 0, 감각수
			RESULT:LCOUNT = ABL:(ARG:3):LCOUNT * 10 + 50
			RESULT:(LCOUNT+4) = 1
		NEXT
	ELSE
		;각 부위에 세뇌/흑화 캐릭터의 손가락 크기를 지정
		LOCAL = GAPING_SIZE(10, 0, ARG:3)
		FOR LCOUNT, 0, 감각수
			TENTACLE_SIZE:0:LCOUNT = LOCAL
			TENTACLE_NUM:0:LCOUNT = 1
		NEXT
		;세뇌/흑화 캐릭터가 남성기를 지닌 경우, ＶＡ만 크기 지정
		IF ISPENIS(ARG:3)
			LOCAL = ((ABL:(ARG:3):욕망 * 3 + ABL:(ARG:3):사정중독 * 6 + ABL:(ARG:3):Ｃ감각 + TALENT:(ARG:3):음란 * 9)) * 18 / 74 + 32
			LOCAL = GAPING_SIZE(LOCAL, 0, ARG:3)
			TENTACLE_SIZE:0:쾌Ｖ = LOCAL
			TENTACLE_SIZE:0:쾌Ａ = LOCAL
			TENTACLE_NUM:0:쾌Ｖ = 1
			TENTACLE_NUM:0:쾌Ａ = 1
		ENDIF

		;촉수가 없으면 부위별 할당・랜덤은 필요없다
		RETURN 0
	ENDIF
;보스 촉수
ELSEIF ARG:0 == 0
	LOCAL = SQRT((RESULT * 2 + 30) * 50)
	;극단적인 사이즈 Off 시에는 억제
	SIF CONFIG_CHECK_MANIAC_F(20) == 0
		LOCAL = SQRT(LOCAL * 40)
	TRYCALLFORM TENTACLE_BOSS_{ARG:1}_TENTACLE_SIZE, ARG:4
;최종 보스 촉수
ELSEIF ARG:0 == 1
	LOCAL = SQRT((RESULT * 6 + 35) * 150)
	;극단적인 사이즈 Off 시에는 억제
	SIF CONFIG_CHECK_MANIAC_F(20) == 0
		LOCAL = SQRT(LOCAL * 75)
	TRYCALLFORM TENTACLE_LASTBOSS_{ARG:1}_TENTACLE_SIZE, ARG:4
;잡몹
ELSEIF ARG:0 == 2
	LOCAL = SQRT((RESULT * 1 + 20) * 30)
	;극단적인 사이즈 Off 시에는 억제
	SIF CONFIG_CHECK_MANIAC_F(20) == 0
		LOCAL = SQRT(LOCAL * 25)
	FOR LCOUNT, 0, 감각수
		RESULT:LCOUNT = 100
	NEXT
	TRYCCALLFORM TENTACLE_MOB_{ARG:1}_TENTACLE_SIZE, ARG:4
	CATCH
		RESULT:0 = 120
		RESULT:4 = 1
		RESULT:1 = 80
		RESULT:5 = 1
		RESULT:2 = 80
		RESULT:6 = 1
		RESULT:3 = 120
		RESULT:7 = 1
	ENDCATCH
ENDIF

;Ｃ
TENTACLE_SIZE:0:쾌Ｃ = LOCAL * RESULT:0 / (520 + RAND:161) + 2
TENTACLE_NUM:0:쾌Ｃ = RESULT:4
;Ｖ
TENTACLE_SIZE:0:쾌Ｖ = LOCAL * RESULT:1 / (90 + RAND:21)
TENTACLE_NUM:0:쾌Ｖ = RESULT:5
;Ａ
TENTACLE_SIZE:0:쾌Ａ = LOCAL * RESULT:2 / (90 + RAND:21)
TENTACLE_NUM:0:쾌Ａ = RESULT:6
;Ｂ
TENTACLE_SIZE:0:쾌Ｂ = LOCAL * RESULT:3 / (255 +  + RAND:91) + 5
TENTACLE_NUM:0:쾌Ｂ = RESULT:7

;--------------------------------------------------
;묘사에 맞는 크기와 갯수로 변화시키는(지문을 우선한다) 처리
;ARGS:0 촉수의 부위　Ｃ　Ｖ　Ａ　Ｂ
;ARGS:1 촉수의 굵기　섬모　가늠　보통　굵음　극태
;ARGS:2 촉수의 수　　단수　복수　대량(たくさん)
@SET_TENTACLE_SIZE_BY_MESSAGE, ARGS:0, ARGS:1, ARGS:2
#DIM SELECT
IF ARGS:0 == "Ｃ"
	SELECT = 0
ELSEIF ARGS:0 == "Ｖ"
	SELECT = 1
ELSEIF ARGS:0 == "Ａ"
	SELECT = 2
ELSEIF ARGS:0 == "Ｂ"
	SELECT = 3
ENDIF
IF ARGS:1 == "섬모"
	IF TENTACLE_SIZE:0:SELECT >= 15
		TENTACLE_SIZE:0:SELECT = RAND:10 + 5
	ENDIF
ELSEIF ARGS:1 == "가늠"
	IF TENTACLE_SIZE:0:SELECT < 15
		TENTACLE_SIZE:0:SELECT = RAND:10 + 15
	ELSEIF TENTACLE_SIZE:0:SELECT >= 40
		TENTACLE_SIZE:0:SELECT = RAND:15 + 25
	ENDIF
ELSEIF ARGS:1 == "보통"
	IF TENTACLE_SIZE:0:SELECT < 40
		TENTACLE_SIZE:0:SELECT = RAND:20 + 40
	ELSEIF TENTACLE_SIZE:0:SELECT >= 85
		TENTACLE_SIZE:0:SELECT = RAND:25 + 60
	ENDIF
ELSEIF ARGS:1 == "굵음"
	IF TENTACLE_SIZE:0:SELECT < 85
		TENTACLE_SIZE:0:SELECT = RAND:30 + 85
	ELSEIF TENTACLE_SIZE:0:SELECT >= 155
		TENTACLE_SIZE:0:SELECT = RAND:40 + 115
	ENDIF
ELSEIF ARGS:1 == "극태"
	IF TENTACLE_SIZE:0:SELECT < 155
		TENTACLE_SIZE:0:SELECT = RAND:50 + 155
	ENDIF
ENDIF
IF ARGS:2 == "대량"
	IF TENTACLE_SIZE:0:SELECT < 5
		TENTACLE_NUM:0:SELECT = RAND:128 + 2
	ELSEIF TENTACLE_SIZE:0:SELECT < 15
		TENTACLE_NUM:0:SELECT = RAND:64 + 2
	ELSEIF TENTACLE_SIZE:0:SELECT < 25
		TENTACLE_NUM:0:SELECT = RAND:32 + 2
	ELSEIF TENTACLE_SIZE:0:SELECT < 40
		TENTACLE_NUM:0:SELECT = RAND:16 + 2
	ELSEIF TENTACLE_SIZE:0:SELECT < 60
		TENTACLE_NUM:0:SELECT = RAND:8 + 2
	ELSEIF TENTACLE_SIZE:0:SELECT < 85
		TENTACLE_NUM:0:SELECT = RAND:4 + 2
	ELSEIF TENTACLE_SIZE:0:SELECT < 115
		TENTACLE_NUM:0:SELECT = RAND:3 + 2
	ELSEIF TENTACLE_SIZE:0:SELECT < 155
		TENTACLE_NUM:0:SELECT = RAND:2 + 2
	ELSE
		TENTACLE_NUM:0:SELECT = 2
	ENDIF
ELSEIF ARGS:2 == "복수"
	IF TENTACLE_SIZE:0:SELECT < 5
		TENTACLE_NUM:0:SELECT = RAND:64 + 2
	ELSEIF TENTACLE_SIZE:0:SELECT < 15
		TENTACLE_NUM:0:SELECT = RAND:32 + 2
	ELSEIF TENTACLE_SIZE:0:SELECT < 25
		TENTACLE_NUM:0:SELECT = RAND:16 + 2
	ELSEIF TENTACLE_SIZE:0:SELECT < 40
		TENTACLE_NUM:0:SELECT = RAND:8 + 2
	ELSEIF TENTACLE_SIZE:0:SELECT < 60
		TENTACLE_NUM:0:SELECT = RAND:4 + 2
	ELSEIF TENTACLE_SIZE:0:SELECT < 85
		TENTACLE_NUM:0:SELECT = RAND:3 + 2
	ELSEIF TENTACLE_SIZE:0:SELECT < 115
		TENTACLE_NUM:0:SELECT = RAND:2 + 2
	ELSE
		TENTACLE_NUM:0:SELECT = 2
	ENDIF
ELSEIF ARGS:2 == "단수"
	TENTACLE_NUM:0:SELECT = 1
ENDIF


;--------------------------------------------------
;결계에 따른 촉수 마릿수의 변동
@SET_TENTACLE_POOL
#DIM LCOUNT
#DIM TENTACLE_POOL
VARSET TENTACLE_POOL
FOR LCOUNT, 0, 감각수
	;결계가 건재한 경우
	IF BASE:(LCOUNT + 30) > 0 && (TFLAG:10 != 1006 || CFLAG:0 == 1)
		TENTACLE_NUM:1:LCOUNT = 0
		TENTACLE_POOL ++
	ELSE
		TENTACLE_NUM:1:LCOUNT = TENTACLE_NUM:0:LCOUNT
	ENDIF
NEXT
;결계에 막힌 만큼만 남은 부위의 갯수가 늘어난다
IF TENTACLE_POOL > 0
	FOR LCOUNT, 0, 감각수
		TENTACLE_NUM:1:LCOUNT = TENTACLE_NUM:1:LCOUNT * (100 + TENTACLE_POOL * 25) / 100
		IF TENTACLE_NUM:0:LCOUNT == TENTACLE_NUM:1:LCOUNT
			TENTACLE_NUM:1:LCOUNT += 1
			TENTACLE_NUM:0:LCOUNT += 1
		ENDIF
	NEXT
ENDIF

;계산에 사용하는 실제 촉수 크기를 설정
CALL SET_TENTACLE_SIZE_R

RETURN 1


;--------------------------------------------------
;유폐 시의 처리
;실제 확장 경험에 대한 가산 처리는 전용 함수로 하기 때문에, 상승치만 반환한다
;대상은 TARGET 고정
;ARG:0		V경험 상승 예정치
;ARG:1		A경험 상승 예정치
;ARG:2		V확장 경험 상승 예정치
;ARG:3		A확장 경험 상승 예정치
;LOCAL:0～3 촉수 크기 ？？？
@PRISON_GAPING, ARG:0, ARG:1, ARG:2, ARG:3, ARG:4, LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3
VARSET TENTACLE_SIZE
;흑화 캐릭터에게 유폐
IF CFLAG:20 == 2
	CALL SET_TENTACLE_SIZE, 0, 0, CFLAG:20, FLAG:111
;촉수에게 유폐
ELSE
	CALL SET_TENTACLE_SIZE, CFLAG:20, CFLAG:21, 0, 0,-TFLAG:10
ENDIF
;REPEAT 감각수
;	TENTACLE_NUM:1:COUNT = TENTACLE_NUM:0:COUNT
;REND

;계산에 사용되는 실제 촉수 크기를 설정
CALL SET_TENTACLE_SIZE_R

;각 부위에 사용되는 촉수 크기 표시
SIF CONFIG_CHECK_MANIAC_F(16) == 1 && CFLAG:34 > 0
	CALL PRINT_TENTACLE_SIZE, LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3


;V확장 판정
IF ARG:0 > 0
	CALL GET_V_GAPING_EXP(GAPING_SIZE_TO_POINT("Ｖ"), TARGET, 1)
	ARG:2 += RESULT
	CALL V_GAPING(GAPING_SIZE_TO_POINT("Ｖ"))
	SIF RESULT > 0 && CONFIG_CHECK_MANIAC_F(16) == 1
		PRINTFORML 　질 지름：＋{RESULT / 10}.{RESULT % 10} cm
ENDIF
;A확장 판정
IF ARG:1 > 0
	CALL GET_A_GAPING_EXP(GAPING_SIZE_TO_POINT("Ａ"), TARGET, 1)
	ARG:3 += RESULT
	CALL A_GAPING(GAPING_SIZE_TO_POINT("Ａ"))
	SIF RESULT > 0 && CONFIG_CHECK_MANIAC_F(16) == 1
		PRINTFORML 애널 지름：＋{RESULT / 10}.{RESULT % 10} cm
ENDIF

RETURN ARG:2, ARG:3


;--------------------------------------------------
;계산에 사용되는 실제 촉수 크기를 설정
@SET_TENTACLE_SIZE_R
REPEAT 감각수
	IF TENTACLE_NUM:0:COUNT > 2
		TENTACLE_SIZE:1:COUNT = TENTACLE_SIZE:0:COUNT * (TENTACLE_NUM:0:COUNT + 4) / 3
	ELSE
		TENTACLE_SIZE:1:COUNT = TENTACLE_SIZE:0:COUNT * TENTACLE_NUM:0:COUNT
	ENDIF
	;극단적인 촉수 굵기를 억제하는 설정이 ON일 경우 A 이상이 되지 않도록 한다
	IF CONFIG_CHECK_MANIAC_F(20) == 1
		LOCAL:1 = GAPING_SIZE(154 - RAND:19)
		LOCAL:2 = GAPING_SIZE(155)
		IF TENTACLE_SIZE:1:COUNT >= LOCAL:2
			IF TENTACLE_NUM:0:COUNT > 2
				TENTACLE_SIZE:0:COUNT = LOCAL:1 / (TENTACLE_NUM:0:COUNT + 4) * 3
			ELSE
				TENTACLE_SIZE:0:COUNT = LOCAL:1 / TENTACLE_NUM:0:COUNT
			ENDIF
		ENDIF
	ENDIF
REND
;피스톤계라면 굵기＆수를 그대로
IF TFLAG:10 >= 15 && TFLAG:10 <= 20 && FLAG:700
	REPEAT 감각수
		IF (COUNT + 101 == 102 && (TFLAG:109 & 질) && (INSERT & 질)) || (COUNT + 101 == 103 && (TFLAG:109 & 애널) && (INSERT & 애널))
			TENTACLE_SIZE:0:COUNT = TFLAG:(COUNT + 101)
			TENTACLE_NUM:0:COUNT = TFLAG:(COUNT + 105)
			TENTACLE_NUM:1:COUNT = TFLAG:(COUNT + 105)
		ENDIF
	REND
;V확장 공격이라면 굵기 보정
ELSEIF (TFLAG:10 == 1009 && FLAG:700) || (TFLAG:10 == 101 && FLAG:700 == 0)
	TIMES TENTACLE_SIZE:0:1, 1.3
;A확장 공격이라면 굵기 보정
ELSEIF (TFLAG:10 == 1010 && FLAG:700) || (TFLAG:10 == 102 && FLAG:700 == 0)
	TIMES TENTACLE_SIZE:0:2, 1.3
ENDIF
;보정했을 때용으로 재계산
REPEAT 감각수
	IF TENTACLE_NUM:0:COUNT > 2
		TENTACLE_SIZE:1:COUNT = TENTACLE_SIZE:0:COUNT * (TENTACLE_NUM:0:COUNT + 4) / 3
	ELSE
		TENTACLE_SIZE:1:COUNT = TENTACLE_SIZE:0:COUNT * TENTACLE_NUM:0:COUNT
	ENDIF
REND

