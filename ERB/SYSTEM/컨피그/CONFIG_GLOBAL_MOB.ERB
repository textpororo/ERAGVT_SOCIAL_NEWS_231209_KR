;GLOBAL　잡몹적 출현 필터
;플래그는 ERH에서 관리
;---------------------------------------------
;필터 처리
;---------------------------------------------
@CONFIG_M
#DIM LCOUNT, 2
#DIM CATEGORY
#DIM PAGE
#DIM SELECT

#DIMS 종족,10
종족:0=무형
종족:1=슬라임
종족:2=식물
종족:3=수생계
종족:4=---
종족:5=인간형
종족:6=동물계
종족:7=곤충
종족:8=물질
종족:9=천사・악마계



LCOUNT:0 = LINECOUNT
$MASTER_LOOP
CALL LB
DRAWLINE
PRINTL 잡몹 출현 필터　카테고리를 선택해 주십시오.
PRINTL ※모든 잡몹 0% 시에는 전투 처리가 생략됩니다.
PRINTL 
FOR LOCAL,0,MOB_CATEGORY
	LOCAL:2=0
	LOCAL:3=0
	FOR LOCAL:1,0,100
		TRYCCALLFORM TENTACLE_MOB_{CATEGORY * 100 + LOCAL:1}
			LOCAL:2++
			LOCAL:3+=MOB_FLAG:LOCAL:(LOCAL:1)
		CATCH
		ENDCATCH
	NEXT
	;平均出現率
	IF LOCAL:2==0 || (LOCAL:3/LOCAL:2)==0
		SETCOLORBYNAME Gray
	ENDIF
	PRINTFORML [{LOCAL}] %종족:LOCAL,16,LEFT%　[{LOCAL+10}] 0％　[{LOCAL+20}] 100％
	RESETCOLOR
NEXT
PRINTL 
PRINTL [101] 모든 카테고리의 잡몹 출현율을 0％로 설정한다
PRINTL [102] 모든 카테고리의 잡몹 출현율을 100％로 설정한다
PRINTL [200] 변경 사항을 저장하고 돌아간다
$INPUT_LOOP
INPUT
IF RESULT >= 0 && RESULT <= 9 && RESULT != 4
	CATEGORY = RESULT
	PAGE = 1
	LCOUNT:1 = LINECOUNT
	$LOOP_0
	DRAWLINE
	PRINTFORML 잡몹 출현 필터　　　PAGE({PAGE}/5)　　　　　　　　　　　　　　　　출현율
	PRINTL 
	FOR LOCAL,1,21
		SELECT = (PAGE - 1) * 20 + LOCAL
		TRYCCALLFORM TENTACLE_MOB_{CATEGORY * 100 + SELECT}
			TRYCALLFORM TENTACLE_MOB_{CATEGORY * 100 + SELECT}_GETNAME
			PRINTFORM [{SELECT,3,RIGHT}] %RESULTS,56,LEFT%　{MOB_FLAG:CATEGORY:SELECT}％
		CATCH
			PRINT  ---
		ENDCATCH
		PRINTL 
	NEXT
	PRINTL 
	PRINT 이 카테고리의 전체 출현율을 
	PRINTPLAIN 　　　
	PRINT [101] 0％로 한다 
	PRINTPLAIN 　　　　
	PRINT [102] 100％로 한다
	PRINTL 
	PRINT [200] 돌아간다　　　
	PRINTPLAIN 　　　　　　　　
	IF PAGE >= 2
		PRINT [201] 앞 페이지
	ELSE
		PRINT 　　　　　　　 
	ENDIF
	PRINTPLAIN 　　　　
	IF PAGE <= 4
		PRINT [202] 뒷 페이지
	ELSE
		PRINT 　　　　　　　 
	ENDIF
	PRINTL 
	$INPUT_LOOP_0
	INPUT
	SELECT = RESULT
	IF SELECT != 101 && SELECT != 102 && SELECT != 201 && SELECT != 202 && SELECT != 200
		TRYCCALLFORM TENTACLE_MOB_{CATEGORY * 100 + SELECT}
		CATCH
			SELECT = -1
		ENDCATCH
	ENDIF
	IF SELECT >= ((PAGE - 1) * 20 + 1) && SELECT <= ((PAGE - 1) * 20 + 20)
		TRYCALLFORM TENTACLE_MOB_{CATEGORY * 100 + SELECT}_GETNAME
		PRINTFORML %RESULTS%의 출현률을 입력해 주십시오.（  0 - 100 ％）
		$INPUT_LOOP_1
		INPUT
		IF RESULT >= 0 && RESULT <= 100
			MOB_FLAG:CATEGORY:SELECT = RESULT
			CLEARLINE LINECOUNT - LCOUNT:1
			GOTO LOOP_0
		ELSE
			GOTO INPUT_LOOP_1
		ENDIF
	ELSEIF RESULT == 101
		FOR LOCAL:1,0,100
			TRYCCALLFORM TENTACLE_MOB_{CATEGORY * 100 + LOCAL:1}
				MOB_FLAG:CATEGORY:(LOCAL:1) = 0
			CATCH
			ENDCATCH
		NEXT
		CLEARLINE LINECOUNT - LCOUNT:1
		GOTO LOOP_0
	ELSEIF RESULT == 102
		FOR LOCAL:1,0,100
			TRYCCALLFORM TENTACLE_MOB_{CATEGORY * 100 + LOCAL:1}
				MOB_FLAG:CATEGORY:(LOCAL:1) = 100
			CATCH
			ENDCATCH
		NEXT
		CLEARLINE LINECOUNT - LCOUNT:1
		GOTO LOOP_0
	ELSEIF RESULT == 200
		CLEARLINE LINECOUNT - LCOUNT:0
		GOTO MASTER_LOOP
	ELSEIF RESULT == 201 && PAGE >= 2
		PAGE --
		CLEARLINE LINECOUNT - LCOUNT:1
		GOTO LOOP_0
	ELSEIF RESULT == 202 && PAGE <= 4
		PAGE ++
		CLEARLINE LINECOUNT - LCOUNT:1
		GOTO LOOP_0
	ELSE
		GOTO INPUT_LOOP_0
	ENDIF
ELSEIF RESULT >= 10 && RESULT<=19
	LOCAL=RESULT-10
		FOR LOCAL:1,0,100
			TRYCCALLFORM TENTACLE_MOB_{LOCAL * 100 + LOCAL:1}
				MOB_FLAG:LOCAL:(LOCAL:1) = 0
			CATCH
			ENDCATCH
		NEXT
	CLEARLINE LINECOUNT - LCOUNT:0
	GOTO MASTER_LOOP
ELSEIF RESULT >= 20 && RESULT<=29
	LOCAL=RESULT-20
		FOR LOCAL:1,0,100
			TRYCCALLFORM TENTACLE_MOB_{LOCAL * 100 + LOCAL:1}
				MOB_FLAG:LOCAL:(LOCAL:1) = 100
			CATCH
			ENDCATCH
		NEXT
	CLEARLINE LINECOUNT - LCOUNT:0
	GOTO MASTER_LOOP

ELSEIF RESULT == 101
	FOR LOCAL,0,MOB_CATEGORY
		FOR LOCAL:1,0,100
			TRYCCALLFORM TENTACLE_MOB_{LOCAL * 100 + LOCAL:1}
				MOB_FLAG:LOCAL:(LOCAL:1) = 0
			CATCH
			ENDCATCH
		NEXT
	NEXT
	CLEARLINE LINECOUNT - LCOUNT:0
	GOTO MASTER_LOOP
ELSEIF RESULT == 102
	FOR LOCAL,0,MOB_CATEGORY
		FOR LOCAL:1,0,100
			TRYCCALLFORM TENTACLE_MOB_{LOCAL * 100 + LOCAL:1}
				MOB_FLAG:LOCAL:(LOCAL:1) = 100
			CATCH
			ENDCATCH
		NEXT
	NEXT
	CLEARLINE LINECOUNT - LCOUNT:0
	GOTO MASTER_LOOP
ELSEIF RESULT == 200
	FOR LOCAL,0,MOB_CATEGORY
		FOR LOCAL:1,0,100
			MOB_GLOBAL:LOCAL:(LOCAL:1) = MOB_FLAG:LOCAL:(LOCAL:1)
		NEXT
	NEXT
	SAVEGLOBAL
	RETURN
ELSE
	GOTO INPUT_LOOP
ENDIF



