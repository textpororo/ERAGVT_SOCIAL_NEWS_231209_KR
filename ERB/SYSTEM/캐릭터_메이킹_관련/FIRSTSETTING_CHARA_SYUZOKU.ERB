
;-------------------------------------------------------------------------------------------------------------------------------------------------------------------
;캐릭터의 종족 설정
@FIRSTSETTING_CHARA_SYUZOKU, ARG
#DIM RACE
#DIM FEAT_NUM
#DIM FEAT_SELECT, 300
RACE = -1
;종족의 개시 번호
LOCAL:1 = 201
;FEAT 개시 번호
LOCAL:2 = 1000
;종족의 수
LOCAL:3 = 11
$MASTER_LOOP
;FEAT 초기화
VARSET FEAT_SELECT
FOR LOCAL, 100, 300
	;所持フィートの読み込み
	SIF TALENT:ARG:(LOCAL + LOCAL:2) > 0
		FEAT_SELECT:LOCAL = 1
NEXT
CALL LB
PRINTL ※종족별로 취득 가능한 FEAT가 설정되어 있어,
PRINTL 　총 3개까지 선택할 수가 있습니다.
PRINTL 　FEAT 이외의 기본 성능은 동등하여 종족 간의 우열은 존재하지 않습니다.
PRINTL 
PRINTFORML {ARG:0}번째 캐릭터『%CALLNAME:ARG%』의 종족을 설정해 주십시오.
PRINTL ・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・
FOR LOCAL, 0, LOCAL:3
	SIF TALENT:ARG:(LOCAL + LOCAL:1) > 0
		SETCOLOR 128,128,255
	SIF LOCAL < 10
		PRINT  
	PRINTFORML [{LOCAL}] %TALENTNAME:(LOCAL + LOCAL:1)%
	RESETCOLOR
NEXT
$INPUT_LOOP
INPUT
IF RESULT < 0 || RESULT > LOCAL:3
	GOTO INPUT_LOOP
ELSE
	RACE = RESULT
	FOR LOCAL, 100, 300
		SIF FEAT_ABLE_F(RACE + LOCAL:1, LOCAL + LOCAL:2) == 0
			FEAT_SELECT:LOCAL = 0
	NEXT
	CALL LB
	$MASTER_LOOP_1
	;뱀파이어라면 밤악마의귀족을 반드시 취득
	SIF RACE == 9
		FEAT_SELECT:110 = 1
	;FEAT의 수를 카운트
	FEAT_NUM = 0
	FOR LOCAL, 100, 300
		SIF FEAT_SELECT:LOCAL > 0
			FEAT_NUM ++
	NEXT
	PRINTFORML %TALENTNAME:(RACE + LOCAL:1)%
	CALL SHORTLINE
	CALL SYUZOKU_INFO, (RACE + LOCAL:1)
	PRINTL 
	PRINTL  (취득 가능 FEAT)             선택
	FOR LOCAL, 100, 300
		SIF FEAT_ABLE_F(RACE + LOCAL:1, LOCAL + LOCAL:2) == 0
			CONTINUE
		IF LOCAL < 200
			PRINT 　★
		ELSE
			PRINT 　☆
		ENDIF
		;글꼴을 굴림체로 바뀌어서 그런지 문자가 흐트러져서 글자수에 따라 공란을 줄이도록 수정
		PRINTFORM %TALENTNAME:(LOCAL + LOCAL:2),24-STRLENSU(TALENTNAME:(LOCAL + LOCAL:2)),LEFT%  [{LOCAL}]  
		IF LOCAL == 110 && FEAT_SELECT:LOCAL > 0
			SETCOLOR 255,0,0
			PRINT ◎
			RESETCOLOR
			PRINTPLAIN  고정 취득
		ELSEIF FEAT_SELECT:LOCAL > 0
			SETCOLOR 255,128,0
			PRINT ○
			RESETCOLOR
		ELSE
			PRINT ×
		ENDIF
		PRINTL 
	NEXT
	PRINTL 
	PRINTFORML 選択したフィート({FEAT_NUM}/3)
	SIF FEAT_NUM > 3
		SETCOLOR 128,128,128
	PRINTL [0] 결정
	RESETCOLOR
	PRINTL [1]キャンセル
	$INPUT_LOOP_0
	INPUT
	IF RESULT == 0 && FEAT_NUM <= 3
	ELSEIF RESULT == 1
		GOTO MASTER_LOOP
	ELSEIF RESULT >= 100 && RESULT <= 300 && FEAT_ABLE_F(RACE + LOCAL:1, RESULT + LOCAL:2) > 0
		LOCAL = RESULT
		CALL LB
		CALL FEAT_INFO, (LOCAL + LOCAL:2)
		PRINTL 
		IF FEAT_SELECT:LOCAL > 0
			FEAT_SELECT:LOCAL = 0
		ELSE
			FEAT_SELECT:LOCAL = 1
		ENDIF
		GOTO MASTER_LOOP_1
	ELSE
		GOTO INPUT_LOOP_0
	ENDIF
	;種族の설정
	FOR LOCAL, LOCAL:1, LOCAL:1 + LOCAL:3
		TALENT:ARG:LOCAL = 0
	NEXT
	TALENT:ARG:(RACE + LOCAL:1) = 1
	PRINTFORML 種族を 『%TALENTNAME:(RACE + (LOCAL:1))%』 に설정しました
	;인조인간の場合
	IF RACE == 3
		IF ISFEMALE(ARG)
			PRINTFORML 인공자궁を内蔵させることにより、%CALLNAME:ARG%を임신可能にすることができます
			PRINTFORML [0]임신機能を付ける
			PRINTFORML [1]임신機能を付けない（불임になります）
			$INPUT_LOOP_1
			INPUT
			IF RESULT == 0
				PRINTFORML %CALLNAME:ARG%は임신可能になります
				TALENT:ARG:미성숙 = 0
			ELSEIF RESULT == 1
				PRINTFORML %CALLNAME:ARG%に임신機能を付けません
				TALENT:ARG:미성숙 = 2
			ELSE
				GOTO INPUT_LOOP_1
			ENDIF
		ENDIF
		IF TALENT:ARG:구상설정 != 1
			PRINTFORML 구상を『ロボ風』に変更しますか？
			PRINTFORML [0]変更する
			PRINTFORML [1]変更しない
			$INPUT_LOOP_2
			INPUT
			IF RESULT == 0
				PRINTFORML 구상をロボ風に설정しました
				TALENT:ARG:구상설정 = 1
			ELSEIF RESULT == 1
				PRINTFORML 설정を変更しません
			ELSE
				GOTO INPUT_LOOP_1
			ENDIF
		ENDIF
	ENDIF
	;FEAT 설정
	FOR LOCAL, 100, 300 
		IF FEAT_SELECT:LOCAL > 0
			TALENT:ARG:(LOCAL + LOCAL:2) = 1
		ELSE
			TALENT:ARG:(LOCAL + LOCAL:2) = 0
		ENDIF
	NEXT
	;프로필에 반영
	CALL SET_PROFILE, ARG
ENDIF
PRINTL 
PRINTL 

;共通설정用
@FIRSTSETTING_racegenre
$loop
FOR local,0,10
	PRINTFORML [{local}]%racegenre:local%
NEXT
printl [99]ランダム
INPUT
IF (INRANGE(RESULT,0,10))
	FLAG:823=RESULT+1
ELSEIF RESULT==99
	FLAG:823=0
ELSE
	goto loop
ENDIF

