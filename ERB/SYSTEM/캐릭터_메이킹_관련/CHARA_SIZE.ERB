

;난수치		캐릭터 부위 사이즈를 결정하는 값　RESULT:0으로 반환한다
;		난수치 = RAND:(11 * 31 * 59 * 15 * 16 * 13 * 23 * 7 * 53)
;11	신장 성장률
;31	신장 보정
;59	체중(BMI)
;15	허리둘레
;16	골반둘레
;13	언더(under)
;23	가슴의 탄력의 강도 (胸の張りの強さ)
;7	가슴 사이즈 성장률
;53	가슴 사이즈 보정

;성장 곡선	5세～19세까지 1년에 얼마나 신장이 늘어날지를 설정한다　RESULT:1로 반환한다
;		또한, 그 캐릭터의 프로필이 설정 되었는지의 여부를 0인지 아닌지로 판정한다
;나이		캐릭터의 나이

;RESULT:2	신장을 반환한다(단위：1mm)
;RESULT:3	체중을 반환한다(단위：0.1kg)
;RESULT:4	가슴 둘레를 반환한다(단위：1mm)
;RESULT:5	허리 둘레를 반환한다(단위：1mm)
;RESULT:6	골반 둘레를 반환한다(단위：1mm)
;RESULT:7	가슴 무게를 반환한다(단위：10kg ※{RESULT:7 / 10}.{RESULT:7 % 10} 로 소수 제일 첫 번째 정도까지 나타낼 수 있다)
@GENERATE_CHAR_SIZE, 대상캐릭터, 변신치
#DIM KEEPTARGET
#DIM LCOUNT, 2

#DIM 대상캐릭터
#DIM 나이치
#DIM 신장
#DIM 체중, 2
#DIM 가슴둘레
#DIM 허리둘레
#DIM 골반둘레
#DIM 언더
#DIM 톱차분
#DIM 가슴무게
#DIM 난수치, 7
#DIM 성장곡선
#DIM 성장치, 15
#DIM BMI
;Rohrer Index 로러 지수, 로렐 지수, 뢰러 지수 등등 되게 많다
#DIM 로러지수
#DIM 변신치

#DIM CAL_VAR
#DIM DEV_VAR, 15
#DIM RAN_VAR, 2

VARSET 성장치

난수치 = CFLAG:대상캐릭터:33
성장곡선 = CFLAG:대상캐릭터:34
IF 변신치 > 0
	나이치 = MAXBASE:대상캐릭터:나이
ELSE
	나이치 = BASE:대상캐릭터:나이
ENDIF

;난수치의 덮어쓰기와 결정
난수치:1 = 난수치 % 11
SIF TALENT:대상캐릭터:신장성장률난수 > 0 && TALENT:대상캐릭터:신장성장률난수 < 12
	난수치:1 = TALENT:대상캐릭터:신장성장률난수 - 1
난수치:2 = 난수치 % 31
SIF TALENT:대상캐릭터:신장보정난수 > 0 && TALENT:대상캐릭터:신장보정난수 < 32
	난수치:2 = TALENT:대상캐릭터:신장보정난수 - 1
난수치:3 = 난수치 % 59
SIF TALENT:대상캐릭터:체중난수 > 0 && TALENT:대상캐릭터:체중난수 < 60
	난수치:3 = TALENT:대상캐릭터:체중난수 - 1
난수치:4 = 난수치 % 15
SIF TALENT:대상캐릭터:허리둘레난수 > 0 && TALENT:대상캐릭터:허리둘레난수 < 16
	난수치:4 = TALENT:대상캐릭터:허리둘레난수 - 1
난수치:5 = 난수치 % 16
SIF TALENT:대상캐릭터:골반둘레난수 > 0 && TALENT:대상캐릭터:골반둘레난수 < 17
	난수치:5 = TALENT:대상캐릭터:골반둘레난수 - 1
난수치:6 = 난수치 % 13
SIF TALENT:대상캐릭터:언더난수 > 0 && TALENT:대상캐릭터:언더난수 < 14
	난수치:6 = TALENT:대상캐릭터:언더난수 - 1

;난수
LCOUNT:1 = 성장곡선

;나이별 성장치 설정(n/100cm)
FOR LCOUNT, 0, 15
	성장치:LCOUNT = LCOUNT:1 % 10
	LCOUNT:1 /= 10
	성장치:LCOUNT *= 30
	IF LCOUNT < 7
		성장치:LCOUNT += 400
	ELSEIF LCOUNT == 7
		성장치:LCOUNT += 210
	ELSEIF LCOUNT == 8
		성장치:LCOUNT += 100
	ENDIF
NEXT

;신장 설정


;최저 신장의 난수 계산
;5세 평균
신장 = 10940 * (난수치:1 + 95) / 100

;0세	66.2cm
SIF 나이치 == 0
	신장 = 신장 * 605 / 1000

;1세	79.4cm
SIF 나이치 == 1
	신장 = 신장 * 725 / 1000

;2세	88.4cm
SIF 나이치 == 2
	신장 = 신장 * 808 / 1000

;3세	95.9cm
SIF 나이치 == 3
	신장 = 신장 * 876 / 1000

;4세	102.8cm
SIF 나이치 == 4
	신장 = 신장 * 939 / 1000

;
IF CHARATALENT_F(대상캐릭터,변신치,"남자")==0 || CHARATALENT_F(대상캐릭터,변신치,"오토코노코")
	;여성의 신장은 12세까지 거의 1차함수적으로 증가, 16세에 걸쳐 급격히 멈춘다
	;5세를 기준으로 한 경우, ~12세까지의 7년은 일년마다 ＋5~6cm, 나머지 ４년에서 +4 +2 +1 +0.5…가 기댓값
	;즉 (35~42)+4+2+1+0.5 → 약 40~50cm 정도 플러스된다
	;일본인 여성은 최종치 158cm가 평균
	FOR LCOUNT, 5,나이치 + 1
		IF LCOUNT < 13
			신장 += 성장치:(LCOUNT - 5)
		ELSEIF LCOUNT < 13
			신장 += 성장치:(LCOUNT - 5)*2/3
		ELSEIF LCOUNT < 17
			신장 += 성장치:(LCOUNT - 5)/((LCOUNT-12)*2)
		ELSEIF LCOUNT < 25
			신장 += 성장치:14 / 8
		ELSEIF LCOUNT < 30
			신장 += 성장치:14 / 12
		ELSEIF LCOUNT < 40
		ELSEIF LCOUNT < 50
			신장 -= 성장치:14 / 12
		ELSE
			신장 -= 성장치:14 / 8
		ENDIF
	NEXT
ELSE
	FOR LCOUNT, 5,나이치 + 1
		IF LCOUNT < 20
			신장 += 성장치:(LCOUNT - 5)
		ELSEIF LCOUNT < 25
			신장 += 성장치:14 / 4
		ELSEIF LCOUNT < 30
			신장 += 성장치:14 / 8
		ELSEIF LCOUNT < 40
		ELSEIF LCOUNT < 50
			신장 -= 성장치:14 / 8
		ELSE
			신장 -= 성장치:14 / 4
		ENDIF
	NEXT
ENDIF


;신장의 난수 계산
신장 = 신장 * ((CHARATALENT_F(대상캐릭터,변신치,"왜소함") == 0) * 25 + CHARATALENT_F(대상캐릭터,변신치,"장신") * 25 + 난수치:2 + 260) / 3000

;FEAT에 따른 신장에의 영향
IF TALENT:대상캐릭터:소형체구 > 0
	신장 = 신장 * 75 / 100
	SIF TALENT:대상캐릭터:요정 > 0
		신장 = 신장 * 50 / 100
ENDIF

;남자
SIF CHARATALENT_F(대상캐릭터,변신치,"남자") && CHARATALENT_F(대상캐릭터,변신치,"오토코노코") == 0
	신장 = 신장 * 1085 / 1000


;신장 덮어쓰기
SIF TALENT:대상캐릭터:신장지정 > 0 && 변신치 == 0
	신장 = TALENT:대상캐릭터:신장지정
SIF TALENT:대상캐릭터:변신시키지정 > 0 && 변신치 == 1
	신장 = TALENT:대상캐릭터:변신시키지정

;순산형:5　포동포동:6　유아배:7
;BMI는 각각 통상 20(미용 체중), 포동포동 22(건강 체중), 날씬함 18(모델 체중)을 기본으로 한다
;장신은 BMI이 저하, 왜소함은 BMI가 상승

;2022/02/06 빵뎅이를 골반둘레이외 순산형과 동일하게
;왕뎅이를 골반둘레이외 포동포동과 동일하게 계산하여 추가

BMI = 난수치:3 / 20 + CHARATALENT_F(대상캐릭터,변신치,"포동포동") * 20 + CHARATALENT_F(대상캐릭터,변신치,"왕뎅이") * 20 + (CHARATALENT_F(대상캐릭터,변신치,"날씬함") == 0) * 20 - CHARATALENT_F(대상캐릭터,변신치,"장신") * 20 + CHARATALENT_F(대상캐릭터,변신치,"왜소함") * 20 - (TALENT:대상캐릭터:요정 > 0) * 10 + (TALENT:대상캐릭터:짐승족 > 0) * 10 + 180
BMI = BMI * LIMIT(나이치 + 100, 100, 120) / 120
로러지수 = (난수치:3 + CHARATALENT_F(대상캐릭터,변신치,"포동포동") * 11 + CHARATALENT_F(대상캐릭터,변신치,"왕뎅이") * 11 + (CHARATALENT_F(대상캐릭터,변신치,"날씬함") == 0) * 11 + 178) / 2

체중 = BMI * 신장 * 신장 / 1000000
체중:1 = 1000000 * 신장 / (13200000 - 6456 * 신장)
체중:1 = 체중:1 * (BMI * 5292307 - 2700000) / 1000000000

CAL_VAR = LIMIT((1500 - 신장) * 4, 0, 1000)

체중 = (체중:0 * (1000 - CAL_VAR) + 체중:1 * CAL_VAR) / 1000 + 난수치:3

;골든 캐논
;신장비에서 가슴은 0.54, 허리둘레는 0.38, 골반둘레는 0.54
;허리둘레의 난수 계산
허리둘레 = 신장 * (BMI * 9 + 난수치:4 * 30 + CHARATALENT_F(대상캐릭터,변신치,"포동포동") * 50 + CHARATALENT_F(대상캐릭터,변신치,"왕뎅이") * 50 + CHARATALENT_F(대상캐릭터,변신치,"유아배") * 200 + (CHARATALENT_F(대상캐릭터,변신치,"순산형") == 0 && CHARATALENT_F(대상캐릭터,변신치,"빵뎅이") == 0 && CHARATALENT_F(대상캐릭터,변신치,"날씬함") == 0) * 100 + (TALENT:대상캐릭터:짐승족 == 0) * 50 + 1650) / 10000

;골반둘레의 난수 계산
골반둘레 = 신장 * (BMI * 7 + 난수치:5 * 30 + CHARATALENT_F(대상캐릭터,변신치,"순산형") * 200 + CHARATALENT_F(대상캐릭터,변신치,"빵뎅이") * 350 + CHARATALENT_F(대상캐릭터,변신치,"왕뎅이") * 450 + CHARATALENT_F(대상캐릭터,변신치,"포동포동") * 25 + (CHARATALENT_F(대상캐릭터,변신치,"유아배") == 0) * 50 + (CHARATALENT_F(대상캐릭터,변신치,"날씬함") == 0) * 100 + (TALENT:대상캐릭터:짐승족 > 0) * 50 + 3710) / 10000

;언더의 난수 계산
언더 = 신장 * (BMI * 5 + 난수치:6 * 5 + CHARATALENT_F(대상캐릭터,변신치,"포동포동") * 25 + CHARATALENT_F(대상캐릭터,변신치,"왕뎅이") * 25 + (CHARATALENT_F(대상캐릭터,변신치,"순산형") == 0 && CHARATALENT_F(대상캐릭터,변신치,"빵뎅이") == 0 && CHARATALENT_F(대상캐릭터,변신치,"날씬함") == 0) * 15 + 3310) / 10000

;25세 이상은 서서히 숙녀 체계화
IF 나이치 >= 25 && 나이치 < 30
	LOCAL = 나이치 - 4
	체중 = 체중 * (1000 + LOCAL * 2 / 3) / 1000
	허리둘레 = 허리둘레 * (1000 + LOCAL / 2) / 1000
	골반둘레 = 골반둘레 * (1000 + LOCAL) / 1000
ELSEIF 나이치 >= 30 && 나이치 < 40
	LOCAL = 나이치 - 4
	체중 = 체중 * (1000 + LOCAL) / 1000
	허리둘레 = 허리둘레 * (1000 + LOCAL * 2 / 3) / 1000
	골반둘레 = 골반둘레 * (1000 + LOCAL) / 1000
	언더 = 언더 * (1000 + LOCAL / 10) / 1000
ELSEIF 나이치 >= 40
	LOCAL = MIN(나이치 - 4, 45)
	체중 = 체중 * (1000 + LOCAL * 3 / 2) / 1000
	허리둘레 = 허리둘레 * (1000 + LOCAL * 3 / 4) / 1000
	골반둘레 = 골반둘레 * (1000 + LOCAL) / 1000
	언더 = 언더 * (1000 + LOCAL / 8) / 1000
ENDIF

;가슴둘레 - 언더의 난수 계산
;컵 크기 때문에 단독으로 함수화
CALL TOP_UNDER, 대상캐릭터, 변신치
톱차분=RESULT
;체중 +=(RESULT * 124 - 18600 * RESULT:1 / 50) / 1000
체중 +=(- 18600 * RESULT:1 / 50) / 1000
;가슴둘레
가슴둘레 = 언더 + 톱차분

;가슴무게=(RESULT * 124)/1000

;톱・언더・컵 사이즈에 기초한 체중 보정
CALL CUP_SIZE(톱차분)
가슴무게=CALC_BREAST_WEIGHT(가슴둘레,언더,RESULT)/100
체중 +=가슴무게



IF CHARATALENT_F(대상캐릭터,변신치,"남자") && CHARATALENT_F(대상캐릭터,변신치,"오토코노코") ==0
	가슴둘레 = -1
	허리둘레 = -1
	골반둘레 = -1
ENDIF

;각 파라미터 덮어쓰기
SIF TALENT:대상캐릭터:체중지정 > 0 && 변신치 == 0
	체중 = TALENT:대상캐릭터:체중지정
SIF TALENT:대상캐릭터:변신시체중지정 > 0 && 변신치 == 1
	체중 = TALENT:대상캐릭터:변신시체중지정
SIF TALENT:대상캐릭터:가슴둘레지정 > 0 && 변신치 == 0
	가슴둘레 = TALENT:대상캐릭터:가슴둘레지정
SIF TALENT:대상캐릭터:변신시가슴둘레지정 > 0 && 변신치 == 1
	가슴둘레 = TALENT:대상캐릭터:변신시가슴둘레지정
SIF TALENT:대상캐릭터:허리둘레지정 > 0 && 변신치 == 0
	허리둘레 = TALENT:대상캐릭터:허리둘레지정
SIF TALENT:대상캐릭터:변신시허리둘레지정 > 0 && 변신치 == 1
	허리둘레 = TALENT:대상캐릭터:변신시허리둘레지정
SIF TALENT:대상캐릭터:골반둘레지정 > 0 && 변신치 == 0
	골반둘레 = TALENT:대상캐릭터:골반둘레지정
SIF TALENT:대상캐릭터:변신시골반둘레지정 > 0 && 변신치 == 1
	골반둘레 = TALENT:대상캐릭터:변신시골반둘레지정
SIF TALENT:대상캐릭터:가슴무게지정 > 0 && 변신치 == 0
	가슴무게 = TALENT:대상캐릭터:가슴무게지정
SIF TALENT:대상캐릭터:변신시가슴무게지정 > 0 && 변신치 == 1
	가슴무게 = TALENT:대상캐릭터:변신시가슴무게지정

RETURN 난수치, 성장곡선, 신장, 체중, 가슴둘레, 허리둘레, 골반둘레, 가슴무게



@TOP_UNDER, 대상캐릭터, 변신치
#DIM 대상캐릭터
#DIM 변신치

#DIM 난수치, 4
#DIM 나이치
#DIM 영향치
#DIM 최소치
#DIM 최대치
#DIM 사이즈치

난수치 = CFLAG:대상캐릭터:33

;난수치의 덮어쓰기와 결정
난수치:1 = 난수치 % 23
SIF TALENT:대상캐릭터:가슴탄력난수 > 0 && TALENT:대상캐릭터:가슴탄력난수 < 24
	난수치:1 = TALENT:대상캐릭터:가슴탄력난수 - 1
난수치:2 = 난수치 % 7
SIF TALENT:대상캐릭터:가슴성장률난수 > 0 && TALENT:대상캐릭터:가슴성장률난수 < 8
	난수치:2 = TALENT:대상캐릭터:가슴성장률난수 - 1
난수치:3 = 난수치 % 53
SIF TALENT:대상캐릭터:가슴사이즈보정난수 > 0 && TALENT:대상캐릭터:가슴사이즈보정난수 < 54
	난수치:3 = TALENT:대상캐릭터:가슴사이즈보정난수 - 1

;나이치 결정
IF 변신치 > 0
	나이치 = MAXBASE:대상캐릭터:나이
ELSE
	나이치 = BASE:대상캐릭터:나이
ENDIF
나이치 = 나이치
SIF 나이치 > 22
	나이치 = 22

;나이치에 따른 영향도
영향치 = (나이치 + 10) * (난수치:2 * 2 + 26 - 난수치:1) / 15 + 난수치:3 / 4
IF 나이치 < 5
	영향치 += 나이치 * 6
ELSEIF 나이치 < 10
	영향치 += 24 + (나이치 - 4) * 12
ELSEIF 나이치 < 15
	영향치 += 84 + (나이치 - 9) * 3
ELSE
	영향치 += 99 + (나이치 - 14) * 1
ENDIF

;사이즈를 계산
IF CHARATALENT_F(대상캐릭터,변신치,"절벽") || CHARATALENT_F(대상캐릭터,변신치,"오토코노코")
	사이즈치 = -65
	최소치 = 0
	최대치 = 124
ELSEIF CHARATALENT_F(대상캐릭터,변신치,"빈유")
	사이즈치 = -40
	최소치 = 0
	최대치 = 149
ELSEIF CHARATALENT_F(대상캐릭터,변신치,"기유")
	사이즈치 = 170
	최소치 = 275
	최대치 = 999
ELSEIF CHARATALENT_F(대상캐릭터,변신치,"마유")
	사이즈치 = 130
	최소치 = 250
	최대치 = 474
ELSEIF CHARATALENT_F(대상캐릭터,변신치,"초유")
	사이즈치 = 90
	최소치 = 225
	최대치 = 399
ELSEIF CHARATALENT_F(대상캐릭터,변신치,"폭유")
	사이즈치 = 50
	최소치 = 200
	최대치 = 324
ELSEIF CHARATALENT_F(대상캐릭터,변신치,"거유")
	사이즈치 = 25
	최소치 = 175
	최대치 = 249
ELSE
	사이즈치 = 0
	최소치 = 100
	최대치 = 174
ENDIF
최소치 = MAX(최소치 - 75 * MAX(15 - 나이치, 0) / 15, 0)
최대치 = MAX(최대치 - 75 * MAX(15 - 나이치, 0) / 15, 0)
사이즈치 = 사이즈치 * (난수치:3 + 50) / 50 + 영향치
;소질 보정
사이즈치 = 사이즈치 * (100 + CHARATALENT_F(대상캐릭터,변신치,"포동포동") * 5) / 100
사이즈치 = 사이즈치 * (100 + CHARATALENT_F(대상캐릭터,변신치,"왕뎅이") * 5) / 100
사이즈치 = 사이즈치 * (100 - CHARATALENT_F(대상캐릭터,변신치,"유아배") * 10) / 100
;최소치, 최대치를 적용
사이즈치 = LIMIT(사이즈치, 최소치, 최대치)

SIF CHARATALENT_F(대상캐릭터,변신치,"남자") && CHARATALENT_F(대상캐릭터,변신치,"오토코노코") ==0
	사이즈치 = -1

RETURN 사이즈치, 영향치


;※TOP_UNDER의 RESULT(사이즈치)를 건넨다
;RESULTS 지원하는 문자 표기
;RESULT 생값(生の値)
@CUP_SIZE, ARG
IF ARG < 25
	LOCAL = 1
	RESULTS = ―
ELSEIF ARG < 75
	RESULTS = AAA
ELSEIF ARG < 100
	RESULTS = AA
ELSEIF ARG < 125
	RESULTS = A
ELSEIF ARG < 150
	RESULTS = B
ELSEIF ARG < 175
	RESULTS = C
ELSEIF ARG < 200
	RESULTS = D
ELSEIF ARG < 225
	RESULTS = E
ELSEIF ARG < 250
	RESULTS = F
ELSEIF ARG < 275
	RESULTS = G
ELSEIF ARG < 300
	RESULTS = H
ELSEIF ARG < 325
	RESULTS = I
ELSEIF ARG < 350
	RESULTS = J
ELSEIF ARG < 375
	RESULTS = K
ELSEIF ARG < 400
	RESULTS = L
ELSEIF ARG < 425
	RESULTS = M
ELSEIF ARG < 450
	RESULTS = N
ELSEIF ARG < 475
	RESULTS = O
ELSEIF ARG < 500
	RESULTS = P
ELSEIF ARG < 525
	RESULTS = Q
ELSEIF ARG < 550
	RESULTS = R
ELSEIF ARG < 575
	RESULTS = S
ELSEIF ARG < 600
	RESULTS = T
ELSEIF ARG < 625
	RESULTS = U
ELSEIF ARG < 650
	RESULTS = V
ELSEIF ARG < 675
	RESULTS = W
ELSEIF ARG < 700
	RESULTS = X
ELSEIF ARG < 725
	RESULTS = Y
ELSEIF ARG < 750
	RESULTS = Z
ELSE
	RESULTS = Z
ENDIF
IF ARG < 25
	LOCAL = 1
ELSE
	LOCAL = MAX(2,(ARG-25)/25)
ENDIF

RETURN LOCAL



;성장곡선 설정
@GENERATE_BODYLINE, 대상캐릭터
#DIM LCOUNT, 2

#DIM 대상캐릭터
#DIM 난수치
#DIM 성장곡선
#DIM 성장치, 15

#DIM CAL_VAR
#DIM DEV_VAR, 15
#DIM RAN_VAR, 2

VARSET 성장치

;난수치 설정
난수치 = RAND:(11 * 31 * 59 * 15 * 16 * 13 * 23 * 7 * 53)

;11	최저 신장
;31	신장
;59	체중(BMI)
;15	허리둘레
;16	골반둘레
;13	언더
;23	컵 최저치
;7	컵 최고치
;53	컵 기준치

;난수치 덮어쓰기
SIF TALENT:대상캐릭터:체형난수치지정 > 0
	난수치 = TALENT:대상캐릭터:체형난수치지정

DEV_VAR:0 = 83
DEV_VAR:1 = 80
DEV_VAR:2 = 75
DEV_VAR:3 = 83
DEV_VAR:4 = 113
DEV_VAR:5 = 108
DEV_VAR:6 = 50
DEV_VAR:7 = 40
DEV_VAR:8 = 30
DEV_VAR:9 = 20
DEV_VAR:10 = 10
DEV_VAR:11 = 10
DEV_VAR:12 = 10
DEV_VAR:13 = 8
DEV_VAR:14 = 6

VARSET RAN_VAR
FOR LCOUNT:0, 0, 15
	RAN_VAR:0 += DEV_VAR:(LCOUNT:0)
NEXT

FOR LCOUNT:0, 0, 4 * 15
	CAL_VAR = RAND:(RAN_VAR:0)
	RAN_VAR:1 = 0
	FOR LCOUNT:1, 0, 15
		RAN_VAR:1 += DEV_VAR:(LCOUNT:1)
		SIF CAL_VAR < RAN_VAR:1
			BREAK
	NEXT
	IF 성장치:(LCOUNT:1 - 1) >= 9
		LCOUNT:0 -= 1
		CONTINUE
	ENDIF
	성장치:(LCOUNT:1 - 1) += 1
NEXT

VARSET LCOUNT
FOR LCOUNT, 0, 15
	LCOUNT:1 *= 10
	LCOUNT:1 += 성장치:(14 - LCOUNT)
NEXT
성장곡선 = LCOUNT:1

;성장곡선 덮어쓰기
SIF TALENT:대상캐릭터:체형성장곡선지정 > 0
	성장곡선 = TALENT:대상캐릭터:체형성장곡선지정


CFLAG:대상캐릭터:33 = 난수치
CFLAG:대상캐릭터:34 = 성장곡선

RETURN 0


;================================================================
; 유방의 무게를 산출한다
;--------------------------------
;	nTB			: 윗가슴(단위:mm)
;	nUB			: 아랫가슴(단위:mm)
;--------------------------------
; 반환값 등
;	유방 ２개분의 무게가 반환된다. 단위는 [g].
;================================================================
@CALC_BREAST_WEIGHT( nTB, nUB,cupsize )
#FUNCTION
	#DIM nTB
	#DIM nUB
	#DIM cupsize

	#DIM nHemisphereValue		; 반구 지수（가슴 형태가 반구 이상이 될 때의 컵 지수.「nUB/4.8/2.5」）
	#DIM nCupValue				; 컵 지수（브라의 컵을 산출하기 위한 수치. 「(nTB-nUB)/2.5」）
	#DIM nBallR					; 유방 반지름
	#DIM nBottomR				; 유방 밑 반지름(底半径)
	#DIM nBreastRadius_Temp
	#DIM nH						; 유방 높이
	#DIM nBreastVolume			; 유방의 부피(단위:세제곱cm)
	#DIM nBreastWeight			; 유방의 중량(단위:g)

	nBreastWeight = 0

	;================================
	;・가슴의 형태와 부피
	;	먼저, 유방의 형태를 일부가 잘려나간 구체로 가정한다.
	;	1) 그 구체의 반지름을 산출한다
	;		「nBallR = (nUB / 15 + nCupValue) * 0.45」
	;	2) 잘려 나간 평면의 반지름을 산출한다
	;		「nBottomR = (nUB / 15 + nCupValue) * 0.45 - (ABS(nCupValue - nHemisphereValue) * 0.39)
	;	3) 잘려 나간 부분을 제외한 구의 높이를 산출한다
	;		「nH = nBallR + √(nBallR^2 - nBottomR^2)」（반구 이상）
	;		「nH = nBallR - √(nBallR^2 - nBottomR^2)」（반구 미만）
	;	4) 부피를 산출한다
	;		「nBreastVolume = nH * (3 * nH * (2 * nBallR - nH) + nH^2) * 314 / 100 / 6」
	;	5) 중량을 산출한다（1세제곱cm당 0.9g으로 한다（지방은 물보다 가볍기 때문））
	;		「nBreastWeight = nBreastVolume * 0.9」
	;
	;	유방 반지름	: 유방의 형태를 일부가 잘려나간 구체로 가정했을 때의 반지름.
	;	유방 밑 반지름(底半径)	: 가슴 밑면의 반지름.
	;	유방 높이	: 가슴의 높이.
	;================================

	nHemisphereValue = nUB * 100 / 48 / 25
	nCupValue = (nTB - nUB) * 2 / 5


	; 유방 반지름 산출
	nBallR = (nUB / 15 + nCupValue) * 45 / 100
	; 유방 밑 반지름 산출
	nBottomR = (nUB / 15 + nCupValue) * 45 / 100 - (ABS( nCupValue - nHemisphereValue ) * 39 / 100)

	;속행불능의 값이 나오면 중량 0으로 간주하고 중단
	SIF POWER( nBallR, 2 ) - POWER( nBottomR, 2 )<0
		RETURNF 0

	; 높이 산출
	IF nCupValue >= nHemisphereValue
		nH = nBallR + SQRT( POWER( nBallR, 2 ) - POWER( nBottomR, 2 ) )
	ELSE
		nH = nBallR - SQRT( POWER( nBallR, 2 ) - POWER( nBottomR, 2 ) )
	ENDIF
	; 유방 부피 산출
	nBreastVolume = nH * (3 * nH * (2 * nBallR - nH) + POWER( nH, 2 )) * 314 / 100 / 6

	; 유방 중량 산출
	nBreastWeight = nBreastVolume *2 * 87 / 100 / 1000

;컵 사이즈에 따른 보정(적당)
IF cupsize>-1
	;컵 사이즈별 대략적인 기준
	SELECTCASE cupsize-1
	;-
	CASE 0
		LOCAL=18
	;AAA
	CASE 1
		LOCAL=60
	;AA
	CASE 2
		LOCAL=100
	;A
	CASE 3
		LOCAL=300
	CASE 4
		LOCAL=460
	CASE 5
		LOCAL=590
	CASE 6
		LOCAL=758
	CASE 7
		LOCAL=1060
	CASE 8
		LOCAL=1620
	CASE 9
		LOCAL=2200
	CASE 10
		LOCAL=3000
	CASE 11
		LOCAL=3800
	CASE 12
		LOCAL=5000
	CASE 13
		LOCAL=6200
	CASEELSE
		LOCAL=6200+1200*(cupsize-13)
	ENDSELECT

	;Ｇ컵 이상의 경우, 계산한 값이 기준치보다 가벼우면 약간 무겁게 보정한다
	;IF LOCAL>nBreastWeight
	IF cupsize>=9
		SELECTCASE cupsize
		CASE 9
			nBreastWeight=(LOCAL*12+nBreastWeight*2)/14
		CASE 10
			nBreastWeight=(LOCAL*11+nBreastWeight*2)/13
		CASEELSE
			nBreastWeight=(LOCAL*10+nBreastWeight*2)/12
		ENDSELECT
	ELSE
		nBreastWeight=(LOCAL+nBreastWeight*10)/11
	ENDIF
ENDIF
	RETURNF nBreastWeight