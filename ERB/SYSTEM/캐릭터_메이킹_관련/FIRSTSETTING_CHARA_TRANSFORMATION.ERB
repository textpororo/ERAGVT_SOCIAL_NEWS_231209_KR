
;-------------------------------------------------------------------------------------------------------------------------------------------------------------------
;캐릭터의 변신 능력 설정
@FIRSTSETTING_CHARA_TRANSABILITY, ARG
PRINTFORML {ARG}번째 캐릭터『%CALLNAME:ARG%』의 전투 능력을 설정해 주십시오.
PRINTL ・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・
PRINTFORML [0] 변신 능력 있음
PRINTFORML [1] 변신 능력 없음, 전투 능력 있음
PRINTFORML [2] 비전투원
$INPUT_LOOP
INPUT
IF RESULT == 0
	TALENT:ARG:변신능력 = 1
	MAXBASE:ARG:공중대시 = LIMIT(CSVBASE_F(NO:ARG,22), 0, 5)
	BASE:ARG:공중대시 = MAXBASE:ARG:공중대시

	CALL GENERATE_CHAR_SIZE, ARG, 1
	MAXBASE:ARG:신장 = RESULT:2
	MAXBASE:ARG:체중 = RESULT:3
	MAXBASE:ARG:가슴둘레 = RESULT:4
	MAXBASE:ARG:허리둘레 = RESULT:5
	MAXBASE:ARG:골반둘레 = RESULT:6
	MAXBASE:ARG:가슴무게 = RESULT:7

	PRINTFORML 전투 능력을『변신 능력 있음』으로 설정했습니다.
ELSEIF RESULT == 1
	TALENT:ARG:변신능력 = 0
	MAXBASE:ARG:공중대시 = LIMIT(CSVBASE_F(NO:ARG,22), 0, 5)
	BASE:ARG:공중대시 = MAXBASE:ARG:공중대시

	CALL GENERATE_CHAR_SIZE, ARG, 1
	MAXBASE:ARG:신장 = RESULT:2
	MAXBASE:ARG:체중 = RESULT:3
	MAXBASE:ARG:가슴둘레 = RESULT:4
	MAXBASE:ARG:허리둘레 = RESULT:5
	MAXBASE:ARG:골반둘레 = RESULT:6
	MAXBASE:ARG:가슴무게 = RESULT:7

	TALENT:ARG:변신시ＴＳ = 0
	PRINTFORML 전투 능력을『변신 능력 없음, 전투 능력 있음』으로 설정했습니다.
ELSEIF RESULT == 2
	TALENT:ARG:변신능력 = -1
	MAXBASE:ARG:공중대시 = LIMIT(CSVBASE_F(NO:ARG,22) - 1, 0, 5)
	BASE:ARG:공중대시 = MAXBASE:ARG:공중대시

	CALL GENERATE_CHAR_SIZE, ARG, 1
	MAXBASE:ARG:신장 = RESULT:2
	MAXBASE:ARG:체중 = RESULT:3
	MAXBASE:ARG:가슴둘레 = RESULT:4
	MAXBASE:ARG:허리둘레 = RESULT:5
	MAXBASE:ARG:골반둘레 = RESULT:6
	MAXBASE:ARG:가슴무게 = RESULT:7

	TALENT:ARG:변신시ＴＳ = 0
	PRINTFORML 전투 능력을『비전투원』으로 설정했습니다.
ELSE
	GOTO INPUT_LOOP
ENDIF
PRINTW 
PRINTL 

;-------------------------------------------------------------------------------------------------------------------------------------------------------------------
;캐릭터의 변신 후 이름 설정
@FIRSTSETTING_CHARA_TRANSAFTERNAME, ARG
#DIMS STR_UP
#DIMS STR_DOWN
RESULTS = 
;변신 후 이름에 관한 설정값
;CSTR:ARG:0 = %STRCLEAR()%
;윗구와 아랫구가 이미 보존되어 있다면 기록해 둔다
STR_UP = %CSTR:ARG:201%
STR_DOWN = %CSTR:ARG:202%
;초기화
CSTR:ARG:201 = %STRCLEAR()%
CSTR:ARG:202 = %STRCLEAR()%
LOCAL:0 = 0

PRINTFORML {ARG:0}번째 캐릭터『%CALLNAME:ARG%』의 변신 후 이름을 설정해 주십시오.
PRINTL   (변신 중의 정식 캐릭터명입니다. 일부 지문이나 스테이터스 표시 등에 사용됩니다.)
PRINTL ・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・
PRINTL [0] 설정하지 않는다
PRINTL [1] 직접 설정한다
PRINTL [2] 랜덤으로 설정한다
PRINTFORML [3]『%CALLNAME:ARG%』%조사만처리(CALLNAME:ARG,"와")% 무작위 조합으로 설정한다
IF FLAG:6 == 1
	PRINTFORML [4]『%SAVESTR:11%』%조사만처리(SAVESTR:11,"와")% 무작위 조합으로 설정한다
	PRINTFORML [5]『%SAVESTR:11% %CALLNAME:ARG%』%조사만처리(CALLNAME:ARG,"로")% 한다
	PRINTFORML [6]『%CALLNAME:ARG% %SAVESTR:11%』%조사만처리(SAVESTR:11,"로")% 한다
ENDIF
$INPUT_LOOP
INPUT
LOCAL:99 = RESULT
IF (FLAG:6 == 0 && (RESULT < 0 || RESULT > 4)) || (FLAG:6 == 1 && (RESULT < 0 || RESULT > 8))
	GOTO INPUT_LOOP
ELSEIF RESULT == 0
	CFLAG:ARG:2 = 0
	;변신 후 통칭도 삭제
	CFLAG:ARG:3 = 0
	CSTR:ARG:1 = %STRCLEAR()%
	PRINTFORML 변신 후 이름을 설정하지 않았습니다.
ELSEIF RESULT == 1
	CFLAG:ARG:2 = 1
	PRINTL 변신 후 이름을 입력해 주십시오.
	SIF CSTR:ARG:0 != ""
		PRINTFORML [999] 변경하지 않는다（%CSTR:ARG:0%）
	$INPUT_LOOP_1_2
	INPUTS
	IF RESULTS == ""
		GOTO INPUT_LOOP_1_2
	ELSEIF RESULTS == "999" && CSTR:ARG:0 != ""
	ELSE
		CSTR:ARG:0 = %RESULTS%
	ENDIF
	PRINTFORML 변신 후 이름을『%CSTR:ARG:0%』%조사만처리(CSTR:ARG:0,"로")% 설정했습니다.
	;변신 후 이름도 맞춘다
	CSTR:ARG:1 = %CSTR:ARG:0%
	CFLAG:ARG:3 = 1
ELSEIF RESULT == 2
	CALL FIRSTSETTING_RANDOMNAMING_ALL
	IF RESULT == 99
		PRINTFORML 변신 후 이름을 고르지 않고 돌아갑니다.
		RETURN
	ENDIF
	CFLAG:ARG:2 = 1
	CSTR:ARG:0 = %STR:(DA:0:RESULT)%%STR:(DA:1:RESULT)%
	PRINTFORML 변신 후 이름을『%CSTR:ARG:0%』%조사만처리(CSTR:ARG:0,"로")% 설정했습니다.
	CSTR:ARG:201 = %STR:(DA:0:RESULT)%
	CSTR:ARG:202 = %STR:(DA:1:RESULT)%
	;변신 후 이름도 맞춘다
	CSTR:ARG:1 = %CSTR:ARG:0%
	CFLAG:ARG:3 = 1
ELSEIF RESULT == 3
	CFLAG:ARG:2 = 1
	CSTR:ARG:201 = %CALLNAME:ARG%
	PRINTL 
	PRINT 조합하는 단어의 
	$INPUT_LOOP_CHILD_3_0
	CALL FIRSTSETTING_RANDOMNAMING, 1, ARG, CALLNAME:ARG
	PRINTL 
	SIF RESULT:1 == 1
		GOTO INPUT_LOOP_CHILD_3_0
	LOCAL:99 = RESULT
	$INPUT_LOOP_CHILD_3_1
	PRINTFORML 변신 후 이름을『%RESULTS%』%조사만처리(RESULTS,"로")% 설정합니다. 이대로 좋습니까?
	PRINTL 
	PRINTL [0] 아니오
	PRINTL [1] 네
	SIF CSTR:ARG:0 != ""
		PRINTFORML [2] 변경하지 않는다（%CSTR:ARG:0%）
	$INPUT_LOOP_CHILD_3_2
	INPUT
	IF RESULT == 0
		CSTR:ARG:201 = %STR_UP%
		CSTR:ARG:202 = %STR_DOWN%
		GOTO INPUT_LOOP_CHILD_3_0
	ELSEIF RESULT == 1
		CSTR:ARG:0 = %RESULTS%
		PRINTFORML 변신 후 이름을『%CSTR:ARG:0%』%조사만처리(CSTR:ARG:0,"로")% 설정했습니다.
		;변신 후 이름도 맞춘다
		CSTR:ARG:1 = %CSTR:ARG:0%
		CFLAG:ARG:3 = 1
	ELSEIF RESULT == 2
	ELSE
		GOTO INPUT_LOOP_CHILD_3_2
	ENDIF
ELSEIF RESULT == 4
	CFLAG:ARG:2 = 1
	CSTR:ARG:201 = %SAVESTR:11%
	PRINTL 
	PRINT 조합하는 단어의 
	$INPUT_LOOP_CHILD_5_0
	CALL FIRSTSETTING_RANDOMNAMING, 1, ARG, SAVESTR:11
	PRINTL 
	SIF RESULT:1 == 1
		GOTO INPUT_LOOP_CHILD_5_0
	LOCAL:99 = RESULT
	$INPUT_LOOP_CHILD_5_1
	PRINTFORML 변신 후 이름을『%RESULTS%』%조사만처리(RESULTS,"로")% 설정합니다. 이대로 좋습니까?
	PRINTL 
	PRINTL [0] 아니오
	PRINTL [1] 네
	SIF CSTR:ARG:0 != ""
		PRINTFORML [2] 변경하지 않는다（%CSTR:ARG:0%）
	$INPUT_LOOP_CHILD_5_2
	INPUT
	IF RESULT == 0
		CSTR:ARG:201 = %STR_UP%
		CSTR:ARG:202 = %STR_DOWN%
		GOTO INPUT_LOOP_CHILD_5_0
	ELSEIF RESULT == 1
		CSTR:ARG:0 = %RESULTS%
		PRINTFORML 변신 후 이름을『%CSTR:ARG:0%』 %조사만처리(CSTR:ARG:0,"로")% 설정했습니다.
		;변신 후 이름도 맞춘다
		CSTR:ARG:1 = %CSTR:ARG:0%
		CFLAG:ARG:3 = 1
	ELSEIF RESULT == 2
	ELSE
		GOTO INPUT_LOOP_CHILD_5_2
	ENDIF

ELSEIF RESULT == 5
	CFLAG:ARG:2 = 1
	CSTR:ARG:201 = %SAVESTR:11%
	CSTR:ARG:202 = %CALLNAME:ARG%
	CSTR:ARG:0 = %SAVESTR:11%%CALLNAME:ARG%
	PRINTFORML 변신 후 이름을『%CSTR:ARG:0%』%조사만처리(CSTR:ARG:0,"로")% 설정했습니다.
	;변신 후 이름도 맞춘다
	CSTR:ARG:1 = %CSTR:ARG:0%
	CFLAG:ARG:3 = 1
ELSEIF RESULT == 6
	CFLAG:ARG:2 = 1
	CSTR:ARG:201 = %CALLNAME:ARG%
	CSTR:ARG:202 = %SAVESTR:11%
	CSTR:ARG:0 = %CALLNAME:ARG%%SAVESTR:11%
	PRINTFORML 변신 후 이름을『%CSTR:ARG:0%』%조사만처리(CSTR:ARG:0,"로")% 설정했습니다.
	;변신 후 이름도 맞춘다
	CSTR:ARG:1 = %CSTR:ARG:0%
	CFLAG:ARG:3 = 1
ENDIF
PRINTL 
PRINTL 


;-------------------------------------------------------------------------------------------------------------------------------------------------------------------
;캐릭터 변신 후 통칭 설정
@FIRSTSETTING_CHARA_TRANSAFTERCALLNAME, ARG
PRINTFORML {ARG:0}번째 캐릭터『%CALLNAME:ARG%』의 변신 후 통칭을 설정해 주십시오.
PRINTL   (※ 변신 중에 일반적인 지문 등에서 표시되는 캐릭터 이름입니다.)
PRINTL ・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・

;변신 후 이름을 직접 설정했을 경우
IF CSTR:ARG:201 == "" && CSTR:ARG:202 == ""
	PRINTL [0] 직접 설정한다
	PRINTFORML [1] 변신 후 이름과 똑같이 설정한다（변신 후 이름：『%CSTR:ARG:0%』）
	PRINTFORML [2] 변신 전의 통칭을 그대로 사용한다
	$INPUT_LOOP_1_1
	INPUT
	IF RESULT < 0 || RESULT > 2
		GOTO INPUT_LOOP_1_1
	ELSEIF RESULT == 0
		PRINTL 변신 후 통칭을 입력해 주십시오.
		SIF CSTR:ARG:1 != ""
			PRINTFORML [999] 변경하지 않는다（%CSTR:ARG:1%）
		$INPUT_LOOP_1_2
		INPUTS
		IF RESULTS == ""
			GOTO INPUT_LOOP_1_2
		ELSEIF RESULTS == "999"
		ELSE
			CSTR:ARG:1 = %RESULTS%
		ENDIF
	ELSEIF RESULT == 1
		CSTR:ARG:1 = %CSTR:ARG:0%
	ELSEIF RESULT == 2
		CSTR:ARG:1 = %CALLNAME:ARG%
	ENDIF
	PRINTFORML 변신 후 통칭을『%CSTR:ARG:1%』%조사만처리(CSTR:ARG:1,"로")% 설정했습니다.

;両方설정されている場合
ELSE
	PRINTFORML [0] 변신 후 통칭을『%CSTR:ARG:201%』%조사만처리(CSTR:ARG:201,"로")% 설정한다
	PRINTFORML [1] 변신 후 통칭을『%CSTR:ARG:202%』%조사만처리(CSTR:ARG:202,"로")% 설정한다
	PRINTL [2] 직접 설정한다
	PRINTFORML [3] 변신 후 이름과 똑같이 설정한다（변신 후 이름：『%CSTR:ARG:0%』）
	PRINTFORML [4] 변신 전의 통칭을 그대로 사용한다
	$INPUT_LOOP_4
	INPUT
	IF RESULT < 0 || RESULT > 4
		GOTO INPUT_LOOP_4
	ELSEIF RESULT == 0
		CSTR:ARG:1 = %CSTR:ARG:201%
	ELSEIF RESULT == 1
		CSTR:ARG:1 = %CSTR:ARG:202%
	ELSEIF RESULT == 2
		PRINTL 변신 후 통칭을 입력해 주십시오.
		SIF CSTR:ARG:1 != ""
			PRINTFORML [999] 변경하지 않는다（%CSTR:ARG:1%）
		$INPUT_LOOP_4_1
		INPUTS
		IF RESULTS == ""
			GOTO INPUT_LOOP_4_1
		ELSEIF RESULTS == "999"
		ELSE
			CSTR:ARG:1 = %RESULTS%
		ENDIF
	ELSEIF RESULT == 3
		CSTR:ARG:1 = %CSTR:ARG:0%
	ELSEIF RESULT == 4
		CSTR:ARG:1 = %CALLNAME:ARG%
	ENDIF
	PRINTFORML 변신 후 통칭을『%CSTR:ARG:1%』%조사만처리(CSTR:ARG:1,"로")% 설정했습니다.
ENDIF
PRINTL 
PRINTL 

;-------------------------------------------------------------------------------------------------------------------------------------------------------------------
;캐릭터 변신 시 구호 설정
@FIRSTSETTING_CHARA_TRANSCALL, ARG
PRINTFORML {ARG:0}번째 캐릭터『%CALLNAME:ARG%』의 변신 시 구호를 설정하시겠습니까?
PRINTL ・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・

PRINTL [0] 아니오
PRINTL [1] 랜덤으로 설정
PRINTL [2] 직접 설정한다
SIF FLAG:7 == 1
	PRINTFORML [3] 공통의 구호로 설정한다
$INPUT_LOOP
INPUT
IF (FLAG:7 == 0 && (RESULT < 0 || RESULT > 2)) || (FLAG:7 == 1 && (RESULT < 0 || RESULT > 3))
	GOTO INPUT_LOOP
ELSEIF RESULT == 0
	CFLAG:ARG:4 = 0
	PRINTL 구호를 설정하지 않았습니다.
ELSEIF RESULT == 1
	CFLAG:ARG:4 = 1
	$INPUT_LOOP_CHILD_1_1
	CSTR:ARG:2 = %FIRSTSETTING_CHANGINGCALL_RANDOM()%
	PRINTFORML 구호를『%CSTR:ARG:2%』%조사만처리(CSTR:ARG:2,"로")% 합니다. 이대로 좋습니까?
	PRINTL [0] 다시 한 번 더 고른다
	PRINTL [1] 네
	$INPUT_LOOP_CHILD_1_2
	INPUT
	IF RESULT < 0 || RESULT > 1
		GOTO INPUT_LOOP_CHILD_1_2
	ELSEIF RESULT == 0
		GOTO INPUT_LOOP_CHILD_1_1
	ELSEIF RESULT == 1
		PRINTFORML 구호를『%CSTR:ARG:2%』%조사만처리(CSTR:ARG:2,"로")% 설정했습니다.
	ENDIF
ELSEIF RESULT == 2
	CFLAG:ARG:4 = 1
	PRINTL 구호를 입력해 주십시오.
	$INPUT_LOOP_CHILD_2
	INPUTS
	IF RESULTS == ""
		GOTO INPUT_LOOP_CHILD_2
	ELSE
		CSTR:ARG:2 = %RESULTS%
		PRINTFORML 구호를『%CSTR:ARG:2%』%조사만처리(CSTR:ARG:2,"로")% 설정했습니다.
	ENDIF
ELSEIF RESULT == 3
	CFLAG:ARG:4 = 1
	CSTR:ARG:2 = %SAVESTR:12%
	PRINTFORML 구호를『%CSTR:ARG:2%』%조사만처리(CSTR:ARG:2,"로")% 설정했습니다.

ENDIF
PRINTL 
PRINTL 


;-------------------------------------------------------------------------------------------------------------------------------------------------------------------
;캐릭터의 자칭 구상 설정
@FIRSTSETTING_CHARA_NANORI, ARG

;캐릭터의 성격의 소질 번호를 취득해 둔다
CALL SEIKAKU_CHECK, "GET_TALENT_VALUE", ARG
LOCAL = RESULT

PRINTFORML {ARG:0}번째 캐릭터『%CALLNAME:ARG%』의 변신 후 자칭 구상을 설정하시겠습니까?
PRINTL （※ 구상 측에서 설정되어 있는 경우에는 구상이 우선시됩니다.）
PRINTL ・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・・

;변신 후 이름을 설정하지 않았을 경우
IF CFLAG:ARG:2 == 0
	PRINTL [0] 아니오
	PRINTL [1] 직접 설정한다
	$INPUT_LOOP_A
	INPUT
	IF RESULT < 0 || RESULT > 1
		GOTO INPUT_LOOP_A
	ELSEIF RESULT == 0
		CFLAG:ARG:5 = 0
		PRINTL 자칭 구상을 설정하지 않았습니다.
	ELSEIF RESULT == 1
		CFLAG:ARG:5 = 1
		PRINTL 자칭 구상을 입력해 주십시오.
		$INPUT_LOOP_CHILD_1
		INPUTS
		IF RESULTS == ""
			GOTO INPUT_LOOP_CHILD_1
		ELSE
			CSTR:ARG:3 = %RESULTS%
			PRINTFORML 자칭 구상을『%CSTR:ARG:3%』%조사만처리(CSTR:ARG:3,"로")% 설정했습니다.
		ENDIF
	ENDIF
;변신 후 이름을 설정했을 경우
ELSEIF CFLAG:ARG:2 == 1
	PRINTL [0] 아니오
	PRINTL [1] 「주제 ＋ 변신 후 이름 ＋ 한마디」의 구성으로 자동 생성
	PRINTL [2] 「변신 후 이름 ＋ 한마디」의 구성으로 자동 생성
	PRINTL [3] 직접 설정한다
	$INPUT_LOOP_B
	INPUT
	IF RESULT < 0 || RESULT > 3
		GOTO INPUT_LOOP_B
	ELSEIF RESULT == 0
		CFLAG:ARG:5 = 0
		PRINTL 자칭 구상을 설정하지 않았습니다.
	ELSEIF RESULT == 1
		CFLAG:ARG:5 = 1
		$INPUT_LOOP_1_1
		CSTR:ARG:3 = %SAVESTR:10%%CSTR:ARG:0%%FIRSTSETTING_CHANGINGCALL_DETAIL(ARG:0, LOCAL)%
		PRINTFORML 자칭 구상을『%CSTR:ARG:3%』%조사만처리(CSTR:ARG:3,"로")% 설정합니다. 이대로 좋습니까?
		PRINTL [0] 다시 한 번 더 고른다
		PRINTL [1] 네
		$INPUT_LOOP_1_2
		INPUT
		IF RESULT < 0 || RESULT > 1
			GOTO INPUT_LOOP_1_2
		ELSEIF RESULT == 0
			GOTO INPUT_LOOP_1_1
		ELSEIF RESULT == 1
			PRINTFORML 자칭 구상을『%CSTR:ARG:3%』%조사만처리(CSTR:ARG:3,"로")% 설정했습니다.
		ENDIF
	ELSEIF RESULT == 2
		CFLAG:ARG:5 = 1
		$INPUT_LOOP_2_1
		CSTR:ARG:3 = %CSTR:ARG:0%%FIRSTSETTING_CHANGINGCALL_DETAIL(ARG:0, LOCAL)%
		PRINTFORML 자칭 구상을『%CSTR:ARG:3%』%조사만처리(CSTR:ARG:3,"로")% 설정합니다. 이대로 좋습니까?
		PRINTL [0] 다시 한 번 더 고른다
		PRINTL [1] 네
		$INPUT_LOOP_2_2
		INPUT
		IF RESULT < 0 || RESULT > 1
			GOTO INPUT_LOOP_2_2
		ELSEIF RESULT == 0
			GOTO INPUT_LOOP_2_1
		ELSEIF RESULT == 1
			PRINTFORML 자칭 구상을『%CSTR:ARG:3%』%조사만처리(CSTR:ARG:3,"로")% 설정했습니다.
		ENDIF
	ELSEIF RESULT == 3
		CFLAG:ARG:5 = 1
		PRINTL 자칭 구상을 입력해 주십시오.
		$INPUT_LOOP_3
		INPUTS
		IF RESULTS == ""
			GOTO INPUT_LOOP_3
		ELSE
			CSTR:ARG:3 = %RESULTS%
			PRINTFORML 자칭 구상을『%CSTR:ARG:3%』%조사만처리(CSTR:ARG:3,"로")% 설정했습니다.
		ENDIF
	ENDIF
ENDIF
PRINTL 
PRINTL 





;**********************************************************
;구호 설정(공통)
@FIRSTSETTING_TRANSCALL
PRINTL 변신 시의 공통 구호를 설정하시겠습니까?
DRAWLINE
PRINTL [0] 구호 없음
PRINTL [1] 랜덤으로 설정한다
PRINTL [2] 직접 설정한다 
PRINTL [99] 돌아간다
$INPUT_LOOP
INPUT
IF RESULT == 0
	FLAG:7 = 0
	PRINTL 구호를 설정하지 않았습니다.
ELSEIF RESULT == 1
	FLAG:7 = 1
	$INPUT_LOOP_CHILD_1_1
	SAVESTR:12 = %FIRSTSETTING_CHANGINGCALL_RANDOM()%
	PRINTFORML 구호를『%SAVESTR:12%』%조사만처리(SAVESTR:12,"로")% 설정합니다. 이대로 좋습니까?
	PRINTL [0] 다시 한 번 더 고른다
	PRINTL [1] 네
	$INPUT_LOOP_CHILD_1_2
	INPUT
	IF RESULT < 0 || RESULT > 1
		GOTO INPUT_LOOP_CHILD_1_2
	ELSEIF RESULT == 0
		GOTO INPUT_LOOP_CHILD_1_1
	ELSEIF RESULT == 1
		PRINTFORML 구호를『%SAVESTR:12%』%조사만처리(SAVESTR:12,"로")% 설정했습니다.
	ENDIF
ELSEIF RESULT == 2
	FLAG:7 = 1
	PRINTL 구호를 입력해 주십시오.
	$INPUT_LOOP_CHILD_2
	INPUTS
	IF RESULTS == ""
		GOTO INPUT_LOOP_CHILD_2
	ELSE
		SAVESTR:12 = %RESULTS%
		PRINTFORML 구호를『%SAVESTR:12%』%조사만처리(SAVESTR:12,"로")% 설정했습니다.
	ENDIF
ELSEIF RESULT == 99
	RETURN 99
ELSE
	GOTO INPUT_LOOP
ENDIF
PRINTL 

