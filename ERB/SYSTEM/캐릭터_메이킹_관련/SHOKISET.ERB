;----------------------
;초기 멤버의 설정 개조
;----------------------

@CHARA_MAKE_FINALIZE_KAI
#DIMS FSET_NAME
PRINTL 
$INPUT_LOOP_0
PRINTL 로드할 초기 세트를 선택해 주십시오.
FOR COUNT, 0, 99
	TRYCALLFORM SHOKISET_NAME_{COUNT}
NEXT
PRINTL [99] 불러오지 않고 돌아간다
INPUT

IF RESULT >= 0 && RESULT < 99
	LOCAL:1 = RESULT
	TRYCCALLFORM SHOKISET_SETUMEI_{LOCAL:1}
		PRINTL 이대로 진행하시겠습니까?
		PRINTL [0] 아니오
		PRINTL [1] 네
		$INPUT_LOOP_1
		INPUT
		IF RESULT < 0 || RESULT > 1
			GOTO INPUT_LOOP_1
		ELSEIF RESULT == 0
			GOTO INPUT_LOOP_0
		ELSEIF RESULT == 1
			REPEAT CHARANUM-1
				DELCHARA 1
				FLAG:8 --
			REND
			CALLFORM SHOKISET_SELECT_{LOCAL:1}
		ENDIF
	CATCH
		GOTO INPUT_LOOP_0
	ENDCATCH
ELSEIF RESULT == 99
	RETURN -1
ELSE
	GOTO INPUT_LOOP_0
ENDIF

;캐릭터 CSV 미정의값의 일괄 설정 처리
CALL SHOKISET_CSVFIX


;-------------------------------
;그룹 선택 ー CSV 불러오기(読み出し)
;-------------------------------
@GROUP_SELECT_CSV
#DIM LCOUNT
#DIM NUM
LCOUNT = 0;RESULT 보존용
NUM = 1;인원수 기록용

PRINTL 불러오려는 캐릭터의 CSV 번호를 입력해 주십시오.

;1명 결정할 때마다 NUM에 가산, NUM가 3을 넘으면 종료
WHILE NUM <= 3
	PRINTFORML {NUM}번째 캐릭터 번호를 입력해 주십시오.
	PRINTL [999] CSV 번호 목록
	INPUT
	LCOUNT = RESULT
	IF LCOUNT == 999
		REPEAT 999
			EXISTCSV COUNT
			IF RESULT == 1
				PRINTFORML {COUNT},%CSVNAME(COUNT,0)%
			ENDIF
		REND
		PRINTW
	ELSE
		EXISTCSV LCOUNT
		;1이 돌아오면 캐릭터가 정의되어 있으므로 결정
		IF RESULT == 1
			PRINTFORML {NUM}번째 캐릭터를「%CSVNAME(LCOUNT,0)%」%조사만처리(CSVNAME(LCOUNT,0),"로")% 설정했습니다.
			ADDCHARA LCOUNT
			FLAG:8 ++
			IF NO:NUM < 300
				NO:NUM = 0
			ENDIF
			NUM += 1
			PRINTL
		ELSE
			PRINTFORML {LCOUNT}번 캐릭터는 등록되어 있지 않습니다.
		ENDIF
	ENDIF
WEND


;캐릭터 CSV 미정의값의 일괄 설정 처리를 하는 함수
;-------------------------------
;캐릭터 CSV 미정의값 일괄 설정
;-------------------------------
@SHOKISET_CSVFIX
#DIM CCOUNT
FOR CCOUNT, 0, CHARANUM
	SIF CCOUNT == MASTER
		CONTINUE
	CALL FIRSTSETTING_CHARA_CSVFIX, CCOUNT
NEXT

