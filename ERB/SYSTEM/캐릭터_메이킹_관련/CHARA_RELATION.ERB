;--------------------------------------------------
;캐릭터의 상관관계에 관한 처리
;--------------------------------------------------
;고유 번호 ARG의 캐릭터가 ARG:1의 캐릭터를 어떻게 생각하는지를 반환한다
@GET_RELATION, ARG:0, ARG:1
LOCALS = 

;인상계---------------------------------

IF RELATION:(ARG:0):(ARG:1) < POWER(2, 20)
	IF GETBIT(RELATION:(ARG:0):(ARG:1),소원한) && GETBIT(RELATION:(ARG:0):(ARG:1),친한)
		LOCALS += "미묘한 거리감의 지인"
	ELSEIF GETBIT(RELATION:(ARG:0):(ARG:1),친한)
		LOCALS += "친한 사이"
	ELSEIF GETBIT(RELATION:(ARG:0):(ARG:1),소원한)
		LOCALS += "아는 사이"
	ELSEIF GETBIT(RELATION:(ARG:0):(ARG:1),사랑하는) && GETBIT(RELATION:(ARG:0):(ARG:1),증오하는)
		LOCALS += "복잡한 관계"
	ELSEIF GETBIT(RELATION:(ARG:0):(ARG:1),사랑하는)
		LOCALS += "소중한 사람"
	ELSEIF GETBIT(RELATION:(ARG:0):(ARG:1),증오하는)
		;동료들의 원쑤!
		LOCALS += "원수"
	ENDIF
ELSE
	SIF GETBIT(RELATION:(ARG:0):(ARG:1),생이별한)
		LOCALS += "생이별한 "
	IF GETBIT(RELATION:(ARG:0):(ARG:1),소원한) && GETBIT(RELATION:(ARG:0):(ARG:1),친한)
		LOCALS += "미묘한 거리감의 "
	ELSEIF GETBIT(RELATION:(ARG:0):(ARG:1),친한) && GETBIT(RELATION:(ARG:0):(ARG:1),친구) == 0
		LOCALS += "친한 "
	ELSEIF GETBIT(RELATION:(ARG:0):(ARG:1),소원한)
		LOCALS += "소원한 "
	ENDIF
	IF GETBIT(RELATION:(ARG:0):(ARG:1),사랑하는) && GETBIT(RELATION:(ARG:0):(ARG:1),증오하는)
		LOCALS += "애증이 소용돌이치는 "
	ELSEIF GETBIT(RELATION:(ARG:0):(ARG:1),사랑하는)
		LOCALS += "사랑하는 "
	ELSEIF GETBIT(RELATION:(ARG:0):(ARG:1),증오하는)
		LOCALS += "증오하는 "
	ENDIF
	SIF GETBIT(RELATION:(ARG:0):(ARG:1),의리의)
		LOCALS += "의리의 "
ENDIF

;관계---------------------------------

LOCAL = 0
IF GETBIT(RELATION:(ARG:0):(ARG:1),부모자식)
	IF TOSHIUE_F(ARG:0, ARG:1) > 0
		IF ISMALE(ARG:1)
			LOCALS += "아들"
			LOCAL ++
		ELSE
			LOCALS += "딸"
			LOCAL ++
		ENDIF
	ELSE
		IF ISMALE(ARG:1)
			LOCALS += "친부"
			LOCAL ++
		ELSE
			LOCALS += "친모"
			LOCAL ++
		ENDIF
	ENDIF
	SIF GETBIT(RELATION:(ARG:0):(ARG:1),형제자매) || GETBIT(RELATION:(ARG:0):(ARG:1),조부모손주) || GETBIT(RELATION:(ARG:0):(ARG:1),아저씨아줌마) || GETBIT(RELATION:(ARG:0):(ARG:1),조카조카딸) || GETBIT(RELATION:(ARG:0):(ARG:1),사촌)
		LOCALS += "/"
ENDIF
IF GETBIT(RELATION:(ARG:0):(ARG:1),형제자매)
	IF TOSHIUE_F(ARG:0, ARG:1)
		IF ISMALE(ARG:1)
			LOCALS += "남동생"
			LOCAL ++
		ELSE
			LOCALS += "여동생"
			LOCAL ++
		ENDIF
	ELSE
		IF ISMALE(ARG:1)
			LOCALS += "형/오빠"
			LOCAL ++
		ELSE
			LOCALS += "누나/언니"
			LOCAL ++
		ENDIF
	ENDIF
	SIF GETBIT(RELATION:(ARG:0):(ARG:1),조부모손주) || GETBIT(RELATION:(ARG:0):(ARG:1),아저씨아줌마) || GETBIT(RELATION:(ARG:0):(ARG:1),조카조카딸) || GETBIT(RELATION:(ARG:0):(ARG:1),사촌)
		LOCALS += "/"
ENDIF
IF GETBIT(RELATION:(ARG:0):(ARG:1),조부모손주)
	IF TOSHIUE_F(ARG:0, ARG:1)
		LOCALS += "손주"
		LOCAL ++
	ELSE
		IF ISMALE(ARG:1)
			LOCALS += "할아버지"
			LOCAL ++
		ELSE
			LOCALS += "할머니"
			LOCAL ++
		ENDIF
	ENDIF
	SIF GETBIT(RELATION:(ARG:0):(ARG:1),아저씨아줌마) || GETBIT(RELATION:(ARG:0):(ARG:1),조카조카딸) || GETBIT(RELATION:(ARG:0):(ARG:1),사촌)
		LOCALS += "/"
ENDIF
IF GETBIT(RELATION:(ARG:0):(ARG:1),아저씨아줌마)
	IF ISMALE(ARG:1)
		LOCALS += "아저씨"
		LOCAL ++
	ELSE
		LOCALS += "아줌마"
		LOCAL ++
	ENDIF
	SIF GETBIT(RELATION:(ARG:0):(ARG:1),사촌)
		LOCALS += "/"
ENDIF
IF GETBIT(RELATION:(ARG:0):(ARG:1),조카조카딸)
	IF ISMALE(ARG:1)
		LOCALS += "조카"
		LOCAL ++
	ELSE
		LOCALS += "조카딸"
		LOCAL ++
	ENDIF
	SIF GETBIT(RELATION:(ARG:0):(ARG:1),사촌)
		LOCALS += "/"
ENDIF
IF GETBIT(RELATION:(ARG:0):(ARG:1),사촌)
	LOCALS += "사촌"
	LOCAL ++
ENDIF
SIF LOCAL && (GETBIT(RELATION:(ARG:0):(ARG:1),친구) || GETBIT(RELATION:(ARG:0):(ARG:1),짝사랑) || GETBIT(RELATION:(ARG:0):(ARG:1),연인) || GETBIT(RELATION:(ARG:0):(ARG:1),약혼자) || GETBIT(RELATION:(ARG:0):(ARG:1),배우자))
	LOCALS += "이며 "
IF GETBIT(RELATION:(ARG:0):(ARG:1),친한) && GETBIT(RELATION:(ARG:0):(ARG:1),소원한) == 0 && GETBIT(RELATION:(ARG:0):(ARG:1),친구)
	LOCALS += "절친"
	LOCAL ++
	SIF LOCAL && (GETBIT(RELATION:(ARG:0):(ARG:1),짝사랑) || GETBIT(RELATION:(ARG:0):(ARG:1),연인) || GETBIT(RELATION:(ARG:0):(ARG:1),약혼자) || GETBIT(RELATION:(ARG:0):(ARG:1),배우자))
		LOCALS += "이자 "
ELSEIF GETBIT(RELATION:(ARG:0):(ARG:1),친구)
	LOCALS += "친구"
	LOCAL ++
	SIF LOCAL && (GETBIT(RELATION:(ARG:0):(ARG:1),짝사랑) || GETBIT(RELATION:(ARG:0):(ARG:1),연인) || GETBIT(RELATION:(ARG:0):(ARG:1),약혼자) || GETBIT(RELATION:(ARG:0):(ARG:1),배우자))
		LOCALS += "이자 "
ENDIF
IF GETBIT(RELATION:(ARG:0):(ARG:1),짝사랑)
	LOCALS += "짝사랑하는 상대"
	LOCAL ++
ENDIF
IF GETBIT(RELATION:(ARG:0):(ARG:1),연인)
	LOCALS += "연인"
	LOCAL ++
ENDIF
IF GETBIT(RELATION:(ARG:0):(ARG:1),약혼자)
	LOCALS += "약혼자"
	LOCAL ++
ENDIF
IF GETBIT(RELATION:(ARG:0):(ARG:1),배우자)
	IF ISMALE(ARG:1)
		LOCALS += "남편"
	ELSE
		LOCALS += "아내"
	ENDIF
	LOCAL ++
ENDIF
SIF LOCAL && (GETBIT(RELATION:(ARG:0):(ARG:1),주인) || GETBIT(RELATION:(ARG:0):(ARG:1),종자) || GETBIT(RELATION:(ARG:0):(ARG:1),노예))
	LOCALS += "임과 동시에 "
IF GETBIT(RELATION:(ARG:0):(ARG:1),주인)
	LOCALS += "주인"
	LOCAL ++
ENDIF
IF GETBIT(RELATION:(ARG:0):(ARG:1),종자)
	LOCALS += "종자"
	LOCAL ++
ENDIF
IF GETBIT(RELATION:(ARG:0):(ARG:1),노예)
	LOCALS += "노예"
	LOCAL ++
ENDIF

;문자열을 반환한다
RESULTS = %LOCALS%


;--------------------------------------------------
;캐릭터 상관관계 부여
;--------------------------------------------------
;고유 번호 ARG:0의 캐릭터에 대한 ARG:1의 관계를 설정한다
;일부 관계는 양방향 처리
@SET_RELATION, ARG:0, ARG:1
#DIM LCOUNT
#DIM SELECT, 64
#DIM CHECK
#DIM LOSE_VIRGIN, 2
#DIM WAITFLAG
VARSET LOCAL
VARSET SELECT
FOR LOCAL,0,64
	SIF GETBIT(RELATION:(ARG:0):(ARG:1),LOCAL) > 0
		SELECT:LOCAL = 1
NEXT
LCOUNT = LINECOUNT
$INPUT_LOOP
CALL LB
PRINTFORML {ARG:0}번째 캐릭터『%CALLNAME:(ARG:0)%』와(과) {ARG:1}번째 캐릭터『%CALLNAME:(ARG:1)%』의 관계를 설정해 주십시오.
DRAWLINE
LOCAL = 0
PRINTL 　인상
SIF SELECT:생이별한 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{생이별한}]　생이별한
RESETCOLOR
SIF SELECT:친한 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{친한}]　친한
RESETCOLOR
SIF SELECT:소원한 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{소원한}]　소원한
RESETCOLOR
SIF SELECT:사랑하는 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{사랑하는}]　사랑하는
RESETCOLOR
SIF SELECT:증오하는 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{증오하는}]　증오하는
RESETCOLOR
SIF SELECT:의리의 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{의리의}]　의리의
RESETCOLOR
DRAWLINE
PRINTL 　관계
SIF SELECT:부모자식 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{부모자식}] 부모자식
RESETCOLOR
SIF SELECT:형제자매 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{형제자매}] 형/오빠/남동생/누나/언니/여동생
RESETCOLOR
SIF SELECT:조부모손주 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{조부모손주}] 할아버지/할머니/손주
RESETCOLOR
SIF SELECT:아저씨아줌마 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{아저씨아줌마}] 아저씨/아줌마
RESETCOLOR
SIF SELECT:조카조카딸 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{조카조카딸}] 조카/조카딸
RESETCOLOR
SIF SELECT:친구 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{친구}] 친구
RESETCOLOR
SIF SELECT:짝사랑 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{짝사랑}] 짝사랑하는 상대
RESETCOLOR
SIF SELECT:연인 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{연인}] 연인
RESETCOLOR
SIF SELECT:약혼자 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{약혼자}] 약혼자
RESETCOLOR
SIF SELECT:배우자 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{배우자}] 배우자
RESETCOLOR
SIF SELECT:주인 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{주인}] 주인
RESETCOLOR
SIF SELECT:종자 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{종자}] 종자
RESETCOLOR
SIF SELECT:노예 > 0
	SETCOLOR 255,60,220
PRINTFORML 　　[{노예}] 노예
RESETCOLOR
DRAWLINE
CHECK = 0
LOSE_VIRGIN:0 = 0
LOSE_VIRGIN:1 = 0
IF SELECT:부모자식 == 0 && SELECT:형제자매 == 0 && SELECT:조부모손주 == 0 && SELECT:아저씨아줌마 == 0 && SELECT:조카조카딸 == 0 && SELECT:사촌 == 0
	IF SELECT:생이별한 > 0
		PRINTL  가족관계가 없는 캐릭터에게「생이별한」은 추가할 수 없습니다.
		CHECK = 1
	ENDIF
	IF SELECT:의리의 > 0
		PRINTL  가족관계가 없는 캐릭터에게「의리의」는 추가할 수 없습니다.
		CHECK = 1
	ENDIF
ENDIF
IF SELECT:부모자식 > 0 && SELECT:의리의 == 0
	IF TOSHIUE_F(ARG:0, ARG:1) > 0 && TALENT:(ARG:0):처녀 > 0 && ISFEMALE(ARG:0)
		PRINTFORML  %CALLNAME:(ARG:0)% 쪽이 연상이지만 처녀이기 때문에 친모가 될 수 없습니다.
		IF TALENT:(ARG:0):고유캐릭터 == 0
			PRINTFORML  결정됐을 경우에는 %CALLNAME:(ARG:0)%의 처녀를 소거합니다.
		ELSE
			CHECK = 1
		ENDIF
		LOSE_VIRGIN:0 = 1
	ELSEIF TOSHIUE_F(ARG:1, ARG:0) > 0 && TALENT:(ARG:1):처녀 > 0 && ISFEMALE(ARG:1)
		PRINTFORML  %CALLNAME:(ARG:1)% 쪽이 연상이지만 처녀이기 때문에 친모가 될 수 없습니다.
		IF TALENT:(ARG:1):고유캐릭터 == 0
			PRINTFORML  결정됐을 경우에는 %CALLNAME:(ARG:1)%의 처녀를 소거합니다.
		ELSE
			CHECK = 1
		ENDIF
		LOSE_VIRGIN:1 = 1
	ENDIF
ENDIF
IF SELECT:조부모손주 > 0 && SELECT:의리의 == 0
	IF TOSHIUE_F(ARG:0, ARG:1) > 0 && TALENT:(ARG:0):처녀 > 0 && ISFEMALE(ARG:0)
		PRINTFORML  %CALLNAME:(ARG:0)% 쪽이 연상이지만 처녀이기 때문에 조모가 될 수 없습니다.
		IF TALENT:(ARG:0):고유캐릭터 == 0
			PRINTFORML  결정됐을 경우에는 %CALLNAME:(ARG:0)%의 처녀를 소거합니다.
		ELSE
			CHECK = 1
		ENDIF
		LOSE_VIRGIN:0 = 1
	ELSEIF TOSHIUE_F(ARG:1, ARG:0) > 0 && TALENT:(ARG:1):처녀 > 0 && ISFEMALE(ARG:1)
		PRINTFORML  %CALLNAME:(ARG:1)% 쪽이 연상이지만 처녀이기 때문에 조모가 될 수 없습니다.
		IF TALENT:(ARG:1):고유캐릭터 == 0
			PRINTFORML  결정됐을 경우에는 %CALLNAME:(ARG:1)%의 처녀를 소거합니다.
		ELSE
			CHECK = 1
		ENDIF
		LOSE_VIRGIN:1 = 1
	ENDIF
ENDIF
SIF CHECK > 0
	SETCOLOR 105,105,105
PRINTL 　[200] 결정
RESETCOLOR
PRINTL 　[999] 취소
INPUT 
SIF RESULT != 200
	CLEARLINE LINECOUNT - LCOUNT
IF RESULT == 200 && CHECK == 0
	RELATION:(ARG:0):(ARG:1) = 0
	FOR LOCAL, 1, 64
		SIF SELECT:LOCAL > 0
			SETBIT RELATION:(ARG:0):(ARG:1), LOCAL
	NEXT
	;처녀를 지우는 처리
	SIF LOSE_VIRGIN:0 > 0
		TALENT:(ARG:0):처녀 = 0
	SIF LOSE_VIRGIN:1 > 0
		TALENT:(ARG:1):처녀 = 0

	WAITFLAG = 0

	;관계의 양방향화
	IF GETBIT(RELATION:(ARG:0):(ARG:1), 생이별한) > 0
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 생이별한) == 0
			WAITFLAG ++
		SETBIT RELATION:(ARG:1):(ARG:0), 생이별한
	ELSE
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 생이별한) > 0
			WAITFLAG ++
		CLEARBIT RELATION:(ARG:1):(ARG:0), 생이별한
	ENDIF
	IF GETBIT(RELATION:(ARG:0):(ARG:1), 의리의) > 0
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 의리의) == 0
			WAITFLAG ++
		SETBIT RELATION:(ARG:1):(ARG:0), 의리의
	ELSE
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 의리의) > 0
			WAITFLAG ++
		CLEARBIT RELATION:(ARG:1):(ARG:0), 의리의
	ENDIF
	IF GETBIT(RELATION:(ARG:0):(ARG:1), 부모자식) > 0
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 부모자식) == 0
			WAITFLAG ++
		SETBIT RELATION:(ARG:1):(ARG:0), 부모자식
	ELSE
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 부모자식) > 0
			WAITFLAG ++
		CLEARBIT RELATION:(ARG:1):(ARG:0), 부모자식
	ENDIF
	IF GETBIT(RELATION:(ARG:0):(ARG:1), 형제자매) > 0
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 형제자매) == 0
			WAITFLAG ++
		SETBIT RELATION:(ARG:1):(ARG:0), 형제자매
	ELSE
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 형제자매) > 0
			WAITFLAG ++
		CLEARBIT RELATION:(ARG:1):(ARG:0), 형제자매
	ENDIF
	IF GETBIT(RELATION:(ARG:0):(ARG:1), 조부모손주) > 0
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 조부모손주) == 0
			WAITFLAG ++
		SETBIT RELATION:(ARG:1):(ARG:0), 조부모손주
	ELSE
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 조부모손주) > 0
		WAITFLAG ++
		CLEARBIT RELATION:(ARG:1):(ARG:0), 조부모손주
	ENDIF
	IF GETBIT(RELATION:(ARG:0):(ARG:1), 아저씨아줌마) > 0
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 조카조카딸) == 0
			WAITFLAG ++
		SETBIT RELATION:(ARG:1):(ARG:0), 조카조카딸
	ELSE
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 조카조카딸) > 0
			WAITFLAG ++
		CLEARBIT RELATION:(ARG:1):(ARG:0), 조카조카딸
	ENDIF
	IF GETBIT(RELATION:(ARG:0):(ARG:1), 조카조카딸) > 0
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 아저씨아줌마) == 0
			WAITFLAG ++
		SETBIT RELATION:(ARG:1):(ARG:0), 아저씨아줌마
	ELSE
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 아저씨아줌마) > 0
			WAITFLAG ++
		CLEARBIT RELATION:(ARG:1):(ARG:0), 아저씨아줌마
	ENDIF
	IF GETBIT(RELATION:(ARG:0):(ARG:1), 사촌) > 0
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 사촌) == 0
			WAITFLAG ++
		SETBIT RELATION:(ARG:1):(ARG:0), 사촌
	ELSE
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 사촌) > 0
			WAITFLAG ++
		CLEARBIT RELATION:(ARG:1):(ARG:0), 사촌
	ENDIF
	IF GETBIT(RELATION:(ARG:0):(ARG:1), 연인) > 0
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 연인) == 0
			WAITFLAG ++
		SETBIT RELATION:(ARG:1):(ARG:0), 연인
	ELSE
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 연인) > 0
			WAITFLAG ++
		CLEARBIT RELATION:(ARG:1):(ARG:0), 연인
	ENDIF
	IF GETBIT(RELATION:(ARG:0):(ARG:1), 약혼자) > 0
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 약혼자) == 0
			WAITFLAG ++
		SETBIT RELATION:(ARG:1):(ARG:0), 약혼자
	ELSE
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 약혼자) > 0
			WAITFLAG ++
		CLEARBIT RELATION:(ARG:1):(ARG:0), 약혼자
	ENDIF
	IF GETBIT(RELATION:(ARG:0):(ARG:1), 배우자) > 0
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 배우자) == 0
			WAITFLAG ++
		SETBIT RELATION:(ARG:1):(ARG:0), 배우자
	ELSE
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 배우자) > 0
			WAITFLAG ++
		CLEARBIT RELATION:(ARG:1):(ARG:0), 배우자
	ENDIF
	IF GETBIT(RELATION:(ARG:0):(ARG:1), 주인) > 0
		IF GETBIT(RELATION:(ARG:1):(ARG:0), 종자) == 0 || GETBIT(RELATION:(ARG:1):(ARG:0), 노예) == 0
			WAITFLAG ++
			PRINTFORML 『%CALLNAME:(ARG:1)%』에게 있어『%CALLNAME:(ARG:0)%』은(는)
			PRINTFORML 　　[{종자}] 종자
			PRINTFORML 　　[{노예}] 노예
			$INPUT_LOOP주인
			INPUT 
			IF RESULT == 41
				SETBIT RELATION:(ARG:1):(ARG:0),종자 
			ELSEIF RESULT == 42
				SETBIT RELATION:(ARG:1):(ARG:0), 노예
			ELSE
				GOTO INPUT_LOOP주인
			ENDIF
		ENDIF
	ELSE
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 종자) > 0 || GETBIT(RELATION:(ARG:1):(ARG:0), 노예) > 0
			WAITFLAG ++
		CLEARBIT RELATION:(ARG:1):(ARG:0), 종자
		CLEARBIT RELATION:(ARG:1):(ARG:0), 노예
	ENDIF
	IF GETBIT(RELATION:(ARG:0):(ARG:1), 종자) > 0 || GETBIT(RELATION:(ARG:0):(ARG:1), 노예) > 0
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 주인) == 0
			WAITFLAG ++
		SETBIT RELATION:(ARG:1):(ARG:0), 주인
	ELSE
		SIF GETBIT(RELATION:(ARG:1):(ARG:0), 주인) > 0
			WAITFLAG ++
		CLEARBIT RELATION:(ARG:1):(ARG:0), 주인
	ENDIF

	IF RELATION:(ARG:0):(ARG:1) > 0
		CALL GET_RELATION, ARG:0, ARG:1
		PRINTFORML 『%CALLNAME:(ARG:0)%』에게 있어『%CALLNAME:(ARG:1)%』은(는)【%RESULTS%】입니다.
	ELSE
		PRINTFORML 『%CALLNAME:(ARG:0)%』와『%CALLNAME:(ARG:1)%』사이에 상관관계는 없습니다.
	ENDIF
	;상대와의 관계에 변동이 있는 경우, 메시지 표시
	IF RELATION:(ARG:1):(ARG:0) > 0 && WAITFLAG > 0
		CALL GET_RELATION, (ARG:1), (ARG:0)
		PRINTFORML 『%CALLNAME:(ARG:1)%』에게 있어『%CALLNAME:(ARG:0)%』은(는)【%RESULTS%】이(가) 됐습니다.
	ELSEIF WAITFLAG > 0
		PRINTFORML 『%CALLNAME:(ARG:1)%』와『%CALLNAME:(ARG:0)%』의 상관관계는 없어졌습니다.
	ENDIF
	PRINTW 

	RETURN
ELSEIF RESULT == 999
	RETURN
ELSEIF (RESULT >= 1 && RESULT <= 의리의) || (RESULT >= 부모자식 && RESULT <= 사촌) || (RESULT >= 친구 && RESULT <= 배우자) || (RESULT >= 주인 && RESULT <= 노예)
	IF SELECT:RESULT > 0
		SELECT:RESULT = 0
	ELSE
		SELECT:RESULT = 1
	ENDIF
	SIF RESULT == 아저씨아줌마
		SELECT:조카조카딸 = 0
	SIF RESULT == 조카조카딸
		SELECT:아저씨아줌마 = 0
	IF RESULT == 짝사랑
		SELECT:연인 = 0
		SELECT:약혼자 = 0
		SELECT:배우자 = 0
	ENDIF
	IF RESULT == 연인
		SELECT:짝사랑 = 0
		SELECT:약혼자 = 0
		SELECT:배우자 = 0
	ENDIF
	IF RESULT == 약혼자
		SELECT:짝사랑 = 0
		SELECT:연인 = 0
		SELECT:배우자 = 0
	ENDIF
	IF RESULT == 배우자
		SELECT:짝사랑 = 0
		SELECT:연인 = 0
		SELECT:약혼자 = 0
	ENDIF
	IF RESULT == 주인
		SELECT:종자 = 0
		SELECT:노예 = 0
	ENDIF
	IF RESULT == 종자
		SELECT:주인 = 0
		SELECT:노예 = 0
	ENDIF
	IF RESULT == 노예
		SELECT:주인 = 0
		SELECT:종자 = 0
	ENDIF
ENDIF
GOTO INPUT_LOOP





;--------------------------------------------------
;상관관계의 모든 캐릭터 탐색
;--------------------------------------------------
@CHECK_ALL_RELATION
#DIM CHARA
#DIM SELECT
#DIM RELATE
#DIM WAITFLAG, 2
;캐릭터 2명 미만이면 중단
SIF CHARANUM < 2
	RETURN
;자기 캐릭터의 전체 탐색
FOR CHARA, 0, CHARANUM
	SIF CHARA == MASTER
		CONTINUE

	;양친이 동료로 있을 경우
	IF CFLAG:CHARA:9 <= -100 || CFLAG:CHARA:7 > 0
		FOR SELECT, 0, CHARANUM
			SIF SELECT == MASTER
				CONTINUE
			SIF SELECT == CHARA
				CONTINUE
			;부모자식
			IF (CFLAG:CHARA:9 <= -100 && CFLAG:SELECT:240 == CFLAG:CHARA:9 * -1 - 100) || (CFLAG:CHARA:7 > 0 && CFLAG:SELECT:240 == CFLAG:CHARA:7)
				SETBIT RELATION:CHARA:SELECT, 부모자식
				SETBIT RELATION:SELECT:CHARA, 부모자식
			ENDIF
		NEXT
	ENDIF

	;상대 캐릭터 탐색
	FOR SELECT, 0, CHARANUM
		SIF SELECT == MASTER
			CONTINUE
		SIF SELECT == CHARA
			CONTINUE
		WAITFLAG:0 = 0
		WAITFLAG:1 = 0

		;자신에게 부모나 자녀가 있을 경우
		FOR RELATE, 0, CHARANUM
			SIF RELATE == MASTER
				CONTINUE
			SIF RELATE == CHARA
				CONTINUE
			;부모가 있을 경우
			IF GETBIT(RELATION:CHARA:RELATE, 부모자식) && TOSHIUE_F(RELATE, CHARA) > 0
				PRINTL 
				;같은 부모를 둔 상대라면 형제자매
				IF GETBIT(RELATION:SELECT:RELATE, 부모자식) && TOSHIUE_F(RELATE, SELECT) > 0
					SIF GETBIT(RELATION:CHARA:SELECT, 형제자매) == 0
						WAITFLAG:0 ++
					SETBIT RELATION:CHARA:SELECT, 형제자매
					SIF GETBIT(RELATION:SELECT:CHARA, 형제자매) == 0
						WAITFLAG:1 ++
					SETBIT RELATION:SELECT:CHARA, 형제자매
					;의리의 경우
					IF GETBIT(RELATION:CHARA:RELATE, 의리의)
						SETBIT RELATION:CHARA:SELECT, 의리의
						SETBIT RELATION:SELECT:CHARA, 의리의
					ENDIF
				ENDIF
				;부모의 부모이고 연상이면 조부모
				IF GETBIT(RELATION:RELATE:SELECT, 부모자식) && TOSHIUE_F(SELECT, RELATE) > 0
					IF TOSHIUE_F(SELECT, CHARA) > 0
						SIF GETBIT(RELATION:CHARA:SELECT, 조부모손주) == 0
							WAITFLAG:0 ++
						SETBIT RELATION:CHARA:SELECT, 조부모손주
						SIF GETBIT(RELATION:SELECT:CHARA, 조부모손주) == 0
							WAITFLAG:1 ++
						SETBIT RELATION:SELECT:CHARA, 조부모손주
						;의리의 경우
						IF GETBIT(RELATION:CHARA:RELATE, 의리의)
							SETBIT RELATION:CHARA:SELECT, 의리의
							SETBIT RELATION:SELECT:CHARA, 의리의
						ENDIF
					ENDIF
				ENDIF
				;부모의 형제자매라면 아저씨아줌마
				IF GETBIT(RELATION:RELATE:SELECT, 형제자매)
					SIF GETBIT(RELATION:CHARA:SELECT, 아저씨아줌마) == 0
						WAITFLAG:0 ++
					SETBIT RELATION:CHARA:SELECT, 아저씨아줌마
					SIF GETBIT(RELATION:SELECT:CHARA, 조카조카딸) == 0
						WAITFLAG:1 ++
					SETBIT RELATION:SELECT:CHARA, 조카조카딸
					;의리의 경우
					IF GETBIT(RELATION:CHARA:RELATE, 의리의)
						SETBIT RELATION:CHARA:SELECT, 의리의
						SETBIT RELATION:SELECT:CHARA, 의리의
					ENDIF
				ENDIF
			ENDIF
			;자녀가 있을 경우
			IF GETBIT(RELATION:CHARA:RELATE, 부모자식) && TOSHIUE_F(CHARA, RELATE) > 0
				;자녀의 자식이고 연하면 손주
				IF GETBIT(RELATION:RELATE:SELECT, 부모자식) && TOSHIUE_F(RELATE, SELECT) > 0
					IF TOSHIUE_F(CHARA, SELECT) > 0
						SIF GETBIT(RELATION:CHARA:SELECT, 조부모손주) == 0
							WAITFLAG:0 ++
						SETBIT RELATION:CHARA:SELECT, 조부모손주
						SIF GETBIT(RELATION:SELECT:CHARA, 조부모손주) == 0
							WAITFLAG:1 ++
						SETBIT RELATION:SELECT:CHARA, 조부모손주
						;의리의 경우
						IF GETBIT(RELATION:CHARA:RELATE, 의리의)
							SETBIT RELATION:CHARA:SELECT, 의리의
							SETBIT RELATION:SELECT:CHARA, 의리의
						ENDIF
					ENDIF
				ENDIF
				;자녀의 조부모이고 상대가 연상이면 부모
				IF GETBIT(RELATION:RELATE:SELECT, 조부모손주) && TOSHIUE_F(SELECT, RELATE) > 0
					IF TOSHIUE_F(SELECT, CHARA) > 0
						SIF GETBIT(RELATION:CHARA:SELECT, 부모자식) == 0
							WAITFLAG:0 ++
						SETBIT RELATION:CHARA:SELECT, 부모자식
						SIF GETBIT(RELATION:SELECT:CHARA, 부모자식) == 0
							WAITFLAG:1 ++
						SETBIT RELATION:SELECT:CHARA, 부모자식
						;의리의 경우
						IF GETBIT(RELATION:CHARA:RELATE, 의리의)
							SETBIT RELATION:CHARA:SELECT, 의리의
							SETBIT RELATION:SELECT:CHARA, 의리의
						ENDIF
					ENDIF
				ENDIF
			ENDIF
		NEXT
		;자신에게 형제자매가 있을 경우
		FOR RELATE, 0, CHARANUM
			SIF RELATE == MASTER
				CONTINUE
			SIF RELATE == CHARA
				CONTINUE
			IF GETBIT(RELATION:CHARA:RELATE, 형제자매)
				;형제자매의 형제자매는 형제자매
				;왜냐하면, 그것이 형제자매니까...
				IF GETBIT(RELATION:RELATE:SELECT, 형제자매)
					SIF GETBIT(RELATION:CHARA:SELECT, 형제자매) == 0
						WAITFLAG:0 ++
					SETBIT RELATION:CHARA:SELECT, 형제자매
					SIF GETBIT(RELATION:SELECT:CHARA, 형제자매) == 0
						WAITFLAG:1 ++
					SETBIT RELATION:SELECT:CHARA, 형제자매
				ENDIF
				;형제자매의 부모이고 연상이면 부모
				IF GETBIT(RELATION:RELATE:SELECT, 부모자식) && TOSHIUE_F(SELECT, RELATE) > 0
					IF TOSHIUE_F(SELECT, CHARA) > 0
						SIF GETBIT(RELATION:CHARA:SELECT, 부모자식) == 0
							WAITFLAG:0 ++
						SETBIT RELATION:CHARA:SELECT, 부모자식
						SIF GETBIT(RELATION:SELECT:CHARA, 부모자식) == 0
							WAITFLAG:1 ++
						SETBIT RELATION:SELECT:CHARA, 부모자식
					ENDIF
				ENDIF
				;형제자매의 조부모이고 연상이면 조부모
				IF GETBIT(RELATION:RELATE:SELECT, 조부모손주) && TOSHIUE_F(SELECT, RELATE) > 0
					IF TOSHIUE_F(SELECT, CHARA) > 0
						SIF GETBIT(RELATION:CHARA:SELECT, 조부모손주) == 0
							WAITFLAG:0 ++
						SETBIT RELATION:CHARA:SELECT, 조부모손주
						SIF GETBIT(RELATION:SELECT:CHARA, 조부모손주) == 0
							WAITFLAG:1 ++
						SETBIT RELATION:SELECT:CHARA, 조부모손주
					ENDIF
				ENDIF
				;형제자매의 자녀는 조카조카딸
				IF GETBIT(RELATION:RELATE:SELECT, 부모자식) && TOSHIUE_F(RELATE, SELECT) > 0
					SIF GETBIT(RELATION:CHARA:SELECT, 조카조카딸) == 0
						WAITFLAG:0 ++
					SETBIT RELATION:CHARA:SELECT, 조카조카딸
					SIF GETBIT(RELATION:SELECT:CHARA, 아저씨아줌마) == 0
						WAITFLAG:1 ++
					SETBIT RELATION:SELECT:CHARA, 아저씨아줌마
				ENDIF
			ENDIF
		NEXT
		;자신에게 조부모나 손주가 있을 경우
		FOR RELATE, 0, CHARANUM
			SIF RELATE == MASTER
				CONTINUE
			SIF RELATE == CHARA
				CONTINUE
			;조부모가 있을 경우
			IF GETBIT(RELATION:CHARA:RELATE, 조부모손주) && TOSHIUE_F(RELATE, CHARA) > 0
				;부모가 아닌 조부모의 자녀는 아저씨아줌마
				IF GETBIT(RELATION:RELATE:SELECT, 부모자식) && TOSHIUE_F(RELATE, SELECT) > 0 && GETBIT(RELATION:CHARA:SELECT, 부모자식) == 0
					SIF GETBIT(RELATION:CHARA:SELECT, 아저씨아줌마) == 0
						WAITFLAG:0 ++
					SETBIT RELATION:CHARA:SELECT, 아저씨아줌마
					SIF GETBIT(RELATION:SELECT:CHARA, 조카조카딸) == 0
						WAITFLAG:1 ++
					SETBIT RELATION:SELECT:CHARA, 조카조카딸
					;의리의 경우
					IF GETBIT(RELATION:CHARA:RELATE, 의리의)
						SETBIT RELATION:CHARA:SELECT, 의리의
						SETBIT RELATION:SELECT:CHARA, 의리의
					ENDIF
				ENDIF
			ENDIF
			;손주가 있을 경우
			IF GETBIT(RELATION:CHARA:RELATE, 조부모손주) && TOSHIUE_F(CHARA, RELATE) > 0
				;손주의 부모가 연하면 자녀
				IF GETBIT(RELATION:RELATE:SELECT, 부모자식) && TOSHIUE_F(SELECT, RELATE) > 0
					IF TOSHIUE_F(CHARA, SELECT) > 0
						SIF GETBIT(RELATION:CHARA:SELECT, 부모자식) == 0
							WAITFLAG:0 ++
						SETBIT RELATION:CHARA:SELECT, 부모자식
						SIF GETBIT(RELATION:SELECT:CHARA, 부모자식) == 0
							WAITFLAG:1 ++
						SETBIT RELATION:SELECT:CHARA, 부모자식
					ENDIF
				ENDIF
			ENDIF
		NEXT
		;자신에게 아저씨아줌마가 있을 경우
		FOR RELATE, 0, CHARANUM
			SIF RELATE == MASTER
				CONTINUE
			SIF RELATE == CHARA
				CONTINUE
			IF GETBIT(RELATION:CHARA:RELATE, 아저씨아줌마)
				;아저씨아줌마의 자녀는 사촌
				IF GETBIT(RELATION:RELATE:SELECT, 부모자식) && TOSHIUE_F(RELATE, SELECT) > 0
					SIF GETBIT(RELATION:CHARA:SELECT, 사촌) == 0
						WAITFLAG:0 ++
					SETBIT RELATION:CHARA:SELECT, 사촌
					SIF GETBIT(RELATION:SELECT:CHARA, 사촌) == 0
						WAITFLAG:1 ++
					SETBIT RELATION:SELECT:CHARA, 사촌
					;의리의 경우
					IF GETBIT(RELATION:CHARA:RELATE, 의리의)
						SETBIT RELATION:CHARA:SELECT, 의리의
						SETBIT RELATION:SELECT:CHARA, 의리의
					ENDIF
				ENDIF
			ENDIF
		NEXT
		;자신에게 사촌이 있을 경우
		FOR RELATE, 0, CHARANUM
			SIF RELATE == MASTER
				CONTINUE
			SIF RELATE == CHARA
				CONTINUE
			IF GETBIT(RELATION:CHARA:RELATE, 사촌)
				;사촌 부모는 아저씨아줌마
				IF GETBIT(RELATION:RELATE:SELECT, 부모자식) && TOSHIUE_F(SELECT, RELATE) > 0
					SIF GETBIT(RELATION:CHARA:SELECT, 아저씨아줌마) == 0
						WAITFLAG:0 ++
					SETBIT RELATION:CHARA:SELECT, 아저씨아줌마
					SIF GETBIT(RELATION:SELECT:CHARA, 조카조카딸) == 0
						WAITFLAG:1 ++
					SETBIT RELATION:SELECT:CHARA, 조카조카딸
					;의리의 경우
					IF GETBIT(RELATION:CHARA:RELATE, 의리의)
						SETBIT RELATION:CHARA:SELECT, 의리의
						SETBIT RELATION:SELECT:CHARA, 의리의
					ENDIF
				ENDIF
			ENDIF
		NEXT

		;관계의 양방향화
		IF GETBIT(RELATION:CHARA:SELECT, 생이별한) > 0
			SIF GETBIT(RELATION:SELECT:CHARA, 생이별한) == 0
				WAITFLAG:1 ++
			SETBIT RELATION:SELECT:CHARA, 생이별한
		ELSE
			SIF GETBIT(RELATION:SELECT:CHARA,생이별한) > 0
				WAITFLAG:1 ++
			CLEARBIT RELATION:SELECT:CHARA, 생이별한
		ENDIF
		IF GETBIT(RELATION:CHARA:SELECT, 의리의) > 0
			SIF GETBIT(RELATION:SELECT:CHARA, 의리의) == 0
				WAITFLAG:1 ++
			SETBIT RELATION:SELECT:CHARA, 의리의
		ELSE
			SIF GETBIT(RELATION:SELECT:CHARA, 의리의) > 0
				WAITFLAG:1 ++
			CLEARBIT RELATION:SELECT:CHARA, 의리의
		ENDIF
		IF GETBIT(RELATION:CHARA:SELECT, 부모자식) > 0
			SIF GETBIT(RELATION:SELECT:CHARA, 부모자식) == 0
				WAITFLAG:1 ++
			SETBIT RELATION:SELECT:CHARA, 부모자식
		ELSE
			SIF GETBIT(RELATION:SELECT:CHARA, 부모자식) > 0
				WAITFLAG:1 ++
			CLEARBIT RELATION:SELECT:CHARA, 부모자식
		ENDIF
		IF GETBIT(RELATION:CHARA:SELECT, 형제자매) > 0
			SIF GETBIT(RELATION:SELECT:CHARA, 형제자매) == 0
				WAITFLAG:1 ++
			SETBIT RELATION:SELECT:CHARA, 형제자매
		ELSE
			SIF GETBIT(RELATION:SELECT:CHARA, 형제자매) > 0
				WAITFLAG:1 ++
			CLEARBIT RELATION:SELECT:CHARA, 형제자매
		ENDIF
		IF GETBIT(RELATION:CHARA:SELECT, 조부모손주) > 0
			SIF GETBIT(RELATION:SELECT:CHARA, 조부모손주) == 0
				WAITFLAG:1 ++
			SETBIT RELATION:SELECT:CHARA, 조부모손주
		ELSE
			SIF GETBIT(RELATION:SELECT:CHARA, 조부모손주) > 0
				WAITFLAG:1 ++
			CLEARBIT RELATION:SELECT:CHARA, 조부모손주
		ENDIF
		IF GETBIT(RELATION:CHARA:SELECT, 아저씨아줌마) > 0
			SIF GETBIT(RELATION:SELECT:CHARA, 조카조카딸) == 0
				WAITFLAG:1 ++
			SETBIT RELATION:SELECT:CHARA, 조카조카딸
		ELSE
			SIF GETBIT(RELATION:SELECT:CHARA, 조카조카딸) > 0
				WAITFLAG:1 ++
			CLEARBIT RELATION:SELECT:CHARA, 조카조카딸
		ENDIF
		IF GETBIT(RELATION:CHARA:SELECT, 조카조카딸) > 0
			SIF GETBIT(RELATION:SELECT:CHARA, 아저씨아줌마) == 0
				WAITFLAG:1 ++
			SETBIT RELATION:SELECT:CHARA, 아저씨아줌마
		ELSE
			SIF GETBIT(RELATION:SELECT:CHARA, 아저씨아줌마) > 0
				WAITFLAG:1 ++
			CLEARBIT RELATION:SELECT:CHARA, 아저씨아줌마
		ENDIF
		IF GETBIT(RELATION:CHARA:SELECT, 사촌) > 0
			SIF GETBIT(RELATION:SELECT:CHARA, 사촌) == 0
				WAITFLAG:1 ++
			SETBIT RELATION:SELECT:CHARA, 사촌
		ELSE
			SIF GETBIT(RELATION:SELECT:CHARA, 사촌) > 0
				WAITFLAG:1 ++
			CLEARBIT RELATION:SELECT:CHARA, 사촌
		ENDIF
		IF GETBIT(RELATION:CHARA:SELECT, 연인) > 0
			SIF GETBIT(RELATION:SELECT:CHARA, 연인) == 0
				WAITFLAG:1 ++
			SETBIT RELATION:SELECT:CHARA, 연인
		ELSE
			SIF GETBIT(RELATION:SELECT:CHARA, 연인) > 0
				WAITFLAG:1 ++
			CLEARBIT RELATION:SELECT:CHARA, 연인
		ENDIF
		IF GETBIT(RELATION:CHARA:SELECT, 약혼자) > 0
			SIF GETBIT(RELATION:SELECT:CHARA, 약혼자) == 0
				WAITFLAG:1 ++
			SETBIT RELATION:SELECT:CHARA, 약혼자
		ELSE
			SIF GETBIT(RELATION:SELECT:CHARA, 약혼자) > 0
				WAITFLAG:1 ++
			CLEARBIT RELATION:SELECT:CHARA, 약혼자
		ENDIF
		IF GETBIT(RELATION:CHARA:SELECT, 배우자) > 0
			SIF GETBIT(RELATION:SELECT:CHARA, 배우자) == 0
				WAITFLAG:1 ++
			SETBIT RELATION:SELECT:CHARA, 배우자
		ELSE
			SIF GETBIT(RELATION:SELECT:CHARA, 배우자) > 0
				WAITFLAG:1 ++
			CLEARBIT RELATION:SELECT:CHARA, 배우자
		ENDIF
		IF GETBIT(RELATION:CHARA:SELECT, 부모자식) == 0 && GETBIT(RELATION:CHARA:SELECT, 형제자매) == 0 && GETBIT(RELATION:CHARA:SELECT, 조부모손주) == 0 && GETBIT(RELATION:CHARA:SELECT, 아저씨아줌마) == 0 && GETBIT(RELATION:CHARA:SELECT, 조카조카딸) == 0
			SIF GETBIT(RELATION:SELECT:CHARA, 생이별한) || GETBIT(RELATION:SELECT:CHARA, 의리의)
				WAITFLAG:1 ++
			CLEARBIT RELATION:SELECT:CHARA, 생이별한
			CLEARBIT RELATION:SELECT:CHARA, 의리의
		ENDIF
		;상대와의 관계에 변동이 있는 경우, 메시지 표시
		IF RELATION:CHARA:SELECT > 0 && WAITFLAG:0 > 0
			CALL GET_RELATION, CHARA, SELECT
			PRINTFORML 『%CALLNAME:CHARA%』에게 있어『%CALLNAME:SELECT%』은(는)【%RESULTS%】이(가) 됐습니다.
		ELSEIF WAITFLAG:0 > 0
			PRINTFORML 『%CALLNAME:CHARA%』와(과)『%CALLNAME:SELECT%』의 상관관계는 없어졌습니다.
		ENDIF
		IF RELATION:SELECT:CHARA > 0 && WAITFLAG:1 > 0
			CALL GET_RELATION, SELECT, CHARA
			PRINTFORML 『%CALLNAME:SELECT%』에게 있어『%CALLNAME:CHARA%』은(는)【%RESULTS%】이(가) 됐습니다.
		ELSEIF WAITFLAG:1 > 0
			PRINTFORML 『%CALLNAME:SELECT%』와(과)『%CALLNAME:CHARA%』의 상관관계는 없어졌습니다.
		ENDIF
		SIF WAITFLAG:0 || WAITFLAG:1
			PRINTL 
		;자식이나 손주를 둔 캐릭터는 처녀를 잃는다
		IF GETBIT(RELATION:CHARA:SELECT, 의리의) == 0 && (GETBIT(RELATION:CHARA:SELECT, 부모자식) || GETBIT(RELATION:CHARA:SELECT, 조부모손주))
			IF TOSHIUE_F(CHARA, SELECT) > 0
				IF TALENT:CHARA:처녀 > 0
					TALENT:CHARA:처녀 = 0
					PRINTFORML 　　관계의 변화에 따라『%CALLNAME:CHARA%』의 처녀를 소거했습니다.
					PRINTL 
				ENDIF
			ENDIF
		ENDIF
	NEXT
NEXT
;양친ＩＤ를 설정
FOR CHARA, 0, CHARANUM
	SIF CHARA == MASTER
		CONTINUE
	;친부 탐색
	FOR SELECT, 0, CHARANUM
		SIF SELECT == MASTER
			CONTINUE
		SIF SELECT == CHARA
			CONTINUE
		;친부모와 자식의 관계임과 동시에 남성이거나 후타나리라면 친부
		SIF GETBIT(RELATION:CHARA:SELECT, 부모자식) && GETBIT(RELATION:CHARA:SELECT, 의리의) == 0 && TOSHIUE_F(SELECT, CHARA) > 0 && (ISMALE(SELECT) || TALENT:SELECT:후타나리 > 0) && CFLAG:CHARA:7 != CFLAG:SELECT:240 && CFLAG:CHARA:9 == 0
			CFLAG:CHARA:9 = CFLAG:SELECT:240 * -1 - 100
	NEXT
	FOR SELECT, 0, CHARANUM
		SIF SELECT == MASTER
			CONTINUE
		SIF SELECT == CHARA
			CONTINUE
		;친부모와 자식의 관계임과 동시에 남성이 아니라면 친모
		SIF GETBIT(RELATION:CHARA:SELECT, 부모자식) && GETBIT(RELATION:CHARA:SELECT, 의리의) == 0 && TOSHIUE_F(SELECT, CHARA) > 0 && ISFEMALE(SELECT) && CFLAG:CHARA:9 * -1 - 100 != CFLAG:SELECT:240 && CFLAG:CHARA:7 == 0
			CFLAG:CHARA:7 = CFLAG:SELECT:240
	NEXT
NEXT



;-------------------------------------------------
;연상 판정
;ARG:0이 ARG:1보다 연상이라면 1을 반환한다
@TOSHIUE_F(ARG:0, ARG:1)
#FUNCTION
IF BASE:(ARG:0):실제나이 > BASE:(ARG:1):실제나이
	LOCAL = 1
ELSEIF BASE:(ARG:0):실제나이 == BASE:(ARG:1):실제나이 && CFLAG:(ARG:0):240 < CFLAG:(ARG:1):240
	LOCAL = 1
ELSE
	LOCAL = 0
ENDIF
RETURNF LOCAL


;------------------------------------------------
;근친상간 판정
;ARG:0과 ARG:1이 근친 관계에 있다면 1을 반환한다
;ARG:2가 0이라면 동료 캐릭터, 1이라면 그 외 캐릭터
@INCEST_F(ARG:0, ARG:1, ARG:2 = 0)
#FUNCTION
IF ARG:2 == 0
	SIF GETBIT(RELATION:(ARG:0):(ARG:1), 의리의)
		RETURNF 0
	SIF GETBIT(RELATION:(ARG:0):(ARG:1), 부모자식) || GETBIT(RELATION:(ARG:0):(ARG:1), 형제자매) || GETBIT(RELATION:(ARG:0):(ARG:1), 조부모손주) || GETBIT(RELATION:(ARG:0):(ARG:1), 아저씨아줌마) || GETBIT(RELATION:(ARG:0):(ARG:1), 조카조카딸)
		RETURNF 1
ELSE
	SIF CFLAG:(ARG:0):9 >= 200
		RETURNF 0
	IF ARG:1 == CFLAG:(ARG:0):9
		RETURNF 1
	ENDIF
ENDIF
RETURNF 0



;--------------------------------------------------
;연애 판정
;(ARG:0)에게 있어 (ARG:1)번째가 사랑하는 상대라면 1을 반환한다
@LOVER_F, ARG, ARG:1
#FUNCTION
SIF (GETBIT(RELATION:(ARG:0):(ARG:1),짝사랑) || GETBIT(RELATION:(ARG:0):(ARG:1),연인) || GETBIT(RELATION:(ARG:0):(ARG:1),약혼자) || GETBIT(RELATION:(ARG:0):(ARG:1),배우자))
	RETURNF 1
RETURNF 0

