;**********************************************************
;キャラクターメイキング処理
;ARG = 修練Ｐの周回ボーナス値
;----------------------------------------------------------
@CHARA_MAKE_MAIN, ARG = 0
#DIM CCOUNT

;共通設定をグローバルから読んでおく(グローバルに設定するってことは頻繁に使いたいはずなので)
	LOADGLOBAL
	FLAG:5 = GLOBAL:5
	FLAG:6 = GLOBAL:6
	FLAG:7 = GLOBAL:7
	FLAG:820 = GLOBAL:8
	FLAG:821 = GLOBAL:9
	FLAG:822 = GLOBAL:20
	FLAG:823 = GLOBAL:21
	FLAG:824 = GLOBAL:22
	FLAG:825 = GLOBAL:23
	SAVESTR:10 = %GLOBALS:15%
	SAVESTR:11 = %GLOBALS:16%
	SAVESTR:12 = %GLOBALS:17%

;設定選択
VARSET LOCAL
$LOOP_SETTING_START
DRAWLINE


FONTBOLD
SETCOLORBYNAME CYAN
PRINTL ◆デフォルト共通設定
FONTREGULAR
RESETCOLOR
	PRINTFORML [1001]ユニットの主題・・・・・・\@(FLAG:5 == 1) ?『%SAVESTR:10%』#なし\@
	PRINTFORML [1002]変身名（上の句）・・・・・\@(FLAG:6 == 1) ?『%SAVESTR:11%』#なし\@
	PRINTFORML [1003]変身名（下の句）ジャンル・\@(FLAG:820 > 0) ?『%transnamegenre:(FLAG:820-1)%』#なし\@
	PRINTFORML [1004]変身時のかけ声・・・・・・\@(FLAG:7 == 1) ?『%SAVESTR:12%』#なし\@
	PRINTFORML [1005]苗字の言語・・・・・・・・\@(FLAG:821 > 0) ?%namelang:(FLAG:821-1)%#デフォルト\@
	PRINTFORML [1006]名前の言語・・・・・・・・\@(FLAG:822 > 0) ?%namelang:(FLAG:822-1)%#デフォルト\@
	PRINTFORML [1007]種族・・・・・・・・・・・\@(FLAG:823 > 0) ?%racegenre:(FLAG:823-1)%#ランダム\@
	PRINTFORML [1008]フィート自動割り当て・・・\@(FLAG:824 == 1) ?あり#なし\@
	PRINTFORML [1009]性格・・・・・・・・・・・\@(FLAG:825 == 1) ?구상有りのみ#完全ランダム\@

	PRINTFORM [170]グローバルに保存　　　　　　
	PRINTFORM [180]グローバルから読み込み　　　
	PRINTFORML [190]初期化　　　
	DRAWLINE
PRINTL

FONTBOLD
SETCOLORBYNAME CYAN
PRINTL ◆個別キャラメイキング
FONTREGULAR
RESETCOLOR
RESULT = 0
FOR LOCAL, 0, CHARANUM
	IF CALLNAME:LOCAL == "범용 캐릭터"
		LOCALS = キャラプロフィール未設定　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　
		PRINTFORM [{LOCAL}]%LOCALS, 90, LEFT%
		PRINTPLAIN 　
		PRINTFORM ♂[{LOCAL + 100}]
		PRINTL 
	ELSE
		SIF LOCAL == MASTER
			CONTINUE
		LOCALS = %CALLNAME:LOCAL%
		PRINTFORM [{LOCAL}]%LOCALS, 20, LEFT%
		PRINTPLAIN 　
		IF ISMALE(LOCAL)
			PRINT 性別：♂ / 
		ELSE
			PRINT 性別：♀ / 
		ENDIF
		CALL SYUZOKU_CHECK, "STRING", LOCAL
		LOCALS = 種族：%RESULTS%
		PRINTFORM %LOCALS, 16, LEFT% / 
		CALL SEIKAKU_CHECK, "STRING", LOCAL
		LOCALS = 性格：%RESULTS%
		PRINTFORM %LOCALS, 16, LEFT% / 
		LOCALS = 
		IF TALENT:LOCAL:변신능력 == -1
			LOCALS += "非戦闘員"
		ELSEIF TALENT:LOCAL:변신능력 == 0
			LOCALS += "変身なし"
		ELSE
			LOCALS += "変身あり"
		ENDIF
		IF CFLAG:LOCAL:0 == 1
			LOCALS += " / 幽閉"
		ELSEIF CFLAG:LOCAL:0 == 2
			LOCALS += " / 洗脳"
		ELSEIF CFLAG:LOCAL:0 == 3
			LOCALS += " / 悪堕ち"
		ELSEIF CFLAG:LOCAL:0 == 9
			LOCALS += " / 故人"
		ENDIF
		PRINTFORM %LOCALS, 20, LEFT%
		PRINTL 
	ENDIF
NEXT

PRINTL
;プロフィール未設定人数の確認
RESULT=0
FOR CCOUNT, 0, CHARANUM
	SIF CALLNAME:CCOUNT == "범용 캐릭터"
		RESULT ++
NEXT

	IF !GAME_OPTION_CHECK_F(OPTION_solo)
		PRINTL [200]プリセットを読み込む　　　　
	ENDIF

	SIF (GLOBAL:100 + GLOBAL:101 + GLOBAL:102) > 0 && !GAME_OPTION_CHECK_F(OPTION_solo)
		PRINTL [300]人数を変更する
PRINTL
PRINTL
PRINTL
PRINTFORM [1000]★キャラメイクを完了する%\@ RESULT ? （未設定のキャラはおまかせ） #  \@,32,LEFT%
PRINTFORML [999]モード選択に戻る


$INPUT_LOOP_0
INPUT
IF RESULT > 100 && RESULT < CHARANUM + 100 && CALLNAME:(RESULT - 100) == "범용 캐릭터"
	TALENT:(RESULT - 100):남자 = 1
	NAME:(RESULT - 100) = 범용 캐릭터(♂)
	CALL FIRSTSETTING_CHARA_MAIN, (RESULT - 100), ARG
	GOTO LOOP_SETTING_START
ELSEIF RESULT > 0 && RESULT < CHARANUM
	CALL FIRSTSETTING_CHARA_MAIN, RESULT, ARG
	GOTO LOOP_SETTING_START

;決定
ELSEIF RESULT == 1000
	PRINTL キャラメイクを終了します
	;キャラメイク完了処理(全員分まとめて)
	CALL CHARA_MAKE_FINALIZE
ELSEIF RESULT == 999
	RETURN -1

ELSEIF RESULT == 1001
	CALL FIRSTSETTING_TITLE
	GOTO LOOP_SETTING_START
ELSEIF RESULT == 1002
	CALL FIRSTSETTING_CROWNNAME
	GOTO LOOP_SETTING_START
ELSEIF RESULT == 1003
	CALL FIRSTSETTING_transnamegenre
	GOTO LOOP_SETTING_START
ELSEIF RESULT == 1004
	CALL FIRSTSETTING_TRANSCALL
	GOTO LOOP_SETTING_START
ELSEIF RESULT == 1005
	CALL FIRSTSETTING_familynamelang
	GOTO LOOP_SETTING_START
ELSEIF RESULT == 1006
	CALL FIRSTSETTING_namelang
	GOTO LOOP_SETTING_START
ELSEIF RESULT == 1007
	CALL FIRSTSETTING_racegenre
	GOTO LOOP_SETTING_START
ELSEIF RESULT == 1008
	FLAG:824=!FLAG:824
	GOTO LOOP_SETTING_START
ELSEIF RESULT == 1009
	FLAG:825=!FLAG:825
	GOTO LOOP_SETTING_START
ELSEIF RESULT == 170
	PRINTFORML 現在保存されているグローバル
	PRINTFORML 　主題・・・・\@(GLOBAL:5 == 1) ?あり 『%GLOBALS:15%』#なし\@
	PRINTFORML 　冠名・・・・\@(GLOBAL:6 == 1) ?あり 『%GLOBALS:16%』#なし\@
	PRINTFORML 　変身名・・・\@(GLOBAL:8 > 0) ?あり 『%transnamegenre:(GLOBAL:8-1)%』#なし\@
	PRINTFORML 　かけ声・・・\@(GLOBAL:7 == 1) ?あり 『%GLOBALS:17%』#なし\@
	PRINTFORML 　苗字言語・・\@(GLOBAL:9 > 0) ?あり 『%namelang:(GLOBAL:9-1)%』#なし\@
	PRINTFORML 　名前言語・・\@(GLOBAL:20 > 0) ?あり 『%namelang:(GLOBAL:20-1)%』#なし\@
	PRINTFORML 　種族・・・・\@(GLOBAL:21 > 0) ?あり 『%racegenre:(GLOBAL:21-1)%』#なし\@
	PRINTFORML 　フィート・・\@(GLOBAL:22 > 0) ?あり#なし\@
	PRINTFORML 　性格・・\@(GLOBAL:23 > 0) ?구상有りのみ#完全ランダム\@
	PRINTL 
	PRINTL 【注意】共通設定のグローバルに上書きします。
	PRINTL         元の状態に復元はできませんが、よろしいですか？
	PRINTL     [0]上書き実行      [99]キャンセルして戻る
	PRINTL 
	$INPUT_LOOP_3_1
	INPUT
	IF RESULT == 0
		GLOBAL:5 = FLAG:5
		GLOBAL:6 = FLAG:6
		GLOBAL:7 = FLAG:7
		GLOBAL:8 = FLAG:820
		GLOBAL:9 = FLAG:821
		GLOBAL:20 = FLAG:822
		GLOBAL:21 = FLAG:823
		GLOBAL:22 = FLAG:824
		GLOBAL:23 = FLAG:825
		GLOBALS:15 = %SAVESTR:10%
		GLOBALS:16 = %SAVESTR:11%
		GLOBALS:17 = %SAVESTR:12%
		SAVEGLOBAL
		PRINTW グローバルとして保存しました。
		GOTO LOOP_SETTING_START
	ELSEIF RESULT == 99
		;PRINTW 上書きをキャンセルしました。
		GOTO LOOP_SETTING_START
	ELSE
		GOTO INPUT_LOOP_3_1
	ENDIF
ELSEIF RESULT == 180
	LOADGLOBAL
	FLAG:5 = GLOBAL:5
	FLAG:6 = GLOBAL:6
	FLAG:7 = GLOBAL:7
	FLAG:820 = GLOBAL:8
	FLAG:821 = GLOBAL:9
	FLAG:822 = GLOBAL:20
	FLAG:823 = GLOBAL:21
	FLAG:824 = GLOBAL:22
	FLAG:825 = GLOBAL:23
	SAVESTR:10 = %GLOBALS:15%
	SAVESTR:11 = %GLOBALS:16%
	SAVESTR:12 = %GLOBALS:17%
	;↓見れば分かるのでコメントアウト
	;PRINTW グローバルから共通設定を読み込みました。
	GOTO LOOP_SETTING_START
ELSEIF RESULT == 190
	FLAG:5 = 0
	FLAG:6 = 0
	FLAG:7 = 0
	FLAG:820 = 0
	FLAG:821 = 0
	FLAG:822 = 0
	FLAG:823 = 0
	FLAG:824 = 0
	FLAG:825 = 0
	SAVESTR:10 = 
	SAVESTR:11 = 
	SAVESTR:12 = 
	;↓見れば分かるのでコメントアウト
	;PRINTW 共通設定を初期化しました。
	GOTO LOOP_SETTING_START

;初期セット読み込み
ELSEIF RESULT == 200 && !GAME_OPTION_CHECK_F(OPTION_solo)
	LOCAL=LINECOUNT
	TRYCALL CHARA_MAKE_FINALIZE_KAI
	IF RESULT < 0
		CLEARLINE LINECOUNT - LINECOUNT
	ENDIF
	GOTO LOOP_SETTING_START
ELSEIF RESULT==300 && (GLOBAL:100 + GLOBAL:101 + GLOBAL:102) > 0 && !GAME_OPTION_CHECK_F(OPTION_solo)
		PRINTL 
		PRINTFORML 周回クリアボーナス：
		PRINTFORML ゲーム開始時の人数を増減できます
		PRINTFORML [ 2] ２人で開始
		PRINTFORML [ 3] ３人で開始(デフォルト)
		PRINTFORML [ 4] ４人で開始
		PRINTFORML [ 5] ５人で開始
		PRINTFORML [ 6] ６人で開始
		PRINTL 
		PRINTFORML [99]変更せずに戻る
		$INPUT_LOOP_CHARA_NUM
		INPUT
		IF RESULT >= 2 && RESULT <= 6
			LOCAL = RESULT
			PRINTFORML {LOCAL}人で開始します
			IF CHARANUM-1<LOCAL
				REPEAT LOCAL-(CHARANUM-1)
					ADDCHARA 0
					FLAG:8 ++
				REND
			ELSEIF LOCAL<CHARANUM-1
				REPEAT (CHARANUM-1)-LOCAL
					DELCHARA CHARANUM-1
					FLAG:8 --
				REND
			ENDIF
			GOTO LOOP_SETTING_START
		ELSEIF RESULT == 99
			GOTO LOOP_SETTING_START
		ELSE
			GOTO INPUT_LOOP_CHARA_NUM
		ENDIF
ELSE
	GOTO INPUT_LOOP_0
ENDIF
