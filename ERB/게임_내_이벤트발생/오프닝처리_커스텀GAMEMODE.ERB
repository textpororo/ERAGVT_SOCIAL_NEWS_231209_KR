;게임 모드별 옵션
;자료도 겸하고 있으므로 게임 모드와 관련된 옵션을 늘렸을 경우 여기에도 기입 부탁드립니다
;※세트에서 사용되는 정수에 대해서는 개발자용 자료\플래그 목록과 DIM.ERH 참조

@GAME_OPTION_CUSTUM
$MASTER_LOOP
PRINTL 　☆커스텀 모드☆
PRINTL 　☆ 옵션 선택 ☆
PRINTFORML [0] 난이도:Easy 　　　　　　　　　　【\@GETBIT(FLAG:0,0)?○#×\@】
PRINTFORML [1] 난이도:Normal 　　　　　　　　　【\@GETBIT(FLAG:0,1)?○#×\@】
PRINTFORML [2] 난이도:Hardcore 　　　　　　　　【\@GETBIT(FLAG:0,2)?○#×\@】
PRINTFORML [3] 1인 플레이　　　　　　　　　　　【\@GETBIT(FLAG:0,3)?○#×\@】
;SURVIVAL～SANDBOX에 해당
PRINTFORML [4] Endless 모드　　　　　　　　　　【\@GETBIT(FLAG:0,4)?○#×\@】
PRINTFORML [5] 주회 인계 불가능　　　　　　　　【\@GETBIT(FLAG:0,5)?○#×\@】
PRINTFORML [6] 보스 격파 시간 제한（기한）없음 【\@GETBIT(FLAG:0,6)?○#×\@】
PRINTFORML [7] 실적 취득 불가・엔딩 없음 　　　【\@GETBIT(FLAG:0,7)?○#×\@】
PRINTFORML [8] 게임 오버 없음　　　　　　　　　【\@GETBIT(FLAG:0,8)?○#×\@】
;INSTANT에 해당
PRINTFORML [9] 플레이어 신규 작성 및 은퇴 가능 【\@GETBIT(FLAG:0,9)?○#×\@】
PRINTFORML [10]적의 공격으로 기초 스탯 감소　　【\@GETBIT(FLAG:0,10)?○#×\@】
PRINTL 
SETCOLOR 105,105,105
FOR COUNT, 0, 모드카운트
	IF FLAG:0 == 모드옵션:COUNT
		PRINTPLAINFORM [{100+COUNT}] %모드네임:COUNT% 모드
	ELSE
		PRINTFORMD [{100+COUNT}] %모드네임:COUNT% 모드
	ENDIF
	PRINTL 
NEXT
RESETCOLOR
PRINTL 
PRINT FLAG:0 = 0b
FOR COUNT, 0, 옵션카운트
	PRINTFORM {GETBIT(FLAG:0,옵션카운트-COUNT-1)}
NEXT
PRINTL 
PRINTFORML FLAG:0 = {FLAG:0}
PRINTL 
CALL SET_LIMIT_DAY
PRINTFORML 1 보스별 제한 일수：{FLAG:2}
PRINTL 
PRINTL [1000] 설정 종료
PRINTL 
$INPUT_LOOP
INPUT
IF INRANGE(RESULT, 0, 옵션카운트-1)
	INVERTBIT FLAG:0, RESULT
	;난이도가 여러 개 설정되었을 시
	IF (GETBIT(FLAG:0, OPTION_easy) + GETBIT(FLAG:0, OPTION_normal) + GETBIT(FLAG:0, OPTION_hardcore)) > 1
		CLEARBIT FLAG:0, OPTION_easy
		CLEARBIT FLAG:0, OPTION_normal
		CLEARBIT FLAG:0, OPTION_hardcore
		SETBIT FLAG:0, RESULT
	;솔로와 가입 은퇴가 동시에 설정되었을 시
	ELSEIF (GETBIT(FLAG:0, OPTION_solo) + GETBIT(FLAG:0, OPTION_가입은퇴있음)) > 1
		CLEARBIT FLAG:0, OPTION_solo
		CLEARBIT FLAG:0, OPTION_가입은퇴있음
		SETBIT FLAG:0, RESULT
	ENDIF
	GOTO MASTER_LOOP
ELSEIF INRANGE(RESULT, 100, 99+모드카운트)
	FLAG:0 = 모드옵션:(RESULT-100)
	GOTO MASTER_LOOP
ELSEIF RESULT == 1000
	IF FLAG:0 == 0
		PRINTW GAMEOVER 모드로 시작할 수 없습니다.
		CLEARLINE 2
		GOTO INPUT_LOOP
	ENDIF
	RETURN
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF


@GAME_OPTION_SET(ARG:0,ARG:1=-1,ARG:2=-1,ARG:3=-1,ARG:4=-1,ARG:5=-1,ARG:6=-1,ARG:7=-1,ARG:8=-1,ARG:9=-1)
FOR COUNT, 0, VARSIZE("ARG")
	SIF ARG:COUNT == -1
		CONTINUE
	SETBIT FLAG:0,ARG:COUNT
NEXT
RETURN


;옵션이 지정되어 있는지 확인하는 함수
@GAME_OPTION_CHECK_F(ARG)
#FUNCTION
RETURNF GETBIT(FLAG:0, ARG)

@GAME_OPTION_CHECK_MULTI_F(ARG:0,ARG:1=-1,ARG:2=-1,ARG:3=-1,ARG:4=-1,ARG:5=-1,ARG:6=-1,ARG:7=-1,ARG:8=-1,ARG:9=-1)
#FUNCTION
#DIM CO
CO = 0
LOCAL = 0,0,0,0,0,0,0,0,0,0
;ARG에서 지정된 옵션과 FLAG:0 옵션이 일치하는지
FOR COUNT, 0, VARSIZE("ARG")
	SIF ARG:COUNT == -1
		CONTINUE
	LOCAL:CO = GETBIT(FLAG:0, ARG:COUNT)
	CO ++
NEXT
FINDELEMENT LOCAL, 1, 0, CO
SIF RESULT == -1
	RETURNF 0
RETURNF 1


;GAMEOVER 모드로 들어간다
@CHANGE_GAMEOVER_MODE
FLAG:0 = 0
DAY:2 = DAY*2+TIME
;게임 오버 모드에 들어가 있으면 1, 아니라면 0
@CHECK_GAMEOVER_F()
#FUNCTION
	RETURNF GAME_MODE_CHECK_F() == MODE_GAMEOVER ? 1 # 0

;현재 모드를 반환하는 함수
;커스텀 모드는 -1을 반환한다
;※판정의 대부분은 게임 오버 모드이므로, 거의 사용하지 않는다고 생각하면 된다
;　2023년 10월 현재 클리어 실적 잠금 해제에만 사용되고 있다
@GAME_MODE_CHECK
FOR COUNT, 0, 모드카운트
	;비트의 일치하는 모드를 찾는다　주회 요소로 시간 제한을 해제했다면 ↓ 아래 사항을 고려
	SIF FLAG:0 == (모드옵션:COUNT | (FLAG:906 ? 64 # 0))
		RETURN COUNT
NEXT
RETURN -1
@GAME_MODE_CHECK_F
#FUNCTION
FOR COUNT, 0, 모드카운트
	SIF FLAG:0 == 모드옵션:COUNT
		RETURNF COUNT
NEXT
RETURNF -1


@BOSS_LIMIT_TIME_CUSTUM
PRINTL ☆보스 1마리당 제한 일수 (1~20)
PRINTL 보스 격파 시에 추가될 제한 일수를 설정할 수 있습니다.
PRINTL 　※참고
PRINTL 　13일：HARDCORE 모드
PRINTL 　12일：SOLO 모드
PRINTL 　11일：NORMAL 모드, INSTANT 모드 (기준치)
PRINTL 　 9일：SANDBOX 모드, FREEPLAY 모드, SURVIVAL 모드
$INPUT_LOOP
INPUT
IF INRANGE(RESULT, 1, 20)
	FLAG:2 = RESULT
	PRINTFORMW {FLAG:2}일로 설정했습니다.
ELSE
	GOTO INPUT_LOOP
ENDIF
