;**********************************************************
;기본 규칙
;가필 수정은, 자유롭게 해주세요.
;
;프로그램의 가독성을 높이기 위한 시도입니다.
;IF구문과 시간차가 신경 쓰이지만, 가독성을 위하여 SELECTCASE구문으로 했습니다.
;무거운 처리를 하나 끼우기 때문에, 잘 활용해주세요.
;
;
;by 雑魚の人 2018/02/11
;**********************************************************
;속어의 자동 전환 및 호출
@WRITTENSLANG(ARGS:0)
#DIMS WRITTENSLANG_WORD
#FUNCTIONS

;초기화
WRITTENSLANG_WORD =

SELECTCASE ARGS:0
	CASE "애널" , "항문" , "アナル" , "肛門"
		WRITTENSLANG_WORD = %SLANG_A(TARGET)%

	;おっぱい와 胸을 두고 찌찌를 쓸지 고민했는데 그냥 젖가슴 가슴으로 하기로 했다
	CASE "젖가슴" , "가슴" , "바스트" , "おっぱい" , "胸" , "バスト"
		WRITTENSLANG_WORD = %SLANG_B(TARGET)%

	CASE "클리토리스" , "음핵" , "クリトリス" , "陰核"
		WRITTENSLANG_WORD = %SLANG_C(TARGET)%

	CASE "힙" , "엉덩이" , "ヒップ" , "お尻"
		WRITTENSLANG_WORD = %SLANG_H(TARGET)%

	CASE "애액" , "러브 주스" , "질 분비액" , "愛液" , "ラブジュース" , "膣分泌液"
		WRITTENSLANG_WORD = %SLANG_Juice(TARGET,"애액")%
	CASE "장액" , "직장액" , "항문액" , "腸液" , "直腸液" , "肛門液"
		WRITTENSLANG_WORD = %SLANG_Juice(TARGET,"장액")%

	CASE "라비아" , "음순" , "ラビア" , "陰唇"
		WRITTENSLANG_WORD = %SLANG_Labia(TARGET)%

	CASE "균열" , "대음순" , "ワレメ" , "大陰唇"
		WRITTENSLANG_WORD = %SLANG_LabiaMajora(TARGET)%

	;ビラビラ가 여성의 음순을 뜻하는 속어라고 하는데, 우리말에서 마땅한 게 안 떠올랐다
	CASE "비라비라" , "소음순" , "ビラビラ" , "小陰唇"
		WRITTENSLANG_WORD = %SLANG_LabiaMinora(TARGET)%

	CASE "마우스" , "입" , "マウス" , "口"
		WRITTENSLANG_WORD = %SLANG_M(TARGET)%

	;원래 젖꼭지만 있었는데 유두를 따로 추가해 봤다
	CASE "니플" , "젖꼭지" , "유두" , "ニップル" , "乳首"
		WRITTENSLANG_WORD = %SLANG_N(TARGET)%

	;음경이라는 말도 따로 추가해 봤다
	CASE "페니스" , "남근" , "자지" , "음경" , "ペニス" , "男根" , "チンコ"
		WRITTENSLANG_WORD = %SLANG_P(TARGET)%

	;일본어로는 ザーメン이라 하기도 해서 자멘 시멘 둘 다 추가했다
	CASE "자멘" , "시멘" , "정액" , "ザーメン" , "精液"
		WRITTENSLANG_WORD = %SLANG_Semen(TARGET)%

	CASE "불알" , "고환" , "테스티클" , "金玉" , "睾丸" , "テスティクル"
		WRITTENSLANG_WORD = %SLANG_Testicle(TARGET)%

	CASE "요도" , "방광" , "尿道" , "膀胱"
		WRITTENSLANG_WORD = %SLANG_U(TARGET)%

	CASE "바기나" , "질" , "ヴァギナ" , "膣"
		WRITTENSLANG_WORD = %SLANG_V(TARGET)%

	;아기방은 그냥 추가해 보고 싶었다
	CASE "자궁" , "아기방" , "子宮"
		WRITTENSLANG_WORD = %SLANG_W(TARGET)%

	;해당 없음으로, 그대로 부위 이름만 반환한다
	CASEELSE
		WRITTENSLANG_WORD = %ARGS:0%
ENDSELECT

RETURNF WRITTENSLANG_WORD





