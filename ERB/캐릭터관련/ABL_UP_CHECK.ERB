;--------------------------------------------------
;능력 상승
;본가era와 달리, 자동으로 능력이 상승하기 때문에 BEGIN ABLUP은 사용하지 않았습니다.
;ARG=1이라면 유폐에서 불려와, PALAM을 JUEL로 변환하는 처리를 하지 않는다
;ARG=2라면 전투 후 처리에서 불려와, JUEL 변환만 하고 능력 상승 처리를 스킵한다
@_ABLUP, ARG
VARSET LOCAL
;구슬 취득, 쾌부위의 절정에 관한 처리는 COMMON_PALAM_CAL에서 함께 계산 완료
LOCAL:0 = 0
LOCAL:1 = 0
IF ARG == 0 || ARG == 2
	SETCOLOR 105,105,105
		SIF FLAG:999 == 1
			PRINTFORML  * DEGUG　구슬의 입수 *
	REPEAT 12
		LOCAL:0 = COUNT
		SIF COUNT > 3
			LOCAL:0 += 6
		IF PALAM:(LOCAL:0) < 100
			LOCAL:1 = 0
		ELSEIF PALAM:(LOCAL:0) < 300
			LOCAL:1 = 1
		ELSEIF PALAM:(LOCAL:0) < 600
			LOCAL:1 = 2
		ELSEIF PALAM:(LOCAL:0) < 1500
			LOCAL:1 = 10
		ELSEIF PALAM:(LOCAL:0) < 3000
			LOCAL:1 = 20
		ELSEIF PALAM:(LOCAL:0) < 6000
			LOCAL:1 = 100
		ELSEIF PALAM:(LOCAL:0) < 10000
			LOCAL:1 = 200
		ELSEIF PALAM:(LOCAL:0) < 30000
			LOCAL:1 = 1000
		ELSEIF PALAM:(LOCAL:0) < 60000
			LOCAL:1 = 2000
		ELSEIF PALAM:(LOCAL:0) < 150000
			LOCAL:1 = 10000
		ELSEIF PALAM:(LOCAL:0) < 300000
			LOCAL:1 = 20000
		ELSE
			LOCAL:1 = 100000
		ENDIF

		;FEAT 효과에 의한 구슬 입수량 증가
		SIF TALENT:배덕의낙인 > 0
			LOCAL:1 *= 135 / 100

		JUEL:(LOCAL:0) += LOCAL:1

		SIF FLAG:999 == 1
			PRINTFORML 　　취득한 %PALAMNAME:(LOCAL:0)%의 구슬 : {LOCAL:1}

		;구슬의 취득량 상한
		IF JUEL:(LOCAL:0) >= 999999 && LOCAL:0 >= 감각수
			JUEL:(LOCAL:0) = 999999
		ELSEIF JUEL:(LOCAL:0) >= 99999999
			JUEL:(LOCAL:0) = 99999999
		ENDIF
	REND
	RESETCOLOR
ENDIF
SIF FLAG:999 == 1
	PRINTL 
SIF ARG == 2
	RETURN

;능력의 상승
;상승 조건이 까다로운 것부터 우선적으로 구슬을 소비한다
;처음에는 조건을 충족하지 못하기 때문에 오르지 않는다

;촉수중독 상승
LOCAL:0 = 0
CALL ABL_UP_20
LOCAL:0 = RESULT

;자위중독 상승
LOCAL:12 = 0
CALL ABL_UP_21
LOCAL:12 = RESULT

;정액중독 상승
LOCAL:1 = 0
CALL ABL_UP_22
LOCAL:1 = RESULT

;사정중독 상승
LOCAL:13 = 0
CALL ABL_UP_24
LOCAL:13 = RESULT

;모유중독 상승
LOCAL:14 = 0
CALL ABL_UP_23
LOCAL:14 = RESULT

;마조끼 상승
LOCAL:2 = 0
CALL ABL_UP_15
LOCAL:2 = RESULT

;봉사정신 상승
LOCAL:3 = 0
CALL ABL_UP_13
LOCAL:3 = RESULT

;노출벽 상승
LOCAL:4 = 0
CALL ABL_UP_14
LOCAL:4 = RESULT

;순종 상승
LOCAL:5 = 0
CALL ABL_UP_10
LOCAL:5 = RESULT

;욕망 상승
LOCAL:6 = 0
CALL ABL_UP_11
LOCAL:6 = RESULT

;기교 상승
LOCAL:7 = 0
CALL ABL_UP_12
LOCAL:7 = RESULT

;C감각 상승
LOCAL:8 = 0
CALL ABL_UP_0
LOCAL:8 = RESULT

;V감각 상승
LOCAL:9 = 0
CALL ABL_UP_1
LOCAL:9 = RESULT

;A감각 상승
LOCAL:10 = 0
CALL ABL_UP_2
LOCAL:10 = RESULT

;B감각 상승
LOCAL:11 = 0
CALL ABL_UP_3
LOCAL:11 = RESULT

;능력 상승과 표시
;표시는 ABL.CSV 순이므로 ベタ書き, 마지막은 뭔 말인지 ㅁ?ㄹ
;C감각
IF ABL:C감각 < LOCAL:8
	ABL:C감각 = LOCAL:8
	PRINTFORML %ABLNAME:0%이 %조사처리(TOSTR(LOCAL:8),"로")% 올랐다.
	;뒷 프로필(裏プロフィール) 취득
	IF CONFIG_CHECK_OTHER_F(6) == 1
		IF ISPENIS()
			CSTR:45 = %MAKESEXUALPROFILE("P", LOCAL:8)%
		ELSE
			CSTR:45 = %MAKESEXUALPROFILE("C", LOCAL:8)%
		ENDIF
		PRINTFORM 성벽：【
		SETCOLOR 255, 255 - 23 * LIMIT(ABL:0, 0, 10), 255 - 11 * LIMIT(ABL:0, 0, 10), 
		PRINTFORM %CSTR:45%
		RESETCOLOR
		PRINTFORML 】%조사만처리(CSTR:45,"를")% 얻었다.
	ENDIF
ENDIF
;V감각
IF ABL:V감각 < LOCAL:9
	ABL:V감각 = LOCAL:9
	PRINTFORML %ABLNAME:1%이 %조사처리(TOSTR(LOCAL:9),"로")% 올랐다.
	;뒷 프로필(裏プロフィール) 취득
	IF CONFIG_CHECK_OTHER_F(6) == 1
		CSTR:46 = %MAKESEXUALPROFILE("V", LOCAL:9)%
		PRINTFORM 성벽：【
		SETCOLOR 255, 255 - 23 * LIMIT(ABL:1, 0, 10), 255 - 11 * LIMIT(ABL:1, 0, 10), 
		PRINTFORM %CSTR:46%
		RESETCOLOR
		PRINTFORML 】%조사만처리(CSTR:46,"를")% 얻었다.
	ENDIF
ENDIF
;A감각
IF ABL:A감각 < LOCAL:10
	ABL:A감각 = LOCAL:10
	PRINTFORML %ABLNAME:2%이 %조사처리(TOSTR(LOCAL:10),"로")% 올랐다.
	;뒷 프로필(裏プロフィール) 취득
	IF CONFIG_CHECK_OTHER_F(6) == 1
		CSTR:47 = %MAKESEXUALPROFILE("A", LOCAL:10)%
		PRINTFORM 성벽：【
		SETCOLOR 255, 255 - 23 * LIMIT(ABL:2, 0, 10), 255 - 11 * LIMIT(ABL:2, 0, 10), 
		PRINTFORM %CSTR:47%
		RESETCOLOR
		PRINTFORML 】%조사만처리(CSTR:47,"를")% 얻었다.
	ENDIF
ENDIF
;B감각
IF ABL:B감각 < LOCAL:11
	ABL:B감각 = LOCAL:11
	PRINTFORML %ABLNAME:3%이 %조사처리(TOSTR(LOCAL:11),"로")% 올랐다.
	;뒷 프로필(裏プロフィール) 취득
	IF CONFIG_CHECK_OTHER_F(6) == 1
		CSTR:48 = %MAKESEXUALPROFILE("B", LOCAL:11)%
		PRINTFORM 성벽：【
		SETCOLOR 255, 255 - 23 * LIMIT(ABL:3, 0, 10), 255 - 11 * LIMIT(ABL:3, 0, 10), 
		PRINTFORM %CSTR:48%
		RESETCOLOR
		PRINTFORML 】%조사만처리(CSTR:48,"를")% 얻었다.
	ENDIF
ENDIF
;순종
IF ABL:순종 < LOCAL:5
	ABL:순종 = LOCAL:5
	PRINTFORML %ABLNAME:10%이 %조사처리(TOSTR(LOCAL:5),"로")% 올랐다.
ENDIF
;욕망
IF ABL:욕망 < LOCAL:6
	ABL:욕망 = LOCAL:6
	PRINTFORML %ABLNAME:11%이 %조사처리(TOSTR(LOCAL:6),"로")% 올랐다.
ENDIF
;기교
IF ABL:기교 < LOCAL:7
	ABL:기교 = LOCAL:7
	PRINTFORML %ABLNAME:12%가 %조사처리(TOSTR(LOCAL:7),"로")% 올랐다.
ENDIF
;봉사정신
IF ABL:봉사정신 < LOCAL:3
	ABL:봉사정신 = LOCAL:3
	PRINTFORML %ABLNAME:13%이 %조사처리(TOSTR(LOCAL:3),"로")% 올랐다.
ENDIF
;노출벽
IF ABL:노출벽 < LOCAL:4
	ABL:노출벽 = LOCAL:4
	PRINTFORML %ABLNAME:14%이 %조사처리(TOSTR(LOCAL:4),"로")% 올랐다.
ENDIF
;마조끼
IF ABL:마조끼 < LOCAL:2
	ABL:마조끼 = LOCAL:2
	PRINTFORML %ABLNAME:15%가 %조사처리(TOSTR(LOCAL:2),"로")% 올랐다.
ENDIF
;촉수중독
IF ABL:촉수중독 < LOCAL:0
	ABL:촉수중독 = LOCAL:0
	PRINTFORML %ABLNAME:20%이 %조사처리(TOSTR(LOCAL:0),"로")% 올랐다.
ENDIF
;자위중독
IF ABL:자위중독 < LOCAL:12
	ABL:자위중독 = LOCAL:12
	PRINTFORML %ABLNAME:21%이 %조사처리(TOSTR(LOCAL:12),"로")% 올랐다.
ENDIF
;정액중독
IF ABL:정액중독 < LOCAL:1
	ABL:정액중독 = LOCAL:1
	PRINTFORML %ABLNAME:22%이 %조사처리(TOSTR(LOCAL:1),"로")% 올랐다.
ENDIF
;모유중독
IF ABL:모유중독 < LOCAL:14
	ABL:모유중독 = LOCAL:14
	PRINTFORML %ABLNAME:23%이 %조사처리(TOSTR(LOCAL:14),"로")% 올랐다.
ENDIF
;사정중독
IF ABL:사정중독 < LOCAL:13
	ABL:사정중독 = LOCAL:13
	PRINTFORML %ABLNAME:24%이 %조사처리(TOSTR(LOCAL:13),"로")% 올랐다.
ENDIF

;각 감각
CALL ABL_UP_EX
LOCAL:8 = RESULT:0
LOCAL:9 = RESULT:1
LOCAL:10 = RESULT:2
LOCAL:11 = RESULT:3

;C감각
IF ABL:C감각 < LOCAL:8
	ABL:C감각 = LOCAL:8
	PRINTFORML %ABLNAME:0%이 %조사처리(TOSTR(LOCAL:8),"로")% 올랐다.
	;뒷 프로필(裏プロフィール) 취득
	IF CONFIG_CHECK_OTHER_F(6) == 1
		IF ISPENIS()
			CSTR:45 = %MAKESEXUALPROFILE("P", LOCAL:8)%
		ELSE
			CSTR:45 = %MAKESEXUALPROFILE("C", LOCAL:8)%
		ENDIF
		PRINTFORM 성벽：【
		SETCOLOR 255, 255 - 23 * LIMIT(ABL:0, 0, 10), 255 - 11 * LIMIT(ABL:0, 0, 10), 
		PRINTFORM %CSTR:45%
		RESETCOLOR
		PRINTFORML 】%조사만처리(CSTR:45,"를")% 얻었다.
	ENDIF
ENDIF
;V감각
IF ABL:V감각 < LOCAL:9
	ABL:V감각 = LOCAL:9
	PRINTFORML %ABLNAME:1%이 %조사처리(TOSTR(LOCAL:9),"로")% 올랐다.
	;뒷 프로필(裏プロフィール) 취득
	IF CONFIG_CHECK_OTHER_F(6) == 1
		CSTR:46 = %MAKESEXUALPROFILE("V", LOCAL:9)%
		PRINTFORM 성벽：【
		SETCOLOR 255, 255 - 23 * LIMIT(ABL:1, 0, 10), 255 - 11 * LIMIT(ABL:1, 0, 10), 
		PRINTFORM %CSTR:46%
		RESETCOLOR
		PRINTFORML 】%조사만처리(CSTR:46,"를")% 얻었다.
	ENDIF
ENDIF
;A감각
IF ABL:A감각 < LOCAL:10
	ABL:A감각 = LOCAL:10
	PRINTFORML %ABLNAME:2%이 %조사처리(TOSTR(LOCAL:10),"로")% 올랐다.
	;뒷 프로필(裏プロフィール) 취득
	IF CONFIG_CHECK_OTHER_F(6) == 1
		CSTR:47 = %MAKESEXUALPROFILE("A", LOCAL:10)%
		PRINTFORM 성벽：【
		SETCOLOR 255, 255 - 23 * LIMIT(ABL:2, 0, 10), 255 - 11 * LIMIT(ABL:2, 0, 10), 
		PRINTFORM %CSTR:47%
		RESETCOLOR
		PRINTFORML 】%조사만처리(CSTR:47,"를")% 얻었다.
	ENDIF
ENDIF
;B감각
IF ABL:B감각 < LOCAL:11
	ABL:B감각 = LOCAL:11
	PRINTFORML %ABLNAME:3%이 %조사처리(TOSTR(LOCAL:11),"로")% 올랐다.
	;뒷 프로필(裏プロフィール) 취득
	IF CONFIG_CHECK_OTHER_F(6) == 1
		CSTR:48 = %MAKESEXUALPROFILE("B", LOCAL:11)%
		PRINTFORM 성벽：【
		SETCOLOR 255, 255 - 23 * LIMIT(ABL:3, 0, 10), 255 - 11 * LIMIT(ABL:3, 0, 10), 
		PRINTFORM %CSTR:48%
		RESETCOLOR
		PRINTFORML 】%조사만처리(CSTR:48,"를")% 얻었다.
	ENDIF
ENDIF

;상태계 소질(음란 등) 취득
;음란 또는 희롱특화체질
;촉수중독 이외의 중독의 합계 Lv6 이상인 동시에 욕망Lv4 이상, 쾌락각인 4 이상, 절정경험 100 이상, 이상경험 1 이상
IF (ABL:자위중독 + ABL:정액중독 + ABL:모유중독 + ABL:사정중독) >= 6 && ABL:욕망 >= 4 && MARK:쾌락각인 >= 4 && EXP:절정경험 >= 100 && EXP:이상경험 >= 1
	IF TALENT:초심 > 0 && TALENT:희롱특화체질 == 0
		TALENT:희롱특화체질 = 1
		;지문: 희롱특화체질 취득 예정지
		CALL MESSAGE_GETTALENT_NABURARE
	ELSEIF TALENT:초심 == 0 && TALENT:음란 == 0
		TALENT:음란 = 1
		;지문: 음란 취득
		CALL MESSAGE_GETTALENT_INRAN
	ENDIF
ENDIF
;희롱특화체질은 강간 횟수 5회 이상 또는 감금으로도 취득
IF (CFLAG:70 > 0 || ((CFLAG:286 + CFLAG:320 + CFLAG:321) > 4)) && TALENT:희롱특화체질 == 0
	TALENT:희롱특화체질 = 1
	;지문: 희롱특화체질 취득 예정지
	CALL MESSAGE_GETTALENT_NABURARE
ENDIF
;촉수의포로
;조건 １：순종과 봉사정신의 합계가 Lv6 이상인 동시에 촉수중독 Lv4 이상, 굴복각인 4 이상, (공포각인 + 고통각인 + 치욕각인) 합계가 6 이상
IF TALENT:촉수의포로 == 0 && (ABL:순종 + ABL:봉사정신) >= 7 && ABL:촉수중독 >= 4 && MARK:굴복각인 >= 4 && (MARK:공포각인 + MARK:고통각인 + MARK:치욕각인) >= 6
	TALENT:촉수의포로 = 1
	;지문: 촉수의포로 취득
	CALL MESSAGE_GETTALENT_TORIKO
;조건 ２：음란을 보유한 동시에 촉수중독 Lv3 이상, 유폐경험 1 이상
ELSEIF TALENT:촉수의포로 == 0 && TALENT:음란 >= 1 && ABL:촉수중독 >= 3 && EXP:유폐경험 >= 1
	TALENT:촉수의포로 = 1
	;지문: 촉수의포로 취득
	CALL MESSAGE_GETTALENT_TORIKO
ENDIF
;음핵
;조건: C감각이 Lv5 이상이며, 전투 중의 C절정 횟수가 50회 이상
IF TALENT:음핵 == 0 && ABL:C감각 >= 5 && EX:C절정 >= 50
	TALENT:음핵 = 1
	;지문: 음핵
	CALL MESSAGE_GETTALENT_INKAKU
ENDIF
;음호
;조건: V감각이 Lv5, V경험이 200 이상이며, 전투 중의 V절정 횟수가 25회 이상
IF TALENT:음호 == 0 && ABL:V감각 >= 5 && EXP:V경험 >= 200 && EX:V절정 >= 25
	TALENT:음호 = 1
	;지문: 음호
	CALL MESSAGE_GETTALENT_INTUBO
ENDIF
;음고
;조건: A감각이 Lv5, A경험이 200 이상이며, 전투 중의 A절정 횟수가 25회 이상
IF TALENT:음고 == 0 && ABL:A감각 >= 5 && EXP:A경험 >= 200 && EX:A절정 >= 25
	TALENT:음고 = 1
	;지문: 음고
	CALL MESSAGE_GETTALENT_INJIRI
ENDIF
;음유
;조건: B감각이 Lv5이며, 전투 중의 B절정 횟수가 50회 이상
IF TALENT:음유 == 0 && ABL:B감각 >= 5 && EX:B절정 >= 50
	TALENT:음유 = 1
	;지문: 음유
	CALL MESSAGE_GETTALENT_INNYUU
ENDIF
;청순파의 소실
;조건: 촉수의포로 또는 음란 획득 시, 또는 (욕망Lv + 모든 중독Lv) 합계가 8 이상으로 초심이 아니게 된다
IF TALENT:초심 < 1 && TALENT:청순파 > 0 && (TALENT:촉수의포로 > 0 || TALENT:음란 > 0 || (ABL:욕망 + ABL:촉수중독 + ABL:자위중독 + ABL:정액중독 + ABL:모유중독 + ABL:사정중독) >= 8)
	TALENT:청순파 = -1
	;지문
	CALL MESSAGE_LOSETALENT_SEIJUNHA
ENDIF
;변신후후타나리 취득(천연 후타나리 또는 평상시 남자인 경우 취득 불가)
;IF CHARATALENT_F(TARGET,0,"후타나리") > 0 && CHARATALENT_F(TARGET,1,"후타나리") == 0 && CHARATALENT_F(TARGET,1,"남자") == 0 && TALENT:변신시후타나리 < 3 && TALENT:후타나리 < 3 && TALENT:변신능력 > 0 && ABL:사정중독 >= 3
;	TALENT:변신시후타나리 = 1
;	TALENT:후타나리 = 1
;	;지문
;	CALL MESSAGE_GETTALENT_THUTANARI
;ENDIF
;후타나리 정착
IF (TALENT:후타나리 == 2 || TALENT:변신시후타나리 == 2) && ABL:사정중독 >= 5
	CALL MESSAGE_GETTALENT_HUTANARI4
	SIF TALENT:후타나리 == 2
		TALENT:후타나리 = 4
	SIF TALENT:변신시후타나리 == 2
		TALENT:변신시후타나리 = 4
ENDIF
;기생후타나리 소실
IF (TALENT:후타나리 == 2 || TALENT:변신시후타나리 == 2) && EXP:사정경험 - CFLAG:39 >= 10
	SIF TALENT:후타나리 == 2
		TALENT:후타나리 = 0
	SIF TALENT:변신시후타나리 == 2
		TALENT:변신시후타나리 = 0
	CALL MESSAGE_LOSETALENT_HUTANARI
ENDIF
	
;VABC의 감도가 높아진다
SIF TALENT:V둔감 == 1 && ABL:V감각 >= 3
	TALENT:V둔감 = 0
SIF TALENT:V민감 == 0 && ABL:V감각 >= 4
	TALENT:V민감 = 1
SIF TALENT:A둔감 == 1 && ABL:A감각 >= 3
	TALENT:A둔감 = 0
SIF TALENT:A민감 == 0 && ABL:A감각 >= 4
	TALENT:A민감 = 1
SIF TALENT:B둔감 == 1 && ABL:B감각 >= 3
	TALENT:B둔감 = 0
SIF TALENT:B민감 == 0 && ABL:B감각 >= 4
	TALENT:B민감 = 1
SIF TALENT:C둔감 == 1 && ABL:C감각 >= 3
	TALENT:C둔감 = 0
SIF TALENT:C민감 == 0 && ABL:C감각 >= 4
	TALENT:C민감 = 1

;여체수용（남성/여성기피가 있다면 반전한다）
;전제조건: "성별변화" 가 있는 동시에 원래 남성인 캐릭터가 여체화된 경우
IF TALENT:여체수용 == 0 && (TALENT:성별변화 % 10 == 1 || (TALENT:변신시TS > 0 && ISFEMALE() && CFLAG:1 > 0))
	;여체수용을 단계화한다
	IF TALENT:여체수용 == 0
		;조건 1: 촉수의포로 획득 시
		IF TALENT:촉수의포로 > 0
			TALENT:여체수용 = 1
			SWAP TALENT:남성기피,TALENT:여성기피
			;지문: 촉수의포로 획득에 의한 여체수용
			CALL MESSAGE_GETTALENT_TSJYUYOU_TORIKO
		;조건 2: 음란 혹은 음호 취득 시, 또는 V감각Lv5 이상이며 사정중독Lv이 (정액중독Lv + 모유중독Lv) 보다 낮을 때
		ELSEIF TALENT:음란 > 0 || TALENT:음호 > 0 || (ABL:V감각 >= 5 && (ABL:정액중독 + ABL:모유중독 > ABL:사정중독))
			TALENT:여체수용 = 1
			SWAP TALENT:남성기피,TALENT:여성기피
			;지문: 음란화에 의한 여체수용
			CALL MESSAGE_GETTALENT_TSJYUYOU_INRAN
		;조건 3: 출산했을 시, 또는 처녀를 사랑하는 사람에게 바쳤을 시
		ELSEIF (EXP:출산경험 > 0 && CFLAG:230 == 사랑하는사람) || (EXP:출산경험 > 0 && CFLAG:230 < -100 && LOVER_F(TARGET, CHARAID_F(CFLAG:230 * -1 - 100))) || CFLAG:206 == 5
			TALENT:여체수용 = 1
			SWAP TALENT:남성기피,TALENT:여성기피
			;지문: 여자의 기쁨에 의한 여체수용
			CALL MESSAGE_GETTALENT_TSJYUYOU_FEMININE
		;조건 4: 매료경험 200 이상
		ELSEIF EXP:매료경험 >= 200
			TALENT:여체수용 = 1
			SWAP TALENT:남성기피,TALENT:여성기피
			;지문: 여자의 매력에 의한 여체수용
			CALL MESSAGE_GETTALENT_TSJYUYOU_CHARM
		;조건 5: 양도를 보유함과 동시에, 음란 혹은 음호 취득 시, 또는 V감각이 Lv5 이상일 때
		ELSEIF TALENT:양도 > 0 && (TALENT:음란 > 0 || TALENT:음호 > 0 || ABL:V감각 >= 5)
			TALENT:여체수용 = 1
			SWAP TALENT:남성기피,TALENT:여성기피
			;지문: 양도에 의한 여체수용
			CALL MESSAGE_GETTALENT_TSJYUYOU_RYOUTOU
		ENDIF
	ENDIF
ENDIF

;실적
CALL GET_STATE_ABLUP,TARGET



;--------------------------------------------------
;촉수중독 상승
;필요한 구슬：욕정, 굴복
;상승 조건 １：굴복각인 2 이상
;상승 조건 ２：Lv4 이상부터는 이상경험이 필요.
@ABL_UP_20
LOCAL = ABL:촉수중독
SIF MARK:굴복각인 < 2
	RETURN LOCAL
REPEAT 5
	;LV0->1
	IF LOCAL == 0
		IF JUEL:욕정 >= 500 && JUEL:굴복 >= 1000 && EXP:피간경험 >= 10
			JUEL:욕정 -= 500
			JUEL:굴복 -= 1000
			LOCAL = 1
		ENDIF
	;LV1->2
	ELSEIF LOCAL == 1
		IF JUEL:욕정 >= 1000 && JUEL:굴복 >= 2000 && EXP:피간경험 >= 20
			JUEL:욕정 -= 1000
			JUEL:굴복 -= 2000
			LOCAL = 2
		ENDIF
	;LV2->3
	ELSEIF LOCAL == 2
		IF JUEL:욕정 >= 1500 && JUEL:굴복 >= 6000 && EXP:피간경험 >= 50
			JUEL:욕정 -= 1500
			JUEL:굴복 -= 6000
			LOCAL = 3
		ENDIF
	;LV3->4
	ELSEIF LOCAL == 3 && EXP:이상경험
		IF JUEL:욕정 >= 3000 && JUEL:굴복 >= 9000 && EXP:피간경험 >= 100
			JUEL:욕정 -= 3000
			JUEL:굴복 -= 9000
			LOCAL = 4
		ENDIF
	;LV4->5
	ELSEIF LOCAL == 4 && EXP:이상경험
		IF JUEL:욕정 >= 6000 && JUEL:굴복 >= 18000 && EXP:피간경험 >= 200
			JUEL:욕정 -= 6000
			JUEL:굴복 -= 18000
			LOCAL = 5
		ENDIF
	ENDIF
REND
RETURN LOCAL

;--------------------------------------------------
;자위중독 상승
;필요한 구슬：욕정, 치정
;상승조건 １：치욕각인 2 이상
;상승조건 ２：Lv4 이상부터는 이상경험이 필요.
@ABL_UP_21
LOCAL = ABL:자위중독
SIF MARK:치욕각인 < 2
	RETURN LOCAL
REPEAT 5
	;LV0->1
	IF LOCAL == 0
		IF JUEL:욕정 >= 500 && JUEL:치정 >= 1000 && EXP:자위경험 >= 10
			JUEL:욕정 -= 500
			JUEL:치정 -= 1000
			LOCAL = 1
		ENDIF
	;LV1->2
	ELSEIF LOCAL == 1
		IF JUEL:욕정 >= 1000 && JUEL:치정 >= 2000 && EXP:자위경험 >= 20
			JUEL:욕정 -= 1000
			JUEL:치정 -= 2000
			LOCAL = 2
		ENDIF
	;LV2->3
	ELSEIF LOCAL == 2
		IF JUEL:욕정 >= 1500 && JUEL:치정 >= 3000 && EXP:자위경험 >= 50
			JUEL:욕정 -= 1500
			JUEL:치정 -= 3000
			LOCAL = 3
		ENDIF
	;LV3->4
	ELSEIF LOCAL == 3 && EXP:이상경험
		IF JUEL:욕정 >= 3000 && JUEL:치정 >= 6000 && EXP:자위경험 >= 100
			JUEL:욕정 -= 3000
			JUEL:치정 -= 6000
			LOCAL = 4
		ENDIF
	;LV4->5
	ELSEIF LOCAL == 4 && EXP:이상경험
		IF JUEL:욕정 >= 6000 && JUEL:치정 >= 12000 && EXP:자위경험 >= 200
			JUEL:욕정 -= 6000
			JUEL:치정 -= 12000
			LOCAL = 5
		ENDIF
	ENDIF
REND
RETURN LOCAL

;--------------------------------------------------
;정액중독 상승
;필요한 구슬：욕정, 굴복
;상승조건 １：쾌락각인 2 이상
;상승조건 ２：Lv4 이상부터는 이상경험이 필요.
@ABL_UP_22
LOCAL = ABL:정액중독
SIF MARK:쾌락각인 < 2
	RETURN LOCAL

REPEAT 5
	;LV0->1
	IF LOCAL == 0
		IF JUEL:욕정 >= 500 && JUEL:굴복 >= 1000 && EXP:정액경험 >= 10
			JUEL:욕정 -= 500
			JUEL:굴복 -= 1000
			LOCAL = 1
		ENDIF
	;LV1->2
	ELSEIF LOCAL == 1
		IF JUEL:욕정 >= 1000 && JUEL:굴복 >= 2000 && EXP:정액경험 >= 25
			JUEL:욕정 -= 1000
			JUEL:굴복 -= 2000
			LOCAL = 2
		ENDIF
	;LV2->3
	ELSEIF LOCAL == 2
		IF JUEL:욕정 >= 1500 && JUEL:굴복 >= 3000 && EXP:정액경험 >= 50
			JUEL:욕정 -= 1500
			JUEL:굴복 -= 3000
			LOCAL = 3
		ENDIF
	;LV3->4
	ELSEIF LOCAL == 3 && EXP:이상경험
		IF JUEL:욕정 >= 3000 && JUEL:굴복 >= 6000 && EXP:정액경험 >= 100
			JUEL:욕정 -= 3000
			JUEL:굴복 -= 6000
			LOCAL = 4
		ENDIF
	;LV4->5
	ELSEIF LOCAL == 4 && EXP:이상경험
		IF JUEL:욕정 >= 6000 && JUEL:굴복 >= 12000 && EXP:정액경험 >= 200
			JUEL:욕정 -= 6000
			JUEL:굴복 -= 12000
			LOCAL = 5
		ENDIF
	ENDIF
REND
RETURN LOCAL

;--------------------------------------------------
;사정중독 상승
;필요한 구슬：쾌C, 욕정
;상승조건 １：사정경험 필요량　LV1=5,LV2=10,LV3=20,LV4=50,LV5=100
;상승조건 ２：Lv4 이상부터는 이상경험이 필요.
@ABL_UP_24
LOCAL = ABL:사정중독

REPEAT 5
	;LV0->LV1
	IF LOCAL == 0
		IF JUEL:쾌C >= 1000 && EXP:사정경험 >= 5
			JUEL:쾌C -= 1000
			LOCAL = 1
		ENDIF
	;LV1->LV2
	ELSEIF LOCAL == 1
		IF JUEL:쾌C >= 3000 && EXP:사정경험 >= 10
			JUEL:쾌C -= 3000
			LOCAL = 2
		ENDIF
	;LV2->LV3
	ELSEIF LOCAL == 2
		IF JUEL:쾌C >= 5000 && EXP:사정경험 >= 20
			JUEL:쾌C -= 5000
			LOCAL = 3
		ENDIF
	;LV3->LV4
	ELSEIF LOCAL == 3 && EXP:이상경험
		IF JUEL:쾌C >= 10000 && EXP:사정경험 >= 50
			JUEL:쾌C -= 10000
			LOCAL = 4
		ENDIF
	;LV4->LV5
	ELSEIF LOCAL == 4 && EXP:이상경험
		IF JUEL:쾌C >= 30000 && EXP:사정경험 >= 100
			JUEL:쾌C -= 30000
			LOCAL = 5
		ENDIF
	ENDIF
	;사정 게이지 재계산
	MAXBASE:사정 = CSVBASE_F(NO:TARGET, 20)
	SIF MAXBASE:사정 < 1
		MAXBASE:사정 = 10000
	MAXBASE:사정 = MAX(MAXBASE:사정 * (10 - ABL:사정중독) / 10, 1000)
REND
RETURN LOCAL

;--------------------------------------------------
;모유중독 상승
;필요한 구슬：쾌B, 욕정
;상승조건 １：모유경험 필요량　LV1=5,LV2=10,LV3=20,LV4=50,LV5=100
;상승조건 ２：Lv4 이상부터는 이상경험이 필요
@ABL_UP_23
LOCAL = ABL:모유중독

REPEAT 5
	;LV0->LV1
	IF LOCAL == 0
		IF JUEL:쾌B >= 1000 && EXP:모유경험 >= 5
			JUEL:쾌B -= 1000
			LOCAL = 1
		ENDIF
	;LV1->LV2
	ELSEIF LOCAL == 1
		IF JUEL:쾌B >= 3000 && EXP:모유경험 >= 10
			JUEL:쾌B -= 3000
			LOCAL = 2
		ENDIF
	;LV2->LV3
	ELSEIF LOCAL == 2
		IF JUEL:쾌B >= 5000 && EXP:모유경험 >= 20
			JUEL:쾌B -= 5000
			LOCAL = 3
		ENDIF
	;LV3->LV4
	ELSEIF LOCAL == 3 && EXP:이상경험
		IF JUEL:쾌B >= 10000 && EXP:모유경험 >= 50
			JUEL:쾌B -= 10000
			LOCAL = 4
		ENDIF
	;LV4->LV5
	ELSEIF LOCAL == 4 && EXP:이상경험
		IF JUEL:쾌B >= 30000 && EXP:모유경험 >= 100
			JUEL:쾌B -= 30000
			LOCAL = 5
		ENDIF
	ENDIF
	;모유 게이지 재계산
	MAXBASE:모유 = CSVBASE_F(NO:TARGET, 21)
	SIF MAXBASE:모유 < 1
		MAXBASE:모유 = 10000
	MAXBASE:모유 = MAX(MAXBASE:모유 * (10 - ABL:모유중독) / 10, 1000)
REND
RETURN LOCAL


;--------------------------------------------------
;마조끼 상승
;필요한 구슬：고통
@ABL_UP_15
LOCAL = ABL:마조끼

REPEAT 5
	;LV0->1
	IF LOCAL == 0
		IF JUEL:고통 >= 1000
			JUEL:고통 -= 1000
			LOCAL = 1
		ENDIF
	;LV1->2
	ELSEIF LOCAL == 1
		IF EXP:고통쾌락경험 >= 2
			IF JUEL:고통 >= 2000
				JUEL:고통 -= 2000
				LOCAL = 2
			ENDIF
		ENDIF
	;LV2->3
	ELSEIF LOCAL == 2
		IF EXP:고통쾌락경험 >= 4
			IF JUEL:고통 >= 5000
				JUEL:고통 -= 5000
				LOCAL = 3
			ENDIF
		ENDIF
	;LV3->4
	ELSEIF LOCAL == 3
		IF EXP:고통쾌락경험 >= 8
			IF JUEL:고통 >= 10000
				JUEL:고통 -= 10000
				LOCAL = 4
			ENDIF
		ENDIF
	;LV4->5
	ELSEIF LOCAL == 4
		IF EXP:고통쾌락경험 >= 16
			IF JUEL:고통 >= 20000
				JUEL:고통 -= 20000
				LOCAL = 5
			ENDIF
		ENDIF
	ENDIF
REND
RETURN LOCAL

;--------------------------------------------------
;봉사정신 상승
;필요한 구슬：공순
@ABL_UP_13
LOCAL = ABL:봉사정신

REPEAT 5
	;LV0->1
	IF LOCAL == 0
		IF JUEL:공순 >= 800
			JUEL:공순 -= 800
			LOCAL = 1
		ENDIF
	;LV1->2
	ELSEIF LOCAL == 1
		IF EXP:봉사쾌락경험 >= 2 || TALENT:촉수의포로 > 0
			IF JUEL:공순 >= 2000
				JUEL:공순 -= 2000
				LOCAL = 2
			ENDIF
		ENDIF
	;LV2->3
	ELSEIF LOCAL == 2
		IF EXP:봉사쾌락경험 >= 10 || TALENT:촉수의포로 > 0
			IF JUEL:공순 >= 6000
				JUEL:공순 -= 6000
				LOCAL = 3
			ENDIF
		ENDIF
	;LV3->4
	ELSEIF LOCAL == 3
		IF EXP:봉사쾌락경험 >= 20 || TALENT:촉수의포로 > 0
			IF JUEL:공순 >= 12000
				JUEL:공순 -= 12000
				LOCAL = 4
			ENDIF
		ENDIF
	;LV4->5
	ELSEIF LOCAL == 4
		IF EXP:봉사쾌락경험 >= 50 || TALENT:촉수의포로 > 0
			IF JUEL:공순 >= 20000
				JUEL:공순 -= 20000
				LOCAL = 5
			ENDIF
		ENDIF
	ENDIF
REND
RETURN LOCAL

;--------------------------------------------------
;노출벽 상승
;필요한 구슬：치정
@ABL_UP_14
LOCAL = ABL:노출벽

REPEAT 5
	;LV0->1
	IF LOCAL == 0
		IF JUEL:치정 >= 1000
			JUEL:치정 -= 1000
			LOCAL = 1
		ENDIF
	;LV1->2
	ELSEIF LOCAL == 1
		IF EXP:노출쾌락경험 >= 2
			IF JUEL:치정 >= 3000
				JUEL:치정 -= 3000
				LOCAL = 2
			ENDIF
		ENDIF
	;LV2->3
	ELSEIF LOCAL == 2
		IF EXP:노출쾌락경험 >= 10
			IF JUEL:치정 >= 7000
				JUEL:치정 -= 7000
				LOCAL = 3
			ENDIF
		ENDIF
	;LV3->4
	ELSEIF LOCAL == 3
		IF EXP:노출쾌락경험 >= 20
			IF JUEL:치정 >= 12000
				JUEL:치정 -= 12000
				LOCAL = 4
			ENDIF
		ENDIF
	;LV4->5
	ELSEIF LOCAL == 4
		IF EXP:노출쾌락경험 >= 50
			IF JUEL:치정 >= 25000
				JUEL:치정 -= 25000
				LOCAL = 5
			ENDIF
		ENDIF
	ENDIF
REND
RETURN LOCAL

;--------------------------------------------------
;순종 상승
;필요한 구슬：굴복 또는 공포
@ABL_UP_10
LOCAL = ABL:순종

REPEAT 5
	;LV0->1
	IF LOCAL == 0
		IF JUEL:굴복 >= 1000
			JUEL:굴복 -= 1000
			LOCAL = 1
		ELSEIF JUEL:공포 >= 1000
			JUEL:공포 -= 1000
			LOCAL = 1
		ENDIF
	;LV1->2
	ELSEIF LOCAL == 1
		IF JUEL:굴복 >= 3000
			JUEL:굴복 -= 3000
			LOCAL = 2
		ELSEIF JUEL:공포 >= 3000
			JUEL:공포 -= 3000
			LOCAL = 2
		ENDIF
	;LV2->3
	ELSEIF LOCAL == 2
		IF JUEL:굴복 >= 6000
			JUEL:굴복 -= 6000
			LOCAL = 3
		ELSEIF JUEL:공포 >= 6000
			JUEL:공포 -= 6000
			LOCAL = 3
		ENDIF
	;LV3->4
	ELSEIF LOCAL == 3
		IF JUEL:굴복 >= 12000
			JUEL:굴복 -= 12000
			LOCAL = 4
		ELSEIF JUEL:공포 >= 12000
			JUEL:공포 -= 12000
			LOCAL = 4
		ENDIF
	;LV4->5
	ELSEIF LOCAL == 4
		IF JUEL:굴복 >= 25000
			JUEL:굴복 -= 25000
			LOCAL = 5
		ELSEIF JUEL:공포 >= 25000
			JUEL:공포 -= 25000
			LOCAL = 5
		ENDIF
	ENDIF
REND

RETURN LOCAL

;--------------------------------------------------
;욕망 상승
;필요한 구슬：욕정
@ABL_UP_11
LOCAL = ABL:욕망

REPEAT 5
	;LV0->1
	IF LOCAL == 0
		IF JUEL:욕정 >= 500
			JUEL:욕정 -= 500
			LOCAL = 1
		ENDIF
	;LV1->2
	ELSEIF LOCAL == 1
		IF JUEL:욕정 >= 2000
			JUEL:욕정 -= 2000
			LOCAL = 2
		ENDIF
	;LV2->3
	ELSEIF LOCAL == 2
		IF JUEL:욕정 >= 5000
			JUEL:욕정 -= 5000
			LOCAL = 3
		ENDIF
	;LV3->4
	ELSEIF LOCAL == 3
		IF JUEL:욕정 >= 10000
			JUEL:욕정 -= 10000
			LOCAL = 4
		ENDIF
	;LV4->5
	ELSEIF LOCAL == 4
		IF JUEL:욕정 >= 20000
			JUEL:욕정 -= 20000
			LOCAL = 5
		ENDIF
	ENDIF
REND

RETURN LOCAL

;--------------------------------------------------
;기교 상승
;필요한 구슬：습득
@ABL_UP_12
LOCAL = ABL:기교

REPEAT 5
	;LV0->1
	IF LOCAL == 0
		IF JUEL:습득 >= 1000
			JUEL:습득 -= 1000
			LOCAL = 1
		ENDIF
	;LV1->2
	ELSEIF LOCAL == 1
		IF JUEL:습득 >= 2000
			JUEL:습득 -= 2000
			LOCAL = 2
		ENDIF
	;LV2->3
	ELSEIF LOCAL == 2
		IF JUEL:습득 >= 4000
			JUEL:습득 -= 4000
			LOCAL = 3
		ENDIF
	;LV3->4
	ELSEIF LOCAL == 3
		IF JUEL:습득 >= 8000
			JUEL:습득 -= 8000
			LOCAL = 4
		ENDIF
	;LV4->5
	ELSEIF LOCAL == 4
		IF JUEL:습득 >= 20000
			JUEL:습득 -= 20000
			LOCAL = 5
		ENDIF
	ENDIF
REND
RETURN LOCAL

;--------------------------------------------------
;C감각 상승
;필요한 구슬：쾌C
@ABL_UP_0
LOCAL = ABL:C감각

REPEAT 5
	;C감각이 LV5 이상이며, 모든 감각LV의 합계가 35 이상이라면 종료
	SIF ABL:V감각 + ABL:A감각 + ABL:B감각 + LOCAL >= 35 && ABL:C감각 >= 5
		BREAK
	;LV0->1
	IF LOCAL == 0
		IF JUEL:쾌C >= 50
			JUEL:쾌C -= 50
			LOCAL = 1
		ENDIF
	;LV1->2
	ELSEIF LOCAL == 1
		IF JUEL:쾌C >= 500
			JUEL:쾌C -= 500
			LOCAL = 2
		ENDIF
	;LV2->3
	ELSEIF LOCAL == 2
		IF JUEL:쾌C >= 5000
			JUEL:쾌C -= 5000
			LOCAL = 3
		ENDIF
	;LV3->4
	ELSEIF LOCAL == 3
		IF JUEL:쾌C >= 10000
			JUEL:쾌C -= 10000
			LOCAL = 4
		ENDIF
	;LV4->5
	ELSEIF LOCAL == 4
		IF JUEL:쾌C >= 20000
			JUEL:쾌C -= 20000
			LOCAL = 5
		ENDIF
	ENDIF
REND
RETURN LOCAL

;--------------------------------------------------
;V감각 상승
;필요한 구슬：쾌V
@ABL_UP_1
LOCAL = ABL:V감각

REPEAT 5
	;V감각이 LV5 이상이며, 모든 감각LV의 합계가 35 이상이라면 종료
	SIF ABL:V감각 + ABL:A감각 + ABL:B감각 + LOCAL >= 35 && ABL:V감각 >= 5
		BREAK
	;LV0->1
	IF LOCAL == 0
		IF JUEL:쾌V >= 75
			JUEL:쾌V -= 75
			LOCAL = 1
		ENDIF
	;LV1->2
	ELSEIF LOCAL == 1
		IF JUEL:쾌V >= 750
			JUEL:쾌V -= 750
			LOCAL = 2
		ENDIF
	;LV2->3
	ELSEIF LOCAL == 2
		IF JUEL:쾌V >= 7500
			JUEL:쾌V -= 7500
			LOCAL = 3
		ENDIF
	;LV3->4
	ELSEIF LOCAL == 3
		IF JUEL:쾌V >= 15000
			JUEL:쾌V -= 15000
			LOCAL = 4
		ENDIF
	;LV4->5
	ELSEIF LOCAL == 4
		IF JUEL:쾌V >= 30000
			JUEL:쾌V -= 30000
			LOCAL = 5
		ENDIF
	ENDIF
REND
RETURN LOCAL

;--------------------------------------------------
;A감각 상승
;필요한 구슬：쾌A
@ABL_UP_2
LOCAL = ABL:A감각

REPEAT 5
	;A감각이 LV5 이상이며, 모든 감각LV의 합계가 35 이상이라면 종료
	SIF ABL:V감각 + ABL:A감각 + ABL:B감각 + LOCAL >= 35 && ABL:A감각 >= 5
		BREAK
	;LV0->1
	IF LOCAL == 0
		IF JUEL:쾌A >= 75
			JUEL:쾌A -= 75
			LOCAL = 1
		ENDIF
	;LV1->2
	ELSEIF LOCAL == 1
		IF JUEL:쾌A >= 750
			JUEL:쾌A -= 750
			LOCAL = 2
		ENDIF
	;LV2->3
	ELSEIF LOCAL == 2
		IF JUEL:쾌A >= 7500
			JUEL:쾌A -= 7500
			LOCAL = 3
		ENDIF
	;LV3->4
	ELSEIF LOCAL == 3
		IF JUEL:쾌A >= 15000
			JUEL:쾌A -= 15000
			LOCAL = 4
		ENDIF
	;LV4->5
	ELSEIF LOCAL == 4
		IF JUEL:쾌A >= 30000
			JUEL:쾌A -= 30000
			LOCAL = 5
		ENDIF
	ENDIF
REND
RETURN LOCAL

;--------------------------------------------------
;B감각 상승
;필요한 구슬：쾌B
@ABL_UP_3
LOCAL = ABL:B감각

REPEAT 5
	;B감각이 LV5 이상이며, 모든 감각LV의 합계가 35 이상이라면 종료
	SIF ABL:V감각 + ABL:A감각 + ABL:B감각 + LOCAL >= 35 && ABL:B감각 >= 5
		BREAK
	;LV0->1
	IF LOCAL == 0
		IF JUEL:쾌B >= 50
			JUEL:쾌B -= 50
			LOCAL = 1
		ENDIF
	;LV1->2
	ELSEIF LOCAL == 1
		IF JUEL:쾌B >= 500
			JUEL:쾌B -= 500
			LOCAL = 2
		ENDIF
	;LV2->3
	ELSEIF LOCAL == 2
		IF JUEL:쾌B >= 5000
			JUEL:쾌B -= 5000
			LOCAL = 3
		ENDIF
	;LV3->4
	ELSEIF LOCAL == 3
		IF JUEL:쾌B >= 10000
			JUEL:쾌B -= 10000
			LOCAL = 4
		ENDIF
	;LV4->5
	ELSEIF LOCAL == 4
		IF JUEL:쾌B >= 20000
			JUEL:쾌B -= 20000
			LOCAL = 5
		ENDIF
	ENDIF
REND
RETURN LOCAL

;--------------------------------------------------
@ABL_UP_EX
#DIM LCOUNT, 3
#DIM CAL_VAR, 3
#DIM LV_VAR, 감각수

;감각LV을 지역(로컬)변수
FOR LCOUNT:0, 0, 감각수
	LV_VAR:(LCOUNT:0) = ABL:(LCOUNT:0)
NEXT

FOR LCOUNT:0, 0, 15
	;모든 감각LV5 이상의 합계가 15 이상이라면 종료
	SIF MAX(LV_VAR:0 - 5,0) + MAX(LV_VAR:1 - 5,0) + MAX(LV_VAR:2 - 5,0) + MAX(LV_VAR:3 - 5,0) >= 15
		BREAK

	;감각을 1레벨씩 살펴본다
	FOR LCOUNT:1, 0, 감각수
		;감각LV이 5 미만이면 무시
		IF ABL:(LCOUNT:1) >= 5
			;레벨업 조건 초기화
			CAL_VAR:0 = 20000

			FOR LCOUNT:2, 0, MIN(LV_VAR:(LCOUNT:1) - 5, 5)
				CAL_VAR:0 *= 150 - LCOUNT:2 * 5
				CAL_VAR:0 /= 100
			NEXT
			CAL_VAR:0 = CAL_VAR:0 / 2 * 5

			;모든 감각LV의 합계가 35 이상이라면 종료
			IF LV_VAR:0 + LV_VAR:1 + LV_VAR:2 + LV_VAR:3 >= 35
				BREAK
			;JUEL이 조건을 충족하는 동시에 감각LV이 20 미만
			ELSEIF JUEL:(LCOUNT:1) >= CAL_VAR:0 && LV_VAR:(LCOUNT:1) < 20
				JUEL:(LCOUNT:1) -= CAL_VAR:0
				LV_VAR:(LCOUNT:1) += 1
			ENDIF
		ENDIF
	NEXT
NEXT

RETURN LV_VAR:0, LV_VAR:1, LV_VAR:2, LV_VAR:3
