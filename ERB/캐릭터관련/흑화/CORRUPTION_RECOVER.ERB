;*******************************************************************************************************
;흑화 캐릭터의 외관을 되돌린다
;CSTR:50〜에 등록된 외관과 현재의 외관을 교체함으로써 외관을 되돌린다.
;일정 확률로 CSTR:50〜에 등록된 외관을 고쳐 쓰는(書き換える) 것으로, 외관의 정착을 표현한다.
;*******************************************************************************************************
@RECOVER_CORRUPTION, ARG
#DIM ExpCorrupt
ExpCorrupt = EXP:ARG:함락경험

;컨피그 미설정이라면 실행하지 않는다
SIF CONFIG_CHECK_PRISON_F(4) == 0
	RETURN 1

;프로필 미설정이라면 실행하지 않는다
SIF CFLAG:ARG:34 == 0
	RETURN 1

;최초 등록이 없다면 실행하지 않는다
SIF GETBIT(CFLAG:ARG:80, 0) == 0
	RETURN 1

;머리
IF GETBIT(CFLAG:ARG:80, 4) == 1 && GETBIT(CFLAG:ARG:81, 0) == 0 && RAND:100 <= (ExpCorrupt * 15)
	CALL STAIN_CORRUPTED_HAIR_COLOR, ARG
	SETBIT CFLAG:ARG:81, 0
ENDIF

;오른쪽 눈
IF GETBIT(CFLAG:ARG:80, 5) == 1 && GETBIT(CFLAG:ARG:81, 1) == 0 && RAND:100 <= ((ExpCorrupt-1) * 15)
	CALL STAIN_CORRUPTED_EYE_COLOR_RIGHT, ARG
	SETBIT CFLAG:ARG:81, 1
ENDIF

;왼쪽 눈
IF GETBIT(CFLAG:ARG:80, 5) == 1 && GETBIT(CFLAG:ARG:81, 2) == 0 && RAND:100 <= ((ExpCorrupt-1) * 15)
	CALL STAIN_CORRUPTED_EYE_COLOR_LEFT, ARG
	SETBIT CFLAG:ARG:81, 2
ENDIF

;피부
IF GETBIT(CFLAG:ARG:80, 1) == 1 && GETBIT(CFLAG:ARG:81, 3) == 0 && RAND:100 <= ((ExpCorrupt-1) * 15)
	CALL STAIN_CORRUPTED_SKIN_COLOR, ARG
	SETBIT CFLAG:ARG:81, 3
ENDIF

;함락경험 10이상 또는 모든 4부위(왼쪽 눈, 오른쪽 눈, 머리색, 피부색)가 흑화 외모로 정착할 시, 눈매를 정착시키고 전락시킨다
IF TALENT:ARG:전락 == 0 && (ExpCorrupt > 10 || CFLAG:81 == 0b1111)
	CSTR:ARG:50 = %CSTR:ARG:18%
	CALL MESSAGE_CORRUPTION_FINAL, ARG
	SETBIT CFLAG:ARG:81, 4
	TALENT:ARG:전락 = 1
ENDIF

;외관 변경
CALL CORRUPT_CHANGE_LOOKS, ARG

;흑화 중 플래그 삭제
CLEARBIT CFLAG:80, 2

RETURN 0
;*******************************************************************************************************

;*******************************************************************************************************
@STAIN_CORRUPTED_HAIR_COLOR, ARG
IF TALENT:ARG:변신능력 == 1
	CSTR:ARG:51 = %CSTR:ARG:31%
ELSE
	CSTR:ARG:51 = %CSTR:ARG:30%
ENDIF
CALL MESSAGE_STAIN_CORRUPTED_HAIR_COLOR, ARG
RETURN 0
;*******************************************************************************************************

;*******************************************************************************************************
@STAIN_CORRUPTED_EYE_COLOR_RIGHT, ARG
IF TALENT:ARG:변신능력 == 1
	CSTR:ARG:52 = %CSTR:ARG:34%
ELSE
	CSTR:ARG:52 = %CSTR:ARG:32%
ENDIF
CALL MESSAGE_STAIN_CORRUPTED_EYE_COLOR_RIGHT, ARG
RETURN 0
;*******************************************************************************************************

;*******************************************************************************************************
@STAIN_CORRUPTED_EYE_COLOR_LEFT, ARG
IF TALENT:ARG:변신능력 == 1
	CSTR:ARG:53 = %CSTR:ARG:35%
ELSE
	CSTR:ARG:53 = %CSTR:ARG:33%
ENDIF
CALL MESSAGE_STAIN_CORRUPTED_EYE_COLOR_LEFT, ARG
RETURN 0
;*******************************************************************************************************

;*******************************************************************************************************
@STAIN_CORRUPTED_SKIN_COLOR, ARG
IF TALENT:ARG:변신능력 == 1
	CSTR:ARG:54 = %CSTR:ARG:37%
ELSE
	CSTR:ARG:54 = %CSTR:ARG:36%
ENDIF
CALL MESSAGE_STAIN_CORRUPTED_SKIN_COLOR, ARG
RETURN 0
;*******************************************************************************************************


