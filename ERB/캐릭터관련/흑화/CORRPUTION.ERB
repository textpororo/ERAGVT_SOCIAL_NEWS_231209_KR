;*******************************************************************************************************
;흑화 시에 외관을 변경한다
;함락경험이 많을 수록, 외관이 돌아갈 확률이 떨어진다
;CSTR:50〜에 흑화 후의 외관을 등록하고, 원래의 외관과 교체한다
;;변신 캐릭터는 변신 후 외관 변경한다

;피부색 변경은 함락경험이 일정 수 이상일 시 발생
;모든 변경이 정착되면 소질【전락】을 추가, 눈매가 돌아오지 않게 된다

;변신 후 이름은 자칭감이 있으므로, 흑화 중에만 변경한다

;진척은 CFLAG80 에 비트로 유지
;0=초회 등록 완료 1=피부색 등록 완료 2=흑화 중（CFLAG:0 복귀 후 외관 복귀 판정이 있으므로, 구분용） 3=헤어 스타일 등록 4=머리색 등록 5=눈색 등록
;정착 상태는 CFLAG81 에 비트로 유지
;0=머리색 정착 1=오른쪽 눈 색 정착 2=왼쪽 눈 색깔 정착 3=피부색 정착 4=모든 부위 정착
;*******************************************************************************************************
@CORRUPT_CHANGE_LOOKS_MAIN, ARG

;컨피그 미설정이라면 실행하지 않는다
SIF CONFIG_CHECK_PRISON_F(4) == 0
	RETURN 1

;프로필 미설정이라면 실행하지 않는다
SIF CFLAG:ARG:34 == 0
	RETURN 1

SIF GETBIT(CFLAG:ARG:80, 0) == 0
	SETBIT CFLAG:80, 0

;눈매
CALL GET_CORRUPTED_EYES, ARG
;변신명
SIF TALENT:ARG:변신능력 == 1
	CALL CORRUPT_GET_SPNAME, ARG

;헤어 스타일 등록
IF GETBIT(CFLAG:ARG:80, 3) == 0 && CONFIG_CHECK_PRISON_F(5) == 1
	CALL GET_CORRUPTED_HAIR, ARG
	SETBIT CFLAG:80, 3
ENDIF

;머리색 등록
IF GETBIT(CFLAG:ARG:80, 4) == 0 && CONFIG_CHECK_PRISON_F(6) == 1
	CALL CORRUPT_CHANGE_HAIR_COLOR, ARG
	SETBIT CFLAG:80, 4
ENDIF

;눈색 등록
IF GETBIT(CFLAG:ARG:80, 5) == 0 && CONFIG_CHECK_PRISON_F(7) == 1
	CALL CORRUPT_CHANGE_EYE_COLOR, ARG
	SETBIT CFLAG:80, 5
ENDIF

;피부색 등록
IF GETBIT(CFLAG:ARG:80, 1) == 0 && EXP:ARG:함락경험 >=3 && CONFIG_CHECK_PRISON_F(8) == 1
	CALL CORRUPT_CHANGE_SKIN, ARG
	SETBIT CFLAG:80, 1
ENDIF

;외관 변경
CALL CORRUPT_CHANGE_LOOKS, ARG

SETBIT CFLAG:80, 2

;색이 정착되어 있지 않았다면, 색 변경 메시지 표시
;헤어 스타일이 등록되어 있거나, 머리색이 등록되어 정착되지 않은 경우
IF CONFIG_CHECK_PRISON_F(5) == 1 || CONFIG_CHECK_PRISON_F(6) == 1
	SIF GETBIT(CFLAG:ARG:80, 3) == 1 || (GETBIT(CFLAG:ARG:80, 4) == 1 && GETBIT(CFLAG:ARG:81, 0) == 0)
		CALL MESSAGE_CORRUPT_CHANGE_LOOKS_HAIR
ENDIF

;전락 시에는 눈매 변경이 발생
SIF GETBIT(CFLAG:ARG:81, 4) == 0
		CALL MESSAGE_CORRUPT_CHANGE_LOOKS_EYES
		
IF CONFIG_CHECK_PRISON_F(8) == 1
	SIF GETBIT(CFLAG:ARG:80, 1) == 1 && GETBIT(CFLAG:ARG:81, 3) == 0
		CALL MESSAGE_CORRUPT_CHANGE_LOOKS_SKIN
ENDIF

SIF TALENT:ARG:변신능력 == 1 && CFLAG:ARG:2 == 1
	CALL MESSAGE_CORRUPT_CHANGE_SP_NAME
RETURN 0

;*******************************************************************************************************

;*******************************************************************************************************
;원래의 외관과 등록된 외관을 교체
;CSTR50:눈매
;CSTR51:머리색
;CSTR52:오른쪽 눈 색
;CSTR53:왼쪽 눈 색
;CSTR54:피부색
;CSTR55:캐릭터의 변신 후 이름
;CSTR56:캐릭터의 변신 후 통칭 
;CSTR57:캐릭터의 앞머리
;CSTR58:캐릭터의 헤어 스타일
;*******************************************************************************************************
@CORRUPT_CHANGE_LOOKS, ARG
#DIM HAIRSTYLE
#DIM HAIRCOLOR
#DIM REYE
#DIM LEYE
#DIM SKIN

;CSTR 번호
IF TALENT:ARG:변신능력 == 1
	HAIRSTYLE = 14
	HAIRCOLOR = 31
	REYE = 34
	LEYE = 35
	SKIN = 37
	
ELSE
	HAIRSTYLE = 13
	HAIRCOLOR = 30
	REYE = 32
	LEYE = 33
	SKIN = 36
ENDIF

;눈매
CALL EXCHANGE_CSTR (ARG, 50, 18)
;헤어 스타일
IF GETBIT(CFLAG:ARG:80, 3) == 1 && CONFIG_CHECK_PRISON_F(5) == 1
	CALL EXCHANGE_CSTR (ARG, 57, 12)
	CALL EXCHANGE_CSTR (ARG, 58, HAIRSTYLE)
ENDIF
;머리색
SIF GETBIT(CFLAG:ARG:80, 4) == 1 && CONFIG_CHECK_PRISON_F(6) == 1
	CALL EXCHANGE_CSTR (ARG, 51, HAIRCOLOR)
;눈색
IF GETBIT(CFLAG:ARG:80, 5) == 1 && CONFIG_CHECK_PRISON_F(7) == 1
	CALL EXCHANGE_CSTR (ARG, 52, REYE)
	CALL EXCHANGE_CSTR (ARG, 53, LEYE)
ENDIF
;피부
SIF GETBIT(CFLAG:ARG:80, 1) == 1 && CONFIG_CHECK_PRISON_F(8) == 1
	CALL EXCHANGE_CSTR (ARG, 54, SKIN)
;이름
IF TALENT:ARG:변신능력 == 1
	CALL EXCHANGE_CSTR (ARG, 55, 0)
	CALL EXCHANGE_CSTR (ARG, 56, 1)
ENDIF

RETURN 0
;*******************************************************************************************************
@EXCHANGE_CSTR, ARG, ARG:1, ARG:2
#DIM EX_A
#DIM EX_B
#DIMS BUFF

EX_A = ARG:1
EX_B = ARG:2

BUFF = %CSTR:ARG:EX_A%
CSTR:ARG:EX_A = %CSTR:ARG:EX_B%
CSTR:ARG:EX_B = %BUFF%
RETURN 0
;*******************************************************************************************************


;*******************************************************************************************************
;머리색 등록
;CSV 등에서 CSTR이 지정되어 있으면 날린다
;*******************************************************************************************************
@CORRUPT_CHANGE_HAIR_COLOR, ARG

SIF CSTR:ARG:51 != ""
	RETURN 1

IF TALENT:ARG:변신능력 == 1
	CALL GET_CORRUPTED_HAIR_COLOR, CSTR:ARG:31
ELSE
	CALL GET_CORRUPTED_HAIR_COLOR, CSTR:ARG:30
ENDIF

CSTR:ARG:51 = %RESULTS%

RETURN 0
;*******************************************************************************************************

;*******************************************************************************************************
;흑화 시 변신 후 이름을 등록
;*******************************************************************************************************
@CORRUPT_GET_SPNAME, ARG

#DIMS STR_UP
#DIMS STR_DOWN
#DIMS DARK_NOUN
STR_UP = %CSTR:ARG:201%
STR_DOWN = %CSTR:ARG:202%


;등록되어 있지 않으면 아무것도 하지 않는다
SIF CFLAG:ARG:2 == 0
	RETURN 1

SIF CSTR:ARG:55 != ""
	RETURN 1

;중점(中흑색) 중복 방지
STR_UP = %REPLACE( STR_UP, "・", "")%

;★흑화와 세뇌로 나누거나 하고 싶다
;　치환 형식을 완전 랜덤이 아니라 문자 수를 갖추도록 궁리하고 싶다

STRDATA DARK_NOUN
	DATA 다크
	DATA 헬
	DATA 이블
	DATA 섀도우
	DATA 킬러
	DATA 데스
	DATA 카오스
	DATA 텐타클
	DATA 맬리스
	DATA 데몬
	DATA 서큐버스
	DATA 나이트메어

	DATA 서큐메어
	DATA 푸시
	DATA 슬레이브
	DATA 커럽션

	;slaughter
	DATA 슬로터
	;butcher
	DATA 부처
	;executioner
	DATA 엑서큐셔너
	;corruptor
	DATA 커럽터
	DATA 슬레이브
	;breeder?
	DATA 브리더
	;devastator
	DATA 데버스테이터
	DATA 디스트로이어
	;fallen
	DATA 폴른
	DATA 이클립스
	DATA 리버스
	;truth
	DATA 트루스
	;pleasure
	DATA 플레저
	;ecstasy
	DATA 엑스터시
	DATA 언체인드
	;ruined
	DATA 루인드

	;liberator
	DATA 리버레이터
	;DATA 피스메이커
	DATA 터미네이터
	DATA 이블퀸
	DATA 엠프리스

	;awaken
	DATA 어웨이큰
	DATA 이블마더
	;incubator
	DATA 인큐베이터
	DATA 스컬지
	DATA 네메시스
	;Charōn
	DATA 카론
	DATA 루시퍼
	DATA 어나더
	DATA 비스트
	DATA 리리스
	DATA 아포칼립스
	;calamity
	DATA 칼라미티
	;wicked
	DATA 위키드
	;vicious
	DATA 비셔스
	;exterminator
	DATA 익스터미네이터
	;Cum Bringer 이름 하나 섹스하네
	DATA 컴브링어
	DATA 피스브링어
	DATA 헤븐브링어
ENDDATA

;(변신 후 이름을 미설정 or 직접 설정한 경우)
;	＝CSTR201과 202이 비어 있는 경우는 덧붙이기만 한다　"아포칼립스・〇〇"　또는　"〇〇・아포칼립스"　같은 느낌으로 변화(랜덤)
IF STR_UP == "" && STR_DOWN == ""
	IF RAND:2 == 0
		CSTR:ARG:55 = %CSTR:ARG:0%・%DARK_NOUN%
	ELSE
		CSTR:ARG:55 = %DARK_NOUN%・%CSTR:ARG:0%
	ENDIF

;(공통 설정의 관명을 사용한 경우)
;	관명이 아닌 쪽을 바꾼다　큐어 〇〇→큐어 리리스　같은 느낌으로 변화
ELSEIF FLAG:6 == 1 && SAVESTR:11 == STR_UP
	CSTR:ARG:55 = %STR_UP% %DARK_NOUN%

;(그 외의 경우)
;	랜덤으로 윗구 or 아랫구 중 아무거나 바꿔치기한다(すり替える)
ELSEIF RAND:2 == 0
	CSTR:ARG:55 = %STR_UP%・%DARK_NOUN%
ELSE
	CSTR:ARG:55 = %DARK_NOUN%・%STR_DOWN%
ENDIF

CSTR:ARG:56 = %CSTR:ARG:55%

RETURN 0
;*******************************************************************************************************


;*******************************************************************************************************
;흑화 시의 피부색을 등록
;*******************************************************************************************************
@CORRUPT_CHANGE_SKIN, ARG

SIF CSTR:ARG:54 != ""
	RETURN 1

IF TALENT:ARG:변신능력 == 1
	CALL GET_CORRUPTED_SKIN, CSTR:ARG:37
	CSTR:ARG:54 = %RESULTS%
ELSE
	CALL GET_CORRUPTED_SKIN, CSTR:ARG:36
	CSTR:ARG:54 = %RESULTS%
ENDIF

RETURN 0
;*******************************************************************************************************

;*******************************************************************************************************
;흑화 시의 피부색을 생성
;백색->갈색or흑색
;살구색->갈색or흑색or백색
;갈색->흑색or백색
;흑색->백색
;*******************************************************************************************************
@GET_CORRUPTED_SKIN, ARGS
#DIMS SKIN_COLOR

IF ARGS == "183//86//17" || ARGS == "갈색"
	STRDATA SKIN_COLOR
		;흑색
		DATA 40//24//24
		;백색
		DATA 255//236//220
	ENDDATA
ELSEIF ARGS == "255//236//220" || ARGS == "백색"
	STRDATA SKIN_COLOR
		;흑색
		DATA 40//24//24
		;갈색
		DATA 183//86//17
	ENDDATA
ELSEIF ARGS == "40//24//24" || ARGS == "검정"
	;백색
	SKIN_COLOR = "255//236//220"
ELSE
	STRDATA SKIN_COLOR
		;흑색
		DATA 40//24//24
		;갈색
		DATA 183//86//17
		;백색
		DATA 255//236//220
	ENDDATA
ENDIF

RESULTS = %SKIN_COLOR%
RETURN 0


;*******************************************************************************************************
;흑화시의 헤어 스타일을 생성
;엣치한 헤어 스타일의 아이디어가 떠오르지 않아서, 일단 때를 벗긴다(垢抜けさせる)
;*******************************************************************************************************
@GET_CORRUPTED_HAIR, ARG
#DIMS ADJ
#DIMS HAIR

IF TALENT:ARG:변신능력 == 1
	HAIR = %CSTR:ARG:14%
ELSE
	HAIR = %CSTR:ARG:13%
ENDIF


;■앞머리
;촌스러운 헤어 스타일은 끊어버려
;눈 가리는 머리스타일 흑화 여주가 덮쳐왔을 때 앞머리 너머로 하트눈 보이는 게 얼마나 꼴리는지 알음? 너무하네
IF CSTR:ARG:57 == "" && (CSTR:ARG:12 == "눈 가림" || CSTR:ARG:12 == "부스스")
	STRDATA CSTR:ARG:57
		;かきあげ
		DATA 앞머리 트임
		;ぱっつん
		DATA 일자머리
		;斜めぱっつん
		DATA 비스듬한 일자머리
		;シャギーカット
		DATA 섀기컷
		;まゆ上ぱっつん
		DATA 눈썹 높이 일자머리
	ENDDATA
ELSEIF CSTR:ARG:57 == ""
	CSTR:ARG:57 = %CSTR:ARG:12%
ENDIF

;■헤어 스타일
;CSV 등에서 이미 설정되어 있다면 종료
SIF CSTR:ARG:58 != ""
	RETURN 0
;그럴듯한 형용사를 만든다
STRDATA ADJ
	DATA 자연스러운
	DATA 페미닌
	;艶やか
	DATA 반지르르한
	DATA 요염한
	DATA 스파이럴
ENDDATA
;진지함계의 헤어 스타일이라면 풀어 버려
IF HAIR == "세 갈레 땋기" || HAIR == "땋은 머리" || HAIR == "한 가닥 묶음"
	STRDATA HAIR
		;ウェーブロング
		DATA 롱 웨이브
		DATA 긴 머리
	ENDDATA
ENDIF

;형용사를 붙인다
CSTR:ARG:58 = %ADJ% %HAIR%
RETURN 0
;*******************************************************************************************************

;*******************************************************************************************************
;흑화 시의 머리색을 생성
;원래와 다른 색으로 한다
;*******************************************************************************************************
@GET_CORRUPTED_HAIR_COLOR, ARGS
#DIMS HAIR_COLOR

$LOOP
STRDATA HAIR_COLOR
	;로즈 레드
	DATA 199//79//144
	;스칼렛
	DATA 222//67//53
	;로즈 핑크
	DATA 239//147//182
	;블론드
	DATA 227//180//102
	;레몬 옐로우
	DATA 232//200//0	
	;코발트 그린
	DATA 93//194//136
	;세루리안 블루 cerulean blue
	DATA 11//116//175
	;바이올렛
	DATA 109//82//171
	;라일락
	DATA 194//162//218
	;스노우 화이트
	DATA 241//241//241
	;애쉬 그레이
	DATA 147//147//147
	;스틸 그레이
	DATA 115//108//121
ENDDATA

SIF HAIR_COLOR == ARGS
	GOTO LOOP
RESULTS = %HAIR_COLOR%
RETURN 0

;*******************************************************************************************************
;흑화시의 눈매을 생성
;*******************************************************************************************************
@GET_CORRUPTED_EYES, ARG
#DIMS ADJ

SIF CSTR:ARG:50 != ""
	RETURN 1

STRDATA ADJ
	;妖艶な
	DATA 요염한
	DATA 선정적인
	;艶めかしい, 첫 번째와 똑같은 요염하다 라는 뜻인데 뭐로 대체할지 잘 몰라서 대충 야하다고 썼다
	DATA 야한
	DATA 소악마적인
	;妖しく光る
	DATA 요상하게 빛나는
	;蠱惑的な
	DATA 매혹적인
	DATA 정욕을 자아내는
	DATA 도발적인
ENDDATA

IF CSTR:ARG:18 == "보통"
	CSTR:ARG:50 = %ADJ% 눈
ELSE
	CSTR:ARG:50 = %ADJ% %CSTR:ARG:18%
ENDIF
RETURN 0
;*******************************************************************************************************

;*******************************************************************************************************
;흑화 시의 눈색을 등록
;*******************************************************************************************************
@CORRUPT_CHANGE_EYE_COLOR, ARG
#DIMS REYE
#DIMS LEYE

IF TALENT:ARG:변신능력 == 1
	REYE = %CSTR:ARG:34%
	LEYE = %CSTR:ARG:35%
ELSE
	REYE = %CSTR:ARG:32%
	LEYE = %CSTR:ARG:33%
ENDIF

;오른쪽 눈을 취득
SIF CSTR:ARG:52 == ""
	CSTR:ARG:52 = %GET_CORRUPTED_EYE_COLOR(REYE)%

;원래가 오드아이라면 변경 후에도 오드아이
;원래가 오드아이가 아니라면, 10％의 확률로 오드아이
SIF CSTR:ARG:53 != ""
	RETURN 1
IF REYE != LEYE || RAND:10 == 0
	$LOOP
	CSTR:ARG:53 = %GET_CORRUPTED_EYE_COLOR(LEYE)%
	SIF CSTR:ARG:53 == CSTR:ARG:52
		GOTO LOOP
ELSE
	CSTR:ARG:53 = %CSTR:ARG:52%
ENDIF

RETURN 0
;*******************************************************************************************************
;흑화 시의 눈색을 생성
;원래와 다른 색으로 한다
;*******************************************************************************************************
@GET_CORRUPTED_EYE_COLOR, ARGS
#FUNCTIONS
#DIMS EYE_COLOR

$LOOP
STRDATA EYE_COLOR
	;루비 레드
	DATA 185//30//104
	;암적색
	DATA 131//39//78
	;버밀리언
	DATA 237//81//78
	;앰버
	DATA 179//125//64
	;크롬 옐로우
	DATA 255//188//0
	;에메랄드 그린
	DATA 77//165//115
	;아이비 그린
	DATA 81//106//57
	;터콰이즈 블루, Turquoise Blue
	DATA 57//158//204
	;울트라마린 블루
	DATA 65//79//163
	;퍼플
	DATA 162//96//191
	;마젠타
	DATA 201//73//162
	;체리 핑크
	DATA 207//97//165
ENDDATA

SIF EYE_COLOR == ARGS
	GOTO LOOP

RETURNF EYE_COLOR
;*******************************************************************************************************

