


@GENERATE_WEAPON_STRS, ARG
#DIM LCOUNT
#DIM LFLAG
#DIM KEEP_VAR
#DIMS RES_STR, 50
#DIMS KEEP_STR, 10

ARG = LIMIT(ARG, 1, 50)
LFLAG = 6

PRINTL 　　　　　생성 문자열　　　　　　　　　　　　　 보존
DO
	IF LFLAG & 2
		FOR LCOUNT, 0, ARG
			VARSET RESULTS
			IF GETBIT(LFLAG, 2) == 0 && GETBIT(LFLAG, 3) == 0
				IF RAND:2
					CALL GENERATE_WEAPON_STR
				ELSE
					CALL GENERATE_WEAPON_STR_JP
				ENDIF
			ELSEIF GETBIT(LFLAG, 2)
				CALL GENERATE_WEAPON_STR
			ELSE
				CALL GENERATE_WEAPON_STR_JP
			ENDIF
			RES_STR:LCOUNT = %RESULTS%
			SIF RESULTS:1 != ""
				RES_STR:LCOUNT = %RES_STR:LCOUNT% <%RESULTS:1%>
		NEXT
		CLEARBIT LFLAG, 1
	ENDIF

	LFLAG |= 1
	FOR LCOUNT, 0, LIMIT(ARG, 10, 50)

		IF LCOUNT < ARG
			PRINTFORM 　[{LCOUNT, 2}] %RES_STR:LCOUNT, 32, LEFT% 
			PRINTBUTTON "→ ", LCOUNT + 300
		ELSE
			PRINTFORM 　　　 %UNICODE(0x2000) * 32%　 
		ENDIF

		PRINTPLAIN  

		IF LCOUNT < 10
			IF KEEP_STR:LCOUNT == ""
				PRINT [--]　―――　　　　　　
			ELSE
				PRINTFORM [{LCOUNT + 50}] %KEEP_STR:LCOUNT, 32, LEFT% [{LCOUNT + 60}] 삭제
			ENDIF
		ELSE
			PRINT 　　　　　　　　　　　　
		ENDIF

		IF LCOUNT == ARG - 3
			PRINTFORM [110] 가타명　 \@GETBIT(LFLAG, 2)?%UNICODE(0x2611)%#%UNICODE(0x2610)%\@
		ENDIF

		IF LCOUNT == ARG - 2
			PRINTFORM [120] 일본명　 \@GETBIT(LFLAG, 3)?%UNICODE(0x2611)%#%UNICODE(0x2610)%\@
		ENDIF

		IF LCOUNT == ARG - 1
			PRINTFORM [130] 랜덤　　 \@GETBIT(LFLAG, 2) == 0 && GETBIT(LFLAG, 3) == 0?%UNICODE(0x2611)%#%UNICODE(0x2610)%\@
		ENDIF

		PRINTL 
	NEXT

	PRINTL 
	PRINTL  [100] 재생성
	DRAWLINE
	PRINTL  [999] 돌아간다

	INPUT

	IF RESULT == 999
		LFLAG = 0
		RESULTS = 
		LOCAL = 0
	ELSEIF RESULT == 100
		LFLAG |= 2
	ELSEIF RESULT == 110
		SETBIT LFLAG, 2
		CLEARBIT LFLAG, 3
		LFLAG |= 2
	ELSEIF RESULT == 120
		CLEARBIT LFLAG, 2
		SETBIT LFLAG, 3
		LFLAG |= 2
	ELSEIF RESULT == 130
;		LFLAG &= 4
;		LFLAG &= 8
		CLEARBIT LFLAG, 2
		CLEARBIT LFLAG, 3
		LFLAG |= 2
	ELSEIF RESULT >= 0 && RESULT < ARG
		PRINTL 
		PRINTFORML %RES_STR:RESULT%
		PRINTL 　
		WAIT
		RESULTS = %RES_STR:RESULT%
		CLEARLINE 3
		LFLAG = 0
		LOCAL = 1
	ELSEIF RESULT >= 50 && RESULT < 60
		PRINTL 
		PRINTFORML %KEEP_STR:(RESULT - 50)%
		PRINTL 　
		WAIT
		RESULTS = %KEEP_STR:(RESULT - 50)%
		CLEARLINE 3
		LFLAG = 0
		LOCAL = 1
	ELSEIF RESULT >= 300 && RESULT < ARG + 300
		IF KEEP_VAR < 10
			KEEP_STR:KEEP_VAR = %RES_STR:(RESULT - 300)%
			KEEP_VAR += 1
		ELSE
			FOR LCOUNT, 1, 10
				KEEP_STR:(LCOUNT - 1) = %KEEP_STR:LCOUNT%
			NEXT
			KEEP_STR:(KEEP_VAR - 1) = %RES_STR:(RESULT - 300)%
		ENDIF
	ELSEIF RESULT >= 60 && RESULT < KEEP_VAR + 60
		FOR LCOUNT, RESULT % 10 + 1, 10
			KEEP_STR:(LCOUNT - 1) = %KEEP_STR:LCOUNT%
		NEXT
		KEEP_STR:9 = 
		KEEP_VAR -= 1
	ENDIF
	SIF LFLAG != 0
		CLEARLINE LIMIT(ARG, 10, 50) + 5
LOOP LFLAG & 1
RETURN LOCAL

@WEAPON_ADD_STRS, ARG, ARG:1
#DIM INS_VAR, 7
#DIM ENT_VAR
#DIM LFLAG
;루프 변수
#DIM LCOUNT, 2

#DIM RES_VAR, 10
;항목명
#DIMS ENT_STR
;
#DIMS INS_STR
;
#DIMS OR_STR, 2
#DIMS RES_STR, 10
#DIMS PRE_NAME, 3, 3
#DIMS SELF_CL

IF CDFLAG:ARG:(ARG:1):무기명원위치
	SUBSTRING CSTR:ARG:(ARG:1 + 4), CDFLAG:ARG:(ARG:1):무기명원위치 / 100, CDFLAG:ARG:(ARG:1):무기명원위치 % 100
	LOCALS = %RESULTS%
ELSE
	LOCALS = %CSTR:ARG:(ARG:1 + 4)%
ENDIF

VARSET OR_STR
;이름과 읽기를 분리
SPLIT LOCALS, " <", OR_STR
SIF OR_STR:1 != ""
	OR_STR:1 = %UNICODE(0x0020)%<%OR_STR:1%
PRINTL 
LFLAG = 1

PRINTFORML 　　무기명：%OR_STR:0%%OR_STR:1%　　　　전투 스타일：%FSTYLE_NAME_F(ARG,ARG:1)%
DO
	WHILE LCOUNT:0 < 7 && GETBIT(LFLAG, 0)
		IF GETBIT(LFLAG, 2)
;검	참　　　　
			IF INS_VAR:5 == 1
				INS_VAR:6 = 1
;도	참　　　　
			ELSEIF INS_VAR:5 == 2
				INS_VAR:6 = 1
;창	　　돌　　
			ELSEIF INS_VAR:5 == 3
				INS_VAR:6 = 4
;도끼	참타　　　
			ELSEIF INS_VAR:5 == 4
				INS_VAR:6 = 3
;망치	　타　　　
			ELSEIF INS_VAR:5 == 5
				INS_VAR:6 = 2
;권	　타돌　　
			ELSEIF INS_VAR:5 == 6
				INS_VAR:6 = 6
;조	참타　　　
			ELSEIF INS_VAR:5 == 7
				INS_VAR:6 = 3
;곤	　타　　　
			ELSEIF INS_VAR:5 == 8
				INS_VAR:6 = 2
;장	　타　　술
			ELSEIF INS_VAR:5 == 9
				INS_VAR:6 = 18
;낫	참　　　술
			ELSEIF INS_VAR:5 == 10
				INS_VAR:6 = 17
;채찍	　타　　　
			ELSEIF INS_VAR:5 == 11
				INS_VAR:6 = 2
;강사	참　　　　
			ELSEIF INS_VAR:5 == 12
				INS_VAR:6 = 1
;체인톱	참　　　　
			ELSEIF INS_VAR:5 == 13
				INS_VAR:6 = 1
;말뚝창	　타돌　　
			ELSEIF INS_VAR:5 == 14
				INS_VAR:6 = 6
;궁	　　　사　
			ELSEIF INS_VAR:5 == 15
				INS_VAR:6 = 8
;총	　　　사　
			ELSEIF INS_VAR:5 == 16
				INS_VAR:6 = 8
;포	　타　사　
			ELSEIF INS_VAR:5 == 17
				INS_VAR:6 = 10
;부	　　　　술
			ELSEIF INS_VAR:5 == 18
				INS_VAR:6 = 16
;주	　　　　술
			ELSEIF INS_VAR:5 == 19
				INS_VAR:6 = 16
			ELSE
				INS_VAR:6 = 0
			ENDIF
			CLEARBIT LFLAG, 2
		ENDIF
		ENT_STR = 
		IF LCOUNT:0 == 0
			ENT_VAR = 2
			ENT_STR = <<읽기>>
		ELSEIF LCOUNT:0 == 1
			ENT_VAR = 3
			ENT_STR = <<추가 문자열>>
		ELSEIF LCOUNT:0 == 2
			ENT_VAR = 4
			ENT_STR = <<단락>>
		ELSEIF LCOUNT:0 == 3
			ENT_VAR = 3
			ENT_STR = <<강조>>
		ELSEIF LCOUNT:0 == 4
			ENT_VAR = 10
			ENT_STR = <<속성>>
		ELSEIF LCOUNT:0 == 5
			ENT_VAR = 21
			ENT_STR = <<무기종>>
		ELSEIF LCOUNT:0 == 6
			ENT_VAR = 5
			ENT_STR = <<공격 속성>>
		ENDIF
		FOR LCOUNT:1, 0, ENT_VAR
			IF LCOUNT:0 == 0
				IF LCOUNT:1 == 0
					INS_STR = 있음
				ELSEIF LCOUNT:1 == 1
					INS_STR = 없음
				ENDIF
			ELSEIF LCOUNT:0 == 1
				IF LCOUNT:1 == 0
					INS_STR = 없음
				ELSEIF LCOUNT:1 == 1
					INS_STR = 접두사
				ELSEIF LCOUNT:1 == 2
					INS_STR = 접미사
				ENDIF
			ELSEIF LCOUNT:0 == 2
				IF LCOUNT:1 == 0
					INS_STR = 없음
				ELSEIF LCOUNT:1 == 1
					INS_STR = 반각( )
				ELSEIF LCOUNT:1 == 2
					INS_STR = 전각(　)
				ELSEIF LCOUNT:1 == 3
					INS_STR = 중점(・)
				ENDIF
			ELSEIF LCOUNT:0 == 3
				IF LCOUNT:1 == 0
					INS_STR = 없음
				ELSEIF LCOUNT:1 == 1
					INS_STR = ""
				ELSEIF LCOUNT:1 == 2
					INS_STR = 「」
				ENDIF
			ELSEIF LCOUNT:0 == 4
				IF LCOUNT:1 == 0
					INS_STR = 없음
				ELSEIF LCOUNT:1 == 1
					INS_STR = 화
				ELSEIF LCOUNT:1 == 2
					INS_STR = 빙
				ELSEIF LCOUNT:1 == 3
					INS_STR = 뇌
				ELSEIF LCOUNT:1 == 4
					INS_STR = 수
				ELSEIF LCOUNT:1 == 5
					INS_STR = 풍
				ELSEIF LCOUNT:1 == 6
					INS_STR = 지
				ELSEIF LCOUNT:1 == 7
					INS_STR = 독
				ELSEIF LCOUNT:1 == 8
					INS_STR = 광
				ELSEIF LCOUNT:1 == 9
					INS_STR = 암
				ENDIF
			ELSEIF LCOUNT:0 == 5
				IF LCOUNT:1 == 0
					INS_STR = 없음
				ELSEIF LCOUNT:1 == 1
					INS_STR = 검
				ELSEIF LCOUNT:1 == 2
					INS_STR = 도
				ELSEIF LCOUNT:1 == 3
					INS_STR = 창
				ELSEIF LCOUNT:1 == 4
					INS_STR = 도끼
				ELSEIF LCOUNT:1 == 5
					INS_STR = 망치
				ELSEIF LCOUNT:1 == 6
					INS_STR = 권
				ELSEIF LCOUNT:1 == 7
					INS_STR = 조
				ELSEIF LCOUNT:1 == 8
					INS_STR = 곤
				ELSEIF LCOUNT:1 == 9
					INS_STR = 장
				ELSEIF LCOUNT:1 == 10
					INS_STR = 낫
				ELSEIF LCOUNT:1 == 11
					INS_STR = 채찍
				ELSEIF LCOUNT:1 == 12
					INS_STR = 강사
				ELSEIF LCOUNT:1 == 13
					INS_STR = 체인톱
				ELSEIF LCOUNT:1 == 14
					INS_STR = 말뚝창
				ELSEIF LCOUNT:1 == 15
					INS_STR = 궁
				ELSEIF LCOUNT:1 == 16
					INS_STR = 총
				ELSEIF LCOUNT:1 == 17
					INS_STR = 포
				ELSEIF LCOUNT:1 == 18
					INS_STR = 부
				ELSEIF LCOUNT:1 == 19
					INS_STR = 주
				ELSEIF LCOUNT:1 == 20
					INS_STR = 자유 입력
				ENDIF
			ELSEIF LCOUNT:0 == 6
				IF LCOUNT:1 == 0
					INS_STR = 참
				ELSEIF LCOUNT:1 == 1
					INS_STR = 타
				ELSEIF LCOUNT:1 == 2
					INS_STR = 돌
				ELSEIF LCOUNT:1 == 3
					INS_STR = 사
				ELSEIF LCOUNT:1 == 4
					INS_STR = 술
				ENDIF
			ENDIF

			IF LCOUNT:1 % 4 == 0
				PRINTL 
				PRINTFORM %ENT_STR, 14, LEFT%
				PRINTPLAIN 　
				ENT_STR = 
			ENDIF
			SIF INS_VAR:(LCOUNT:0) == LCOUNT:1 && LCOUNT:0 != 6
				SETCOLOR 128, 255, 128

			SIF GETBIT(INS_VAR:6, LCOUNT:1) && LCOUNT:0 == 6
				SETCOLOR 128, 255, 128

			;읽기가 없다
			SIF LCOUNT:0 == 0 && OR_STR:1 == ""
				SETCOLOR 128, 128, 128
			;접두사 이외
			SIF INS_VAR:1 != 1 && (LCOUNT:0 == 2 || LCOUNT:0 == 3)
				SETCOLOR 128, 128, 128
			;추가 문자열 없음
			SIF INS_VAR:1 == 0 && (LCOUNT:0 == 4 || LCOUNT:0 == 5 || LCOUNT:0 == 6)
				SETCOLOR 128, 128, 128

			PRINTFORM [{LCOUNT:0 * 10 + (LCOUNT:0 == 6) * 20 + LCOUNT:1, 2}] %INS_STR, 8, LEFT%
			IF LCOUNT:0 != 6
				PRINTFORM \@INS_VAR:(LCOUNT:0) == LCOUNT:1 ? %UNICODE(0x2611)% # %UNICODE(0x2610)%\@
			ELSE
				PRINTFORM \@GETBIT(INS_VAR:(LCOUNT:0), LCOUNT:1) ? %UNICODE(0x2611)% # %UNICODE(0x2610)%\@
			ENDIF

			SIF LCOUNT:0 == 5 && LCOUNT:1 == 20
				PRINTFORM \@SELF_CL == "" ?  # （%SELF_CL%）\@
			RESETCOLOR
			PRINTPLAIN 　 　
		NEXT
		LCOUNT:0 += 1
		PRINTL 
	WEND
	LCOUNT:0 = 0
	IF GETBIT(LFLAG, 0)
		PRINTL 
		PRINTL 　　　　　　　　　　　　　　　　　　　　　　　　　　　[100] 생성　　　　 [200]초기화　　
		DRAWLINE
		PRINTL  [999] 돌아간다
		INPUT
		IF RESULT < 0
		ELSEIF RESULT == 70
			INS_VAR:5 = 20
			PRINTL 무기의 종류를 입력해 주십시오.（１〜２글자 정도를 권장합니다）
			INPUTS
			SELF_CL = %RESULTS%
			SIF SELF_CL == ""
				INS_VAR:5 = 0
			CLEARLINE 1
		ELSEIF RESULT < 60
			INS_VAR:(RESULT / 10) = RESULT % 10
		ELSEIF RESULT < 70
			INS_VAR:5 = RESULT - 50
		ELSEIF RESULT >= 80 && RESULT < 85
			INVERTBIT INS_VAR:6, RESULT - 80
		ELSEIF RESULT == 100
			LFLAG = 2
		ELSEIF RESULT == 200
			VARSET INS_VAR
		ELSEIF RESULT == 999
			LFLAG = 0
		ELSE
			
		ENDIF
		SIF RESULT >= 50 && RESULT <= 70
			SETBIT LFLAG, 2
		SIF GETBIT(LFLAG, 0)
			CLEARLINE 27
	ENDIF

	WHILE LCOUNT:0 < 10 && GETBIT(LFLAG, 1)
		IF INS_VAR:2 == 0
			LOCALS:1 = 
		ELSEIF INS_VAR:2 == 1
			LOCALS:1 =  %UNICODE(0x0020)%
		ELSEIF INS_VAR:2 == 2
			LOCALS:1 = 　
		ELSEIF INS_VAR:2 == 3
			LOCALS:1 = ・
		ENDIF

		IF INS_VAR:3 == 0
			LOCALS:2 = 
			LOCALS:3 = 
		ELSEIF INS_VAR:3 == 1
			LOCALS:2 = "
			LOCALS:3 = "
		ELSEIF INS_VAR:3 == 2
			LOCALS:2 = 「
			LOCALS:3 = 」
		ENDIF

		CALL GENERATE_ADD_STR, CDFLAG:(ARG):(ARG:1):전투스타일, INS_VAR:1, INS_VAR:4, INS_VAR:5, INS_VAR:6, SELF_CL
		IF INS_VAR:1 == 0
			LOCALS:0 = 
			LOCALS:4 = 
		ELSEIF INS_VAR:1 == 1
			LOCALS:0 = %RESULTS%
			LOCALS:4 = 
		ELSEIF INS_VAR:1 == 2
			LOCALS:0 = 
			LOCALS:1 = 
			LOCALS:2 = 
			LOCALS:3 = 
			LOCALS:4 = 의 %RESULTS%
		ENDIF

		LOCALS:5 = %LOCALS:0%%LOCALS:1%%LOCALS:2%
		LOCALS:6 = %OR_STR:0%\@INS_VAR:0 == 0 ? %OR_STR:1% # \@
		LOCALS:7 = %LOCALS:3%%LOCALS:4%

		STRLENS LOCALS:5
		LOCAL:0 = RESULT
		STRLENS LOCALS:6
		LOCAL:1 = RESULT
		STRLENS LOCALS:7
		LOCAL:2 = RESULT
		RES_VAR:(LCOUNT:0) = LOCAL:0 * 100 + LOCAL:1

		RES_STR:(LCOUNT:0) = %LOCALS:5%%LOCALS:6%%LOCALS:7%

		PRINTFORML 　 [{LCOUNT:0 + 110}] %RES_STR:(LCOUNT:0)%
		LCOUNT:0 += 1
	WEND

	IF GETBIT(LFLAG, 1)
		PRINTL 
		PRINTL  [100] 재생성　 [200] 설정 변경
		DRAWLINE
		PRINTL  [999] 돌아간다
		LCOUNT:0 = 0
		INPUT
		IF RESULT == 200
			LFLAG = 1
;15+27
			CLEARLINE 42
		ELSEIF RESULT >= 110 && RESULT < 120
			RESULTS = %RES_STR:(RESULT - 110)%
			RETURN RES_VAR:(RESULT - 110)
			LFLAG = 0
		ELSEIF RESULT == 999
			LFLAG = 0
		ELSE
			CLEARLINE 15
		ENDIF
	ENDIF

LOOP LFLAG
