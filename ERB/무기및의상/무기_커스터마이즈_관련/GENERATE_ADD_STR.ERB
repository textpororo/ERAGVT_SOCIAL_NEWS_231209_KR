;접두어・접미어용 문자 추가 방법

;이 아래(75행째 이후)에 있는 각각의 카테고리의 문자군에, "/ (반각[半角]슬래시)" 로 나눠서
;마음에 드는 문자를 기입해 주세요.
;마지막 글자 뒤에 "/" 를 붙이면, 빈 글자를 반환하는 경우가 있다는 점 주의해 주세요.

@GENERATE_ADD_STR, WEAPON_ST, ADD_POS, WEAPON_AT, WEAPON_CL, ATTACK_AT, SELF_CL

#DIM CONST 연속 = 1
#DIM CONST 장갑 = 2
#DIM CONST 교란 = 3
#DIM CONST 중격 = 4
#DIM CONST 광역 = 5
#DIM CONST 전력 = 6
#DIM CONST 지략 = 7
#DIM CONST 설치 = 8
#DIM CONST 사역 = 9
#DIM CONST 전투스타일 = 10

#DIM CONST 화 = 11
#DIM CONST 빙 = 12
#DIM CONST 뇌 = 13
#DIM CONST 수 = 14
#DIM CONST 풍 = 15
#DIM CONST 지 = 16
#DIM CONST 독 = 17
#DIM CONST 광 = 18
#DIM CONST 암 = 19
#DIM CONST 속성 = 20

#DIM CONST 참 = 21
#DIM CONST 타 = 22
#DIM CONST 돌 = 23
#DIM CONST 사 = 24
#DIM CONST 술 = 25
#DIM CONST 참타 = 26
#DIM CONST 타돌 = 27
#DIM CONST 돌참 = 28
#DIM CONST 공격속성 = 29

#DIM CONST 무기없음 = 30
;剣 칼 검, 刀 칼 도
#DIM CONST 검 = 31
#DIM CONST 도 = 32
#DIM CONST 창 = 33
;斧 도끼 부 槌 망치 퇴, 망치 추. 장비명으로 주로 "~~ 도끼", "~~ 망치" 이렇게 나오곤 하니까 각각 도끼, 망치라고 번역했다
#DIM CONST 도끼 = 34
#DIM CONST 망치 = 35
#DIM CONST 권 = 36
;爪 손톱 조, 클로처럼 주먹에 긴 갈고리나 칼날을 다는 무기를 중국 북송 때 "조", "조갑(爪甲)" 이라는 이름으로 썼다 하여 "조" 라고 번역했다
#DIM CONST 조 = 37
;棍 몽둥이 곤, 쌍절곤 할 때 그 곤
#DIM CONST 곤 = 38
#DIM CONST 장 = 39
#DIM CONST 낫 = 40
#DIM CONST 채찍 = 41
;鋼糸 강철 실 이런 느낌
#DIM CONST 강사 = 42
;鎖鋸 사슬톱
#DIM CONST 체인톱 = 43
#DIM CONST 말뚝창 = 44
;弓 활 궁
#DIM CONST 궁 = 45
#DIM CONST 총 = 46
#DIM CONST 포 = 47
;符 부호 부, 면죄부 부적 할 때 그 부
#DIM CONST 부 = 48
#DIM CONST 주 = 49
#DIM CONST 무기종 = 50
#DIM CONST 전반 = 51

#DIM AD_VAR, 55
#DIMS AD_STR, 55
#DIMS AD_CHR, 55, 100

;추가 문자 수
#DIM ADD_LEN
;속성 문자 요소 수
#DIM AT_NO, 2
;문자 요소 수
#DIM CHAR_NO, 2

#DIM NOW_NO, 2
;전투스타일
#DIM WEAPON_ST
;속성
#DIM WEAPON_AT
;무기종　
#DIM WEAPON_CL
;공격속성　1:참　2:타　4:돌　8:사　16:술
#DIM ATTACK_AT

#DIM ADD_POS

#DIM LFLAG
#DIM LCOUNT, 2

#DIMS ADD_CHAR, 3
#DIMS SELF_CL

;====================================================================================
;		 /////     추가 문자 설정부     /////
;====================================================================================

;전투스타일
AD_STR:연속 = 준/순/신/섬/쌍/속/익/연
AD_STR:장갑 = 계/각/호/갑/경/강/채/수/순/장/전/비/벽
AD_STR:교란 = 은/영/질/순/신/섬/치/도/둔/인/무/사
AD_STR:중격 = 강/굉/중/대/괴/압/폭/쇄
;鏖 오살할 오
AD_STR:광역 = 압/확/향/산/진/파/폭/멸/섬/작/오
;獄 옥 옥, 尽 다할 진, 渾 흐릴 혼
AD_STR:전력 = 극/현/옥/혼/쇄/수/진/총/통/분/임/열/혼
;策 꾀 책, 読 읽을 독, 謀 꾀 모, 理 다스릴 리
AD_STR:지략 = 기/혜/현/책/술/선/지/도/독/모/이/약/혹
;陣 진칠 진, 浮 뜰 부, 마지막 덫은 罠 낚싯줄 민
AD_STR:설치 = 쇄/살/진/거/정/조/체/치/박/부/덫
;鳥 새 조, 操 잡을 조. 하나로 통일
AD_STR:사역 = 수/익/사/낭/조/군/역/부/식/사

;속성
;緋 비단 비
;炎 불꽃 염, 아름다울 담 焔 불꽃 염
AD_STR:화 = 화/염/담/혁/주/적/비
;零 떨어질 령
AD_STR:빙 = 빙/동/냉/영/은/청/창/백
;麟 기린 린
AD_STR:뇌 = 뇌/황/금/호/광/홍/굉/자/섬/전/백/린
;雫(물방울 놔)의 일본어 훈독은 しずく(물방울)
;霰 싸락눈 산
AD_STR:수 = 우/운/하/해/흑/놔/청/적/파/무/유/낭/도/박/산/청/창
;嵐 아지랑이 람 翠 푸를/물총새 취 碧 푸를 벽
AD_STR:풍 = 풍/남/취/청/벽/녹
AD_STR:지 = 지/진/토/황/취/벽/녹
;蠱 독벌레/고혹할 고
;死 죽을 사 蛇 긴뱀 사, 구불구불 갈 이
AD_STR:독 = 독/사/이/비/고/자
;星 별 성 聖 성인 성. 하나로 통일
AD_STR:광 = 광/성/정/천/양/익
AD_STR:암 = 암/음/원/사/심/마/명

;공격속성
;切 끊을 절, 온통 체 絶 끊을 절
AD_STR:참 = 참/절/체/섬
AD_STR:타 = 타/쇄/충/파
;刺 찌를 자/척
AD_STR:돌 = 돌/예/관/자/척/천/통
AD_STR:사 = 사/순/저/탄
;封 봉할 봉 奉 받들 봉. 하나로 통일
AD_STR:술 = 술/기/호/주/진/박/봉/법/마/영
AD_STR:참타 = 할/단
;穿 뚫을 천
AD_STR:타돌 = 관/쇄/충/천/파
;薙 깎을 체
AD_STR:돌참 = 체

;무기종
;무기종 지정 없음
AD_STR:무기없음 = 기/구/장
;刃 칼날 인 -> 날
AD_STR:검 = 검/날
AD_STR:도 = 도/날/태도
;무로마치 시대 후기에 만들어진 日本号 일본호라는 창이 있다고 한다
;鉾 칼끝 모, 창 끝부분에 칼이 달려있다는 얘기로 이해하고 도끼창(할버드)으로 번역했다
AD_STR:창 = 창/호/도끼창
AD_STR:도끼 = 도끼
AD_STR:망치 = 망치
AD_STR:권 = 권
AD_STR:조 = 조/클로
AD_STR:곤 = 곤/봉
AD_STR:장 = 장
AD_STR:낫 = 낫
AD_STR:채찍 = 채찍
;마지막 그물은 罠 낚싯줄 민
AD_STR:강사 = 강사/현/강선/실/철사/철선/그물
AD_STR:체인톱 = 전기톱/기계톱/기검/톱
AD_STR:말뚝창 = 말뚝창/철갑/파일 벙커/말뚝/기창/말뚝타
AD_STR:궁 = 궁
AD_STR:총 = 총
AD_STR:포 = 포
;札 편지 찰
AD_STR:부 = 부/패
AD_STR:주 = 주/옥/보옥/보주

;기타 추가용 문자
;影/王/牙/凶/業/銀/撃/血/護/皇/魂/死/浄/心/真/鎮/天/覇/縛/秘/封/宝/鳳/名/命/妖/裏/龍/霊/神
AD_STR:전반 = 그림자/왕/어금니/흉/업/은/격/혈/호/황/혼/사/정/심/진・/진/천/패/박/비/봉/밤/봉황/명/생명/요/이/용/영/신

;====================================================================================
;		 /////     　　이상　　     /////
;====================================================================================

FOR LCOUNT, 0, 55
	SPLIT AD_STR:LCOUNT, "/", AD_CHR:0:0
	AD_VAR:LCOUNT = RESULT
	REPEAT 100
		SIF AD_CHR:0:COUNT == ""
			BREAK
		AD_CHR:LCOUNT:COUNT = %AD_CHR:0:COUNT%
		AD_CHR:0:COUNT = 
	REND
NEXT

VARSET CHAR_NO
VARSET AT_NO

;무기종:20
CHAR_NO:1 = 1
AT_NO:1 = 1

FOR LCOUNT, 0, 55
	IF LCOUNT >= 화 && LCOUNT < 속성
		AT_NO:1 += AD_VAR:LCOUNT
	ELSE
		CHAR_NO:1 += AD_VAR:LCOUNT
	ENDIF
NEXT

ADD_LEN = RAND:4

SIF WEAPON_CL && ADD_POS != 2
	ADD_LEN += 1

ADD_LEN = LIMIT(ADD_LEN, 2, 3)

VARSET ADD_CHAR
VARSET LFLAG
VARSET NOW_NO

SIF ADD_LEN > 1
	NOW_NO:0 = RAND:2

DO
	CHAR_NO:0 = CHAR_NO:1
	AT_NO:0 = AT_NO:1

;속성 관련
;------------------------------------------
	FOR LCOUNT:1, 화, 속성
		SIF WEAPON_AT != LCOUNT:1 - 10
			CONTINUE
		FOR LCOUNT, 0, AD_VAR:(LCOUNT:1)
			AT_NO:0 -= 1
			IF WEAPON_AT == LCOUNT:1 - 10 && GETBIT(LFLAG, 1) == 0 && RAND:AT_NO == 0
				ADD_CHAR:(NOW_NO:0) = %AD_CHR:(LCOUNT:1):LCOUNT%
				SETBIT LFLAG, 0
				SETBIT LFLAG, 1
				GOTO END_ADD_CHAR
			ENDIF
		NEXT
	NEXT

;전반문자
;RAND를 부풀려서 나올 확률을 조금 저하시킨다
;------------------------------------------
	FOR LCOUNT, 0, AD_VAR:전반
		CHAR_NO:0 -= 1
		IF RAND:(CHAR_NO + 16) == 0
			ADD_CHAR:(NOW_NO:0) = %AD_CHR:전반:LCOUNT%
			SETBIT LFLAG, 0
			GOTO END_ADD_CHAR
		ENDIF
	NEXT

;전투스타일 관련
;------------------------------------------
	FOR LCOUNT:1, 연속, 전투스타일
		FOR LCOUNT, 0, AD_VAR:(LCOUNT:1)
			CHAR_NO:0 -= 1
			IF WEAPON_ST == LCOUNT:1 && GETBIT(LFLAG, 2) == 0 && RAND:CHAR_NO == 0
				ADD_CHAR:(NOW_NO:0) = %AD_CHR:(LCOUNT:1):LCOUNT%
				SETBIT LFLAG, 0
				SETBIT LFLAG, 2
				GOTO END_ADD_CHAR
			ENDIF
		NEXT
	NEXT

;공격속성 관련
;참 + 타
;------------------------------------------
	FOR LCOUNT, 0, AD_VAR:참타
		CHAR_NO:0 -= 1
		IF ATTACK_AT & 1 && ATTACK_AT & 2 && GETBIT(LFLAG, 3) == 0 && RAND:CHAR_NO == 0
			ADD_CHAR:(NOW_NO:0) = %AD_CHR:참타:LCOUNT%
			SETBIT LFLAG, 0
			SETBIT LFLAG, 3
			GOTO END_ADD_CHAR
		ENDIF
	NEXT

;타 + 돌
;------------------------------------------
	FOR LCOUNT, 0, AD_VAR:타돌
		CHAR_NO:0 -= 1
		IF ATTACK_AT & 2 && ATTACK_AT & 4 && GETBIT(LFLAG, 3) == 0 && RAND:CHAR_NO == 0
			ADD_CHAR:(NOW_NO:0) = %AD_CHR:타돌:LCOUNT%
			SETBIT LFLAG, 0
			SETBIT LFLAG, 3
			GOTO END_ADD_CHAR
		ENDIF
	NEXT

;참 + 돌
;------------------------------------------
	FOR LCOUNT, 0, AD_VAR:돌참
		CHAR_NO:0 -= 1
		IF ATTACK_AT & 4 && ATTACK_AT & 1 && GETBIT(LFLAG, 3) == 0 && RAND:CHAR_NO == 0
			ADD_CHAR:(NOW_NO:0) = %AD_CHR:돌참:LCOUNT%
			SETBIT LFLAG, 0
			SETBIT LFLAG, 3
			GOTO END_ADD_CHAR
		ENDIF
	NEXT

;------------------------------------------
	FOR LCOUNT:1, 참, 공격속성
		FOR LCOUNT, 0, AD_VAR:(LCOUNT:1)
			CHAR_NO:0 -= 1
			IF GETBIT(ATTACK_AT, LCOUNT:1 - 21) && GETBIT(LFLAG, 3) == 0 && RAND:CHAR_NO == 0
				ADD_CHAR:(NOW_NO:0) = %AD_CHR:(LCOUNT:1):LCOUNT%
				SETBIT LFLAG, 0
				SETBIT LFLAG, 3
				GOTO END_ADD_CHAR
			ENDIF
		NEXT
	NEXT

;------------------------------------------
;	IF CHAR_NO:1 == 0
;		CHAR_NO:0 += 1
;	ELSE
;		CHAR_NO:0 -= 1
;		IF ATTACK_AT & 8 && RAND:CHAR_NO == 0
;			ADD_CHAR:(NOW_NO:0) = 심안
;			SETBIT LFLAG, 0
;			GOTO END_ADD_CHAR
;		ENDIF
;	ENDIF

;무기종 관련
;------------------------------------------
	FOR LCOUNT:1, 30, 50
		FOR LCOUNT, 0, AD_VAR:(LCOUNT:1)
			CHAR_NO:0 -= 1
			IF WEAPON_CL == LCOUNT:1 - 30 && NOW_NO:0 == ADD_LEN - 1 && RAND:CHAR_NO == 0
				ADD_CHAR:(NOW_NO:0) = %AD_CHR:(LCOUNT:1):LCOUNT%
				SETBIT LFLAG, 0
				SETBIT LFLAG, 4
				GOTO END_ADD_CHAR
			ENDIF
		NEXT
	NEXT

;자유 입력
;------------------------------------------
	IF WEAPON_CL == 20 && NOW_NO:0 == ADD_LEN - 1
		ADD_CHAR:(NOW_NO:0) = %SELF_CL%
		SETBIT LFLAG, 0
		SETBIT LFLAG, 4
		GOTO END_ADD_CHAR
	ENDIF
	$END_ADD_CHAR

	;요소 수 결정
	;어떤 요소에 히트했다
	IF LFLAG & 1
		CLEARBIT LFLAG, 0
		;문자가 겹친다
		SIF (ADD_CHAR:0 != "" && ADD_CHAR:0 == ADD_CHAR:1) || (ADD_CHAR:1 != "" && ADD_CHAR:1 == ADD_CHAR:2) || (ADD_CHAR:2 != "" && ADD_CHAR:2 == ADD_CHAR:0)
			CONTINUE

		IF NOW_NO:0 == ADD_LEN - 1
			;마지막이 무기종이 아니다
			SIF GETBIT(LFLAG, 4) == 0 && ((ADD_LEN == 3 && RAND:8) || (ADD_LEN == 2 && RAND:6))
				CONTINUE
			SIF ADD_POS == 2 && GETBIT(LFLAG, 4) == 0 && RAND:8
				CONTINUE
			;4문자 이상
			STRLENSU ADD_CHAR:(NOW_NO:0)
			SIF RESULT > 1 && ADD_LEN > 2 && RAND:3
				CONTINUE
			;ㅁ?ㄹ
			SIF ADD_LEN == 2 && RESULT > 1 && RAND:3 == 0
				ADD_CHAR:0 = 

		ENDIF
		IF NOW_NO:1 == 0
			INVERTBIT NOW_NO:0, 0
		ELSE
			NOW_NO:0 = 2
		ENDIF
		NOW_NO:1 += 1
		CLEARBIT LFLAG, 0
	ENDIF

LOOP NOW_NO:1 < ADD_LEN
RESULTS = %ADD_CHAR:0%%ADD_CHAR:1% %ADD_CHAR:2%
