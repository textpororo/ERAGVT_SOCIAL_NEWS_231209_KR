;무기 커스터마이즈 기능에 관한 처리군
;------------------------------------------------------------------------------------------------------------------------------

;무기 커스터마이즈 화면
;-------------------------------------------------
;ARG   = 캐릭터 번호
;ARG:1 = 캐릭터 메이킹 중이라면 1
@WEAPON_CUSTOMIZE, ARG, ARG:1 = 0
#DIM CONST LENGTH = 60
$REPRINT
CALL LB
VARSET LOCAL
VARSET LOCALS
PRINTL ---///　　　　ＷＥＡＰＯＮ　ＣＵＳＴＯＭＩＺＥ　　///----------------
PRINTL 
PRINTFORML %NAME:ARG%　의 현재 장비
CALL SHORTLINE
PRINTFORML ◆근거리 무기
IF CSTR:ARG:5 != ""
	PRINTFORML ├[10] 무기명 『%CSTR:ARG:5%』
ELSE
	PRINTFORML ├[10] 무기명 （무기명이 설정되어 있지 않습니다.）
ENDIF
RESETCOLOR
CALL FSTYLE_ATTACK,ARG,1,1
PRINTFORML └[12] 전투 스타일　<<%FSTYLE_NAME_F(ARG,1)%>>%"", LENGTH - 40, LEFT%
PRINTFORM 　　　　　---> 위력　　　：【{RESULT}】
CALL SET_FSTYLE_INFO,ARG,1
PRINTFORML （ %RESULTS:0% ）
PRINTFORML 　　　　　---> %RESULTS:1%
PRINTFORML 　　　　　---> %RESULTS:2%

CALL SHORTLINE
PRINTFORML ◆중거리 무기
IF CSTR:ARG:6 != ""
	PRINTFORML ├[20] 무기명 『%CSTR:ARG:6%』
ELSE
	PRINTFORML ├[20] 무기명 （무기명이 설정되어 있지 않습니다.）
ENDIF
RESETCOLOR
CALL FSTYLE_ATTACK,ARG,2,1
PRINTFORML └[22] 전투 스타일　<<%FSTYLE_NAME_F(ARG,2)%>>%"", LENGTH - 40, LEFT%
PRINTFORM 　　　　　---> 위력　　　：【{RESULT}】
CALL SET_FSTYLE_INFO,ARG,2
PRINTFORML （ %RESULTS:0% ）
PRINTFORML 　　　　　---> %RESULTS:1%
PRINTFORML 　　　　　---> %RESULTS:2%

CALL SHORTLINE
PRINTFORML ◆원거리 무기
IF CSTR:ARG:7 != ""
	PRINTFORML ├[30] 무기명 『%CSTR:ARG:7%』
ELSE
	PRINTFORML ├[30] 무기명 （무기명이 설정되어 있지 않습니다.）
ENDIF
RESETCOLOR
CALL FSTYLE_ATTACK,ARG,3,1
PRINTFORML └[32] 전투 스타일　<<%FSTYLE_NAME_F(ARG,3)%>>%"", LENGTH - 40, LEFT%
PRINTFORM 　　　　　---> 위력　　　：【{RESULT}】
CALL SET_FSTYLE_INFO,ARG,3
PRINTFORML （ %RESULTS:0% ）
PRINTFORML 　　　　　---> %RESULTS:1%
PRINTFORML 　　　　　---> %RESULTS:2%
DRAWLINE
PRINTL  [999] 돌아간다
INPUT
IF RESULT == 999
	RETURN
ELSEIF RESULT == 10 || RESULT == 20 || RESULT == 30
	CALL SETTING_WEAPON_NAME, ARG, (RESULT / 10)
	GOTO REPRINT
ELSEIF RESULT == 12 || RESULT == 22 || RESULT == 32
	CALL SETTING_FSTYLE, ARG, (RESULT / 10)
	GOTO REPRINT
ELSE
	GOTO REPRINT
ENDIF






;무기의 이름을 설정
@SETTING_WEAPON_NAME,ARG,ARG:1
$REPRINT
IF ARG:1 == 1
	PRINT 근거리 
ELSEIF ARG:1 == 2
	PRINT 중거리 
ELSEIF ARG:1 == 3
	PRINT 원거리 
ENDIF
PRINTL  무기의 이름을 정해 주십시오.
PRINT 현재 무기명 ：
IF CSTR:ARG:(ARG:1 + 4) != ""
	PRINTFORML 『%CSTR:ARG:(ARG:1 + 4)%』
ELSE
	PRINTFORML （무기명이 설정되어 있지 않습니다.）
ENDIF
PRINTL 
PRINTL  [0] 직접 입력한다
PRINTL  [1] 랜덤 생성 기능을 사용한다
SIF CSTR:ARG:(ARG:1 + 4) == ""
	SETCOLOR 96, 96, 96
PRINTFORML  [\@CSTR:ARG:(ARG:1 + 4) == "" ? - # 2 \@] 추가 문자열 설정
RESETCOLOR
PRINTL  [3] 소거한다
SIF CSTR:ARG:5 != "" && ARG:1 != 1
	PRINTFORML  [4] 근거리와 같은 이름　『%CSTR:ARG:5%』
SIF CSTR:ARG:6 != "" && ARG:1 != 2
	PRINTFORML  [5] 중거리와 같은 이름　『%CSTR:ARG:6%』
SIF CSTR:ARG:7 != "" && ARG:1 != 3
	PRINTFORML  [6] 원거리와 같은 이름　『%CSTR:ARG:7%』
PRINTL  [999] 돌아간다
$INPUT_LOOP
INPUT
IF RESULT == 0
	CLEARLINE 1
	PRINTL 
	CALL SHORTLINE
	IF ARG:1 == 1
		PRINT 근거리 
	ELSEIF ARG:1 == 2
		PRINT 중거리 
	ELSEIF ARG:1 == 3
		PRINT 원거리 
	ENDIF
	PRINTL 무기에 설정할 이름을 입력해 주십시오：
	INPUTS
	IF ARG:1 == 1
		CSTR:ARG:5 = %RESULTS%
	ELSEIF ARG:1 == 2
		CSTR:ARG:6 = %RESULTS%
	ELSEIF ARG:1 == 3
		CSTR:ARG:7 = %RESULTS%
	ENDIF
	CDFLAG:ARG:(ARG:1):무기명원위치 = 0
	GOTO REPRINT
ELSEIF RESULT == 1
	CLEARLINE 1
	CALL GENERATE_WEAPON_STRS, 20
	IF ARG:1 == 1 && RESULT
		CSTR:ARG:5 = %RESULTS%
	ELSEIF ARG:1 == 2 && RESULT
		CSTR:ARG:6 = %RESULTS%
	ELSEIF ARG:1 == 3 && RESULT
		CSTR:ARG:7 = %RESULTS%
	ENDIF
	CDFLAG:ARG:(ARG:1):무기명원위치 = 0
	GOTO REPRINT
ELSEIF RESULT == 2 && CSTR:ARG:(ARG:1 + 4) != ""
	CLEARLINE 1
	CALL WEAPON_ADD_STRS, ARG, ARG:1
	IF RESULT
		CSTR:ARG:(ARG:1 + 4) = %RESULTS%
		CDFLAG:ARG:(ARG:1):무기명원위치 = RESULT
	ENDIF
	GOTO REPRINT
ELSEIF RESULT == 3
	CLEARLINE 1
	IF ARG:1 == 1
		CSTR:ARG:5 = 
	ELSEIF ARG:1 == 2
		CSTR:ARG:6 = 
	ELSEIF ARG:1 == 3
		CSTR:ARG:7 = 
	ENDIF
	CDFLAG:ARG:(ARG:1):무기명원위치 = 0
	PRINTL 무기명을 소거했습니다.
	PRINTW 
	GOTO REPRINT
ELSEIF RESULT == 4 && CSTR:ARG:5 != "" && ARG:1 != 1
	IF ARG:1 == 2
		CSTR:ARG:6 = %CSTR:ARG:5%
		CDFLAG:ARG:2:무기명원위치 = CDFLAG:ARG:1:무기명원위치
	ELSE
		CSTR:ARG:7 = %CSTR:ARG:5%
		CDFLAG:ARG:3:무기명원위치 = CDFLAG:ARG:1:무기명원위치
	ENDIF
	RETURN
ELSEIF RESULT == 5 && CSTR:ARG:6 != "" && ARG:1 != 2
	IF ARG:1 == 1
		CSTR:ARG:5 = %CSTR:ARG:6%
		CDFLAG:ARG:1:무기명원위치 = CDFLAG:ARG:2:무기명원위치
	ELSE
		CSTR:ARG:7 = %CSTR:ARG:6%
		CDFLAG:ARG:3:무기명원위치 = CDFLAG:ARG:2:무기명원위치
	ENDIF
	RETURN
ELSEIF RESULT == 6 && CSTR:ARG:7 != "" && ARG:1 != 3
	IF ARG:1 == 1
		CSTR:ARG:5 = %CSTR:ARG:7%
		CDFLAG:ARG:1:무기명원위치 = CDFLAG:ARG:3:무기명원위치
	ELSE
		CSTR:ARG:6 = %CSTR:ARG:7%
		CDFLAG:ARG:2:무기명원위치 = CDFLAG:ARG:3:무기명원위치
	ENDIF
	RETURN
ELSEIF RESULT == 999
	RETURN
ELSE
	GOTO INPUT_LOOP
ENDIF



;전투 스타일을 설정
@SETTING_FSTYLE,ARG,ARG:1
IF ARG:1 == 1
	PRINT 근거리 
ELSEIF ARG:1 == 2
	PRINT 중거리 
ELSEIF ARG:1 == 3
	PRINT 원거리 
ENDIF
PRINTL 무기의 전투 스타일을 선택해 주십시오.
PRINTL ※[통상] 이외의 동일한 전투 스타일을 2개 이상 중복하여 선택할 수 없습니다.
PRINTFORML 현재 전투 스타일：<<%FSTYLE_NAME_F(ARG,ARG:1)%>>
PRINTL 
PRINTL  [0] <<통상>>　　　통상적인 전투 스타일　　　　 어느 쪽에도 편향되지 않아 다루기가 쉬운 계열（+ 버스트 공격의 반동↓）
PRINTL 
PRINTL  [1] <<연속>>　　【민첩】이 높은 캐릭터 추천　　연속으로 ２회 공격이 가능하여 명중률은 단연 탑급
PRINTL 
PRINTL  [2] <<교란>>　　【민첩】이 높은 캐릭터 추천　　치명타율과 위력이 높지만 가드가 약한 하이리스크 하이리턴형
PRINTL 
PRINTL  [3] <<장갑>>　　【방어】가 높은 캐릭터 추천　　가드가 단단해져 서툰 거리의 서포트에 능하다
PRINTL 
PRINTL  [4] <<중격>>　　【방어】가 높은 캐릭터 추천　　회피율이나 직격률은 뒤처지지만 위력이 높아 고점은 확실한 타입
PRINTL 
PRINTL  [5] <<반격>>　　【방어】가 높은 캐릭터 추천　　상대방의 공격을 막는 동시에 강력한 일격으로 반격하는 딜러형 탱커
PRINTL 
PRINTL  [6] <<광역>>　　【공격】이 높은 캐릭터 추천　　직격하지 않더라도 위력이 높아 지속적인 피해를 주기에 적합하다
PRINTL 
PRINTL  [7] <<전력>>　　【공격】이 높은 캐릭터 추천　　위력이 매우 높지만 반동도 큰 필사의 일격을 가한다
PRINTL 
;PRINTL   [8] <<속성>>　　【공격】이 높은 캐릭터 추천　　공격을 제외한 방어, 민첩, 지성 중 가장 높은 스탯에 따라 추가 효과를 부여한다 (아직 미정)
;PRINTL 
PRINTL  [8] <<지략>>　　【지성】이 높은 캐릭터 추천　　지성이 높을 수록 직격하기 쉬워진다
PRINTL 
PRINTL  [9] <<설치>>　　【지성】이 높은 캐릭터 추천　　위력은 낮지만 상대방의 빈틈을 만드는 추가 효과를 가진다
PRINTL 
PRINTL [10] <<사역>>　　【지성】이 높은 캐릭터 추천　　상황에 따라 다채로운 공격을 펼칠 수 있다
PRINTL 
PRINTL  [999] 돌아간다
$INPUT_LOOP
INPUT
IF RESULT >= 0 && RESULT <= 10
	CLEARLINE 1

	[SKIPSTART]
	IF ARG:1 == 1
		CDFLAG:ARG:근거리:전투스타일 = RESULT
	ELSEIF ARG:1 == 2
		CDFLAG:ARG:중거리:전투스타일 = RESULT
	ELSEIF ARG:1 == 3
		CDFLAG:ARG:원거리:전투스타일 = RESULT
	ENDIF
	[SKIPEND]

	SELECTCASE ARG:1
		CASE 1
			IF RESULT != 0 && GROUPMATCH(RESULT, CDFLAG:ARG:중거리:전투스타일, CDFLAG:ARG:원거리:전투스타일)
				PRINTW 동일한 전투 스타일은 2개 이상 중복해서 선택할 수 없습니다.
				CLEARLINE 1
				GOTO INPUT_LOOP
			ELSE
				CDFLAG:ARG:근거리:전투스타일 = RESULT
			ENDIF
		CASE 2
			IF RESULT != 0 && GROUPMATCH(RESULT, CDFLAG:ARG:근거리:전투스타일, CDFLAG:ARG:원거리:전투스타일)
				PRINTW 동일한 전투 스타일은 2개 이상 중복해서 선택할 수 없습니다.
				CLEARLINE 1
				GOTO INPUT_LOOP
			ELSE
				CDFLAG:ARG:중거리:전투스타일 = RESULT
			ENDIF
		CASE 3
			IF RESULT != 0 && GROUPMATCH(RESULT, CDFLAG:ARG:근거리:전투스타일, CDFLAG:ARG:중거리:전투스타일)
				PRINTW 동일한 전투 스타일은 2개 이상 중복해서 선택할 수 없습니다.
				CLEARLINE 1
				GOTO INPUT_LOOP
			ELSE
				CDFLAG:ARG:원거리:전투스타일 = RESULT
			ENDIF
	ENDSELECT

	RETURN
ELSEIF RESULT == 999
	RETURN
ELSE
	GOTO INPUT_LOOP
ENDIF

	