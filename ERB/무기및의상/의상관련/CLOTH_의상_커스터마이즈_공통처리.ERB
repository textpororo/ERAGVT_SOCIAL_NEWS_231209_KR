;의상 데이터베이스(커스터마이즈) 압축을 위한 공통부 자르기
;ARG:참조 캐릭터
;ID:의상 ID
;MODE:데이터베이스에서 취득하는 파라미터
;SHOPR:
;	데이터 문자열에서 검색하여 수치를 잘라내고 싶을 뿐(보통의 언어에 있어서의 매치)이지만 ERA에서는 매우 번거로운 일이 되어 버렸다
;	여기서부터 순종적에 split해서 루프로 돌리는 편이 알기 쉬울지도 모르겠네
@CLOTH_HOSEI(ARG,ID,MODE,SHOPR)
#DIM ID,1
#DIMS MODE,1
#DIM SHOPR,1
;발견 위치
	#DIM sp,1
	#DIM ep,1
	;구분 문자
	#DIM sep,1
	;함수 개별 호출 필수 플래그(분기 조건이 특수한 경우…하카마의「겉옷이 없으면 회피 상승」등)
	#DIM needcall,1
;결과
#DIM res,1

;스테이터스 보정 문자열 획득
TRYCCALLFORM CLOTH_STATUS_{ID}
CATCH
	SAVESTR:0'=""
ENDCATCH
LOCALS'=SAVESTR:0

IF LOCALS==""
	;
ENDIF

sp=-1
ep=-1
sep=-1
needcall=-1
res=-99999

;●평상시의 데이터를 검색
STRFIND LOCALS,MODE,0
sp=RESULT

IF sp>-1
	;포인터를 키워드 직후(=파라미터 기술 위치)로 이동
	STRLENS ""+mode
	sp+=RESULT

	;개별적으로 함수를 부를 필요가 있는(복잡한 분기 조건을 가진) 경우
	STRFIND LOCALS,"!",sp
	IF RESULT==sp
		CALLFORM CLOTH_HOSEI_%MODE%_{ID}
		res=RESULT
	;통상의 문자열 데이터로 지정되어 있는 경우
	ELSE

		;변신 후 파라미터 변동하는 경우
		STRFIND LOCALS,"@",sp
		ep=RESULT
		;발견한 "@"가 구분 문자보다 오른쪽에 있으면 다른 항목에 대한 오폭이므로 취소
		STRFIND LOCALS,",",sp
		sep=RESULT
		SIF ep>sep
			ep=-1


		;PRINTL
		;PRINTFORML %MODE%:%LOCALS%⇒{sp},{ep-sp}=★{res}

		;변신 후 데이터가 있는 경우
		IF ep>-1
			SUBSTRING LOCALS,sp,ep-sp
			res=toint(RESULTS)
			;변신 중
			IF CFLAG:ARG:1>0
				sp=ep+1
				STRFIND LOCALS,",",sp
				ep=RESULT
				SUBSTRING LOCALS,sp,ep-sp

				res=toint(RESULTS)

			ENDIF
		;변신 후 데이터가 없는 경우
		ELSE
			STRFIND LOCALS,",",sp
			ep=RESULT
			SUBSTRING LOCALS,sp,ep-sp

			res=toint(RESULTS)

		ENDIF
	ENDIF

	;PRINTFORML %MODE%:%LOCALS%⇒{sp},{ep-sp}=★{res}

ENDIF


;●보정을 발견할 수 없다 → 파라미터별 디폴트값을 반환한다
IF res==-99999
	SIF SHOPR == 1
		RETURN res
	IF MODE=="KOUGEKI" || MODE=="BOUGYO" || MODE=="BINSYOU" || MODE=="CHISEI" || MODE=="SEITAISEI" || MODE=="YUDAN" || MODE=="LIQUID" || MODE=="WAVE" || MODE=="SHYNESS"
		res=100
	ELSEIF MODE=="TAIRYOKU" || MODE=="KIRYOKU" 
		res=95
	ELSEIF MODE=="DEF"
		IF ID >= 300 && ID <= 400
			res=DEFAULT_INNER_DEF
		ELSE
			res=DEFAULT_OUTER_DEF
		ENDIF
	ELSEIF MODE=="SLOT"
		;양의 값이라면ｎ개의 범용 슬롯, 0이라면 고유 커스터마이즈, -1이라면 완전히 커스터마이즈 없음
		res=0
	ELSE
		res=0
	ENDIF
;●보정 발견 시
ELSE
	;
ENDIF



;●파라미터의 종류에 따라 보정을 계산
;　단「장비 없음」일 경우 EQUIP에 의상 커스터마이즈 데이터가 재하지 않기 때문에 무조건 디폴트값을 반환한다
IF ID==0
	RETURN res
ENDIF
;중장・경장 레벨
IF CFLAG:1 == 0 && ID >= 100 && ID <= 199
	LOCAL = ID - 100
ELSE
	LOCAL = ID
ENDIF
IF MODE=="HP"
	;디폴트로 내구를 가지지않는 의상은 노터치
	IF res!=-1
		res += 5 * (FIGURE_SPLIT(EQUIP:LOCAL,11) - FIGURE_SPLIT(EQUIP:LOCAL,10))
	ENDIF
ELSEIF MODE=="HIT"
	res *=-1
	res += 1 * (FIGURE_SPLIT(EQUIP:LOCAL,10) - FIGURE_SPLIT(EQUIP:LOCAL,11))
;속옷 보호・삽입 저항
ELSEIF MODE=="DEF"
	res -= 5 * (FIGURE_SPLIT(EQUIP:LOCAL,13) - FIGURE_SPLIT(EQUIP:LOCAL,12))

ENDIF



RETURN res







@CLOTH_CUSTOMIZE_OPTION_DRAW(ID,ARG)
#DIM CUSTOM, 9
#DIM ID
#DIM EQUIPID
RESULT = 0
IF ID >= 100 && ID <= 199
	EQUIPID = ID - (CFLAG:1==0 ?100 #0)
ELSE
	EQUIPID = ID
ENDIF

;PRINTFORML {ARG}:{EQUIPID}= {EQUIP:ARG:EQUIPID}

REPEAT 9
	CUSTOM:COUNT = FIGURE_SPLIT(EQUIP:ARG:EQUIPID, COUNT + 1)
REND
TRYCCALLFORM CLOTH_CUSTOMIZE_OPTION_DRAW_{ID}(CUSTOM,EQUIPID,ARG)
CATCH
	PRINTL
	;PRINTFORML CLOTH_CUSTOMIZE_OPTION_DRAW_{ID}がありません
ENDCATCH



@CLOTH_CUSTOMIZE_OPTION_COMMON(ID,ARG,ARGS:0,ARGS:1,ARGS:2,ARGS:3,ARGS:4,ARGS:5,ARGS:6,ARGS:7,ARGS:8,ARGS:9)
;장비 ID(단 -999라면 루프 내에서 호출된 판정 함수)
#DIM ID,1

#DIM 항목수,1
#DIM 선택불가플래그,10
#DIMS 카테고리,1
#DIMS 보충텍스트,1

;루프에서 호출되는 경우
IF ID==-999
	;선택 불가 카테고리의 입력만 끊는다
	$LOOP
	INPUT
	IF (RESULT % 10 == 0) && RESULT>=0 && RESULT <=항목수*10 && 선택불가플래그:(RESULT/10)==1
		CLEARLINE 1
		GOTO LOOP
	ELSE
		;CLEARLINE 1
		;CLEARLINE MAXNUM+5
		RETURN RESULT
	ENDIF
ENDIF

항목수=8
FOR LOCAL,0,10,1
	IF ARGS:LOCAL=="@@"
		항목수=LOCAL-1
		BREAK
	ENDIF

	LOCALS'=ARGS:LOCAL
	IF LOCALS==""
		선택불가플래그:LOCAL=1
		PRINTL
		CONTINUE
	ENDIF

	LOCAL:1=strfind(LOCALS,"!!")
	IF strfind(LOCALS,"!!")>-1
		선택불가플래그:LOCAL=1
		REPLACE LOCALS,"^(.+)!!.+$",""
		카테고리'=SUBSTRING(LOCALS,0,LOCAL:1)
		REPLACE LOCALS,"^.+!!(.+)$",""
		보충텍스트'=SUBSTRING(LOCALS,2+LOCAL:1)
		SETCOLOR 105,105,105
	ELSE
		선택불가플래그:LOCAL=0
		카테고리'=LOCALS
		보충텍스트'=""
		RESETCOLOR
	ENDIF

	PRINTFORML  [{LOCAL*10}] % "",LOCAL==0 ? 1 # 0%%카테고리,20,LEFT%%보충텍스트%
NEXT
RESETCOLOR

SIF ID>=0 && ID<=199 && CFLAG:ARG:1 > 0
	PRINTFORML  [{(항목수+1)*10}] 추가 장비
PRINTL 

PRINTL  [90] 초기화

IF (ID < 300 || ID > 400) && CFLAG:ARG:40 == CFLAG:ARG:41
	IF CFLAG:ARG:1 > 0
		SIF EQUIP:ARG:ID == EQUIP:ARG:(ID - 100)
			SETCOLOR 105,105,105
		PRINTFORML  [91] 변신 전 %ITEMNAME:ID%의 커스터마이즈를 복사한다
		PRINTFORML  [92] 변신 전 %ITEMNAME:ID%의 커스터마이즈를 붙여넣는다
	ELSE
		SIF EQUIP:ARG:ID == EQUIP:ARG:(ID + 100)
			SETCOLOR 105,105,105
		PRINTFORML  [91] 변신 후 %ITEMNAME:(ID+100)%의 커스터마이즈를 복사한다
		PRINTFORML  [92] 변신 후 %ITEMNAME:(ID+100)%의 커스터마이즈를 붙여넣는다
	ENDIF
	RESETCOLOR
ENDIF
PRINTL  [99] 돌아간다





;"플레이버@효과텍스트"
;효과란이 !!로 시작하고 있다면 선택불가플래그
;효과텍스트에는 다음과 같은 키워드를 기술할 수 있습니다
;	「경1」…내구-5 명중+1	경6까지 가능
;	「중1」…내구+5 명중-1	중6까지 가능
;
;	「보1」…속옷 보호+5		보6까지 가능
;	「백1」…속옷 보호-5		백6까지 가능
;
@CLOTH_CUSTOMIZE_OPTION_PRINT(now,ARGS:0,ARGS:1,ARGS:2,ARGS:3,ARGS:4,ARGS:5,ARGS:6,ARGS:7,ARGS:8,ARGS:9,ARGS:10)
#dim maxnum,1
#dim now,1
#dim 선택불가플래그,10
#dims 플레이버,1
#dims 효과텍스트,1

maxnum=8
FOR LOCAL,0,11
	locals'=args:local

	;끝 표시 발견 시
	IF locals=="@@"
		maxnum=LOCAL-1
		BREAK
	ENDIF




	;압축되어 있었던 스테이터스 보정을 분할
	replace locals,"^([^@]+)@?.*$","$1"
	플레이버'=results
	replace locals,"^[^@]*@?(.*)$","$1"
	효과텍스트'=results

	IF strfind(효과텍스트,"!!")==0
		선택불가플래그:LOCAL=1
		효과텍스트=%substring(효과텍스트,2)%
		SETCOLOR 105,105,105
	ELSE
		선택불가플래그:LOCAL=0
		IF now == LOCAL
			SETCOLOR 120,250,0
		ELSE
			RESETCOLOR
		ENDIF
	ENDIF

	replace 효과텍스트,"경1","내구-5 명중+1 "
	효과텍스트'=results
	replace 효과텍스트,"경2","내구-10 명중+2 "
	효과텍스트'=results
	replace 효과텍스트,"경3","내구-15 명중+3 "
	효과텍스트'=results
	replace 효과텍스트,"경4","내구-20 명중+4 "
	효과텍스트'=results
	replace 효과텍스트,"경5","내구-25 명중+5 "
	효과텍스트'=results
	replace 효과텍스트,"경6","내구-30 명중+6 "
	효과텍스트'=results

	replace 효과텍스트,"중1","내구+5 명중-1 "
	효과텍스트'=results
	replace 효과텍스트,"중2","내구+10 명중-2 "
	효과텍스트'=results
	replace 효과텍스트,"중3","내구+15 명중-3 "
	효과텍스트'=results
	replace 효과텍스트,"중4","내구+20 명중-4 "
	효과텍스트'=results
	replace 효과텍스트,"중5","내구+25 명중-5 "
	효과텍스트'=results
	replace 효과텍스트,"중6","내구+30 명중-6 "
	효과텍스트'=results

	replace 효과텍스트,"보1","속옷 보호+5"
	효과텍스트'=results
	replace 효과텍스트,"보2","속옷 보호+10"
	효과텍스트'=results
	replace 효과텍스트,"보3","속옷 보호+15"
	효과텍스트'=results
	replace 효과텍스트,"보4","속옷 보호+20"
	효과텍스트'=results
	replace 효과텍스트,"보5","속옷 보호+25"
	효과텍스트'=results
	replace 효과텍스트,"보6","속옷 보호+30"
	효과텍스트'=results

	replace 효과텍스트,"백1","속옷 보호-5"
	효과텍스트'=results
	replace 효과텍스트,"백2","속옷 보호-10"
	효과텍스트'=results
	replace 효과텍스트,"백3","속옷 보호-15"
	효과텍스트'=results
	replace 효과텍스트,"백4","속옷 보호-20"
	효과텍스트'=results
	replace 효과텍스트,"백5","속옷 보호-25"
	효과텍스트'=results
	replace 효과텍스트,"백6","속옷 보호-30"
	효과텍스트'=results



	PRINTFORML  [{LOCAL}] %플레이버,28,LEFT%%효과텍스트,20,LEFT%
	RESETCOLOR
NEXT

$INPUT_LOOP_0
INPUT
IF RESULT >= 0 && RESULT <= maxnum && 선택불가플래그:RESULT==0
	RETURN RESULT
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP_0
ENDIF

RETURN



;↑위 함수의 속옷 버전
;	「방1」…방심도　-6～6까지 가능
@INNER_CUSTOMIZE_OPTION_PRINT(now,ARGS:0,ARGS:1,ARGS:2,ARGS:3,ARGS:4,ARGS:5,ARGS:6,ARGS:7,ARGS:8,ARGS:9,ARGS:10)
#dim maxnum,1
#dim now,1
#dim 선택불가플래그,10
#dims 플레이버,1
#dims 효과텍스트,1

maxnum=9
FOR LOCAL,0,10
	locals'=args:local

	;끝 표시 발견 시
	IF locals=="@@"
		maxnum=LOCAL-1
		BREAK
	ENDIF


	SIF now == LOCAL
		SETCOLOR 120,250,0

	;압축되어 있었던 스테이터스 보정을 분할
	replace locals,"^([^@]+)@?.*$","$1"
	플레이버'=results
	replace locals,"^[^@]*@?(.*)$","$1"
	효과텍스트'=results

	IF strfind(효과텍스트,"!!")==0
		선택불가플래그:LOCAL=1
		효과텍스트=%substring(효과텍스트,2)%
		SETCOLOR 105,105,105
	ELSE
		선택불가플래그:LOCAL=0
	ENDIF

	replace 효과텍스트,"경1","내구-5 "
	효과텍스트'=results
	replace 효과텍스트,"경2","내구-10 "
	효과텍스트'=results
	replace 효과텍스트,"경3","내구-15 "
	효과텍스트'=results
	replace 효과텍스트,"경4","내구-20 "
	효과텍스트'=results
	replace 효과텍스트,"경5","내구-25 "
	효과텍스트'=results
	replace 효과텍스트,"경6","내구-30 "
	효과텍스트'=results

	replace 효과텍스트,"중1","내구+5 "
	효과텍스트'=results
	replace 효과텍스트,"중2","내구+10 "
	효과텍스트'=results
	replace 효과텍스트,"중3","내구+15 "
	효과텍스트'=results
	replace 효과텍스트,"중4","내구+20 "
	효과텍스트'=results
	replace 효과텍스트,"중5","내구+25 "
	효과텍스트'=results
	replace 효과텍스트,"중6","내구+30 "
	효과텍스트'=results

	replace 효과텍스트,"방1","방심도+1 "
	효과텍스트'=results
	replace 효과텍스트,"방2","방심도+2 "
	효과텍스트'=results
	replace 효과텍스트,"방3","방심도+3 "
	효과텍스트'=results
	replace 효과텍스트,"방4","방심도+4 "
	효과텍스트'=results
	replace 효과텍스트,"방5","방심도+5 "
	효과텍스트'=results
	replace 효과텍스트,"방6","방심도+6 "
	효과텍스트'=results
	replace 효과텍스트,"방-1","방심도-1 "
	효과텍스트'=results
	replace 효과텍스트,"방-2","방심도-2 "
	효과텍스트'=results
	replace 효과텍스트,"방-3","방심도-3 "
	효과텍스트'=results
	replace 효과텍스트,"방-4","방심도-4 "
	효과텍스트'=results
	replace 효과텍스트,"방-5","방심도-5 "
	효과텍스트'=results
	replace 효과텍스트,"방-6","방심도-6 "
	효과텍스트'=results

	replace 효과텍스트,"보1","삽입 저항+5"
	효과텍스트'=results
	replace 효과텍스트,"보2","삽입 저항+10"
	효과텍스트'=results
	replace 효과텍스트,"보3","삽입 저항+15"
	효과텍스트'=results
	replace 효과텍스트,"보4","삽입 저항+20"
	효과텍스트'=results
	replace 효과텍스트,"보5","삽입 저항+25"
	효과텍스트'=results
	replace 효과텍스트,"보6","삽입 저항+30"
	효과텍스트'=results

	replace 효과텍스트,"백1","삽입 저항-5"
	효과텍스트'=results
	replace 효과텍스트,"백2","삽입 저항-10"
	효과텍스트'=results
	replace 효과텍스트,"백3","삽입 저항-15"
	효과텍스트'=results
	replace 효과텍스트,"백4","삽입 저항-20"
	효과텍스트'=results
	replace 효과텍스트,"백5","삽입 저항-25"
	효과텍스트'=results
	replace 효과텍스트,"백6","삽입 저항-30"
	효과텍스트'=results



	PRINTFORML  [{LOCAL}] %플레이버,28,LEFT%%효과텍스트,20,LEFT%
	RESETCOLOR
NEXT

$INPUT_LOOP_0
INPUT
IF RESULT >= 0 && RESULT <= maxnum && 선택불가플래그:RESULT==0
	RETURN RESULT
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP_0
ENDIF

RETURN



;커스터마이즈 초기화
@CLOTH_CUSTOMIZE_OPTION_INITIALIZE(num,id,refcustom)
#DIM num,1
#DIM id,1
#DIM ref refcustom,0

EQUIP:num:id=0
SIF CFLAG:num:1 > 0
	CFLAG:num:(900 + id) = 0
VARSET refcustom


;커스터마이즈 복사
@CLOTH_CUSTOMIZE_OPTION_COPY91(ARG,id,CUSTOM_NUM,refCUSTOM)
#DIM id,1
#DIM CUSTOM_NUM,1
#DIM ref refCUSTOM,0
	IF CFLAG:ARG:1 > 0
		REPEAT CUSTOM_NUM
			refCUSTOM:COUNT = FIGURE_SPLIT(EQUIP:ARG:(ID - 100), COUNT + 1)
		REND
		PRINTFORMW 변신 전 %ITEMNAME:ID%의 커스터마이즈를 복사했습니다.
	ELSE
		REPEAT CUSTOM_NUM
			refCUSTOM:COUNT = FIGURE_SPLIT(EQUIP:ARG:(ID + 100), COUNT + 1)
		REND
		PRINTFORMW 변신 후 %ITEMNAME:(ID+100)%의 커스터마이즈를 복사했습니다.
	ENDIF

;커스터마이즈 붙여넣기
@CLOTH_CUSTOMIZE_OPTION_COPY92(ARG,ID)
#DIM ID,1
IF CFLAG:ARG:1 > 0
	EQUIP:ARG:(ID - 100) = EQUIP:ARG:ID
	PRINTFORMW 변신 전 %ITEMNAME:ID%의 커스터마이즈를 붙여넣었습니다.
ELSE
	EQUIP:ARG:(ID + 100) = EQUIP:ARG:ID
	PRINTFORMW 변신 후 %ITEMNAME:(ID+100)%의 커스터마이즈를 붙여넣었습니다.
ENDIF

;설정 기록
@CLOTH_CUSTOMIZE_OPTION_SAVE(ARG,ID,CUSTOM_NUM,refCUSTOM)
#DIM ID,1
#DIM CUSTOM_NUM,1
#DIM REF refCUSTOM,0
LOCAL = 0
REPEAT CUSTOM_NUM
	SIF COUNT == 0
		CONTINUE
	LOCAL += refCUSTOM:COUNT * POWER(10, COUNT)
REND
LOCAL += refCUSTOM:0
EQUIP:ARG:ID = LOCAL
