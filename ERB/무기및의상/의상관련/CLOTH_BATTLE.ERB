;**********************************************************
;戦闘中の衣装に関する処理

;----------------------------------------------------------
;衣装の耐久度を設定
@CLOTH_BATTLE_SETHP
#DIM KEEP_CFLAG,1

CALL CLOTH_HOSEI(TARGET,CFLAG:40,"HP")
TCVARn:20 = RESULT
TCVARn:21 = RESULT

KEEP_CFLAG=CFLAG:1
CFLAG:1 = 1
CALL CLOTH_HOSEI(TARGET,CFLAG:41,"HP")
IF RESULT!=-1
	CALL CLOTH_CUSTOMIZE_COMMONPARTS_CAL, RESULT, "HP"
ENDIF
TCVARn:22 = RESULT
TCVARn:23 = RESULT
CFLAG:1 = KEEP_CFLAG

CALL CLOTH_HOSEI(TARGET,CFLAG:42,"HP")

TCVARn:24 = RESULT
TCVARn:25 = RESULT


;----------------------------------------------------------
;衣装の耐久度表示
@CLOTH_BATTLE_DISPHP
#DIM 最大文字数,1
最大文字数=0


LOCALS = ◆アウター無し
SIF COSTUME_NAME(TARGET) != ""
	LOCALS = ◆%COSTUME_NAME(TARGET)%

LOCALS:1 = ◆インナー無し
IF CLOTH_NO_INNER && TCVARn:24 >= 0
	LOCALS:1 = ◇（下着兼用）
ELSEIF CFLAG:TARGET:42!=0
	LOCALS:1 = ◆%INNER_NAME(TARGET)%
ENDIF

最大文字数=MAX(STRLENS(LOCALS:0),STRLENS(LOCALS:1))+2


PRINTL 

;●アウター
IF TCVARn:(CFLAG:1==0 ? 20 # 22) < 0
	SIF TCVARn:41 && CFLAG:(CFLAG:1==0 ? 40 # 41) == 199
		SETCOLOR 255, 128, 192
	PRINTFORM %""+LOCALS+"",最大文字数,LEFT%
	BAR 0, 1, 5
	PRINTFORM （─） 
ELSEIF TCVARn:(CFLAG:1==0 ? 20 # 22) == 0
	SIF CFLAG:40 != 0
		SETCOLOR 255, 0, 0
	SIF CFLAG:40 == 0
		SETCOLOR 123, 123, 123
	PRINTFORM %""+LOCALS+"",最大文字数,LEFT%
	BAR 0, 1, 15
	PRINTFORM （なし）
ELSEIF TCVARn:(CFLAG:1==0 ? 21 # 23) > 0
	LOCAL:3 = 0
	IF CLOTH_OUTER_PER < CLOTH_OUTER_DEF
		LOCAL:0=255
		LOCAL:1=123
		LOCAL:2=0
		LOCAL:3=2
	ELSEIF CLOTH_OUTER_PER < CLOTH_OUTER_DEF + (100 - CLOTH_OUTER_DEF) / 2
		LOCAL:0=255
		LOCAL:1=255
		LOCAL:2=0
		LOCAL:3=1
	ENDIF
	SETCOLOR LOCAL:0,LOCAL:1,LOCAL:2
	SIF LOCAL:3 == 0
		RESETCOLOR
	PRINTFORM %""+LOCALS+"",最大文字数,LEFT%
	PRINTFORM %UNICODE(0x208D)%
	IF LOCAL:3 == 0
		LOCAL:0=40
		LOCAL:1=70
		LOCAL:2=210
	ENDIF
	CALLFORM COLOR_BAR(TCVARn:(CFLAG:1==0 ? 21 # 23), TCVARn:(CFLAG:1==0 ? 20 # 22), 15, LOCAL:0,LOCAL:1,LOCAL:2,-160,10,18,6,2,UNICODE(0x25AE),UNICODE(0x25AE))
	SETCOLOR LOCAL:0,LOCAL:1,LOCAL:2
	SIF LOCAL:3 == 0
		RESETCOLOR
	PRINTFORM %UNICODE(0x208E)%
	PRINTFORM （{TCVARn:(CFLAG:1==0 ? 21 # 23)}/{TCVARn:(CFLAG:1==0 ? 20 # 22)}）
	
ELSE
	SETCOLOR 255, 0, 0
	PRINTFORM %""+LOCALS+"",最大文字数,LEFT%
	PRINTFORM %UNICODE(0x208D)%
	CALLFORM COLOR_BAR(TCVARn:(CFLAG:1==0 ? 21 # 23), TCVARn:(CFLAG:1==0 ? 20 # 22), 15, 255,0,0,-160,10,18,6,2,UNICODE(0x25AE),UNICODE(0x25AE))
	SETCOLOR 255, 0, 0
	PRINTFORM %UNICODE(0x208E)%
	PRINTFORM （0/{TCVARn:(CFLAG:1==0 ? 20 # 22)}）
ENDIF

PRINTL
PRINT 　
CALL CLOTH_CUSTOMIZE_OPTION_DRAW(CFLAG:1==0 ? CFLAG:40 # CFLAG:41,TARGET)


RESETCOLOR
;PRINTL




;●インナー
IF TCVARn:24 < 0
	SIF CFLAG:42 == 400
		SETCOLOR 255, 128, 192
	PRINTFORM %""+LOCALS:1+"",最大文字数,LEFT%
	BAR 0, 1, 5
	PRINTFORM （─）
ELSEIF CLOTH_NO_INNER
	IF TCVARn:41 && CFLAG:(CFLAG:1==0 ? 40 # 41) == 199
		SETCOLOR 255, 128, 192
	ELSEIF TCVARn:(CFLAG:1==0 ? 20 # 22) >= 0
		IF TCVARn:(CFLAG:1==0 ? 21 # 23)==0
			SETCOLOR 255, 0, 0
		ELSEIF CLOTH_OUTER_PER < CLOTH_OUTER_DEF
			SETCOLOR 255, 123, 0
		ELSEIF CLOTH_OUTER_PER < CLOTH_OUTER_DEF + (100 - CLOTH_OUTER_DEF) / 2
			SETCOLOR 255, 255, 0
		ENDIF
	ENDIF
	PRINTFORM %LOCALS:1,最大文字数,LEFT%
	;BAR 0, 1, 5
	;PRINTFORM （─）
;通常時
ELSEIF TCVARn:24 == 0
	SIF CFLAG:42 != 0
		SETCOLOR 255, 0, 0
	SIF CFLAG:42 == 0
		SETCOLOR 123, 123, 123
	PRINTFORM %""+LOCALS:1+"",最大文字数,LEFT%
	BAR 0, 1, 15
	PRINTFORM （なし）
ELSEIF TCVARn:25 > 0
	LOCAL:3 = 0
	IF CLOTH_INNER_PER < CLOTH_INNER_DEF
		LOCAL:0=255
		LOCAL:1=123
		LOCAL:2=0
		LOCAL:3=2
	ELSEIF CLOTH_INNER_PER < CLOTH_INNER_DEF + (100 - CLOTH_INNER_DEF) / 2
		LOCAL:0=255
		LOCAL:1=255
		LOCAL:2=0
		LOCAL:3=1
	ENDIF
	SETCOLOR LOCAL:0,LOCAL:1,LOCAL:2
	SIF LOCAL:3 == 0
		RESETCOLOR
	PRINTFORM %""+LOCALS:1+"",最大文字数,LEFT%
	PRINTFORM %UNICODE(0x208D)%
	IF LOCAL:3 == 0
		LOCAL:0=40
		LOCAL:1=70
		LOCAL:2=210
	ENDIF
	CALLFORM COLOR_BAR(TCVARn:25, TCVARn:24, 15, LOCAL:0,LOCAL:1,LOCAL:2,-160,10,18,6,2,UNICODE(0x25AE),UNICODE(0x25AE))
	SETCOLOR LOCAL:0,LOCAL:1,LOCAL:2
	SIF LOCAL:3 == 0
		RESETCOLOR
	PRINTFORM %UNICODE(0x208E)%
	PRINTFORM （{TCVARn:25,3,RIGHT}/{TCVARn:24,3,RIGHT}）

ELSE
	SETCOLOR 255, 0, 0
	PRINTFORM %""+LOCALS:1+"",最大文字数,LEFT%
	PRINTFORM %UNICODE(0x208D)%
	CALLFORM COLOR_BAR(0, TCVARn:24, 15, 244,0,0,-160,10,18,6,2,UNICODE(0x25AE),UNICODE(0x25AE))
	SETCOLOR 255, 0, 0
	PRINTFORM %UNICODE(0x208E)%
	PRINTFORM （  0/{TCVARn:24,3,RIGHT}）

ENDIF

PRINTL
PRINT 　
CALL CLOTH_CUSTOMIZE_OPTION_DRAW(CFLAG:42,TARGET)


;----------------------------------------------------------
;衣装へのダメージ
@CLOTH_BATTLE_DAMAGE, ARG, ARG:1 = 0
LOCAL:0 = 0
LOCAL:1 = 0
LOCAL:2 = 50
LOCAL:3 = ARG

;フェイタリティ後は省略
SIF TFLAG:23==-1
	RETURN

;성기 가리개に対するダメージは非常に小さくなる
IF CFLAG:42 == 398 && ARG > 0
	IF TCVARn:2 == 자세：Ｖ방어
		LOCAL:3 = MAX(LOCAL:3 / 6, 1)
	ELSE
		LOCAL:3 = MAX(LOCAL:3 / 3, 1)
	ENDIF
ENDIF
IF CFLAG:1 == 0
	CALL CLOTH_HOSEI(TARGET,CFLAG:TARGET:40,"DEF")
	;TRYCCALLFORM CLOTH_DEF_{CFLAG:40}
		LOCAL:2 = RESULT
	;CATCH
	;	LOCAL:2 = DEFAULT_OUTER_DEF
	;ENDCATCH

	IF TCVARn:20 > 0 || TCVARn:24 > 0
		IF TCVARn:21 > 0 || TCVARn:25 > 0
			IF TCVARn:21 == 0 || PERCENT_CAL_F(TCVARn:21,TCVARn:20) < LOCAL:2
				;インナー兼用衣装の場合はインナーにダメージを与えない
				CALL CLOTH_NO_INNER, TARGET
				IF TCVARn:25 > 0 && RESULT == 0
					SIF TCVARn:0 != 0 && ARG:1 == 0
						LOCAL:3 = LOCAL:3 / 3
					LOCAL:1 = PERCENT_CAL_F(TCVARn:25,TCVARn:24)
					TCVARn:25 = LIMIT(TCVARn:25 - LOCAL:3, 0, TCVARn:25)
					SIF ARG:1 == 0 && TCVARn:25 > 0
						ARG = ARG / 3
				ELSEIF ARG:1 == 0 && TCVARn:25 > 0
					ARG = ARG / 6
				ENDIF
			ENDIF
			LOCAL:0 = PERCENT_CAL_F(TCVARn:21,TCVARn:20)
			TCVARn:21 = LIMIT(TCVARn:21 - ARG, 0, TCVARn:21)
		ENDIF
	ENDIF
ELSE
	CALL CLOTH_HOSEI(TARGET,CFLAG:41,"DEF")
	;TRYCCALLFORM CLOTH_DEF_{CFLAG:41}
		LOCAL:2 = RESULT
	;CATCH
	;	LOCAL:2 = DEFAULT_OUTER_DEF
	;ENDCATCH
	IF TCVARn:22 > 0 || TCVARn:24 > 0
		IF TCVARn:23 > 0 || TCVARn:25 > 0
			IF TCVARn:23 == 0 || PERCENT_CAL_F(TCVARn:23,TCVARn:22) < LOCAL:2
				;インナー兼用衣装の場合はインナーにダメージを与えない
				CALL CLOTH_NO_INNER, TARGET
				IF TCVARn:25 > 0 && RESULT == 0
					SIF TCVARn:0 != 0 && ARG:1 == 0
						LOCAL:3 = LOCAL:3 / 3
					LOCAL:1 = PERCENT_CAL_F(TCVARn:25,TCVARn:24)
					TCVARn:25 = LIMIT(TCVARn:25 - LOCAL:3, 0, TCVARn:25)
					SIF ARG:1 == 0 && TCVARn:25 > 0
						ARG = ARG / 3
				ELSEIF ARG:1 == 0 && TCVARn:25 > 0
					ARG = ARG / 6
				ENDIF
			ENDIF
			LOCAL:0 = PERCENT_CAL_F(TCVARn:23,TCVARn:22)
			TCVARn:23 = LIMIT(TCVARn:23 - ARG, 0, TCVARn:23)
		ENDIF
	ENDIF
ENDIF

;耐久力関連の情報を更新
CALL REFRESH_CLOTH_DATA

;地の文
IF CFLAG:1 == 0
	IF LOCAL:0 > 0 && TCVARn:21 <= 0 && TCVARn:20 > 0
		;지문: アウターが破れた
		CALL MESSAGE_BATTLE_CLOTH_OUTERBREAK
		;지문: 洗脳/悪堕ちキャラが操作キャラのアウターが破れたのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_BATTLE_CLOTH_OUTERBREAK
			
	ELSEIF LOCAL:0 >= CLOTH_OUTER_DEF && PERCENT_CAL_F(TCVARn:21,TCVARn:20) < CLOTH_OUTER_DEF && TCVARn:20 > 0
		;지문: アウターが破れ始めた
		CALL MESSAGE_BATTLE_CLOTH_OUTERDAMAGE
		;지문: 洗脳/悪堕ちキャラが操作キャラのアウターが破れ始めたのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_BATTLE_CLOTH_OUTERDAMAGE
			
	ENDIF
ELSE
	IF LOCAL:0 > 0 && TCVARn:23 <= 0 && TCVARn:22 > 0
		;지문: アウターが破れた
		CALL MESSAGE_BATTLE_CLOTH_OUTERBREAK
		;지문: 洗脳/悪堕ちキャラが操作キャラのアウターが破れたのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_BATTLE_CLOTH_OUTERBREAK
			
	ELSEIF LOCAL:0 >= CLOTH_OUTER_DEF && PERCENT_CAL_F(TCVARn:23,TCVARn:22) < CLOTH_OUTER_DEF && TCVARn:22 > 0
		;지문: アウターが破れ始めた
		CALL MESSAGE_BATTLE_CLOTH_OUTERDAMAGE
		;지문: 洗脳/悪堕ちキャラが操作キャラのアウターが破れ始めたのを見た
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_BATTLE_CLOTH_OUTERDAMAGE

	ENDIF
ENDIF

IF LOCAL:1 && TCVARn:25 <= 0 && TCVARn:24 > 0
	;지문: インナーが破れた
	CALL MESSAGE_BATTLE_CLOTH_INNERBREAK
	;지문: 洗脳/悪堕ちキャラが操作キャラのインナーが破れたのを見た
	SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
		CALL MESSAGE_OTHER_BATTLE_CLOTH_INNERBREAK
		
ELSEIF LOCAL:1 >= CLOTH_INNER_DEF && PERCENT_CAL_F(TCVARn:25,TCVARn:24) < CLOTH_INNER_DEF && TCVARn:24 > 0
	;지문: インナーが破れ始めた
	CALL MESSAGE_BATTLE_CLOTH_INNERDAMAGE
	;지문: 洗脳/悪堕ちキャラが操作キャラのインナーが破れ始めたのを見た
	SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
		CALL MESSAGE_OTHER_BATTLE_CLOTH_INNERDAMAGE
		
ENDIF




;----------------------------------------------------------
;衣装による補正
@CLOTH_BATTLE_HOSEI, ARGS, ARG = -999
#DIM KEEP_TARGET
KEEP_TARGET = TARGET
SIF ARG != -999
	TARGET = ARG
LOCAL = 100
IF ARGS == "CRITICAL" || ARGS == "ANTIBUP" || ARGS == "ANTICUP" || ARGS == "HEALUP" || ARGS == "EXBOOST" || ARGS == "AIRPLUS" || ARGS == "HIT" || ARGS == "AVOID"
	LOCAL = 0
ENDIF
;未変身
IF CFLAG:1 == 0
	IF FLAG:700 && (TCVARn:21 == 0 || PERCENT_CAL_F(TCVARn:21,TCVARn:20) < 1) && KEEP_TARGET == TARGET
		;破壊済みなら補正無し
	ELSE
		CALL CLOTH_HOSEI(TARGET,CFLAG:TARGET:40,ARGS)
			IF ARGS == "CRITICAL" || ARGS == "ANTIBUP" || ARGS == "ANTICUP" || ARGS == "HEALUP" || ARGS == "EXBOOST" || ARGS == "AIRPLUS" || ARGS == "HIT" || ARGS == "AVOID"
				LOCAL += RESULT
			ELSE
				LOCAL = LOCAL * RESULT / 100
			ENDIF
	ENDIF
;変身中
ELSE
	IF FLAG:700 && (TCVARn:23 == 0 || PERCENT_CAL_F(TCVARn:23,TCVARn:22) < 1) && KEEP_TARGET == TARGET
		;破壊済みなら補正無し
	ELSE
		CALL CLOTH_HOSEI(TARGET,CFLAG:TARGET:41,ARGS)
			IF ARGS == "CRITICAL" || ARGS == "ANTIBUP" || ARGS == "ANTICUP" || ARGS == "HEALUP" || ARGS == "EXBOOST" || ARGS == "AIRPLUS" || ARGS == "HIT" || ARGS == "AVOID"
				LOCAL += RESULT
			ELSE
				LOCAL = LOCAL * RESULT / 100
			ENDIF
	ENDIF
ENDIF
;インナー
IF FLAG:700 && (TCVARn:25 == 0 || PERCENT_CAL_F(TCVARn:25,TCVARn:24) < 1) || CLOTH_NO_INNER > 0 && KEEP_TARGET == TARGET
		;破壊済みなら補正無し
ELSE
		CALL CLOTH_HOSEI(TARGET,CFLAG:TARGET:42,ARGS)
		IF ARGS == "CRITICAL" || ARGS == "ANTIBUP" || ARGS == "ANTICUP" || ARGS == "HEALUP" || ARGS == "EXBOOST" || ARGS == "AIRPLUS" || ARGS == "HIT" || ARGS == "AVOID"
			LOCAL += RESULT
		ELSE
			LOCAL = LOCAL * RESULT / 100
		ENDIF
ENDIF
;変身中ならカスタマイズパーツについてもチェックする
;ただし耐久無し衣装の場合はノータッチ
IF CFLAG:1 > 0 && (ARGS!="HP" || LOCAL!=-1)
	CALL CLOTH_CUSTOMIZE_COMMONPARTS_CAL, LOCAL, ARGS
	LOCAL = RESULT
ENDIF
TARGET = KEEP_TARGET
RETURN LOCAL



;----------------------------------------------------------
;衣装の状態判定
;着衣が破壊されていなければビットを立てる
@CLOTH_CHECK(ARG)
#FUNCTION
LOCAL = 0
IF CFLAG:ARG:1 == 0 && TCVARn:ARG:21 > 0
	LOCAL |= 2
ELSEIF CFLAG:ARG:1 == 1 && TCVARn:ARG:23 > 0
	LOCAL |= 4
ENDIF
SIF TCVARn:ARG:25 > 0 || (CLOTH_NO_INNER > 0 && LOCAL > 0)
	LOCAL |= 1

RETURNF LOCAL




;--------------------------------------------------
;ARGが着ている現在のアウターの名称を返す式中関数
;現在の状態と無関係に取得するならtransで指定可能
@COSTUME_NAME(ARG,trans=-1)
#FUNCTIONS
#dim trans

IF trans==0 || (trans==-1 && CFLAG:ARG:1 == 0)
	IF CFLAG:ARG:40 == 0
		LOCALS = 
	ELSEIF CFLAG:ARG:40 == 990
		LOCALS = %CSTR:ARG:80%
	ELSEIF CSTR:ARG:8 != ""
		LOCALS = %CSTR:ARG:8%
	ELSE
		LOCALS = %ITEMNAME:(CFLAG:ARG:40)%
	ENDIF
ELSE
	IF CFLAG:ARG:41 == 0
		LOCALS = 
	ELSEIF CFLAG:ARG:41 == 991
		LOCALS = %CSTR:ARG:81%
	ELSEIF CSTR:ARG:9 != ""
		LOCALS = %CSTR:ARG:9%
	ELSE
		LOCALS = %ITEMNAME:(CFLAG:ARG:41)%
	ENDIF
ENDIF
RETURNF LOCALS
;--------------------------------------------------
;インナーの名称を返す式中関数
@INNER_NAME(ARG)
#FUNCTIONS
IF CFLAG:ARG:42 == 0
	LOCALS = 
ELSEIF CFLAG:ARG:42 == 992
	LOCALS = %CSTR:ARG:82%
ELSE
	LOCALS = %ITEMNAME:(CFLAG:ARG:42)%
ENDIF
RETURNF LOCALS



;--------------------------------------------------
;インナー兼用の判定
@CLOTH_NO_INNER, ARG
RESULT = 0
CALL CLOTH_HOSEI(TARGET,CFLAG:(CFLAG:ARG:1 > 0 ? 41 # 40),"NOINNER")
RETURN RESULT

;--------------------------------------------------
;衣装耐久力参照に関する間数
@REFRESH_CLOTH_DATA
;衣装の状態を見る
IF CFLAG:1 == 0
	CALL PERCENT_CAL, TCVARn:21, TCVARn:20
	CLOTH_OUTER_PER = RESULT
	CALL CLOTH_HOSEI(TARGET,CFLAG:40,"DEF")
	CLOTH_OUTER_DEF = RESULT
	;変身後のアウターの耐久力を同期
	SIF TCVARn:20 > 0 && TCVARn:22 > 0
		TCVARn:23 = TCVARn:22 * CLOTH_OUTER_PER / 100
ELSE
	CALL PERCENT_CAL, TCVARn:23, TCVARn:22
	CLOTH_OUTER_PER = RESULT
	CALL CLOTH_HOSEI(TARGET,CFLAG:41,"DEF")
	CLOTH_OUTER_DEF = RESULT
	;変身前のアウターの耐久力を同期
	SIF TCVARn:20 > 0 && TCVARn:22 > 0
		TCVARn:21 = TCVARn:20 * CLOTH_OUTER_PER / 100
ENDIF
CALL PERCENT_CAL, TCVARn:25, TCVARn:24
CLOTH_INNER_PER = RESULT

SIF CFLAG:1 == 0 && TCVARn:20 < 0
	CLOTH_OUTER_PER = 0
SIF CFLAG:1 > 0 && TCVARn:22 < 0
	CLOTH_OUTER_PER = 0
SIF TCVARn:24 < 0
	CLOTH_INNER_PER = 0

CALL CLOTH_NO_INNER, TARGET
;インナー兼用衣装の処理
IF RESULT > 0
	CLOTH_INNER_PER = CLOTH_OUTER_PER
	CLOTH_NO_INNER = 1
	;挿入抵抗力の設定
	CALL CLOTH_HOSEI(TARGET,CFLAG:(CFLAG:1==0 ? 40 # 41),"DEF")
	CLOTH_INNER_DEF = RESULT
	;インナーの耐久力をアウターに同期
	SIF TCVARn:24 > 0 && TCVARn:(CFLAG:1==0 ? 20 # 22) > 0
		TCVARn:25 = TCVARn:24 * CLOTH_OUTER_PER / 100
;通常はインナーについて処理
ELSE
	CLOTH_NO_INNER = 0
	;挿入抵抗力の設定
	CALL CLOTH_HOSEI(TARGET,CFLAG:42,"DEF")
	CLOTH_INNER_DEF = RESULT

	;変身状態の逆側の衣装がインナー兼用なら(アウター+インナー)/2の耐久力を逆側の衣装に同期
	CALL CLOTH_HOSEI(TARGET,CFLAG:(CFLAG:1==0 ? 41 # 40),"NOINNER")
	IF RESULT > 0
		IF TCVARn:(CFLAG:1==0 ? 20 # 22) > 0 && TCVARn:24 > 0
			TCVARn:(CFLAG:1==0 ? 23 # 21) = TCVARn:(CFLAG:1==0 ? 22 # 20) * (CLOTH_OUTER_PER + CLOTH_INNER_PER) / 200
		ELSEIF TCVARn:(CFLAG:1==0 ? 20 # 22) > 0
			TCVARn:(CFLAG:1==0 ? 23 # 21) = TCVARn:(CFLAG:1==0 ? 22 # 20) * CLOTH_OUTER_PER / 100
		ELSEIF TCVARn:24 > 0
			TCVARn:(CFLAG:1==0 ? 23 # 21) = TCVARn:(CFLAG:1==0 ? 22 # 20) * CLOTH_INNER_PER / 100
		ENDIF
	ENDIF
ENDIF


